// ГМ2 = Глобавльный Модуль для Ветис 2.*
// В модуле находятся функции для взамиодействия с Ветис 2.*
//
// ===============================================================
//Авторы:
// Синявский Филипп phsin@kb99.pro
// Жуков Дмитрий zhukov@kb99.pro

Перем ТаблицаПереопределенныхФункций;

Функция ПолучитьПартии2( спПараметрыФункции ) Экспорт Далее
Функция ПолучитьИмяФайла_МаршрутДоставки(Док) Экспорт Далее
Функция ПолучитьИмяФайлаРегионализация( Док ) Экспорт Далее
	
//Служебные функции
Функция Этот(Конт) 	Возврат Конт; КонецФункции
Функция Сам() 	Возврат Этот(Контекст); КонецФункции

Процедура Конструктор()

	//Служебная таблица для перекрытия переопределенных функций
	ТаблицаПереопределенныхФункций = СоздатьОбъект("ТаблицаЗначений");
	ТаблицаПереопределенныхФункций.НоваяКолонка("НаименованиеФункции");
	ТаблицаПереопределенныхФункций.НоваяКолонка("Объект");

	глМеркурийИнтеграция_2 = СоздатьОбъект("МеркурийИнтеграция_2");
    глМеркурийИнтеграция_2.Инициализация(Сам());

КонецПроцедуры

Процедура Деструктор()
КонецПроцедуры
//

Процедура Инициализация(ГлобавльныйМодуль) Экспорт
	ГМ = ГлобавльныйМодуль;
КонецПроцедуры

//{======= ФУНКЦИИ ПО РАБОТЕ С ПЕРЕОПРЕДЕЛЕННЫМИ ФУНКЦИЯМИ ==============
// Автор Анисимов Михаил mbIkola
Функция ПереопределитьФункцию(ИмяФункции,Объект) Экспорт
	НС = 0;
	темп = Врег(ИмяФункции);
		
	Если ТаблицаПереопределенныхФункций.НайтиЗначение(ИмяФункции,НС,"НаименованиеФункции")=0 Тогда
		ТаблицаПереопределенныхФункций.НоваяСтрока();
		ТаблицаПереопределенныхФункций.НаименованиеФункции 	= темп;
		ТаблицаПереопределенныхФункций.Объект 				= Объект;
		
		Если ГМ.ПолучитьКонстанту("ВыводитьПодробнуюИнформацию") = 1 Тогда 
			Сообщить("Выполняется переопределение функции <"+ИмяФункции+"> ","i");
		КонецЕсли;		
		
	Иначе
		ТаблицаПереопределенныхФункций.ПолучитьСтрокуПоНомеру(НС);
		ТаблицаПереопределенныхФункций.Объект 		= Объект;
	КонецЕсли;

	Возврат 1;
КонецФункции // ПереопределитьФункцию(ИмяФункции,Объект)

Функция ПроверитьПереопределениеФункции(ИмяФункции) Экспорт
	НС = 0;
	темп = Врег(ИмяФункции);

	Если ТаблицаПереопределенныхФункций.НайтиЗначение(темп,НС,"НаименованиеФункции")=0 Тогда
		Возврат "";
	Иначе
		ТаблицаПереопределенныхФункций.ПолучитьСтрокуПоНомеру(НС);
		Возврат ТаблицаПереопределенныхФункций.Объект;
	КонецЕсли;
КонецФункции // МодульВыполнения
//}======================================================================

//======= ВСПОМОГАЛЬНЫЕ ФУНКЦИИ ==============

Функция УровниУпаковки_из_XML(PList) Экспорт
	//Список физической упаковки. vd:packageList
	// С уровнями
	//   Уровни <dt:level>
	//1 	Внутренний уровень. Уровень, при котором упаковка отсутствует, но тем не менее есть необходимость наносить маркировку. Например, яйцо, шкуры, мясо, сыр. Явно указывается, что упаковка отсутствует.
	//2 	Потребительский уровень. Товар в упаковке для розничной торговли, маркированный штриховым кодом для сканирования на кассе.
	//3 	Промежуточный уровень. Уровень упаковки, Если он существует, который находится между потребительским И торговым уровнем.
	//4 	Торговый уровень. Товар в упаковке, предназначенной для заказа, оплаты И доставки. Это согласованный между ритейлером И изготовителем (ИЛИ другим участником) уровень упаковки товара, в котором товар заказывается, оплачивается И доставляется.
	//5 	Дополнительный уровень. Товар в упаковке, которую нельзя однозначно отнести к торговому ИЛИ транспортному уровню.
	//6 	Транспортный (Логистический) уровень. Товар в упаковке, предназначенной для отгрузки покупателю (ритейлеру) при выполнении заказа.

	// Классы <dt:productMarks class = Тип, описывающий тип маркировки.
	//UNDEFINED 	Класс неопределен. Это значение используется По умолчанию.
	//BN 	Номер производственной партии.
	//    Важно, чтобы номера партий совпадали при совершении операции незавершённого производства.
	//SSCC 	SSCC-код (глобально-уникальный код грузовых контейнеров - Serial Shipping Container Code).
	//EAN8 	Маркировка в соответствии с EAN-8.
	//EAN13 	Маркировка в соответствии с EAN-13.
	//EAN128 	Маркировка в соответствии с EAN-128.
	//BUNDLE 	Маркировка вышестоящей групповой упаковки, например, паллеты. Может использоваться для поиска группы вет.сертификатов для партий, находящихся на данной паллете.
	ТЗУровни = Создатьобъект("ТаблицаЗначений");
	ТЗУровни.НоваяКолонка("НомерУровня","Число",1,0,,2);
	ТЗУровни.НоваяКолонка("Упаковка","Справочник.ВСД_ФормыУпаковки");
	ТЗУровни.НоваяКолонка("КолВо","Число",9,0,,5);
	ТЗУровни.НоваяКолонка("Маркировки");

	Попытка
	    Для l1 = 0 По PList.length - 1 Цикл
			Узел = PList.item(l1);
			ТЗУровни.НоваяСтрока();
			ТЗУровни.НомерУровня = Узел.selectSingleNode("dt:level").text;   // Уровень упаковки
			//Попытка	ПГУИД = Узел.selectSingleNode("dt:packingType").selectSingleNode("bs:guid").text; Исключение ПГУИД = ""; КонецПопытки;
			//Попытка	ПУИД = Узел.selectSingleNode("dt:packingType").selectSingleNode("bs:uuid").text; Исключение ПУИД = "";	КонецПопытки;
			//dt:globalID  	Уникальный идентификатор упаковки.
			//Попытка ГлИД = Узел.selectSingleNode("dt:packingType").selectSingleNode("dt:globalID").text;Исключение	ГлИД = "";	КонецПопытки;
			// <dt:name>  	Наименование упаковки.
			//Попытка	ПакИмя = Узел.selectSingleNode("dt:packingType").selectSingleNode("dt:name").text;Исключение ПакИмя = ""; КонецПопытки;
			// dt:quantity  	Количество единиц упаковки.
			Попытка ТЗУровни.КолВо = Число(Узел.selectSingleNode("dt:quantity").text);Исключение  КонецПопытки;

			Попытка ТЗУровни.Упаковка = ГМ.НайтиФормуУпаковки( Узел.selectSingleNode("dt:packingType").selectSingleNode("bs:guid").text, Узел.selectSingleNode("dt:packingType").selectSingleNode("dt:name").text); Исключение  КонецПопытки;

			СзМаркировки = СоздатьОбъект("Списокзначений");
			//Маркировка партии продукции. Например
			//      <dt:productMarks class="EAN128">7456873456-147885</dt:productMarks>
                //      <dt:productMarks class="UNDEFINED">custom marking</dt:productMarks>
			Попытка
               	Маркировки = Узел.selectNodes("dt:productMarks");

				Для кк =0 По Маркировки.length - 1 Цикл
					МаркН = Маркировки.item(кк);
					МаркЗнач = МаркН.text;
					МаркКласс = МаркН.getattribute("class");
					СзМаркировки.ДобавитьЗначение(Маркзнач,МаркКласс);
				КонецЦикла;
			Исключение КонецПопытки;
		    Если СзМаркировки.РазмерСписка()>0 Тогда
		        ТЗУровни.Маркировки = ЗначениеВСтроку(СзМаркировки);
		    КонецЕсли;
		КонецЦикла;
	Исключение	КонецПопытки;

	Если ТЗУровни.КоличествоСтрок()>0 Тогда
        //Возврат ЗначениеВСтроку(ТЗУровни);
		Возврат ТЗУровни;
	Иначе
		Возврат "";
    КонецЕсли;
КонецФункции

Функция СвязанныеДокументы_из_XML(DocList) Экспорт
	// Связанные документы - м.б. несколько, для гашения в тестовой версии достаточно ТТН
	// другие связанные документы не нашел куда включать в запросе на гашение
    // На всякий случай считаем их в ТЗ

    // ТипОтношенияДок
	//1 	Сопроводительный документ. 	Complementary accompanying document.
	//2 	Предшествующий документ. 	Preceding document (Ancestor).
	//3 	Следующий документ. 	Subsequent/following document (Child).
	//4 	Документ, взамен которого выдан текущий документ. 	Replaced document.
	//5 	Документ, заменяющий текущий документ. 	Replaced by document.
	//6 	Связанный документ. 	Related document.

	ТзСвДокументов = СоздатьОбъект("ТаблицаЗначений");
	ТзСвДокументов.НоваяКолонка("ТипДокумента","Число");
	ТзСвДокументов.НоваяКолонка("НомерДокумента","Строка");
	ТзСвДокументов.НоваяКолонка("СерияДокумента","Строка");
	ТзСвДокументов.НоваяКолонка("ДатаДокумента","Строка");
	ТзСвДокументов.НоваяКолонка("ТипОтношенияДок","Число");

	Попытка
	    Для l1 = 0 По DocList.length - 1 Цикл
			Узел = DocList.item(l1);
			ТЗСвДокументов.НоваяСтрока();
            Попытка ТЗСвДокументов.ТипДокумента = 		Узел.selectSingleNode("vd:type").text; Исключение КонецПопытки;
            Попытка ТЗСвДокументов.НомерДокумента =		Узел.selectSingleNode("vd:issueNumber").text; Исключение КонецПопытки;
			Попытка ТЗСвДокументов.СерияДокумента =  	Узел.selectSingleNode("vd:issueSeries").text; Исключение КонецПопытки;
			Попытка ТЗСвДокументов.ДатаДокумента =  	Узел.selectSingleNode("vd:issueDate").text; Исключение КонецПопытки;
			Попытка ТЗСвДокументов.ТипОтношенияДок =	Узел.selectSingleNode("vd:relationshipType").text; Исключение КонецПопытки;
		КонецЦикла;
	Исключение КонецПопытки;

	Если ТЗСвДокументов.КоличествоСтрок()>0 Тогда
	    //Возврат ЗначениеВСтроку(ТЗСвДокументов);
		Возврат ТзСвДокументов;
	Иначе
		Возврат "";
	КонецЕсли;

КонецФункции

Функция СписокНомеровПартий_из_XML(batchList) Экспорт
	// Номер Партии - м.б. несколько
	СпНомераПартий =  СоздатьОбъект("СписокЗначений");
	Попытка
	    Для l1 = 0 По batchList.length - 1 Цикл
			//НомерПартии = batchList.item(l1).selectSingleNode("vd:batchID").text;
			СпНомераПартий.ДобавитьЗначение(batchList.item(l1).text);
		КонецЦикла;
	Исключение 	КонецПопытки;
	Возврат ЗначениеВСтроку(СпНомераПартий);
КонецФункции

Функция СоздатьТзМаршрутСледования() Экспорт
	//Порядок колонок имеет значние при сохранинии в Файл => структура должна быть единообразна

	ТЗточкиМаршрута = СоздатьОбъект("ТаблицаЗначений");
	ТзТочкиМаршрута.НоваяКолонка("ВыбХС","Справочник.ВСД_ХозСубъект");
	ТзТочкиМаршрута.НоваяКолонка("ВыбПлощадка","Справочник.ВСД_Площадка",,,"Площадка",20);
	ТзТочкиМаршрута.НоваяКолонка("Страна","Справочник.ВСД_Страна");
	ТзТочкиМаршрута.НоваяКолонка("Регион","Справочник.ВСД_Регион");
	ТзТочкиМаршрута.НоваяКолонка("Район","Справочник.ВСД_Район");
	ТзТочкиМаршрута.НоваяКолонка("Город","Справочник.ВСД_Город");
	ТзТочкиМаршрута.НоваяКолонка("НасПункт",);
	ТзТочкиМаршрута.НоваяКолонка("Улица","Справочник.ВСД_Улица");
	ТзТочкиМаршрута.НоваяКолонка("Дом","Строка");
	ТзТочкиМаршрута.НоваяКолонка("Строение","Строка");
	ТзТочкиМаршрута.НоваяКолонка("Помещение","Строка");
	ТзТочкиМаршрута.НоваяКолонка("Перегрузка","Число",1,0,); //будем считать, что отмечаются только пункты перегрузки = true
	ТзТочкиМаршрута.НоваяКолонка("ТипТранспорта","Число",2,0,"Тип транспорта",5);
	ТзТочкиМаршрута.НоваяКолонка("НомерТС","Строка",50,,"Номер ТС",10);  // ЖД Что это за реквизит???
	ТзТочкиМаршрута.НоваяКолонка("номерАвто");
	ТзТочкиМаршрута.НоваяКолонка("номерКонтейнера");
	ТзТочкиМаршрута.НоваяКолонка("номерВагона");
	ТзТочкиМаршрута.НоваяКолонка("номерПрицепа");
	ТзТочкиМаршрута.НоваяКолонка("НазваниеСудна");
	ТзТочкиМаршрута.НоваяКолонка("НомерАвиарейса");
	ТзТочкиМаршрута.НоваяКолонка("UUID",); //ТзТочкиМаршрута.НоваяКолонка("UUID","Строка",36, ,"UUID",10);
	ТзТочкиМаршрута.НоваяКолонка("НомерТочки");//ТзТочкиМаршрута.НоваяКолонка("НомерТочки","Число",3,0,"Номер точки",5);
	ТзТочкиМаршрута.НоваяКолонка("Название");

	Возврат ТзТочкиМаршрута;
КонецФункции

Функция МаршрутСледования_из_XML(Routelist) Экспорт
    // Упрощенная схема - выбраны Площадки, а не ручная запись адреса
	тзТочкиМаршрута = СоздатьТзМаршрутСледования();

	Попытка
	    Для l1 = 0 По Routelist.length - 1 Цикл
			Узел = Routelist.item(l1);
			ТЗточкиМаршрута.НоваяСтрока();
            Попытка ТЗточкиМаршрута.UUID					 = 		Узел.selectSingleNode("bs:uuid").text; Исключение КонецПопытки;
            Попытка ТЗточкиМаршрута.НомерТочки				 =		Узел.selectSingleNode("vd:sqnId").text; Исключение КонецПопытки;
			Попытка Площадка_GUID = Узел.selectSingleNode("vd:enterprise").selectSingleNode("bs:guid").text; Исключение КонецПопытки;
			Попытка ТЗточкиМаршрута.ВыбПлощадка				 =  	ГМ.НайтиПлощадку( Площадка_GUID ); Исключение КонецПопытки;
			//Попытка ТЗточкиМаршрута.ПредприятиеНаименование	 =  	Узел.selectSingleNode("vd:enterprise").selectSingleNode("dt:name").text; Исключение КонецПопытки;
            Попытка ТЗточкиМаршрута.Перегрузка				 = 		?(Узел.selectSingleNode("vd:transshipment").text = "true",1,0); Исключение КонецПопытки;
            Попытка ТЗточкиМаршрута.ТипТранспорта			 = 		Число(Узел.selectSingleNode("vd:nextTransport").selectSingleNode("vd:transportType").text); Исключение КонецПопытки;

			Если ТЗточкиМаршрута.ТипТранспорта = 1 Тогда
				Попытка ТЗточкиМаршрута.номеравто 			= Узел.selectSingleNode("vd:nextTransport").selectSingleNode("vd:transportNumber").selectSingleNode("vd:vehicleNumber").text; Исключение КонецПопытки;
				Попытка ТЗточкиМаршрута.номерПрицепа 	= Узел.selectSingleNode("vd:nextTransport").selectSingleNode("vd:transportNumber").selectSingleNode("vd:trailerNumber").text; Исключение КонецПопытки;;
				Попытка ТЗточкиМаршрута.номерКонтейнера 	= Узел.selectSingleNode("vd:nextTransport").selectSingleNode("vd:transportNumber").selectSingleNode("vd:containerNumber").text; Исключение КонецПопытки;
			Иначеесли ТЗточкиМаршрута.ТипТранспорта = 2 Тогда
				Попытка ТЗточкиМаршрута.номерВагона 			= Узел.selectSingleNode("vd:nextTransport").selectSingleNode("vd:transportNumber").selectSingleNode("vd:wagonNumber").text; Исключение КонецПопытки;
			Иначеесли ТЗточкиМаршрута.ТипТранспорта = 3 Тогда
				Попытка ТЗточкиМаршрута.НомерАвиарейса 			= Узел.selectSingleNode("vd:nextTransport").selectSingleNode("vd:transportNumber").selectSingleNode("vd:flightNumber").text; Исключение КонецПопытки;
			Иначеесли (ТЗточкиМаршрута.ТипТранспорта = 4) ИЛИ (ТЗточкиМаршрута.ТипТранспорта = 5) ИЛИ (ТЗточкиМаршрута.ТипТранспорта = 6) Тогда
				Попытка ТЗточкиМаршрута.НазваниеСудна 			= Узел.selectSingleNode("vd:nextTransport").selectSingleNode("vd:transportNumber").selectSingleNode("vd:shipName").text; Исключение КонецПопытки;
//			Иначеесли ТЗВСД.ТипТС = "7" Тогда
//					ТЗВСД.номеравто = transportInfo.selectSingleNode("*[local-name()='transportNumber'] ").selectSingleNode("*[local-name()='shipName']").text;

			КонецЕсли;
            ТЗточкиМаршрута.НомерТС = СокрЛП(ТЗточкиМаршрута.номеравто) + СокрЛП(ТЗточкиМаршрута.номерВагона) + СокрЛП(ТЗточкиМаршрута.НомерАвиарейса)+ СокрЛП(ТЗточкиМаршрута.НазваниеСудна);; //Что-то тут не так
		КонецЦикла;
	Исключение КонецПопытки;

	ТЗточкиМаршрута.Сортировать("+НомерТочки");

	Если ТЗточкиМаршрута.КоличествоСтрок()>0 Тогда
	    Возврат тзТочкиМаршрута;
	Иначе
		Возврат "";
	КонецЕсли;

КонецФункции


Функция Производители_из_XML(ProducerList) Экспорт
	//Информация о предприятиях-производителях продукции.
   	//Если предприятие было задано из справочника, то вернутся uuid И guid
   	//Если предприятие было указано текстом, то вернется name

	ТзПроизводителей = СоздатьОбъект("ТаблицаЗначений");
	ТзПроизводителей.НоваяКолонка("Площадка","Справочник.ВСД_Площадка");
	ТзПроизводителей.НоваяКолонка("Роль","Строка",15);
	ТзПроизводителей.НоваяКолонка("Имя","Строка",50);
	Попытка
	    Для l1 = 0 По ProducerList.length - 1 Цикл
			ГУидПроизводителя = ProducerList.item(l1).selectSingleNode("dt:enterprise").selectSingleNode("bs:guid").text;
			ТзПроизводителей.НоваяСтрока();
			ТзПроизводителей.Площадка = ГМ.НайтиПлощадку(ГУидПроизводителя);
			Попытка ТзПроизводителей.Роль =  ProducerList.item(l1).selectSingleNode("dt:role").text; Исключение ТзПроизводителей.Роль = "PRODUCER"; КонецПопытки;
			Попытка ТзПроизводителей.Имя =  ProducerList.item(l1).selectSingleNode("dt:name").text; Исключение ТзПроизводителей.Имя = ""; КонецПопытки;

		КонецЦикла;
	Исключение 	
	КонецПопытки;
	//Возврат  ЗначениеВСтроку(ТзПроизводителей);
	Возврат  ТзПроизводителей;
КонецФункции

Процедура Фасовка_из_XML(packaging,ФасовкаНаименование="",ФасовкаФормаУпаковки="",ФасовкаКоличество="",ФасовкаОбъем="",ФасовкаЕдиницаИзм="") Экспорт
	//<dt:packaging> Фасовка.
	// Фасовка м.б. указана По требованию сети, Параметры фасовки привязаны к ВД_ПродукцияЭлемент
		// 	Тип упаковки.
		//Попытка	ТипФасовки = packaging.selectSingleNode("dt:packagingType").text; Исключение ТипФасовки ="";	КонецПопытки;
		// 	Идентификатор версии типа упаковки.
		//Попытка ФасУИД =  packaging.selectSingleNode("dt:packagingType").selectSingleNode("bs:uuid").text; Исключение КонецПопытки;

	Попытка ФасовкаНаименование = packaging.selectSingleNode("dt:packagingType").selectSingleNode("dt:name").text; 	Исключение 	КонецПопытки;
	Попытка ФасовкаФормаУпаковки = ГМ.НайтиФормуУпаковки( packaging.selectSingleNode("dt:packagingType").selectSingleNode("bs:guid").text,ФасовкаНаименование );	Исключение 	КонецПопытки;
	//  	Уникальный идентификатор упаковки.
	//Попытка	ФасУН =  packaging.selectSingleNode("dt:packagingType").selectSingleNode("dt:globalID").text; Исключение КонецПопытки;
	//  	Количество единиц упаковки.
	Попытка	ФасовкаКоличество = Число(packaging.selectSingleNode("dt:quantity").text);	Исключение	КонецПопытки;
	// Объём единицы упаковки товара.
	Попытка	ФасовкаОбъем = Число(packaging.selectSingleNode("dt:volume").text); Исключение	КонецПопытки;
	// 	Глобальный идентификатор единицы измерения. , есть uuid И имя - <dt:name>
	Попытка	ФасовкаЕдиницаИзм = ГМ.НайтиЕдИзмерения(packaging.selectSingleNode("dt:unit").selectSingleNode("bs:guid").text);	Исключение 	КонецПопытки;
КонецПроцедуры

// =========== Общие функции ==================

//https://htmlweb.ru/html/symbols.php
Функция ЗаменитьСпецСимволы(Стр) Экспорт
    Рез = СокрЛП(Стр);
	Рез = СтрЗаменить(Рез,"&" ,"&amp;" );
	Рез = СтрЗаменить(Рез,"""","&quot;");
	Рез = СтрЗаменить(Рез,"<" ,"&lt;"  );
	Рез = СтрЗаменить(Рез,">" ,"&gt;"  );
	Рез = СтрЗаменить(Рез,"'" ,"&#039;");
	Рез = СтрЗаменить(Рез,"№" ,"&#8470;");
	//Рез = СтрЗаменить(Рез,"/" ,"//");
	Возврат Рез;
КонецФункции

// Поиск СПРАВОЧНИКА ВСД_Док По uuid
Функция НайтиВСДпоUUID( uuid, ВидДокумента="ВСД_Док", СоздаватьДокумент = 1 ) Экспорт

	Если ПустоеЗначение(UUID)=1 Тогда
		Возврат "";
	КонецЕсли;

	тз = СоздатьОбъект("ТаблицаЗначений");
	Если ГМ.ЭтоSQL=1 Тогда
		RS = СоздатьОбъект("ODBCRecordset");
		RS.УстБД1С();

		ТекстЗапроса = "
		|select
	    | ДокВСД.id [id $Справочник."+ВидДокумента+"]
	    |from $Справочник."+ВидДокумента+" as ДокВСД (NOLOCK)
	  	|where
	  	|	( $ДокВСД.UUID = :uuid)
	  	|";
		RS.УстановитьТекстовыйПараметр("uuid", uuid);
		Если ГМ.ПолучитьКонстанту("ВыводитьПодробнуюИнформацию")=1 Тогда  
			RS.Отладка(1);
		КонецЕсли;		
		тз = RS.ВыполнитьИнструкцию(ТекстЗапроса);
	Иначе
		Запрос = ГМ.базаДанных.НовыйЗапрос();
		ТекстЗапроса = "
		|select
	    | ДокВСД.id [id $Справочник."+ВидДокумента+"]
	    |from [Справочник."+ВидДокумента+"] as ДокВСД
	  	|where
	  	|	( ДокВСД.UUID = :uuid)
	  	|";
		Запрос.Подставлять("uuid", uuid);
		Если ГМ.ПолучитьКонстанту("ВыводитьПодробнуюИнформацию")=1 Тогда 
			Запрос.Отладка();
		КонецЕсли;		
		ТЗ = Запрос.ВыполнитьЗапрос(ТекстЗапроса);
	КонецЕсли;

	ВсдОбъект = СоздатьОбъект("Справочник."+ВидДокумента);
	Если ПустоеЗначение(тз)=1 Тогда
		Если  СоздаватьДокумент = 1 Тогда
			ВсдОбъект.Новый();
			ГМ.СообщитьИнфо("Создан новый "+ВидДокумента+" № "+ВсдОбъект.Код, ВсдОбъект);
		КонецЕсли;
	Иначе		
		ВсдОбъект.НайтиЭлемент( тз.ПолучитьЗначение(1,1) );
		ВсдОбъект.СнятьПометкуУдаления();
		Состояние("Найден "+ВсдОбъект.ТекущийЭлемент());
	КонецЕсли;

	Возврат ВсдОбъект;
КонецФункции

// Поиск документа По ууид
Функция НайтиДокументВСДпоUUID( uuid, ВидДокумента, СоздаватьДокумент = 1 ) Экспорт

	Если ПустоеЗначение(UUID)=1 Тогда
		Возврат "";
	КонецЕсли;

	тз = СоздатьОбъект("ТаблицаЗначений");		
	Если ГМ.ЭтоSQL=1 Тогда
		RS = СоздатьОбъект("ODBCRecordset");
		RS.УстБД1С();

		ТекстЗапроса = "
		|select
	    | ДокВСД.idDoc [id $Документ."+ВидДокумента+"]
	    |from $Документ."+ВидДокумента+" as ДокВСД (NOLOCK)
	  	|where
	  	|	( $ДокВСД.UUID = :uuid)
	  	|";
		RS.УстановитьТекстовыйПараметр("uuid", uuid);
		Если ГМ.ПолучитьКонстанту("ВыводитьПодробнуюИнформацию")=1 Тогда  
			RS.Отладка(1);
		КонецЕсли;		
		тз = RS.ВыполнитьИнструкцию(ТекстЗапроса);
	Иначе
		Запрос = ГМ.базаДанных.НовыйЗапрос();
		ТекстЗапроса = "
		|select
	    | ДокВСД.idDoc [id $Документ."+ВидДокумента+"]
	    |from [Документ."+ВидДокумента+"] as ДокВСД
	  	|where
	  	|	( ДокВСД.UUID = :uuid)
	  	|";
		Запрос.Подставлять("uuid", uuid);
		Если ГМ.ПолучитьКонстанту("ВыводитьПодробнуюИнформацию")=1 Тогда 
			Запрос.Отладка();
		КонецЕсли;		
		ТЗ = Запрос.ВыполнитьЗапрос(ТекстЗапроса);
	КонецЕсли;
	
	ВсдОбъект = СоздатьОбъект("Документ."+ВидДокумента);
	Если ПустоеЗначение(тз)=1 Тогда
		Если  СоздаватьДокумент = 1 Тогда
			ВсдОбъект.Новый();
			ГМ.СообщитьИнфо("Создан новый "+ВидДокумента+" № "+ВсдОбъект.НомерДок, ВсдОбъект);
		КонецЕсли;

	Иначе
		ВсдОбъект.НайтиДокумент(тз.ПолучитьЗначение(1,1));
		ВсдОбъект.СделатьНеПроведенным();

		Состояние("Найден "+ВсдОбъект.ТекущийДокумент());
	КонецЕсли;

	Возврат ВсдОбъект;
КонецФункции


//{================= Партии ============================

// найдем документ ВСД_АктуальныеПартии на основании докСсылка
Функция НайтиДокПартий(докСсылка)
	
	докОбъект = "";
		
	Если ГМ.ПолучитьКонстанту("КонтрольОстатковПоРегистру")=1 Тогда 
	
		Если ПустоеЗначение(докСсылка)=1 Тогда 		
			
			докОбъект = СоздатьОбъект("Документ.ВСД_АктуальныеПартии");
			докОбъект.Новый();
			докОбъект.ДатаДок = Текущаядата();
			докОбъект.Отправитель_ХозСубъект = ГМ.ПолучитьКонстанту("Отправитель_ХозСубъект");
			докОбъект.Отправитель_Площадка = ГМ.ПолучитьКонстанту("Отправитель_Площадка");
			докОбъект.applicationID = ГМ.Компонента.ApplicationId;
			//докОбъект.Статус = ;
			Попытка докОбъект.Автор = ГМ.ПолучитьАвтора(); Исключение КонецПопытки;
			Попытка докОбъект.Фирма = докСсылка.Фирма; Исключение КонецПопытки;
					
			докОбъект.ДокОснование = докСсылка;
			докОбъект.Записать();	
			
			Возврат докОбъект;
	
		ИначеЕсли (докСсылка.Вид() = "ВСД_АктуальныеПартии") Тогда 
			докОбъект = СоздатьОбъект("Документ");
			докОбъект.НайтиДокумент( докСсылка );			
			
			Возврат докОбъект;
			
		ИначеЕсли (докСсылка.Вид() = "ВСД2_Инвентаризация") ИЛИ 
				(докСсылка.Вид() = "ВСД2_Производство") ИЛИ
				(докСсылка.Вид() = "ВСД2_Транзакция") ИЛИ
				(докСсылка.Вид() = "ВСД2") ИЛИ
				(докСсылка.Вид() = "ВСД2_ОбъединениеПартий") Тогда 
			
			ВыбДок = "";
			Док = СоздатьОбъект("Документ");
			Док.ВыбратьПодчиненныеДокументы(докСсылка.ДатаДок, КонГода(докСсылка.ДатаДок), докСсылка);
			Пока Док.ПолучитьДокумент() = 1 Цикл
				Если Док.ПометкаУдаления() = 1 Тогда
					Продолжить;
				КонецЕсли;
				Если (Док.Вид()="ВСД_АктуальныеПартии") Тогда
					Док.СделатьНеПроведенным();
					Док.УдалитьСтроки();
					Док.Записать();
					ВыбДок = Док.ТекущийДокумент();
				КонецЕсли;
			КонецЦикла;
			
			Если ПустоеЗначение( ВыбДок )=1 Тогда 
			
				докОбъект = СоздатьОбъект("Документ.ВСД_АктуальныеПартии");
				докОбъект.Новый();
				докОбъект.ДатаДок = Текущаядата();
				докОбъект.Отправитель_ХозСубъект = ГМ.ПолучитьКонстанту("Отправитель_ХозСубъект");
				докОбъект.Отправитель_Площадка = ГМ.ПолучитьКонстанту("Отправитель_Площадка");
				докОбъект.applicationID = ГМ.Компонента.ApplicationId;
				//докОбъект.Статус = ;
				Попытка докОбъект.Автор = ГМ.ПолучитьАвтора(); Исключение КонецПопытки;						
				Попытка докОбъект.Фирма = докСсылка.Фирма; Исключение КонецПопытки;
						
				докОбъект.ДокОснование = докСсылка;
				докОбъект.Записать();	
			Иначе
				докОбъект = СоздатьОбъект("Документ");
				докОбъект.НайтиДокумент( ВыбДок );			
				
			КонецЕсли;
			
			Возврат докОбъект;
			
		КонецЕсли;
	КонецЕсли;
	
	Возврат докОбъект;

КонецФункции

Функция ПолучитьИмяФайла_УровниУпаковки(Объект) Экспорт
	//{Проверим на переопределение функции, Если переопределена вызовем из перекрывающего модуля
	ОбъектВыполнения = ПроверитьПереопределениеФункции("ПолучитьИмяФайла_УровниУпаковки");
	Если ПустоеЗначение(ОбъектВыполнения)=0 Тогда
		Возврат ОбъектВыполнения.ПолучитьИмяФайла_УровниУпаковки(Объект);
	КонецЕсли;
	//}

	Каталог = ГМ.ПолучитьКонстанту("КаталогМодуля")+"Levels\";
	Если ФС.СуществуетФайл(Каталог) = 0 Тогда
		Попытка
		    ФС.СоздатьКаталог(Каталог);
		Исключение
			Предупреждение("Не создан каталог Levels"); Возврат "";
		КонецПопытки;
	КонецЕсли;
    Если ТипЗначения(Объект) = 12 Тогда  //документ
   		Рез = Каталог+Объект.uuid+".mrk";
	Иначе
		Рез = Каталог+Объект.guid+".mrk";
    КонецЕсли;

	Возврат Рез;
КонецФункции

Функция ПолучитьИмяФайла_Партия_XML( Объект ) 
	
	Каталог = ГМ.ПолучитьКонстанту("КаталогМодуля")+"batch\";
	Если ФС.СуществуетФайл(Каталог) = 0 Тогда
		Попытка
		    ФС.СоздатьКаталог(Каталог);
		Исключение
			Предупреждение("Не создан каталог batch"); Возврат "";
		КонецПопытки;
	КонецЕсли;

	Рез = Каталог+"batch_"+Объект.guid+".xml";

	Возврат Рез;	
КонецФункции

Функция ЗаписатьПартию2( stockEntry ) 

	//стандартное поведение функции
		active = stockEntry.selectSingleNode("bs:active").text;
		last = stockEntry.selectSingleNode("bs:last").text;
		uuid = stockEntry.selectSingleNode("bs:uuid").text;
		guid = stockEntry.selectSingleNode("bs:guid").text;

		Партия = ГМ.НайтиПартиюПоGUID(guid);

		Попытка Партия.active = ?( active="true",1,0) Исключение	КонецПопытки;
		Попытка Партия.last = ?( last="true",1,0) Исключение	КонецПопытки;
				
		Если active="false" Тогда
			Сообщить("Изменения в партию "+Партия+" uuid ["+uuid+"] не внесены. Признак партии = не активна","i");
			Если Партия.Выбран()=1 Тогда 
				Партия.Записать();
			КонецЕсли;
			Партия.Удалить(0);
			Возврат Партия;
		КонецЕсли;

		st = stockEntry.selectSingleNode("bs:status").text;
		entryNumber = stockEntry.selectSingleNode("vd:entryNumber").text;
		updateDate = stockEntry.selectSingleNode("bs:updateDate").text;
		createDate = stockEntry.selectSingleNode("bs:createDate").text;

		ГМ.СообщитьИнфо(" ВСД_Партия от "+createDate+" # "+entryNumber, Партия.ТекущийЭлемент() );

		Партия.ВсдДата = ГМ.СтрокаВДату(createDate);
		Партия.ДатаИзменения = ГМ.СтрокаВДату( updateDate );
		Партия.Статус = st;
		Партия.GUID = guid;
		Партия.UUID = uuid;
		Партия.НомерЗаписи = entryNumber;


		batch = stockEntry.selectSingleNode("vd:batch");

		product 	= batch.selectSingleNode("vd:product").selectSingleNode("bs:guid").text;
		subProduct 	= batch.selectSingleNode("vd:subProduct").selectSingleNode("bs:guid").text;
		Попытка	productItem = batch.selectSingleNode("*[local-name()='productItem'] ");	Исключение	productItem = "";	КонецПопытки;

		Партия.Продукция 	= ГМ.НайтиПродукцию(product);
		Партия.ВидПродукции = ГМ.НайтиВидПродукции(subProduct);

		Партия.Продукция_Элемент = ГМ.Найти_Продукция_Элемент2(productItem, Партия.Продукция, Партия.ВидПродукции);
		Партия.Наименование = Партия.Продукция_Элемент.Наименование;
		Партия.НаименованиеПродукции = Партия.Продукция_Элемент.Наименование;

		volume 		= batch.selectSingleNode("vd:volume").text;
		Попытка	unitGuid	= batch.selectSingleNode("vd:unit").selectSingleNode("bs:guid").text;Исключение	unitGuid = "";	КонецПопытки;
		Попытка	unitUuid	= batch.selectSingleNode("vd:unit").selectSingleNode("bs:uuid").text;Исключение	unitUuid = ""; КонецПопытки;
		
		Партия.Количество 	= Число(volume);
		Партия.ЕдиницаИзмерения = ГМ.НайтиЕдИзмерения( unitGuid, unitUUID );;

		ДатаИзготовления1=""; ДатаИзготовления2="";	ДатаИзготовления="";
		Попытка dateOfProduction = batch.selectSingleNode("vd:dateOfProduction"); исключение КонецПопытки;
		ГМ.Дата_из_XML(dateOfProduction, ДатаИзготовления1, ДатаИзготовления2, ДатаИзготовления);
		Партия.ДатаИзготовления1 = ДатаИзготовления1;
		Партия.ДатаИзготовления2 = ДатаИзготовления2;
		Партия.ДатаИзготовления =  ДатаИзготовления;

		ДатаСрокГодности1=""; ДатаСрокГодности2=""; ДатаСрокГодности="";
		Попытка expiryDate = batch.selectSingleNode("vd:expiryDate"); исключение КонецПопытки;
		ГМ.Дата_из_XML(expiryDate, ДатаСрокГодности1, ДатаСрокГодности2, ДатаСрокГодности);
		Партия.ДатаСрокГодности = ДатаСрокГодности;
		Партия.ДатаСрокГодности1 = ДатаСрокГодности1;
		Партия.ДатаСрокГодности2 = ДатаСрокГодности2;
		
		Попытка
			НомераПартий = СписокНомеровПартий_из_XML(batch.selectNodes("vd:batchID"));
			ВремНомераПартий = ЗначениеизСтроки(НомераПартий);
			Если ВремНомераПартий.РазмерСписка() = 1 Тогда
				Партия.НомерПартии = ВремНомераПартий.ПолучитьЗначение(1);
			Иначе
				СписокСтрокойСразделителями = "";
				Для _нп =1 По ВремНомераПартий.РазмерСписка() Цикл
					СписокСтрокойСразделителями = СписокСтрокойСразделителями + ВремНомераПартий.ПолучитьЗначение(_нп)+";";
				КонецЦикла;
				Партия.НомерПартии = СписокСтрокойСразделителями;
			КонецЕсли;
		Исключение
			Ошибка = "Не удалось прочитать список № Партий для записи в справочник ";
			ГМ.СообщитьОбОшибке(Ошибка + ОписаниеОшибки(), Сам(), "НомерСтрокиМдуля(524)");;
		КонецПопытки;

	 //	Попытка	Скоропортящийся = batch.selectSingleNode("vd:perishable").text; Исключение	Скоропортящийся = ""; КонецПопытки;

		// <vd:origin> 	vd:BatchOrigin 	[0..1] 	Сведения о происхождении продукции.
		//		vd:productItem> 	dt:ProductItem 	[1..1] 	Наименование продукции в номенклатуре производителя.
		//			<bs:uuid> 	bs:UUID 	[0..1] 	Идентификатор версии номенклатуры продукции.
		//			<dt:globalID> 	dt:GTINType 	[0..1] 	Trade Identification Number (GTIN) - идентификационный номер продукции производителя. 	<dt:globalID>85672854</dt:globalID>
		//			<dt:name> 	bs:String255 	[1..1] 	Наименование продукции.
		//			<dt:code> 	bs:String255 	[0..1] 	Артикул (код) продукции в соответствии с внутренним кодификатором производителя.
		//		<vd:country> 	dt:Country 	[1..1] 	Страна происхождения партии продукции.
		// сведения о стране

		//Информация о предприятие-производителе продукции.
		Попытка 
			сзПроизводителей = Производители_из_XML(batch.selectSingleNode("vd:origin").selectNodes("vd:producer")); 
		   	Если сзПроизводителей.КоличествоСтрок()>0 Тогда
	   			Партия.Производитель_Площадка = сзПроизводителей.ПолучитьЗначение(1,1);
	   		КонецЕсли;
	   	Исключение КонецПопытки;

		//Попытка Некач = batch.selectSingleNode("vd:lowGradeCargo").text; Исключение	Некач = "";	КонецПопытки;
	//Список физической упаковки. vd:packageList
	// С уровнями
	
	// !!!!
		Попытка package = batch.selectSingleNode("vd:packageList").selectNodes("dt:package"); Исключение package = ""; КонецПопытки;
		УровниУпаковки = УровниУпаковки_из_XML( package );

		Если ПустоеЗначение(УровниУпаковки) = 0 Тогда
			ИмяФайла = ПолучитьИмяФайла_УровниУпаковки(Партия);
			ЗначениеВФайл(ИмяФайла,УровниУпаковки);
			Попытка Партия.ИмяФайлаУровниУпаковки = ИмяФайла Исключение 	КонецПопытки;
		Иначе
			Попытка Партия.ИмяФайлаУровниУпаковки = ""; Исключение 	КонецПопытки;
        КонецЕсли;


		//Попытка Партия.Получатель_ХозСубъект = ГМ.НайтиХСпоGUID( batch.selectSingleNode("vd:owner").selectSingleNode("bs:guid").text); Исключение	КонецПопытки;
		Партия.Получатель_ХозСубъект = ГМ.СписокКонстант.Получить("Отправитель_ХозСубъект");
		Партия.Получатель_Площадка = ГМ.СписокКонстант.Получить("Отправитель_Площадка");

		Если ПустоеЗначение(Партия.Автор)=1 Тогда
			Партия.Автор = ГМ.ПолучитьАвтора();
		КонецЕсли;

		Попытка Партия.Филиал = Партия.Автор.Филиал; Исключение КонецПопытки;

		Если Партия.Количество=0 Тогда

			Если ( ПустоеЗначение( Партия.ТекущийЭлемент() ) =0 ) Тогда
				ГМ.СообщитьИнфо("Партия guid ["+guid+"] uuid ["+uuid+"] Количество = 0. Партия удалена. ", Партия.ТекущийЭлемент());
				Партия.Удалить(0);
			Иначе
				Сообщить("Партия guid ["+guid+"] uuid ["+uuid+"] Количество = 0. Партия пропущена. ");
				Возврат "";
			КонецЕсли;
		КонецЕсли;

        // "105" Запись была создана после аннулирования ВСД ИЛИ транзакции, ссылающихся на запись журнала, 
        // помеченную для переноса в архив ИЛИ перенесённую в архив. 
        //{[+]serpent, 14.08.2019
        Если СокрЛП(Партия.Статус) = "105" Тогда
            Сообщить("Партия guid ["+guid+"] uuid ["+uuid+"]  помечена для переноса в архив И не может быть использована. Партия удалена. ","!");
            Партия.Удалить(0);
        КонецЕсли;
        //}[+]_
		
		Попытка
			ВСД_UUID = stockEntry.selectSingleNode("vd:vetDocument").selectSingleNode("bs:uuid").text;
			ДокВсдОбъект = НайтиВСДпоUUID( ВСД_UUID, "ВСД_Док", 0 );
			Партия.документВСД = ДокВсдОбъект.ТекущийЭлемент();
		Исключение КонецПопытки;
		
		Партия.ИмяФайлаXML = ПолучитьИмяФайла_Партия_XML( Партия.ТекущийЭлемент() );	
    	objDom=СоздатьОбъект("MSXML2.DOMDocument.6.0");
		objDom.appendChild( stockEntry );
		objDom.save( Партия.ИмяФайлаXML );			
		
		Партия.Записать();		
		
		Возврат Партия.ТекущийЭлемент();
КонецФункции // ЗаписатьПартию2


Функция ЗагрузитьXML_Партии2(имяФайла, докОснование="", Смещение = 0, НачПериода = "", КонПериода = "" ) Экспорт

	Ответ = "";
	
	//стандартное поведение функции
    Сообщить("Загрузка XML-файла: "+ИмяФайла);

    objDom=СоздатьОбъект("MSXML2.DOMDocument.6.0");
    objDom.load(ИмяФайла);

	Если ГМ.НайтиОшибки()>0 Тогда
		Возврат "";
	КонецЕсли;

    objDom.setProperty("SelectionNamespaces", "
		|xmlns:soap='http://schemas.xmlsoap.org/soap/envelope/'
		|xmlns:bs='http://api.vetrf.ru/schema/cdm/base'
		|xmlns:merc='http://api.vetrf.ru/schema/cdm/mercury/g2b/applications/v2'
		|xmlns:dt='http://api.vetrf.ru/schema/cdm/dictionary/v2'
		|xmlns:vd='http://api.vetrf.ru/schema/cdm/mercury/vet-document/v2'
		|xmlns:default='http://www.w3.org/2001/XMLSchema'
		|xmlns:my='http://api.vetrf.ru/schema/cdm/application'
		|" );

    stockEntryList = objDom.selectNodes("//*[local-name() = 'stockEntry']") ;
	//stockEntryList = objDom.selectNodes("//merc:stockEntry") ;

	ГМ.СообщитьИнфо("Получено "+stockEntryList.length+" партий","");
	// ЖД посмотрим, сколько пришло партий
	Попытка ПришлоПартий = Число(stockEntryList.length); исключение ПришлоПартий = 1; КонецПопытки;
	
	Если stockEntryList.length>0 Тогда 
		докПартии = НайтиДокПартий( докОснование );			
	КонецЕсли;
		
    Для i1 = 0 По stockEntryList.length - 1 Цикл
		
        stockEntry=stockEntryList.item(i1);

		Партия = ЗаписатьПартию2( stockEntry );

		Попытка			
			Если (Партия<>"") Тогда
				
				Если ( Партия.active=1 ) Тогда 					
					докПартии.НоваяСтрока();
					докПартии.Партия = Партия.ТекущийЭлемент();
					докПартии.Количество = Партия.Количество;
				КонецЕсли;			
				
				Ответ = Партия;
				
			КонецЕсли;
		Исключение
		КонецПопытки;		
		
	КонецЦикла;
	
	//Если ПустоеЗначение(докПартии)=0 Тогда
	Попытка		
		докПартии.Записать();
		докПартии.Провести();
	Исключение
	КонецПопытки;
		
	//КонецЕсли;
	
	//Рекурсивный вызов
	Если ПришлоПартий = 1000 Тогда
		ГМ.УдалитьXML( имяФайла ); //На случай рекурсивного вызова
		//ПолучитьПартии2(ГМ.СписокКонстант.Получить("Отправитель_Площадка"), Смещение+1000, 0, НачПериода, КонПериода);
		
		спПараметры = СоздатьОбъект("СписокЗначений");	
		спПараметры.Установить("Отправитель_Площадка", ГМ.ПолучитьКонстанту("Отправитель_Площадка"));
		спПараметры.Установить("Отправитель_ХозСубъект", ГМ.ПолучитьКонстанту("Отправитель_ХозСубъект"));
		спПараметры.Установить("Смещение", Смещение+1000);
		спПараметры.Установить("УдалятьПартии", 0 );		
		спПараметры.Установить("ПартииНачало", НачПериода);
		спПараметры.Установить("ПартииОкончание", КонПериода);
		ПолучитьПартии2( спПараметры );
	КонецЕсли;

	//Возвращает последнюю созданную партию = для гашения 1 запрос/документ = 1 партия
	Возврат Ответ;
	
КонецФункции


Функция Партии_ПоGUID_Запрос( ВыбПартия )
	
	Запрос = "
	|<SOAP-ENV:Envelope 
	|xmlns:dt='http://api.vetrf.ru/schema/cdm/dictionary/v2' 
	|xmlns:bs='http://api.vetrf.ru/schema/cdm/base' 
	|xmlns:merc='http://api.vetrf.ru/schema/cdm/mercury/g2b/applications/v2' 
	|xmlns:apldef='http://api.vetrf.ru/schema/cdm/application/ws-definitions' 
	|xmlns:apl='http://api.vetrf.ru/schema/cdm/application' 
	|xmlns:vd='http://api.vetrf.ru/schema/cdm/mercury/vet-document/v2' 
	|xmlns:SOAP-ENV='http://schemas.xmlsoap.org/soap/envelope/'>
	|<SOAP-ENV:Header/>
	|<SOAP-ENV:Body>
	|<apldef:submitApplicationRequest>	
	|<apldef:apiKey>"+ ГМ.ПолучитьКонстанту("param_api_key") +"</apldef:apiKey>
	|<apl:application>
	|<apl:serviceId>mercury-g2b.service:2.0</apl:serviceId>
	|<apl:issuerId>"+ ГМ.ПолучитьКонстанту("param_issuer_id") +"</apl:issuerId>
	|<apl:issueDate>" + ГМ.Преобразовать_Дата_в_Строка(ТекущаяДата(), "T00:00:00") + "</apl:issueDate>	
	|<apl:data>
	|<merc:getStockEntryByGuidRequest>
	|<merc:localTransactionId>[GUID]</merc:localTransactionId>
	|<merc:initiator>
	|<vd:login>"+ ГМ.ПолучитьКонстанту("param_intiator_login") +"</vd:login>
	|</merc:initiator>
	|<bs:guid>"+ ВыбПартия.guid +"</bs:guid>
	|<dt:enterpriseGuid>"+ВыбПартия.Получатель_Площадка.GUID+"</dt:enterpriseGuid>
	|</merc:getStockEntryByGuidRequest>
	|</apl:data>
	|</apl:application>
	|</apldef:submitApplicationRequest>
	|</SOAP-ENV:Body>
	|</SOAP-ENV:Envelope>
	|";	
	Возврат Запрос;
КонецФункции

Функция Партии_ПолучитьПоGUID( ВыбПартия ) Экспорт
	t1 = ГМ.Старт();	
	
	Сообщить("Отправляем запрос партий По GUID "+ВыбПартия);
	ЗапросXML = Партии_ПоGUID_Запрос( ВыбПартия );	
	Service = "platform/services/2.0/ApplicationManagementService";
	Action = "submitApplicationRequest";	
	
	метод = ГМ.Компонента.GetMethod();
    метод.Service = Service;
    метод.Action = Action;

	appID = ГМ.ОтправитьЗапросSoap(ЗапросXML, метод);

	ГМ.УдалитьXML( ГМ.Компонента.LogFilename);
	
	Если ПустоеЗначение(appID)=0 Тогда
		Результат = "";
		Для А=1 По 10 Цикл
			Если ((Результат="IN_PROCESS") ИЛИ (ПустоеЗначение(Результат)=1)) Тогда

				ГМ.Пауза( );

				Сообщить("Отправляем запрос на получение ответа [партии По GUID]" );
				Результат = ГМ.ПолучитьРезультатСервер( appID );
				Сообщить("Результат = "+Результат);

				ЗагрузитьXML_Партии2( ГМ.Компонента.LogFilename );

				ГМ.УдалитьXML( ГМ.Компонента.LogFilename );

			КонецЕсли;
		КонецЦикла;

	КонецЕсли;

	ГМ.Финиш(t1, Сам(), "ПолучитьПартии", );
	
	Возврат appID;
КонецФункции
//}

//{================= Загрузить ВСД ======================

//создает струутуру ТаблицыЗначений для загрузки списка ВСД
Функция СтруктураТзВСД2() Экспорт
	
	//{Проверим на переопределение функции, Если переопределена вызовем из перекрывающего модуля
	ОбъектВыполнения = ПроверитьПереопределениеФункции("СтруктураТзВСД2");
	Если ПустоеЗначение(ОбъектВыполнения)=0 Тогда
		Возврат ОбъектВыполнения.СтруктураТзВСД2();
	КонецЕсли;
	//}
	
	тзВСД = СоздатьОбъект("ТаблицаЗначений");
	//тзВСД.НоваяКолонка("сЦвет",,,,,,);
	тзВСД.НоваяКолонка("Пометка",,,,"V",5,);
	//тзВСД.НоваяКолонка("ВладелецХС",,,,"Владелец ХозСубъект",20,);  //1С Элемент справочника. owner
	тзВСД.НоваяКолонка("Отправитель_ХозСубъект",,,,"Отправитель ХозСубъект",20,);  //1С Элемент справочника. owner
	тзВСД.НоваяКолонка("НаименованиеПродукции","Строка",,,"Продукция");
	тзВСД.НоваяКолонка("Количество","Число",15,3,"Объем", 10);
	тзВСД.НоваяКолонка("КоличествоПринять","Число",15,3,"Принять", 10);
	тзВСД.НоваяКолонка("КоличествоВозврат","Число",15,3,"Вернуть", 5);
	
	тзВСД.НоваяКолонка("Продукция_Элемент",,,,"Продукция_Элемент",10);
	тзВСД.НоваяКолонка("Номенклатура",,,,"Номенклатура",10);

	//тзВСД.НоваяКолонка("ПлощадкаИзПЭ","Справочник.ВСД_Площадка",,,"Площадка П/Э",10,);  //1С Элемент справочника
	//тзВСД.НоваяКолонка("ПлощадкаИзВСД","Справочник.ВСД_Площадка",,,"Площадка ВСД",10,);  //1С Элемент справочника
	
	тзВСД.НоваяКолонка("ТтнНомер",,,,"№ ТТН",10);
	тзВСД.НоваяКолонка("ВсдДата",,,,"Дата ВСД",10);
	
	тзВСД.НоваяКолонка("ДокВСД");
	тзВСД.НоваяКолонка("ВСД_ЭкземплярКласса");
	
	тзВСД.НоваяКолонка("ДокОснование","Документ",,,"Док основание",10);
	тзВСД.НоваяКолонка("uuid","Строка",,,"UUID документа");
	тзВСД.НоваяКолонка("Партия",);
	
	тзВСД.НоваяКолонка("ФормаВСД");
	тзВСД.НоваяКолонка("Статус");
	тзВСД.НоваяКолонка("СтатусВСД");
	тзВСД.НоваяКолонка("типВСД");	
	
	тзВСД.НоваяКолонка("ВсдСерия");
	тзВСД.НоваяКолонка("ВсдНомер");
	
	
	тзВСД.НоваяКолонка("GTIN",,,,"Штрихкод / GTIN",10);
	тзВСД.НоваяКолонка("Артикул",,,,"Артикул",10);
	тзВСД.НоваяКолонка("ФасовкаФормаУпаковки");
	тзВСД.НоваяКолонка("ФасовкаНаименование");
	тзВСД.НоваяКолонка("ФасовкаКоличество");
	тзВСД.НоваяКолонка("ФасовкаЕдиницаИзм");
	тзВСД.НоваяКолонка("ФасовкаОбъем");
	
	тзВСД.НоваяКолонка("ДатаДок");
	тзВСД.НоваяКолонка("Отправитель_ХозСубъект");
	тзВСД.НоваяКолонка("Отправитель_Площадка");
	тзВСД.НоваяКолонка("Получатель_ХозСубъект");
	тзВСД.НоваяКолонка("Получатель_Площадка");
	тзВСД.НоваяКолонка("Производитель_Площадка");	
	тзВСД.НоваяКолонка("Продукция");
	тзВСД.НоваяКолонка("ВидПродукции");
	
	тзВСД.НоваяКолонка("ЕдиницаИзмерения");
	тзВСД.НоваяКолонка("ДатаИзготовления1",,,,"ДатаИзготовления1",10);
	тзВСД.НоваяКолонка("ДатаИзготовления");
	тзВСД.НоваяКолонка("ДатаИзготовления2",,,,"ДатаИзготовления2",10);
	тзВСД.НоваяКолонка("ДатаСрокГодности1",,,,"ДатаСрокГодности1",10);
	тзВСД.НоваяКолонка("ДатаСрокГодности");
	тзВСД.НоваяКолонка("ДатаСрокГодности2",,,,"ДатаСрокГодности2",10);
	тзВСД.НоваяКолонка("НомерПартии",,,,"№ партии", 10);
	тзВСД.НоваяКолонка("КодТНВЭД",,,,"Код ТНВЭД", 10);
	тзВСД.НоваяКолонка("ПроизводителиСписок");
	тзВСД.НоваяКолонка("УровниУпаковки");
	тзВСД.НоваяКолонка("ТипТС");
	тзВСД.НоваяКолонка("номеравто");
	тзВСД.НоваяКолонка("номерПолуприцепа");
	тзВСД.НоваяКолонка("номерКонтейнера");
	тзВСД.НоваяКолонка("термическоесостояние");
	//тзВСД.НоваяКолонка("ВСДместность");
	//тзВСД.НоваяКолонка("особыеотметки");
	тзВСД.НоваяКолонка("ТтнТип","Число",1,0);
	тзВСД.НоваяКолонка("ТтнСерия");
	тзВСД.НоваяКолонка("ТтнДата","Дата");
	тзВСД.НоваяКолонка("РезультатыИсследований");
	тзВСД.НоваяКолонка("cargoInspected");
	тзВСД.НоваяКолонка("Перевозчик_ХозСубъект");
	тзВСД.НоваяКолонка("Скоропортящийся");
	тзВСД.НоваяКолонка("Некачественный");
	тзВСД.НоваяКолонка("Страна_GUID");
	тзВСД.НоваяКолонка("Фирма");
	тзВСД.НоваяКолонка("ВидВСД");
	тзВСД.НоваяКолонка("applicationID");
	тзВСД.НоваяКолонка("Комментарий");
	тзВСД.НоваяКолонка("СвязанныеДокументы");
	тзВСД.НоваяКолонка("МаршрутСледования");
	тзВСД.НоваяКолонка("Цель");	
	
	Возврат тзВСД;
КонецФункции

Функция ПолучитьИмяФайла_ВСД_XML(Док) Экспорт

	Каталог = ГМ.ПолучитьКонстанту("КаталогМодуля")+"vd\";
	Если ФС.СуществуетФайл(Каталог) = 0 Тогда
		Попытка
		    ФС.СоздатьКаталог(Каталог);
		Исключение
			Предупреждение("Не создан каталог vd"); Возврат "";
		КонецПопытки;
	КонецЕсли;

	Рез = Каталог+"vd_"+СокрЛП(Док.UUID)+".xml";

	Возврат Рез;
КонецФункции

//Функция ЗагрузитьXML_ВСД2(имяФайла, докОснование="",докОбъект = "" ) Экспорт
Функция ЗагрузитьXML_ВСД2(имяФайла, докОснование="", СохранитьДокументВСД=0, Префикс="нет") Экспорт
	// Тестирование
	// Параметр докОбъект это ТаблицаЗначений, в которую загрузим ВСД, вместо создания документов в БД.
	
	//стандартное поведение функции
    objDom=СоздатьОбъект("MSXML2.DOMDocument.6.0");
    objDom.load(ИмяФайла);

	Если ГМ.НайтиОшибки(докОснование)>0 Тогда
		Возврат "";
	КонецЕсли;


    objDom.setProperty("SelectionNamespaces", "
		|xmlns:bs='http://api.vetrf.ru/schema/cdm/base'
		|xmlns:merc='http://api.vetrf.ru/schema/cdm/mercury/g2b/applications/v2'
		|xmlns:dt='http://api.vetrf.ru/schema/cdm/dictionary/v2'
		|xmlns:vd='http://api.vetrf.ru/schema/cdm/mercury/vet-document/v2'
		|xmlns:default='http://www.w3.org/2001/XMLSchema'
		|xmlns:my='http://api.vetrf.ru/schema/cdm/application'
		|xmlns:soap='http://schemas.xmlsoap.org/soap/envelope/'
		|" );

	тзРезультат = СтруктураТзВСД2();
		
	applicationId = objDom.selectSingleNode("//*[local-name() = 'applicationId']").text ;

	Если Префикс="нет" Тогда 		
	
		vetDocumentList = objDom.selectNodes("//*[local-name() = 'vetDocument']") ;
		
	Иначе
		
		// в StockEntry добавили реквизит vetDocument, поэтому надо искать с префиксом
		//vetDocumentList = objDom.selectNodes("//merc:vetDocument") ;
		vetDocumentList = objDom.selectNodes("//"+Префикс+":vetDocument") ;
		
	КонецЕсли;
	
	    Для i1 = 0 По vetDocumentList.length - 1 Цикл
	        vetDocument=vetDocumentList.item(i1);
	
			// ищем Сведения об измененных ВСД.
			Попытка
				forma 	 = vetDocument.selectSingleNode("vd:vetDForm").text;
			Исключение
				Продолжить;//Это привязанный к партии ВСД
			КонецПопытки;
			//uuid = vetDocument.selectSingleNode("*[local-name()='uuid']").text;
	
			тзРезультат.НоваяСтрока();
			тзРезультат.Пометка=1;
	
			ВСД_Класс = СоздатьОбъект("ВСД_vetDocument");
			тзРезультат.ДокВСД = ВСД_Класс.Заполнить( vetDocument, докОснование );
			тзРезультат.ДокВСД = ВСД_Класс.ВСД_Док;
			тзРезультат.ВСД_ЭкземплярКласса = ВСД_Класс;
			
			//тзРезультат.UUID = uuid;	
			
		КонецЦикла;

	Возврат тзРезультат;	
	
КонецФункции


//Функция ПолучитьРезультат_ВСД_2(applicationID, докСсылка="", Смещение = 0,  ПартияНачПериода = "", ПартияКонПериода = "", Префикс="merc") Экспорт
Функция ПолучитьРезультат_ВСД_2( спПараметры, ИспользоватьПаузу = 1, Партия = "") Экспорт
	
	//спПараметры = СоздатьОбъект("СписокЗначений");	
	applicationID = спПараметры.Получить("applicationID");
	докСсылка = спПараметры.Получить("докСсылка");
	Смещение = спПараметры.Получить("Смещение");
	Если ПустоеЗначение(Смещение)=1 Тогда 
		Смещение = 0;
	КонецЕсли;
	ПартияНачПериода = спПараметры.Получить("ПартияНачПериода");
	ПартияКонПериода = спПараметры.Получить("ПартияКонПериода");
	Префикс = спПараметры.Получить("Префикс");
	Если ПустоеЗначение(Префикс)=1 Тогда 
		Префикс = "merc";
	КонецЕсли;
	
	//Реквизит Смещение добавлен для возможности рекурсивного вызова ПолучитьПартии2
	Если ПустоеЗначение(applicationID)=1 Тогда
		Сообщить("Пустая ссылка applicationID","!");
		Возврат "Пустая ссылка applicationID";
	КонецЕсли;

	Если ИспользоватьПаузу=1 Тогда 
		ГМ.Пауза( );
	КонецЕсли;

	ГМ.СообщитьИнфо("Получаем ответ для ["+докСсылка+"] " + applicationID, докСсылка);
	Результат = ГМ.ПолучитьРезультатСервер( applicationID );
    Файл_Ответа_XML = ГМ.Компонента.LogFilenameResponse;
	Если ПустоеЗначение(докСсылка)=0 Тогда
		ГМ.СохранитьСтатусЗапроса(докСсылка);
		
		тзВСД = ЗагрузитьXML_ВСД2( Файл_Ответа_XML , докСсылка, 1, Префикс );
				
		Попытка 
			СтатусВСД = тзВСД.ПолучитьЗначение(1,"ДокВСД").СтатусВСД; 
		Исключение 
			СтатусВСД=""; 
		КонецПопытки;
			
		ГМ.СохранитьОтветВДокументе(докСсылка, СтатусВСД, applicationID);
	КонецЕсли;

    Партия = ЗагрузитьXML_Партии2( Файл_Ответа_XML, докСсылка, Смещение, ПартияНачПериода, ПартияКонПериода); 
	
	ГМ.УдалитьXML( Файл_Ответа_XML);

	Возврат Результат;
	
КонецФункции

Функция ЗагрузитьВСДпоUUID_ЗапросXML( док )
	
	Запрос = "
	|<SOAP-ENV:Envelope xmlns:dt='http://api.vetrf.ru/schema/cdm/dictionary/v2' 
	| xmlns:bs='http://api.vetrf.ru/schema/cdm/base' 
	| xmlns:merc='http://api.vetrf.ru/schema/cdm/mercury/g2b/applications/v2' 
	| xmlns:apldef='http://api.vetrf.ru/schema/cdm/application/ws-definitions' 
	| xmlns:apl='http://api.vetrf.ru/schema/cdm/application' 
	| xmlns:vd='http://api.vetrf.ru/schema/cdm/mercury/vet-document/v2' 
	| xmlns:SOAP-ENV='http://schemas.xmlsoap.org/soap/envelope/'>
	|<SOAP-ENV:Header/>
	|<SOAP-ENV:Body>
	|<apldef:submitApplicationRequest>
	|<apldef:apiKey>"+ СокрЛП( ГМ.СписокКонстант.Получить("param_api_key") ) +"</apldef:apiKey>
	|<apl:application>
	|<apl:serviceId>mercury-g2b.service:2.0</apl:serviceId>
	|<apl:issuerId>"+ СокрЛП( ГМ.СписокКонстант.Получить("param_issuer_id") ) +"</apl:issuerId>
	|<apl:issueDate>" + ГМ.Преобразовать_Дата_в_Строка(ТекущаяДата(), "T00:00:00") + "</apl:issueDate>
	|<apl:data>
	|<merc:getVetDocumentByUuidRequest>
	|<merc:localTransactionId>[GUID]</merc:localTransactionId>
	|<merc:initiator>
	|<vd:login>"+ СокрЛП( ГМ.СписокКонстант.Получить("param_intiator_login") ) +"</vd:login>
	|</merc:initiator>
	|<bs:uuid>"+ СокрЛП(док.UUID) +"</bs:uuid>
	|<dt:enterpriseGuid>"+ СокрЛП(Док.Отправитель_Площадка.GUID) +"</dt:enterpriseGuid>
	|</merc:getVetDocumentByUuidRequest>
	|</apl:data>
	|</apl:application>
	|</apldef:submitApplicationRequest>
	|</SOAP-ENV:Body>
	|</SOAP-ENV:Envelope>
	|";
	
	Возврат Запрос;
	
КонецФункции

Функция ЗагрузитьВСДпоUUID( докОснование ) Экспорт
	
	//тзДок = СоздатьОбъект("ТаблицаЗначений");
	тзДок = ГМ.Выбрать_ВСД( докОснование );
	
	тзДок.ВыбратьСтроки();
	Пока тзДок.ПолучитьСтроку() = 1 Цикл
				
		ЗапросXML = ЗагрузитьВСДпоUUID_ЗапросXML( тзДок.док );
	
		метод = ГМ.Компонента.GetMethod();
		метод.Service = "platform/services/2.0/ApplicationManagementService";
	    метод.Action = "submitApplicationRequest";
	
		appID = ГМ.ОтправитьЗапросSoap(ЗапросXML, метод);
	
		ГМ.СохранитьЗапросВДокументе( тзДок.док, appID );
		
		ГМ.УдалитьXML( ГМ.Компонента.LogFilename);
	
	    Результат = "";
		Если ПустоеЗначение(appID)=0 Тогда
			спПараметры = СоздатьОбъект("СписокЗначений");	
			спПараметры.Установить("applicationID", appID);
			спПараметры.Установить("докСсылка", тзДок.док);
			спПараметры.Установить("Смещение", 0);
			спПараметры.Установить("Префикс", "нет");			
	
			//Результат = ПолучитьРезультат_ВСД_2( appID, тзДок.док, 0, , ,  "нет" );
			Результат = ПолучитьРезультат_ВСД_2( спПараметры );
			
			ГМ.СообщитьИнфо("Результат = "+Результат, тзДок.док);
			Для А=1 По 10 Цикл
				Если (Результат="IN_PROCESS") Тогда
					Результат = ПолучитьРезультат_ВСД_2( спПараметры );
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	
		ГМ.УдалитьXML( ГМ.Компонента.LogFilename);

	КонецЦикла;
	Возврат Результат;
	
КонецФункции

//}

//{====================  Получить Партии  ===================================

Функция ПолучитьПартии2_ТекстЗапросаXML(Отправитель_Площадка, Смещение=0, НачПериода = "", КонПериода = "") Экспорт
	//{Проверим на переопределение функции, Если переопределена вызовем из перекрывающего модуля
	ОбъектВыполнения = ПроверитьПереопределениеФункции("ПолучитьПартии2_ТекстЗапросаXML");
	Если ПустоеЗначение(ОбъектВыполнения)=0 Тогда
		Возврат ОбъектВыполнения.ПолучитьПартии2_ТекстЗапросаXML(Отправитель_Площадка, Смещение, НачПериода, КонПериода);
	КонецЕсли;
	//}

	//стандартное поведение функции
	Если ПустоеЗначение(Отправитель_Площадка.GUID)=1 Тогда
		Сообщить("В выбранной Площадке пустой GUID");
		Возврат "REJECTED";
	КонецЕсли;

	НеНулевые = ГМ.ПолучитьКонстанту("ЗагружатьТолькоНеПустыеПартии");
	
	//выражение [GUID] будет заменено на новый Guid в формате: d82d83f7-944b-45e9-88dc-67892eae1e3e
	Запрос = "
	|<SOAP-ENV:Envelope
	|xmlns:dt='http://api.vetrf.ru/schema/cdm/dictionary/v2'
	|xmlns:bs='http://api.vetrf.ru/schema/cdm/base'
	|xmlns:merc='http://api.vetrf.ru/schema/cdm/mercury/g2b/applications/v2'
	|xmlns:apldef='http://api.vetrf.ru/schema/cdm/application/ws-definitions'
	|xmlns:apl='http://api.vetrf.ru/schema/cdm/application'
	|xmlns:vd='http://api.vetrf.ru/schema/cdm/mercury/vet-document/v2'
	|xmlns:SOAP-ENV='http://schemas.xmlsoap.org/soap/envelope/'>
	|<SOAP-ENV:Header/>
	|<SOAP-ENV:Body>
	|<apldef:submitApplicationRequest>
	|<apldef:apiKey>"+ГМ.СписокКонстант.Получить("param_api_key")+"</apldef:apiKey>
	|<apl:application>
	//метод 2.1 пока не работает в продуктиве
	|<apl:serviceId>mercury-g2b.service:2.1</apl:serviceId>
	//метод 2.0
	//|<apl:serviceId>mercury-g2b.service:2.0</apl:serviceId>
	|<apl:issuerId>"+ГМ.СписокКонстант.Получить("param_issuer_id")+"</apl:issuerId>
	|<apl:issueDate>"+ ГМ.Преобразовать_Дата_в_Строка(ТекущаяДата(), "T00:00:00") +"</apl:issueDate>
	|<apl:data>
	|<merc:getStockEntryListRequest>
	|<merc:localTransactionId>[GUID]</merc:localTransactionId>
	|<merc:initiator>
	|<vd:login>"+ГМ.СписокКонстант.Получить("param_intiator_login")+"</vd:login>
	|</merc:initiator>
	|<bs:listOptions>
	|<bs:count>1000</bs:count>
	|<bs:offset>" + Смещение + "</bs:offset>
	|</bs:listOptions>
	|<dt:enterpriseGuid>"+СокрЛП(Отправитель_Площадка.GUID)+"</dt:enterpriseGuid>";
	Если (ПустоеЗначение(НачПериода) = 0) ИЛИ (НеНулевые = 1) Тогда
		Запрос = Запрос +"
    	|<merc:searchPattern>";
		Если НеНулевые = 1 Тогда
			Запрос = Запрос +"
	    	|<vd:blankFilter>NOT_BLANK</vd:blankFilter>";
		КонецЕсли;
		Если ПустоеЗначение(НачПериода) = 0 Тогда
			Запрос = Запрос +"
	    	|<vd:receiptDateInterval>
			| <bs:beginDate>"+ ГМ.Преобразовать_Дата_в_Строка(НачПериода, "T00:00:00") +"</bs:beginDate>
			| <bs:endDate>"+ ГМ.Преобразовать_Дата_в_Строка(КонПериода, "T23:59:59") +"</bs:endDate>
		    |</vd:receiptDateInterval>";
		КонецЕсли;
		Запрос = Запрос +"
    	|</merc:searchPattern>";
	КонецЕсли;
	Запрос = Запрос +"
	|</merc:getStockEntryListRequest>
	|</apl:data>
	|</apl:application>
	|</apldef:submitApplicationRequest>
	|</SOAP-ENV:Body>
	|</SOAP-ENV:Envelope>
	|";

	Возврат Запрос;
КонецФункции

//Функция  ПолучитьПартии2(Отправитель_Площадка, Смещение=0, УдалятьПартии=1, ПартияНачПериода = "", ПартияКонПериода = "", НомерПовтора=0) Экспорт
Функция  ПолучитьПартии2( спПараметрыФункции ) Экспорт
	//{Проверим на переопределение функции, Если переопределена вызовем из перекрывающего модуля
	ОбъектВыполнения = ПроверитьПереопределениеФункции("ПолучитьПартии2");
	Если ПустоеЗначение(ОбъектВыполнения)=0 Тогда
		Возврат ОбъектВыполнения.ПолучитьПартии2( спПараметрыФункции );
	КонецЕсли;
	//}

	Отправитель_Площадка = спПараметрыФункции.Получить("Отправитель_Площадка");
	Отправитель_ХозСубъект = спПараметрыФункции.Получить("Отправитель_ХозСубъект");
	Смещение = спПараметрыФункции.Получить("Смещение");
	Если ПустоеЗначение(Смещение)=1 Тогда 
		Смещение = 0;
	КонецЕсли;
	ПартияНачПериода = спПараметрыФункции.Получить("ПартияНачПериода");
	ПартияКонПериода = спПараметрыФункции.Получить("ПартияКонПериода");
	УдалятьПартии = спПараметрыФункции.Получить("УдалятьПартии");
	Если СокрЛП(УдалятьПартии)="" Тогда 
		УдалятьПартии = 1;
	КонецЕсли;
	НомерПовтора = спПараметрыФункции.Получить("НомерПовтора");
	Если СокрЛП(НомерПовтора)="" Тогда 
		НомерПовтора = 0;
	КонецЕсли;
	
	
	Если НомерПовтора>ГМ.ПолучитьКонстанту("КоличествоПовторовОтправки") Тогда 
		ГМ.СообщитьИнфо("Превышено количество повторов отправки [ПолучитьПартии2]");
		Возврат "";
	КонецЕсли;
	
	t1 = ГМ.Старт();	
	
	//стандартное поведение функции
	Если ПустоеЗначение(Отправитель_Площадка.GUID)=1 Тогда
		Сообщить("В выбранной Площадке пустой GUID");
		Возврат "REJECTED";
	КонецЕсли;

	ЗапросXML = ПолучитьПартии2_ТекстЗапросаXML(Отправитель_Площадка, Смещение, ПартияНачПериода, ПартияКонПериода);

	метод = ГМ.Компонента.GetMethod();
	метод.Service = "platform/services/2.1/ApplicationManagementService";
    метод.Action = "submitApplicationRequest";

	appID = ГМ.ОтправитьЗапросSoap(ЗапросXML, метод);

	ГМ.УдалитьXML( ГМ.Компонента.LogFilename);

	Если ПустоеЗначение(appID)=0 Тогда

		Если ГМ.ПолучитьКонстанту("ЗагружатьТолькоНеПустыеПартии")=1 Тогда					
			Если УдалятьПартии=1 Тогда					
				// удаляем только если параметр["Загружать только активные партии (для Ветис.API версии >= 2.1)?"] = 1
				// При изменении партий в Меркурии (на сайте) - старые партии пропадают, появляются новые
				// поэтому в 1С остаются "остатки", которые нужно удалять
				Если ГМ.ПолучитьКонстанту("ОчищатьСправочникВСД_Партии")=1 Тогда
					Состояние("Удаление справочника ВСД_Партии");					
					СпрПартия = СоздатьОбъект("Справочник.ВСД_Партия");
					НачатьТранзакцию(); //Ускоримся
					СпрПартия.ВыбратьЭлементы();
					Пока СпрПартия.ПолучитьЭлемент() = 1 Цикл
						Если ПустоеЗначение(ПартияНачПериода) = 0 Тогда // не удаляем, т.к. запрашиваем По периоду
							Если (СпрПартия.ВсдДата < ПартияНачПериода) ИЛИ ( СпрПартия.ВсдДата > ПартияКонПериода) Тогда
								Продолжить;
							КонецЕсли;
						КонецЕсли;
						Если ( СпрПартия.Получатель_Площадка = Отправитель_Площадка ) И
							 (СпрПартия.Получатель_ХозСубъект = Отправитель_ХозСубъект) Тогда
							СпрПартия.Удалить(0);
						КонецЕсли;
					КонецЦикла;
					ЗафиксироватьТранзакцию();
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;

		Если ГМ.ПолучитьКонстанту("КонтрольОстатковПоРегистру")=1 Тогда 		
			ДокОбъект = СоздатьОбъект("Документ.ВСД_АктуальныеПартии");
			ДокОбъект.Новый();
			ДокОбъект.ДатаДок = ТекущаяДата();
			ДокОбъект.АвтоВремяТекущее();
			ДокОбъект.НачДата = ПартияНачПериода;
			ДокОбъект.КонДата = ПартияКонПериода;
			ДокОбъект.Отправитель_ХозСубъект = ГМ.СписокКонстант.Получить("Отправитель_ХозСубъект");
			ДокОбъект.Отправитель_Площадка = ГМ.СписокКонстант.Получить("Отправитель_Площадка");		
			ДокОбъект.Записать();
			докПартии = ДокОбъект.ТекущийДокумент();
		Иначе
			докПартии = "";
		КонецЕсли;
		
		спПараметры = СоздатьОбъект("СписокЗначений");	
		спПараметры.Установить("applicationID", appID);
		спПараметры.Установить("докСсылка", докПартии);
		спПараметры.Установить("Смещение", Смещение);
		спПараметры.Установить("ПартияНачПериода", ПартияНачПериода);
		спПараметры.Установить("ПартияКонПериода", ПартияКонПериода);

		Результат = ПолучитьРезультат_ВСД_2( спПараметры ); 
		Сообщить("Результат = "+Результат);
		Для А=1 По 10 Цикл
			Если (Результат="IN_PROCESS") ИЛИ (ПустоеЗначение(Результат)=1) Тогда
				
				Результат = ПолучитьРезультат_ВСД_2( спПараметры );
				
			ИначеЕсли (Результат="REJECTED") Тогда

				// Новый запрос
				//ПолучитьПартии2(Отправитель_Площадка, Смещение, УдалятьПартии, ПартияНачПериода, ПартияКонПериода, (НомерПовтора+1) );
				спПараметрыФункции.Установить("НомерПовтора", (НомерПовтора+1) );
				ПолучитьПартии2( спПараметрыФункции );				
				Прервать;
				
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;

	ГМ.Финиш(t1, Сам(), "ПолучитьПартии2");
	ГМ.СообщитьИнфо("Загрузка партий завершена");
	
	Возврат appID;

КонецФункции
//}

//{ =================== Список ВСД
//Функция ПолучитьСписокВСД2_ЗапросXML(Площадка, vetDocumentType, vetDocumentStatus, Смещение) Экспорт
Функция ПолучитьСписокВСД2_ЗапросXML( спУсловия ) Экспорт

	//спУсловия = СоздатьОбъект("СписокЗначений");
	Площадка = спУсловия.Получить("Площадка");
	Если ПустоеЗначение(Площадка)=1 Тогда
		Сообщить("В условиях не указана Площадка","!");
		Возврат "";
	КонецЕсли;
	vetDocumentType = спУсловия.Получить("vetDocumentType");
	Если ПустоеЗначение(vetDocumentType)=1 Тогда
		vetDocumentType = "INCOMING";
	КонецЕсли;
	vetDocumentStatus = спУсловия.Получить("vetDocumentStatus");
	Если ПустоеЗначение(vetDocumentStatus)=1 Тогда
		vetDocumentStatus = "CONFIRMED";
	КонецЕсли;
	Смещение = спУсловия.Получить("Смещение");
	Если ПустоеЗначение(Смещение)=1 Тогда
		Смещение = 0;
	КонецЕсли;

	НачДата = спУсловия.Получить("НачДата");
	Если ПустоеЗначение(НачДата)=1 Тогда
		НачДата = 0;
	КонецЕсли;
	КонДата = спУсловия.Получить("КонДата");
	Если ПустоеЗначение(КонДата)=1 Тогда
		КонДата = 0;
	КонецЕсли;

	Поставщик_ХозСубъект = спУсловия.Получить("Поставщик_ХозСубъект");
	Поставщик_Площадка = спУсловия.Получить("Поставщик_Площадка");

	НомерДок = спУсловия.Получить("НомерДок");
	ДатаДок = спУсловия.Получить("ДатаДок");

	//	DocumentType v2.0
	// http://help.vetrf.ru/wiki/DocumentType_v2.0
	//1	Товарно-транспортная накладная
	//5	Транспортная накладная
	//6	ТОРГ-12
	ТипДок = спУсловия.Получить("ТипДок");
	Если ПустоеЗначение(ТипДок)=1 Тогда
		ТипДок = 1;
	КонецЕсли;


	Запрос = "
	|<SOAP-ENV:Envelope xmlns:dt='http://api.vetrf.ru/schema/cdm/dictionary/v2'
	|xmlns:bs='http://api.vetrf.ru/schema/cdm/base'
	|xmlns:merc='http://api.vetrf.ru/schema/cdm/mercury/g2b/applications/v2'
	|xmlns:apldef='http://api.vetrf.ru/schema/cdm/application/ws-definitions'
	|xmlns:apl='http://api.vetrf.ru/schema/cdm/application'
	|xmlns:vd='http://api.vetrf.ru/schema/cdm/mercury/vet-document/v2'
	|xmlns:SOAP-ENV='http://schemas.xmlsoap.org/soap/envelope/'>
	|<SOAP-ENV:Header/>
	|<SOAP-ENV:Body>
	|<apldef:submitApplicationRequest>
	|<apldef:apiKey>"+ СокрЛП( ГМ.СписокКонстант.Получить("param_api_key") ) +"</apldef:apiKey>
	|<apl:application>
	//|<apl:serviceId>mercury-g2b.service:2.0</apl:serviceId>
	|<apl:serviceId>mercury-g2b.service:2.1</apl:serviceId>
	|<apl:issuerId>"+ СокрЛП( ГМ.СписокКонстант.Получить("param_issuer_id") ) +"</apl:issuerId>
	|<apl:issueDate>" + ГМ.Преобразовать_Дата_в_Строка(ТекущаяДата(), "T00:00:00") + "</apl:issueDate>
	|<apl:data>
	|<merc:getVetDocumentListRequest>
	|<merc:localTransactionId>[GUID]</merc:localTransactionId>
	|<merc:initiator>
	|<vd:login>"+ СокрЛП( ГМ.СписокКонстант.Получить("param_intiator_login") ) +"</vd:login>
	|</merc:initiator>
	|<bs:listOptions>
	|<bs:count>1000</bs:count>
	|<bs:offset>"+Смещение+"</bs:offset>
	|</bs:listOptions>
	|<vd:vetDocumentType>"+ vetDocumentType +"</vd:vetDocumentType>
	|<vd:vetDocumentStatus>"+ vetDocumentStatus +"</vd:vetDocumentStatus>
	|";
	//2.1
	Если ПустоеЗначение(НачДата)=0 Тогда
		Запрос=Запрос+"
		|<vd:issueDateInterval>
		|<bs:beginDate>"+ ГМ.Преобразовать_Дата_в_Строка( НачДата, "T00:00:00" ) +"</bs:beginDate>
		|<bs:endDate>"+ ГМ.Преобразовать_Дата_в_Строка( КонДата, "T23:59:59") +"</bs:endDate>
		|</vd:issueDateInterval>";
	КонецЕсли;

	Если (ПустоеЗначение(Поставщик_ХозСубъект)=0) ИЛИ ( ПустоеЗначение(Поставщик_Площадка)=0 ) Тогда
		Запрос=Запрос+" <vd:consignor>";
		Если ПустоеЗначение(Поставщик_ХозСубъект)=0 Тогда
			Запрос=Запрос+"
			|<dt:businessEntity>
			|<bs:guid>"+ СокрЛП( Поставщик_ХозСубъект.GUID ) +"</bs:guid>
			|</dt:businessEntity>";
		КонецЕсли;
		Если ПустоеЗначение(Поставщик_Площадка)=0 Тогда
			Запрос=Запрос+"
			|<dt:enterprise>
			|<bs:guid>"+ СокрЛП(Поставщик_Площадка.GUID) +"</bs:guid>
			|</dt:enterprise>";
		КонецЕсли;
		Запрос=Запрос+" </vd:consignor>";
	КонецЕсли;
	Если ПустоеЗначение(НомерДок)=0 Тогда
		Запрос=Запрос+"
		|<vd:referencedDocument>
		|<vd:issueNumber>"+ СокрЛП(НомерДок) +"</vd:issueNumber>
		|<vd:issueDate>"+ ДатаДок +"</vd:issueDate>
		|<vd:type>"+ ТипДок +"</vd:type>
		|</vd:referencedDocument>";
	КонецЕсли;
	//
	Запрос=Запрос+"
	|<dt:enterpriseGuid>"+ СокрЛП(Площадка.GUID) +"</dt:enterpriseGuid>
	|</merc:getVetDocumentListRequest>
	|</apl:data>
	|</apl:application>
	|</apldef:submitApplicationRequest>
	|</SOAP-ENV:Body>
	|</SOAP-ENV:Envelope>
	|";
	Возврат Запрос;
КонецФункции


// http://help.vetrf.ru/wiki/GetVetDocumentListOperation_v2.0#scenario1
Функция  ПолучитьСписокВСД( спУсловия ) Экспорт
//	список ВСД может быть отфильтрован По следующим критериям
//	Тип ВСД:
//        входящий ВСД;				INCOMING
//        исходящий ВСД;			OUTGOING
//        производственный ВСД;		PRODUCTIVE
//        транспортный ВСД;			TRANSPORT
//        возвратный ВСД.			RETURNABLE
//    Статус ВСД:
//        оформлен;		CONFIRMED 	Оформлен. Действующий сертификат, По которому разрешено совершать транзакцию с грузом.
//        погашен;		UTILIZED 	Погашен. Действующий сертификат, По которому транзакция уже была совершена.
//        аннулирован.	WITHDRAWN 	Аннулирован. Не действующий более сертификат.

	Площадка = спУсловия.Получить("Площадка");
	Если ПустоеЗначение(Площадка.GUID)=1 Тогда
		Сообщить("В выбранной Площадке пустой GUID");
		Возврат "REJECTED";
	КонецЕсли;

~Метка:  //+
	ЗапросXML = ПолучитьСписокВСД2_ЗапросXML( спУсловия );

	метод = ГМ.Компонента.GetMethod();
	//метод.Service = "platform/services/2.0/ApplicationManagementService";
	метод.Service = "platform/services/2.1/ApplicationManagementService";
    метод.Action = "submitApplicationRequest";

	appID = ГМ.ОтправитьЗапросSoap(ЗапросXML, метод);

	ГМ.УдалитьXML( ГМ.Компонента.LogFilename);

    Результат = "";
	Если ПустоеЗначение(appID)=0 Тогда
		
		спПараметры = СоздатьОбъект("СписокЗначений");	
		спПараметры.Установить("applicationID", appID);
		
		Результат = ПолучитьРезультат_ВСД_2( спПараметры );
		Сообщить("Результат = "+Результат);
		Для А=1 По 10 Цикл
			Если (Результат="IN_PROCESS") Тогда
				Результат = ПолучитьРезультат_ВСД_2( спПараметры );
			КонецЕсли;
		КонецЦикла;
		//+
		Если Результат="REJECTED" Тогда
			Перейти ~метка;
		КонецЕсли;
		//+
	КонецЕсли;

	ГМ.УдалитьXML( ГМ.Компонента.LogFilename);

	Возврат appID;

КонецФункции

Функция ОтправитьЗапрос2( док, ЗапросXML, ПолучитьОтветВетис=1)
	
	Если ГМ.ПолучитьКонстанту("ОтправлятьЗапросБезПроверкиСтатусаДок")<>1 Тогда 
		Если (ПустоеЗначение(Док.Статус)=1) ИЛИ (СокрЛП(Док.Статус) = "COMPLETED") ИЛИ (СокрЛП(Док.Статус) = "REJECTED") Тогда 
			// все ок
		Иначе
			ГМ.СообщитьОбОшибке("Запрос с applicationID = "+ Док.applicationID +" отправлен, но ответ Ветис не получен
			|ЗАПРЕЩЕНА отправка запросов, по которым не получен ответ Ветис  !
			|Для получения ответа нажмите кнопку [ВСД] -> [Получить ответ Ветис] ");
			Возврат "";
		КонецЕсли;
	КонецЕсли;
	
	метод = ГМ.Компонента.GetMethod();
	метод.Service = "platform/services/2.0/ApplicationManagementService";
    метод.Action = "submitApplicationRequest";

	appID = ГМ.ОтправитьЗапросSoap(ЗапросXML, метод);

	ГМ.СохранитьЗапросВДокументе( док, appID );

	Если ГМ.НайтиОшибки( док )>0 Тогда
		Возврат appID;
	КонецЕсли;	
	
	ГМ.УдалитьXML( ГМ.Компонента.LogFilename);

	Если ( ПустоеЗначение(appID)=0 ) И (ПолучитьОтветВетис=1) Тогда

		спПараметры = СоздатьОбъект("СписокЗначений");	
		спПараметры.Установить("applicationID", appID);
		спПараметры.Установить("докСсылка", док );

		Результат = ПолучитьРезультат_ВСД_2( спПараметры );
		ГМ.СообщитьИнфо("Результат = "+Результат, док);
		Для А=1 По 10 Цикл
			Если (Результат="IN_PROCESS") Тогда
				Результат = ПолучитьРезультат_ВСД_2( спПараметры );
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Возврат appID;
	
КонецФункции

//}


//{ ================== Лабораторные исследования =========================

Функция ВСД_ЛабораторныеИсследования_ЗапросXML(док)
	//док = СоздатьОбъект("Документ.ВСД_ЛабораторныеИсследования");
	//{Проверим на переопределение функции, Если переопределена вызовем из перекрывающего модуля
	ОбъектВыполнения = ПроверитьПереопределениеФункции("ВСД_ЛабораторныеИсследования_ЗапросXML");
	Если ПустоеЗначение(ОбъектВыполнения)=0 Тогда
		Возврат ОбъектВыполнения.ВСД_ЛабораторныеИсследования_ЗапросXML(док);
	КонецЕсли;
	//}

	Запрос ="
	|<SOAP-ENV:Envelope xmlns:dt='http://api.vetrf.ru/schema/cdm/dictionary/v2'
	|xmlns:bs='http://api.vetrf.ru/schema/cdm/base'
	|xmlns:merc='http://api.vetrf.ru/schema/cdm/mercury/g2b/applications/v2'
	|xmlns:apldef='http://api.vetrf.ru/schema/cdm/application/ws-definitions'
	|xmlns:apl='http://api.vetrf.ru/schema/cdm/application'
	|xmlns:vd='http://api.vetrf.ru/schema/cdm/mercury/vet-document/v2'
	|xmlns:SOAP-ENV='http://schemas.xmlsoap.org/soap/envelope/'>
	|<SOAP-ENV:Header/>
	|<SOAP-ENV:Body>
	|<apldef:submitApplicationRequest>
	|<apldef:apiKey>"+ СокрЛП( ГМ.СписокКонстант.Получить("param_api_key") ) +"</apldef:apiKey>
	|<apl:application>
	|<apl:serviceId>mercury-g2b.service:2.0</apl:serviceId>
	|<apl:issuerId>"+ СокрЛП( ГМ.СписокКонстант.Получить("param_issuer_id") ) +"</apl:issuerId>
	|<apl:issueDate>"+ ГМ.Преобразовать_Дата_в_Строка(док.ДатаДок, "T00:00:00") +"</apl:issueDate>
	|<apl:data>
	|<merc:updateVeterinaryEventsRequest>
	|<merc:localTransactionId>" + СокрЛП(док.НомерДок) + "</merc:localTransactionId>
	|<merc:initiator>
	|<vd:login>"+ СокрЛП( ГМ.СписокКонстант.Получить("param_intiator_login") ) +"</vd:login>
	|</merc:initiator>
	|<merc:enterprise>
	|<bs:guid>"+ СокрЛП(док.Партия.Производитель_Площадка.GUID) +"</bs:guid>
	|</merc:enterprise>
	|<vd:stockEntry>
	|<bs:guid>"+ СокрЛП( док.Партия.GUID ) +"</bs:guid>
	|<vd:vetEventList>
	|<vd:laboratoryResearch>
	|<vd:actualDateTime>"+ ГМ.Преобразовать_Дата_в_Строка(док.ДатаРезультата, "T00:00:00") +"</vd:actualDateTime>
	|<vd:operator>
	|<dt:name>" + СокрЛП(док.НаименованиеЛаборатории) + "</dt:name>
	|</vd:operator>
	|<vd:referencedDocument>
	|<vd:issueNumber>"+ СокрЛП(док.НомерАктаОтбораПроб) +"</vd:issueNumber>
	|<vd:issueDate>"+ ГМ.Преобразовать_Дата_в_Строка(док.ДатаОтбораПроб) +"</vd:issueDate>
	|<vd:type>9</vd:type>
	//|<vd:relationshipType>6</vd:relationshipType>
	|</vd:referencedDocument>
	|<vd:expertiseID>"+ СокрЛП(док.НомерЭкспертизы) +"</vd:expertiseID>
	|<vd:disease>
	|<dt:name>"+ СокрЛП(док.НаименованиеПоказателя) +"</dt:name>
	|</vd:disease>
	|<vd:method>
	|<dt:name>"+ СокрЛП(док.МетодИсследования) +"</dt:name>
	|</vd:method>
	//Допустимые значения: UNKNOWN, POSITIVE, NEGATIVE
	|<vd:result>"+ СокрЛП(док.РезультатИсследования.Идентификатор()) +"</vd:result>
	|<vd:conclusion>"+ СокрЛП(док.Заключение)  +"</vd:conclusion>
	|</vd:laboratoryResearch>
	|</vd:vetEventList>
	|</vd:stockEntry>
	|</merc:updateVeterinaryEventsRequest>
	|</apl:data>
	|</apl:application>
	|</apldef:submitApplicationRequest>
	|</SOAP-ENV:Body>
	|</SOAP-ENV:Envelope>
	|";

	Возврат Запрос;
КонецФункции

Функция Проверка_ВСД2_ЛабораторныеИсследования( док )

	Если ( ПустоеЗначение( док.applicationID ) = 0 ) И ( СокрЛП(док.Статус) <> "REJECTED") Тогда
	    Сообщить("Запрос отправлен, повторная отправка запроса запрещена","!!");
		Возврат 0;
	КонецЕсли;	
	
	Возврат 1;
КонецФункции

Функция Отправить_ВСД_ЛабораторныеИсследования( док ) Экспорт

	t1 = ГМ.Старт();
	
	Если Проверка_ВСД2_ЛабораторныеИсследования(док)=0 Тогда
		Возврат 0;
	КонецЕсли;	
	
	ЗапросXML = ВСД_ЛабораторныеИсследования_ЗапросXML(док);
	
	appID = ОтправитьЗапрос2( док, ЗапросXML );

//	метод = ГМ.Компонента.GetMethod();
//	метод.Service = "platform/services/2.0/ApplicationManagementService";
//    метод.Action = "submitApplicationRequest";
//
//	appID = ГМ.ОтправитьЗапросSoap(ЗапросXML, метод);
//
//	ГМ.СохранитьЗапросВДокументе(док);
//
//	Если ГМ.НайтиОшибки( док )>0 Тогда
//		Возврат appID;
//	КонецЕсли;	
//	
//	ГМ.УдалитьXML( ГМ.Компонента.LogFilename);
//
//	Если ПустоеЗначение(appID)=0 Тогда
//
//		спПараметры = СоздатьОбъект("СписокЗначений");	
//		спПараметры.Установить("applicationID", appID);
//		спПараметры.Установить("докСсылка", док );
//
//		//Результат = ПолучитьРезультат_ВСД_2(appID, док ); //ДокОб.ТекущийДокумент()
//		Результат = ПолучитьРезультат_ВСД_2( спПараметры );
//		Сообщить("Результат = "+Результат);
//		Для А=1 По 10 Цикл
//			Если (Результат="IN_PROCESS") Тогда
//				//Результат = ПолучитьРезультат_ВСД_2(appID, док ); //ДокОб.ТекущийДокумент()
//				Результат = ПолучитьРезультат_ВСД_2( спПараметры );
//			КонецЕсли;
//		КонецЦикла;
//	КонецЕсли;

	ГМ.Финиш(t1, Сам(), "Отправить_ВСД_ЛабораторныеИсследования", док);
	
	Возврат appID;
КонецФункции
//}

//==================== Мультимодальные перевозки
//http://help.vetrf.ru/wiki/PrepareOutgoingConsignmentOperation_v2.0#.D0.9C.D1.83.D0.BB.D1.8C.D1.82.D0.B8.D0.BC.D0.BE.D0.B4.D0.B0.D0.BB.D1.8C.D0.BD.D1.8B.D0.B5_.D0.BF.D0.B5.D1.80.D0.B5.D0.B2.D0.BE.D0.B7.D0.BA.D0.B8
// док = ВСД2_Транзакция
Функция Запрос_МаршрутДоставки( док, НомерСтроки=0 )
	
	Если ТипЗначенияСтр( док )= "Документ" Тогда 
		Если Метаданные.Документ(док.Вид()).РеквизитШапки("ИмяФайлаМаршрутДоставки").Выбран() = 0 Тогда
			Возврат "";
		КонецЕсли;
	КонецЕсли;
	Если ПустаяСтрока(док.ИмяФайлаМаршрутДоставки) = 1 Тогда
		Возврат "";
	КонецЕсли;

	тзМаршрутДоставки = ЗначениеИзФайла( док.ИмяФайлаМаршрутДоставки );
	Если ТипЗначенияСтр(тзМаршрутДоставки) <> "ТаблицаЗначений" Тогда
		Возврат "";
	КонецЕсли;

	запрос = "";

	тзМаршрутДоставки.ВыбратьСтроки();
	Пока тзМаршрутДоставки.ПолучитьСтроку() = 1 Цикл
		НомерСтроки=НомерСтроки+1;

		запрос=запрос+"
		|<vd:routePoint>
		|<vd:sqnId>"+ НомерСтроки +"</vd:sqnId>
		|";
		Если ПустоеЗначение( тзМаршрутДоставки.ВыбПлощадка.GUID )=0 Тогда
			запрос=запрос+"
			|<vd:enterprise>
			|<bs:guid>"+ тзМаршрутДоставки.ВыбПлощадка.GUID +"</bs:guid>
			|</vd:enterprise>
			|";
		Иначе
			запрос=запрос+"
			|<vd:location>
			|<dt:name>"+тзМаршрутДоставки.Название+"</dt:name>
			|<dt:address>";
			Если ПустоеЗначение(тзМаршрутДоставки.Страна) = 0 Тогда
				Запрос=Запрос+"
				|<dt:country>
				|<bs:guid>"+СокрЛП(тзМаршрутДоставки.Страна.guid)+"</bs:guid>
				|</dt:country>";
			КонецЕсли;
			Если ПустоеЗначение(тзМаршрутДоставки.Регион) = 0 Тогда
				Запрос=Запрос+"
				|<dt:region>
				|<bs:guid>"+СокрЛП(тзМаршрутДоставки.Регион.guid)+"</bs:guid>
				|</dt:region>";
			КонецЕсли;
			Если ПустоеЗначение(тзМаршрутДоставки.Район) = 0 Тогда
				Запрос=Запрос+"
				|<dt:district>
				|<bs:guid>"+СокрЛП(тзМаршрутДоставки.Район.guid)+"</bs:guid>
				|</dt:district>";
			КонецЕсли;
			Если ПустоеЗначение(тзМаршрутДоставки.Город) = 0 Тогда
				Запрос=Запрос+"
				|<dt:locality>
				|<bs:guid>"+СокрЛП(тзМаршрутДоставки.Город.guid)+"</bs:guid>
				|</dt:locality>";
			КонецЕсли;
			Если ПустоеЗначение(тзМаршрутДоставки.Улица) = 0 Тогда
				Запрос=Запрос+"
				|<dt:street>
				|<bs:guid>"+СокрЛП(тзМаршрутДоставки.Улица.guid)+"</bs:guid>
				|</dt:street>";
			КонецЕсли;
			Если ПустоеЗначение(тзМаршрутДоставки.Дом) = 0 Тогда
				Запрос=Запрос+"
				|<dt:house>"+СокрЛП(тзМаршрутДоставки.Дом)+"</dt:house>";
			КонецЕсли;
			Если ПустоеЗначение(тзМаршрутДоставки.Строение) = 0 Тогда
				Запрос=Запрос+"
				|<dt:building>"+СокрЛП(тзМаршрутДоставки.Строение)+"</dt:building>";
			КонецЕсли;
			Если ПустоеЗначение(тзМаршрутДоставки.Помещение) = 0 Тогда
				Запрос=Запрос+"
				|<dt:room>"+СокрЛП(тзМаршрутДоставки.Помещение)+"</dt:room>";
			КонецЕсли;

			Запрос=Запрос+"
			|</dt:address>
			|</vd:location>
			|";
		КонецЕсли;
		запрос=запрос+"
		//|<vd:transshipment>"+?(тзМаршрутДоставки.Перегрузка = 1,"true","false")+"</vd:transshipment>
		//отражать будем только пункты перегрузки
		|<vd:transshipment>true</vd:transshipment>
		|<vd:nextTransport>
		|<vd:transportType>"+ тзМаршрутДоставки.ТипТранспорта +"</vd:transportType>
		|<vd:transportNumber>";

		Если ПустоеЗначение(тзМаршрутДоставки.номерКонтейнера) = 0 Тогда
			Запрос=Запрос+"
           	|<vd:containerNumber>"+СокрЛП(тзМаршрутДоставки.номерКонтейнера)+"</vd:containerNumber>";
		КонецЕсли;
		Если ПустоеЗначение(тзМаршрутДоставки.НомерАвто) = 0 Тогда
			Запрос=Запрос+"
           	|<vd:vehicleNumber>"+СокрЛП(тзМаршрутДоставки.НомерАвто)+"</vd:vehicleNumber>";
		КонецЕсли;
		Если ПустоеЗначение(тзМаршрутДоставки.номерприцепа) = 0 Тогда
			Запрос=Запрос+"
           	|<vd:trailerNumber>"+СокрЛП(тзМаршрутДоставки.номерприцепа)+"</vd:trailerNumber>";
		КонецЕсли;
		Если ПустоеЗначение(тзМаршрутДоставки.номерВагона) = 0 Тогда
			Запрос=Запрос+"
           	|<vd:wagonNumber>"+СокрЛП(тзМаршрутДоставки.номерВагона)+"</vd:wagonNumber>";
		КонецЕсли;
		Если ПустоеЗначение(тзМаршрутДоставки.НазваниеСудна) = 0 Тогда
			Запрос=Запрос+"
           	|<vd:shipName>"+СокрЛП(тзМаршрутДоставки.НазваниеСудна)+"</vd:shipName>";
		КонецЕсли;
		Если ПустоеЗначение(тзМаршрутДоставки.НомерАвиарейса) = 0 Тогда
			Запрос=Запрос+"
           	|<vd:flightNumber>"+СокрЛП(тзМаршрутДоставки.НомерАвиарейса)+"</vd:flightNumber>";
		КонецЕсли;

		Запрос=Запрос+"
  	    |</vd:transportNumber>";


		запрос=запрос+"
		|</vd:nextTransport>
		|</vd:routePoint>
		|";
	КонецЦикла;

	Возврат запрос;
КонецФункции

//{ =========================  Проверка условий перевозки / Регионализации
Функция РегионализацияМаршрута_2_ЗапросXML(док)

	//{Проверим на переопределение функции, Если переопределена вызовем из перекрывающего модуля
	ОбъектВыполнения = ПроверитьПереопределениеФункции("РегионализацияМаршрута_2_ЗапросXML");
	Если ПустоеЗначение(ОбъектВыполнения)=0 Тогда
		Возврат ОбъектВыполнения.РегионализацияМаршрута_2_ЗапросXML(док);
	КонецЕсли;
	//}

	// сворачиваем по GUID 
	тзВидыПродукции = СоздатьОбъект("ТаблицаЗначений");	
	тзВидыПродукции.НоваяКолонка("GUID");
	Если ( ВРЕГ(Док.Вид()) = "ВСД2_ВХОДЯЩИЙ" ) или ( ВРЕГ(Док.Вид()) = "ВСД_ДОК" ) Тогда
		тзВидыПродукции.НоваяСтрока();
		тзВидыПродукции.GUID = СокрЛП(док.ВидПродукции.GUID);
	Иначе
		Док.ВыбратьСтроки();
		Пока Док.ПолучитьСтроку()=1 Цикл
			Если ПустоеЗначение(док.Партия.ВидПродукции.GUID)=0 Тогда
				тзВидыПродукции.НоваяСтрока();
				тзВидыПродукции.GUID = СокрЛП(док.ВидПродукции.GUID);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	тзВидыПродукции.Свернуть("GUID", "");	
	
	Запрос = "
		|<SOAP-ENV:Envelope xmlns:dt='http://api.vetrf.ru/schema/cdm/dictionary/v2'
		|xmlns:bs='http://api.vetrf.ru/schema/cdm/base'
		|xmlns:merc='http://api.vetrf.ru/schema/cdm/mercury/g2b/applications/v2'
		|xmlns:apldef='http://api.vetrf.ru/schema/cdm/application/ws-definitions'
		|xmlns:apl='http://api.vetrf.ru/schema/cdm/application'
		|xmlns:vd='http://api.vetrf.ru/schema/cdm/mercury/vet-document/v2'
		|xmlns:SOAP-ENV='http://schemas.xmlsoap.org/soap/envelope/'>
		|<SOAP-ENV:Header/>
		|<SOAP-ENV:Body>
		|<apldef:submitApplicationRequest>
		|<apldef:apiKey>"+ СокрЛП( ГМ.СписокКонстант.Получить("param_api_key") ) +"</apldef:apiKey>
		|<apl:application>
		|<apl:serviceId>mercury-g2b.service:2.0</apl:serviceId>
		|<apl:issuerId>"+ СокрЛП( ГМ.СписокКонстант.Получить("param_issuer_id") ) +"</apl:issuerId>
		|<apl:issueDate>" + ГМ.Преобразовать_Дата_в_Строка( ТекущаяДата(), "T00:00:00") + "</apl:issueDate>
		|<apl:data>
		|<merc:checkShipmentRegionalizationRequest>
		|<merc:localTransactionId>" + ГМ.Компонента.GUID() + "</merc:localTransactionId>
		|<merc:initiator>
		|<vd:login>"+ СокрЛП( ГМ.СписокКонстант.Получить("param_intiator_login") ) +"</vd:login>
		|</merc:initiator>
		|";

	тзВидыПродукции.ВыбратьСтроки();
	Пока тзВидыПродукции.ПолучитьСтроку() = 1 Цикл
		Запрос = Запрос+"
		|<dt:cargoType>
		|<bs:guid>"+ тзВидыПродукции.GUID +"</bs:guid>
		|</dt:cargoType>";
		
	КонецЦикла;
		
	//<vd:sqnId>1</vd:sqnId>
	//Порядовый номер точки маршрута.
    //Не обязателен для заполнения, будет сгенерирован автоматически в ответе

	// <vd:enterprise>
	//Сведения о предприятии, которое является точкой маршрута.
    //Указывается в случае, Если не указано значение location

	НомерСтроки=1;

	Если док.Вид()="ВСД2_входящий" Тогда
		Отправитель_Площадка = СокрЛП(док.Получатель_Площадка.GUID);
		Получатель_Площадка = СокрЛП(док.Отправитель_Площадка.GUID);
	Иначе
		Отправитель_Площадка = СокрЛП(док.Отправитель_Площадка.GUID);
		Получатель_Площадка = СокрЛП(док.Получатель_Площадка.GUID);
	КонецЕсли;

	Запрос = Запрос+"
	|<vd:shipmentRoute>
	|<vd:routePoint>
	|<vd:sqnId>1</vd:sqnId>
	|<vd:enterprise>
	|<bs:guid>"+ Отправитель_Площадка +"</bs:guid>
	|</vd:enterprise>
	|</vd:routePoint>
	|";
	ЗапросМаршрут = Запрос_МаршрутДоставки( док, НомерСтроки );
	Запрос=Запрос+ЗапросМаршрут+"
	|<vd:routePoint>
	|<vd:sqnId>"+ (НомерСтроки+1) +"</vd:sqnId>
	|<vd:enterprise>
	|<bs:guid>"+ Получатель_Площадка +"</bs:guid>
	|</vd:enterprise>
	|</vd:routePoint>
	|</vd:shipmentRoute>
	|</merc:checkShipmentRegionalizationRequest>
	|</apl:data>
	|</apl:application>
	|</apldef:submitApplicationRequest>
	|</SOAP-ENV:Body>
	|</SOAP-ENV:Envelope>
	|";
	Возврат Запрос;
КонецФункции

// https://help.vetrf.ru/wiki/CheckShipmentRegionalizationOperation_v2.0
Функция ЗагрузитьXML_ПеревозкаРазрешена_2(имяФайла) Экспорт

	//{Проверим на переопределение функции, Если переопределена вызовем из перекрывающего модуля
	ОбъектВыполнения = ПроверитьПереопределениеФункции("ЗагрузитьXML_ПеревозкаРазрешена_2");
	Если ПустоеЗначение(ОбъектВыполнения)=0 Тогда
		Возврат ОбъектВыполнения.ЗагрузитьXML_ПеревозкаРазрешена_2(имяФайла);
	КонецЕсли;
	//}

    objDom=СоздатьОбъект("MSXML2.DOMDocument.6.0");
    objDom.load(ИмяФайла);
    objDom.setProperty("SelectionNamespaces", "
		|xmlns:bs='http://api.vetrf.ru/schema/cdm/base'
		|xmlns:merc='http://api.vetrf.ru/schema/cdm/mercury/g2b/applications/v2'
		|xmlns:dt='http://api.vetrf.ru/schema/cdm/dictionary/v2'
		|xmlns:vd='http://api.vetrf.ru/schema/cdm/mercury/vet-document/v2'
		|xmlns:default='http://www.w3.org/2001/XMLSchema'
		|" );


	Попытка
		applicationId = objDom.selectSingleNode("//*[local-name() = 'applicationId']").text ;

		RouteSectionList = objDom.selectNodes("//*[local-name() = 'r13nRouteSection']") ;
	Исключение
		Сообщить("Не удалось прочитать ответ о разрешении");
		Возврат "";
		//Пример на ветисе корявый...
		applicationId = objDom.selectSingleNode("//*[local-name() = 'apl:application']").selectSingleNode("applicationId").text ;

		RouteSectionList = objDom.selectSingleNode("//*[local-name() = 'apl:application']").selectNodes("r13nRouteSection") ;
	КонецПопытки;

	УсловияПеревозки = СоздатьОбъект("ТаблицаЗначений");
	УсловияПеревозки.НоваяКолонка("Пометка", , , , "V", 3,);
	УсловияПеревозки.НоваяКолонка("НомерОтрезкаМаршрута", , , , "№ маршрута",5,);
	УсловияПеревозки.НоваяКолонка("Решение", , , , "Решение",3,);
	УсловияПеревозки.НоваяКолонка("ВидПродукции", , , , ,30,);	
	УсловияПеревозки.НоваяКолонка("Название", , , , ,50,);
	УсловияПеревозки.НоваяКолонка("UUID", , , , ,36,);
	УсловияПеревозки.НоваяКолонка("GUID", , , , ,36,);
	

	Сообщить("Получено "+RouteSectionList.length+" записей По отрезкам маршрута","i");
    Для i1 = 0 По RouteSectionList.length - 1 Цикл
        RouteSection = RouteSectionList.item(i1);
		НомерОтрезкаМаршрута = RouteSection.selectSingleNode("vd:sqnId").text;
		
		RuleList = RouteSection.SelectNodes("vd:appliedR13nRule");
		Сообщить("Получено "+RuleList.length+" записей правил перемещения","i");
		Для k1 = 0 По RuleList.length - 1 Цикл
			appliedR13nRule = RuleList.item(k1);
			
			//Решение о возможности перемещения груза
			//1 - перемещение разрешено;
			//2 - перемещение разрешено при обязательном соблюдении условий;
			//3 - перемещение запрещено.
			Решение = Число(appliedR13nRule.selectSingleNode("dt:decision").text);
			
			ВидПродукции = ГМ.НайтиВидПродукции( appliedR13nRule.selectSingleNode("dt:cargoType").selectSingleNode("bs:guid").text);

			Если Решение=2 Тогда 
				requirements = appliedR13nRule.selectNodes("dt:requirement");
				Сообщить("Получено "+requirements.length+" записей По правилам для "+ВидПродукции,"i");
				Для i2 = 0 По requirements.length - 1 Цикл
					
					Решение = Число( requirements.item(i2).selectSingleNode("dt:type").text );
					
					Если Решение = 2 Тогда
						
						conditionGroup = requirements.item(i2).selectNodes("dt:conditionGroup");
						Для i3=0 По conditionGroup.length - 1 Цикл
							conditionList = conditionGroup.item(i3).selectNodes("dt:condition");
							Для i4=0 По conditionList.length - 1 Цикл
								Попытка
									
									condition = conditionList.item(i4);
									УсловияПеревозки.НоваяСтрока();
									УсловияПеревозки.Пометка = 1;
									УсловияПеревозки.НомерОтрезкаМаршрута = НомерОтрезкаМаршрута;
									УсловияПеревозки.Решение = Решение;
									УсловияПеревозки.ВидПродукции = ВидПродукции;
									УсловияПеревозки.Название = condition.selectSingleNode("dt:text").text;
									УсловияПеревозки.UUID = condition.selectSingleNode("bs:uuid").text;
									УсловияПеревозки.GUID = condition.selectSingleNode("bs:guid").text;
									
									Если ГМ.ПолучитьКонстанту("ВыводитьПодробнуюИнформацию")=1 Тогда  
										Сообщить( Лев(Строка(i4+1)+". Перемещение при условии: "+ СокрЛП(УсловияПеревозки.Название),400));
									КонецЕсли;
								Исключение
									ГМ.СообщитьОбОшибке("Ошибка при загрузке условий перевозки "+ОписаниеОшибки());
								КонецПопытки;
							КонецЦикла;
						КонецЦикла;
					Иначе 
						// запрещено						
						УсловияПеревозки.НоваяСтрока();
						УсловияПеревозки.Пометка = 1;
						УсловияПеревозки.НомерОтрезкаМаршрута = НомерОтрезкаМаршрута;
						УсловияПеревозки.Решение = Решение;
						УсловияПеревозки.ВидПродукции = ВидПродукции;
						
						текст = ""+НомерОтрезкаМаршрута+ ". Перемещение ЗАПРЕЩЕНО для вида продукции ["+ВидПродукции+"] ["+ВидПродукции.GUID+"]";
						Предупреждение(текст);
						Сообщить( текст, "!!" );
					КонецЕсли;
				КонецЦикла;
			Иначе
				//condition = conditionList.item(i4);
				УсловияПеревозки.НоваяСтрока();
				УсловияПеревозки.НомерОтрезкаМаршрута = НомерОтрезкаМаршрута;
				УсловияПеревозки.Пометка = 1;
				УсловияПеревозки.Решение = Решение;
				УсловияПеревозки.ВидПродукции = ВидПродукции;
				
				Если Решение = 1 Тогда
					Если ГМ.ПолучитьКонстанту("ВыводитьПодробнуюИнформацию")=1 Тогда  
						Сообщить(""+НомерОтрезкаМаршрута+" Перемещение разрешено "+ВидПродукции,"i");			
					КонецЕсли;
				Иначе
					Сообщить(""+НомерОтрезкаМаршрута+" Перемещение ЗАПРЕЩЕНО " +ВидПродукции,"!!");
				КонецЕсли;				
				
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;

	Возврат УсловияПеревозки;

КонецФункции

Функция ПолучитьИмяФайлаРегионализация( Док ) Экспорт
	// {Проверим на переопределение функции, Если переопределена вызовем из перекрывающего модуля
	ОбъектВыполнения = ПроверитьПереопределениеФункции("ПолучитьИмяФайлаРегионализация");
	Если ПустоеЗначение(ОбъектВыполнения)=0 Тогда
		Возврат ОбъектВыполнения.ПолучитьИмяФайлаРегионализация(Док);
	КонецЕсли;
	// }

	КаталогРегионализации = ГМ.ПолучитьКонстанту("КаталогМодуля")+"Region\";
	Если ФС.СуществуетФайл(КаталогРегионализации) = 0 Тогда
		Попытка
		    ФС.СоздатьКаталог(КаталогРегионализации);
		Исключение
			Предупреждение("Не создан каталог Region"); Возврат "";
		КонецПопытки;
	КонецЕсли;

	Если ТипЗначенияСтр(Док)="Документ" Тогда 
		Рез = КаталогРегионализации+"УсловияПеревозки_"+СокрЛП(Док.НомерДок)+"_"+СтрЗаменить(Док.ДатаДок,".","")+".mrk";
	Иначе
		Рез = КаталогРегионализации+"УсловияПеревозки_"+СокрЛП(Док.Код)+"_"+СтрЗаменить(Док.ДатаДок,".","")+".mrk";
	КонецЕсли;

	Возврат Рез;
КонецФункции

// Функция ПолучитьРезультат_Регион_2( applicationID, докСсылка, ЗаписыватьВДокумент=0 ) Экспорт
Функция УсловияПеревозки_Ответ( applicationID, докСсылка, ЗаписыватьВДокумент=0, ИспользоватьПаузу=1 ) Экспорт	

	//стандартное поведение функции
	Если ПустоеЗначение(applicationID)=1 Тогда
		Сообщить("Пустая ссылка applicationID","!");
		Возврат "Пустая ссылка applicationID";
	КонецЕсли;

	Если ИспользоватьПаузу=1 Тогда 
		ГМ.Пауза( );
	КонецЕсли;

	Сообщить("Отправляем запрос ["+докСсылка+"]" );
	Результат = ГМ.ПолучитьРезультатСервер( applicationID );

	Для А=1 По 10 Цикл
		Если (Результат="IN_PROCESS") Тогда		
			ГМ.Пауза( );
			Результат = ГМ.ПолучитьРезультатСервер( applicationID );
		КонецЕсли;
	КонецЦикла;
		
    тзУсловияПеревозки = ЗагрузитьXML_ПеревозкаРазрешена_2( ГМ.Компонента.LogFilename );

	ГМ.УдалитьXML( ГМ.Компонента.LogFilename );
	
	Если ЗаписыватьВДокумент=1 Тогда 
		ДокОб = создатьОбъект("Документ");
		Попытка			
			Если ПустоеЗначение(докСсылка)=0 Тогда
				//ГМ.СохранитьСтатусЗапроса(докСсылка);
				ГМ.СохранитьОтветВДокументе( докСсылка, , applicationID );
				
				Если ТипЗначенияСтр( тзУсловияПеревозки ) = "ТаблицаЗначений" Тогда
					ДокОб.НайтиДокумент( докСсылка );
					Если тзУсловияПеревозки.КоличествоСтрок() = 0 Тогда
						ДокОб.ИмяФайлаРегионализация ="";
					Иначе
						ДокОб.ИмяФайлаРегионализация = ПолучитьИмяФайлаРегионализация( докСсылка );
						ЗначениеВФайл(ДокОб.ИмяФайлаРегионализация, тзУсловияПеревозки);
					КонецЕсли;
					ДокОб.Записать();
				КонецЕсли;
				
			КонецЕсли;
		Исключение
			Сообщить(ОписаниеОшибки());
		КонецПопытки;
	КонецЕсли;	

	Возврат тзУсловияПеревозки;
КонецФункции


// Функция ПроверитьВозможностьПеремещения(док, ПолучитьОтветВетис=1, ЗаписыватьВДокумент=0 ) Экспорт
Функция УсловияПеревозки_Получить( док, ПолучитьОтветВетис=1, ЗаписыватьВДокумент=0 ) Экспорт

	Если ПустоеЗначение(док)=1 Тогда
		Сообщить("Нет документа ВСД","!");
		Возврат "";
	КонецЕсли;

	ЗапросXML = РегионализацияМаршрута_2_ЗапросXML(док);

	метод = ГМ.Компонента.GetMethod();
	метод.Service = "platform/services/2.0/ApplicationManagementService";
    метод.Action = "submitApplicationRequest";

	appID = ГМ.ОтправитьЗапросSoap(ЗапросXML, метод);

	Если ЗаписыватьВДокумент=1 Тогда
		ГМ.СохранитьЗапросВДокументе( док, appID );
	КонецЕсли;
	
	Если ГМ.НайтиОшибки( док )>0 Тогда
		Возврат appID;
	КонецЕсли;	
		
	ГМ.УдалитьXML( ГМ.Компонента.LogFilename);
	
    тзУсловияПеревозки = "";
	Если ( ПустоеЗначение(appID)=0 ) И (ПолучитьОтветВетис=1) Тогда
		тзУсловияПеревозки = УсловияПеревозки_Ответ( appID, док, ЗаписыватьВДокумент );
	КонецЕсли;

	Возврат тзУсловияПеревозки;
КонецФункции

//} Конец Условия перевозки / Регионализация

Функция МаркировкаДокументаВЗапрос( Док )
	
	consignment = "";
	
    // нужно По реквизитам строки пробежать И проверить наличие упаковок
	УровнейВдок = 6; // макс кол-во уровней в документе
	МаркировокВдок = 2; // макс маркировок на уровне в док-те

	НомУровня=0; ЕстьСведенияОбУпаковках = 0;
	Для НомУровня=1 По УровнейВдок Цикл
		Упак =  СокрЛП(Док.ПолучитьАтрибут("ФормаУпак"+НомУровня));
		Если ПустоеЗначение(Упак)=0 Тогда
			ЕстьСведенияОбУпаковках = 1;
			Прервать;
		КонецЕсли;
	КонецЦикла;

    Если ЕстьСведенияОбУпаковках = 1 Тогда
		consignment = consignment+ "
		|<vd:packageList>";
        Для НомУровня=1 По УровнейВдок Цикл
			Попытка
				ГУИДУпак =  СокрЛП(Док.ПолучитьАтрибут("ФормаУпак"+НомУровня).GUID);
			Исключение
				Сообщить("Не указана форма упаковки на уровне "+НомУровня);
				Продолжить;
			КонецПопытки;
			Если ПустоеЗначение(ГУИДУпак)=1 Тогда
			    Продолжить;
			КонецЕсли;
			consignment = consignment+ "
			|<dt:package>
			|<dt:level>"+НомУровня+"</dt:level>
			|<dt:packingType>
			|<bs:guid>"+ ГУИДУпак +"</bs:guid>
			|</dt:packingType>
			|<dt:quantity>"+ Док.ПолучитьАтрибут("КолВоУпак"+НомУровня) +"</dt:quantity>";

			// Маркировки
			мм=0;
			Для мм=1 По МаркировокВДок Цикл
				КлассМарк = СокрЛП(Док.ПолучитьАтрибут("КлассМаркировки"+мм+"_"+НомУровня));
				Если ПустоеЗначение(КлассМарк) = 1 Тогда
				    Продолжить;
				КонецЕсли;
				знМарк = СокрЛП(Док.ПолучитьАтрибут("ЗначениеМаркировки"+мм+"_"+НомУровня));
				consignment = consignment+ "
				|<dt:productMarks class='"+КлассМарк+"'>"+ ЗаменитьСпецСимволы( знМарк ) +"</dt:productMarks>";

			КонецЦикла;
			consignment = consignment+ "
	   		|</dt:package>";
	    КонецЦикла;
		consignment = consignment+ "
		|</vd:packageList>";
	КонецЕсли;
	
	Возврат  consignment;
	
КонецФункции

//{ ========================== ВСД2_Транзакция ==========================
// http://help.vetrf.ru/wiki/PrepareOutgoingConsignmentOperation_v2.0
Функция ВСД2_транзакция_ЗапросXML(док, тзУсловияПеревозки)
	//{Проверим на переопределение функции, Если переопределена вызовем из перекрывающего модуля
	ОбъектВыполнения = ПроверитьПереопределениеФункции("ВСД2_транзакция_ЗапросXML");
	Если ПустоеЗначение(ОбъектВыполнения)=0 Тогда
		Возврат ОбъектВыполнения.ВСД2_транзакция_ЗапросXML(док, тзУсловияПеревозки);
	КонецЕсли;
	//}

	//стандартное поведение функции
	//док = СоздатьОбъект("Документ.ВСД_транзакция");

	Запрос_Шапка = "<SOAP-ENV:Envelope
	| xmlns:dt='http://api.vetrf.ru/schema/cdm/dictionary/v2'
	|xmlns:bs='http://api.vetrf.ru/schema/cdm/base'
	|xmlns:merc='http://api.vetrf.ru/schema/cdm/mercury/g2b/applications/v2'
	|xmlns:apldef='http://api.vetrf.ru/schema/cdm/application/ws-definitions'
	|xmlns:apl='http://api.vetrf.ru/schema/cdm/application'
	|xmlns:vd='http://api.vetrf.ru/schema/cdm/mercury/vet-document/v2'
	|xmlns:SOAP-ENV='http://schemas.xmlsoap.org/soap/envelope/'>
  	|<SOAP-ENV:Header/>
  	|<SOAP-ENV:Body>
    |<apldef:submitApplicationRequest>
	|<apldef:apiKey>"+ СокрЛП( ГМ.СписокКонстант.Получить("param_api_key") ) +"</apldef:apiKey>
	|<apl:application>
	|<apl:serviceId>mercury-g2b.service:2.1</apl:serviceId>
	|<apl:issuerId>"+ СокрЛП( ГМ.СписокКонстант.Получить("param_issuer_id") ) +"</apl:issuerId>
	|<apl:issueDate>" + ГМ.Преобразовать_Дата_в_Строка(док.ДатаДок, "T00:00:00", 1 ) + "</apl:issueDate>
	|<apl:data>
	|<merc:prepareOutgoingConsignmentRequest>
	|<merc:localTransactionId>" + СокрЛП(док.НомерДок) + "</merc:localTransactionId>
	|<merc:initiator>
	|<vd:login>"+ СокрЛП( ГМ.СписокКонстант.Получить("param_intiator_login") ) +"</vd:login>
	|</merc:initiator>
	|";

//************************
    delivery = "
    | <merc:delivery>";

	Если ПустоеЗначение(док.ТтнДата) = 0 Тогда   // Есть ТТН
    	delivery = delivery + "
    	|<vd:deliveryDate>" + ГМ.Преобразовать_Дата_в_Строка(док.ТтнДата, "T00:00:00") + "</vd:deliveryDate>";
	Иначе
    	delivery = delivery + "
    	|<vd:deliveryDate>" + ГМ.Преобразовать_Дата_в_Строка(док.ДатаДок, "T00:00:00") + "</vd:deliveryDate>";
	КонецЕсли;

    delivery = delivery + "
    |<vd:consignor>
    |<dt:businessEntity>
    |<bs:guid>"+СокрЛП(док.Отправитель_ХозСубъект.GUID)+"</bs:guid>
    |</dt:businessEntity>
    |<dt:enterprise>
    |<bs:guid>"+ СокрЛП(док.Отправитель_Площадка.GUID) +"</bs:guid>
    |</dt:enterprise>
    |</vd:consignor>
    |<vd:consignee>
    |<dt:businessEntity>
    |<bs:guid>"+ СокрЛП(док.Получатель_ХозСубъект.GUID) +"</bs:guid>
    |</dt:businessEntity>
    |<dt:enterprise>
    |<bs:guid>"+ СокрЛП(док.Получатель_Площадка.GUID) +"</bs:guid>
    |</dt:enterprise>";

	//<dt:globalID>4607009526782</dt:globalID> 	dt:GLNType 	[0..*]  [ ... ] Global Location Number - уникальный номер площадки
	delivery = delivery+"
    |</vd:consignee>";

	consignment="";
	Док.ВыбратьСтроки();
	Пока Док.ПолучитьСтроку() = 1 Цикл
//        |<vd:consignment id='id1'>
		consignment = consignment+ "
		|<vd:consignment id='_" + Док.НомерСтроки + "'>";

		//Указание согласованного наименования И фасовки (указание записи журнала И productItem с наименованием/артикулом/GTIN получателя в запросе).
			consignment = consignment+ "
    	    |<vd:productItem>";
			Если Док.Продукция_Элемент.ВидПродукции <> Док.Партия.ВидПродукции Тогда
	    	    // При изменении Вида продукции у ProductItem ошибка при списании ранее полученной партии
				// Указанное наименование продукции относится к другому виду продукции.				
				ГМ.СообщитьИнфо("изменен Вида продукции ВСД_Продукция_Элемент, ВСД_Продукция_Элемент относится к другому виду продукции => отправляем с пустым GUID", док, "!");
			ИначеЕсли СокрЛП(Док.Продукция_Элемент.Артикул) <> СокрЛП(Док.КодПродукции) Тогда
				ГМ.СообщитьИнфо("в документе изменен [КодПродукции] => ВСД_Продукция_Элемент отправляем с пустым GUID", док, "!");
			ИначеЕсли ( Док.Продукция_Элемент.ПометкаУдаления() = 1 ) Тогда
				//удаленные ProductItem отправляем без GUID
				ГМ.СообщитьИнфо("в строке №"+ Док.НомерСтроки +" ВСД_Продукция_Элемент ["+ СокрЛП(Док.Продукция_Элемент.Наименование) +"] удален => отправляем с пустым GUID", док, "!");
			Иначе
				consignment = consignment+?(ПустоеЗначение(док.Продукция_Элемент.GUID)= 0,	"          <bs:guid>"+ СокрЛП(док.Продукция_Элемент.GUID) +"</bs:guid>","");				
    	    КонецЕсли;
    	    consignment = consignment+?(ПустоеЗначение(док.GTIN)= 0, " <dt:globalID>"+ СокрЛП(док.GTIN) +"</dt:globalID>","");
        	consignment = consignment +	" <dt:name>"+ ЗаменитьСпецСимволы( док.НаименованиеПродукции )+"</dt:name>";
	        consignment = consignment+?(ПустоеЗначение(док.КодПродукции)= 0, " <dt:code>"+ СокрЛП(док.КодПродукции) +"</dt:code>","");			
			
			Если ПустоеЗначение(док.ТипФасовки)=0 Тогда
				consignment = consignment+ "
    	    	|<dt:packaging>
	        	|<dt:packagingType>
		        |<bs:guid>"+ СокрЛП(док.ТипФасовки.GUID) +"</bs:guid>
		//						<dt:globalID>BE</dt:globalID> 	dt:PackingCodeType 	[0..1] 	Уникальный идентификатор упаковки.
    		    |</dt:packagingType>
        		|<dt:quantity>"+ док.КолВоЕдиницФасовки +"</dt:quantity>
		        |<dt:volume>"+ док.ОбъемЕдиницыФасовки +"</dt:volume>
    		    |<dt:unit>
        		|<bs:guid>"+ СокрЛП(док.ЕдИзмОбъемаФасовки.GUID) +"</bs:guid>
		        |</dt:unit>
    		    |</dt:packaging>";
			КонецЕсли;

			consignment = consignment+ "
    	    |</vd:productItem>";

//		КонецЕсли;

		consignment = consignment+ "
		|<vd:volume>"+ Док.Количество +"</vd:volume>
        |<vd:unit>
        |<bs:guid>"+ СокрЛП(док.ЕдиницаИзмерения.GUID) +"</bs:guid>
        |</vd:unit>
        |";

		consignment = consignment+ "<vd:lowGradeCargo>"+ ГМ.булево(док.Некачественный)+"</vd:lowGradeCargo> ";
		
		consignment = consignment + МаркировкаДокументаВЗапрос( Док );
		
		Если ПустоеЗначение(док.ВидПроисхожденияНепищевойПродукции)=0 Тогда
			consignment = consignment+ "
			|<vd:applicableClassifications>
			| <vd:nonFoodSource>"+ док.ВидПроисхожденияНепищевойПродукции.Идентификатор() +"</vd:nonFoodSource>
			|</vd:applicableClassifications>";
		КонецЕсли;


		consignment = consignment+ "
		|<vd:sourceStockEntry>
        |<bs:guid>"+ СокрЛП(док.Партия.GUID) +"</bs:guid>
        |</vd:sourceStockEntry>
        |</vd:consignment>
		|";
	КонецЦикла; //По строкам документа
	//Тип товарно-транспортной накладной.
    //Допустимые типы для waybill: 1, 2, 3, 4, 5
	ЗапросПодвал = "";
	Если ПустоеЗначение(док.Перевозчик_ХозСубъект) = 0 Тогда
	ЗапросПодвал = "
        |<vd:broker>
        |<bs:guid>"+ СокрЛП(док.Перевозчик_ХозСубъект.GUID) +"</bs:guid>
        |</vd:broker>";
	КонецЕсли;

	//ТермическоеСостояние="";
	//ГМ.ТермическиеУсловияПеревозки.ПолучитьЗначение( док.ТермическоеСостояние, ТермическоеСостояние );
	//Если ПустоеЗначение(ТермическоеСостояние)=1 Тогда
	//	Предупреждение("Не указано ТермическоеСостояние");
	//	Возврат "";
	//КонецЕсли;
	//
	ЗапросПодвал = ЗапросПодвал+"
    |<vd:transportInfo>
    |<vd:transportType>1</vd:transportType>
    |<vd:transportNumber>";
	
	Если ПустоеЗначение(док.номерКонтейнера) = 0 Тогда
		ЗапросПодвал = ЗапросПодвал+"
    	|<vd:containerNumber>"+ СокрЛП(док.номерКонтейнера) +"</vd:containerNumber>";
	КонецЕсли;
	Если ПустоеЗначение(док.номерАвто) = 0 Тогда
		ЗапросПодвал = ЗапросПодвал+"
    	|<vd:vehicleNumber>"+ СокрЛП(док.номерАвто) +"</vd:vehicleNumber>";
	КонецЕсли;
	Если ПустоеЗначение(док.номерПолуприцепа) = 0 Тогда
		ЗапросПодвал = ЗапросПодвал+"
    	|<vd:trailerNumber>"+ СокрЛП(док.номерПолуприцепа) +"</vd:trailerNumber>";
	КонецЕсли;
	
	ЗапросПодвал = ЗапросПодвал+"
    |</vd:transportNumber>
    |</vd:transportInfo>
    |<vd:transportStorageType>"+ док.ТермическоеСостояние.Идентификатор() +"</vd:transportStorageType>";

	ЗапросПодвал=ЗапросПодвал + "
	|<vd:shipmentRoute>
	|"+ Запрос_МаршрутДоставки( док )+"
	|</vd:shipmentRoute> ";

	ЗапросПодвал = ЗапросПодвал+"
		|<vd:accompanyingForms>";

	Если ПустоеЗначение(док.ТтнДата) = 0 Тогда	// Без ТТН
		ЗапросПодвал = ЗапросПодвал+"
        |<vd:waybill>
        |<vd:issueSeries>"+ СокрЛП(док.ТтнСерия) +"</vd:issueSeries>
        |<vd:issueNumber>"+ СокрЛП(док.ТтнНомер) +"</vd:issueNumber>
        |<vd:issueDate>"+ ГМ.Преобразовать_Дата_в_Строка(док.ТтнДата) +"</vd:issueDate>
        |<vd:type>1</vd:type>
        |</vd:waybill>";
	КонецЕсли;

	//!!! Цели в ТЧ документа
	Док.ВыбратьСтроки();
	Пока Док.ПолучитьСтроку() = 1 Цикл

		ЗапросПодвал = ЗапросПодвал+"
	        |<vd:vetCertificate for='_"+ док.НомерСтроки +"'>
	        |<vd:authentication>
	        |<vd:purpose>
	        |<bs:guid>"+ СокрЛП( док.Цель.Guid ) +"</bs:guid>
	        |</vd:purpose>
	        |<vd:cargoInspected>"+ ГМ.булево(док.cargoInspected) +"</vd:cargoInspected>
			|<vd:cargoExpertized>"+док.РезультатыИсследований.Идентификатор()+"</vd:cargoExpertized>
			|<vd:locationProsperity>"+ СокрЛП(док.Местность) +"</vd:locationProsperity>
			|";

			Если ПустоеЗначение(тзУсловияПеревозки)=0 Тогда
				тзУсловияПеревозки.ВыбратьСтроки();
				Пока тзУсловияПеревозки.ПолучитьСтроку() = 1 Цикл
					Если ((тзУсловияПеревозки.Решение = 2) И (тзУсловияПеревозки.ВидПродукции = док.ВидПродукции)) Тогда
						ЗапросПодвал=ЗапросПодвал+"
						|<vd:r13nClause>
						|<vd:condition>
						|<bs:guid>" + тзУсловияПеревозки.GUID + "</bs:guid>
						|</vd:condition>
						|</vd:r13nClause>
						|";
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;
			ОсобыеОтметки="";
			Если ГМ.ПолучитьКонстанту("ОсобыеОтметкиЗагружатьИзПараметров")=1 Тогда 
				//из параметров
				ОсобыеОтметки = СокрЛП(ГМ.ПолучитьКонстанту("ВСД_ОсобыеОтметки"));
			КонецЕсли;
			// + добавим из документа
			ОсобыеОтметки = ОсобыеОтметки +" "+ СокрЛП(док.особыеотметки);
			ЗапросПодвал = ЗапросПодвал+ ?(Пустоезначение(особыеотметки) = 0, "<vd:specialMarks>"+ ОсобыеОтметки +"</vd:specialMarks>","");
			
		ЗапросПодвал = ЗапросПодвал+ "
        |</vd:authentication>
        |</vd:vetCertificate>";
	КонецЦикла;

		// Связанные документы
		// http://help.vetrf.ru/wiki/DocumentType_v2.0
		// тип 16 = Заказ, 6 = ТОРГ12,  1 = ТрН
//        |<vd:issueSeries>А</vd:issueSeries>
//        |<vd:issueDate>2017-09-26</vd:issueDate>

		СвязанныеДокументы = "";
		Если ПустоеЗначение(Док.НомерЗаказаПокупателя) = 0 Тогда
			СвязанныеДокументы = "
			|<vd:relatedDocument>
    	    |<vd:issueNumber>"+СокрЛП(док.НомерЗаказаПокупателя)+"</vd:issueNumber>
			|<vd:issueDate>"+ ГМ.Преобразовать_Дата_в_Строка(док.ДатаЗаказаПокупателя) +"</vd:issueDate>
    	    |<vd:type>16</vd:type>
        	|<vd:relationshipType>1</vd:relationshipType>
	        |	     </vd:relatedDocument>";
		КонецЕсли;

		Если ПустоеЗначение(Док.ТОРГ12Номер) = 0 Тогда
	        СвязанныеДокументы = СвязанныеДокументы+ "
	        |<vd:relatedDocument>
    	    |<vd:issueNumber>"+СокрЛП(док.ТОРГ12Номер)+"</vd:issueNumber>
	        |<vd:issueDate>"+ ГМ.Преобразовать_Дата_в_Строка(док.ТОРГ12Дата) +"</vd:issueDate>
    	    |<vd:type>6</vd:type>
        	|<vd:relationshipType>1</vd:relationshipType>
	        |</vd:relatedDocument>";
        КонецЕсли;

		Если ПустоеЗначение(Док.УпдНомер) = 0 Тогда
	        СвязанныеДокументы = СвязанныеДокументы+ "
	        |<vd:relatedDocument>
    	    |<vd:issueNumber>"+СокрЛП(док.УпдНомер)+"</vd:issueNumber>
	        |<vd:issueDate>"+ ГМ.Преобразовать_Дата_в_Строка(док.УпдДата) +"</vd:issueDate>
    	    |<vd:type>23</vd:type>
        	|<vd:relationshipType>1</vd:relationshipType>
	        |</vd:relatedDocument>";
        КонецЕсли;
		
		//!!! UUID на основании аннулированного ВСД в ТЧ документа
		Док.ВыбратьСтроки();
		Пока Док.ПолучитьСтроку() = 1 Цикл
			Если ПустоеЗначение(Док.СвязанныйДокументВСД) = 0 Тогда
				СвязанныеДокументы = СвязанныеДокументы+"
				|<vd:relatedDocument for='_"+ док.НомерСтроки +"'>
	    	    |<vd:issueNumber>"+СокрЛП( док.СвязанныйДокументВСД.UUID )+"</vd:issueNumber>
				|<vd:issueDate>"+ ГМ.Преобразовать_Дата_в_Строка(док.СвязанныйДокументВСД.ДатаДок) +"</vd:issueDate>
	    	    |<vd:type>11</vd:type>
	        	|<vd:relationshipType>2</vd:relationshipType>
		        |	     </vd:relatedDocument>";
			КонецЕсли;
		КонецЦикла;
		
		Если ПустоеЗначение(СвязанныеДокументы) = 0 Тогда
			ЗапросПодвал = ЗапросПодвал+СвязанныеДокументы;
		КонецЕсли;

	ЗапросПодвал = ЗапросПодвал+ "
		|</vd:accompanyingForms>
        |</merc:delivery>
		|</merc:prepareOutgoingConsignmentRequest>
		|</apl:data>
		|</apl:application>
		|</apldef:submitApplicationRequest>
		|</SOAP-ENV:Body>
		|</SOAP-ENV:Envelope>
		|";






	Запрос = Запрос_Шапка+
		delivery+
		consignment+
//		accompanyingForms_Шапка+
//		vetCertificate+
		ЗапросПодвал;

	Возврат Запрос;
КонецФункции

Функция Проверка_ВСД2_транзакция(док) Экспорт
	//{Проверим на переопределение функции, Если переопределена вызовем из перекрывающего модуля
	ОбъектВыполнения = ПроверитьПереопределениеФункции("Проверка_ВСД2_транзакция");
	Если ПустоеЗначение(ОбъектВыполнения)=0 Тогда
		Возврат ОбъектВыполнения.Проверка_ВСД2_транзакция(док);
	КонецЕсли;
	//}

	//стандартное поведение функции
	Результат = 1; //все в порядке

	//Если ( ПустоеЗначение( док.applicationID ) = 0 ) И ( СокрЛП(док.Статус) <> "REJECTED") Тогда
	//    Сообщить("Запрос отправлен, повторная отправка запроса запрещена","!!");
	//	Возврат 0;
	//КонецЕсли;	
	
	Если ПустоеЗначение(док.Отправитель_ХозСубъект.GUID)=1 Тогда
		Предупреждение("Не указан Отправитель_ХозСубъект.GUID Выполните синхронизацию справочников","!");
		Результат = 0;
	КонецЕсли;

	Если ПустоеЗначение(док.Отправитель_Площадка.GUID)=1 Тогда
		Предупреждение("Не указан Отправитель_Площадка.GUID Выполните синхронизацию справочников","!");
		Результат = 0;
	КонецЕсли;
	Если ПустоеЗначение(док.Получатель_ХозСубъект.GUID)=1 Тогда
		Предупреждение("Не указан Получатель_ХозСубъект.GUID Выполните синхронизацию справочников","!");
		Результат = 0;
	КонецЕсли;
	Если ПустоеЗначение(док.Получатель_Площадка.GUID)=1 Тогда
		Предупреждение("Не указан Получатель_Площадка.GUID Выполните синхронизацию справочников","!");
		Результат = 0;
	КонецЕсли;
	Если ( док.Отправитель_ХозСубъект.GUID = док.Получатель_ХозСубъект.GUID ) И 
		 ( док.Отправитель_Площадка.GUID = док.Получатель_Площадка.GUID) Тогда
		Предупреждение("Отправитель И Получатель совпадают, они должны отличаться!","!");
		Результат = 0;
	КонецЕсли;


	Если ПустоеЗначение(док.номерАвто)=1 Тогда
		Предупреждение("Не указан Госномер Автомобиля","!");
		Результат = 0;
	КонецЕсли;

	Если ПустоеЗначение(док.НомерЗаказаПокупателя)=0 Тогда
		Если ПустоеЗначение(док.ДатаЗаказаПокупателя)=1 Тогда
			Предупреждение("Не указан ДатаЗаказаПокупателя","!");
			Результат = 0;
		КонецЕсли;
	КонецЕсли;
	Если ПустоеЗначение(док.ТОРГ12Номер)=0 Тогда
		Если ПустоеЗначение(док.ТОРГ12Дата)=1 Тогда
			Предупреждение("Не указан ТОРГ12Дата","!");
			Результат = 0;
		КонецЕсли;
	КонецЕсли;
	Если ПустоеЗначение(док.ТтнНомер)=1 Тогда
		Предупреждение("Не указан ТТН Номер","!");
		Результат = 0;
		Если ПустоеЗначение(док.ТТНДата)=1 Тогда
			Предупреждение("Не указан ТТН Дата","!");
			Результат = 0;
		КонецЕсли;
	КонецЕсли;
	
	Док.ВыбратьСтроки();
	Пока Док.ПолучитьСтроку() = 1 Цикл

		Если ПустоеЗначение(док.РезультатыИсследований)=1 Тогда
			Предупреждение("Не указаны Результаты Исследований  в строке "+док.НомерСтроки,"!");
			Результат = 0;
		КонецЕсли;
	
		Если ПустоеЗначение(док.Цель)=1 Тогда
			Предупреждение("Не указана Цель перевозки в строке "+док.НомерСтроки,"!");
			Результат = 0;
		КонецЕсли;

		Если ПустоеЗначение(док.Партия.GUID)=1 Тогда
			Предупреждение("Не указан Партия.GUID Выполните синхронизацию справочников","!");
			Результат = 0;
		КонецЕсли;
		//Если ПустоеЗначение(док.Партия.ВсдДата)=1 Тогда
		//	Сообщить("Не указан Партия.ВсдДата","!");
		//	Результат = 0;
		//КонецЕсли;
		//Если ПустоеЗначение(док.Партия.ФормаУпаковки)=1 Тогда
		//	Сообщить("Не указан Партия.ФормаУпаковки","!");
		//	Результат = 0;
		//КонецЕсли;
		//Если ПустоеЗначение(док.ФормаУпаковки)=1 Тогда
		//	Сообщить("Не указан ФормаУпаковки","!");
		//	Результат = 0;
		//КонецЕсли;
		Если  док.Количество=0 Тогда
			Предупреждение("не указано Количество в строке "+док.НомерСтроки,"!");
			Результат = 0;
		КонецЕсли;
		//Если док.КоличествоМест =0 Тогда
		//	Сообщить("не указано Количество коробов"+док,"!");
		//	Результат = 0;
		//КонецЕсли;

		Если ПустоеЗначение(док.Партия.Продукция.Тип)=1 Тогда
			Предупреждение("Не указан Партия.Продукция.Тип","!");
			Результат = 0;
		КонецЕсли;

		Если ГМ.ПолучитьКонстанту("ПроверятьМаркировку")=1 Тогда 
			//проверка маркировки
			ФормаУпак=0; КолвоУпак=0; ЗначениеМаркировки=0;
			Для а=1 По 6 Цикл
				ФормаУпак = ФормаУпак + (1 - ПустоеЗначение( док.ПолучитьАтрибут( "ФормаУпак"+а ) ) );
				КолвоУпак = КолвоУпак + (1 - ПустоеЗначение( док.ПолучитьАтрибут( "КолвоУпак"+а ) ) );
				Для б=1 По 2 Цикл
					//КлассМаркировки = КлассМаркировки + (1 - ПустоеЗначение( док.ПолучитьАтрибут( "КлассМаркировки"+б+"_"+а ) ) );
					ЗначениеМаркировки = ЗначениеМаркировки + (1 - ПустоеЗначение( док.ПолучитьАтрибут( "ЗначениеМаркировки"+б+"_"+а ) ) );
				КонецЦикла;
			КонецЦикла;		
			Если ( ФормаУпак = 0 ) Тогда
				Сообщить("Не заполнена Форма Упаковки ( 6 уровеней упаковки)","!");
				Результат = 0;
			КонецЕсли;
			Если ( КолвоУпак = 0 ) Тогда
				Сообщить("Не заполнено Количество Упаковки ( 6 уровеней упаковки)","!");
				Результат = 0;
			КонецЕсли;
			Если ( ЗначениеМаркировки = 0 ) Тогда
				Сообщить("Не заполнена Маркировка Упаковки ( 6 уровеней упаковки)","!");
				Результат = 0;
			КонецЕсли;
		КонецЕсли;		
		
		Если (ГМ.ПолучитьКонстанту("ПарамПроверятьСрокГодности")=1) И (док.Некачественный=0) Тогда
			
			Если ПустоеЗначение( док.Партия.ДатаСрокГодности1 )=1 Тогда 
				Сообщить("Не указан Срок годности в строке "+док.НомерСтроки ,"!");
				Результат = 0;
			КонецЕсли;			
			
			Если ПустоеЗначение(док.Партия.ДатаСрокГодности2 )=1 Тогда
				СрокГодности1 = ГМ.Преобразовать_СтрокаДата_в_Дата( док.Партия.ДатаСрокГодности1 );
				СрокГодностиКоличествоДней = ГМ.ПолучитьКонстанту("ПарамПроверятьСрокГодностиКоличествоДней");
				Если ( СрокГодности1 - СрокГодностиКоличествоДней)<= ТекущаяДата() Тогда
					Сообщить("Выбранна просроченная партия По срокам годности (менее "+ СрокГодностиКоличествоДней +" дней до окончания срока) в строке "+док.НомерСтроки,"!");
					Результат = 0;
				КонецЕсли;		
			Иначе
				СрокГодности2 = ГМ.Преобразовать_СтрокаДата_в_Дата( док.Партия.ДатаСрокГодности2 );
				СрокГодностиКоличествоДней = ГМ.ПолучитьКонстанту("ПарамПроверятьСрокГодностиКоличествоДней");
				Если ( СрокГодности2 - СрокГодностиКоличествоДней)<= ТекущаяДата() Тогда
					Сообщить("Выбранна просроченная партия По срокам годности (менее "+ СрокГодностиКоличествоДней +" дней до окончания срока) в строке "+док.НомерСтроки,"!");
					Результат = 0;
				Иначе
					СрокГодности1 = ГМ.Преобразовать_СтрокаДата_в_Дата( док.Партия.ДатаСрокГодности1 );
					СрокГодностиКоличествоДней = ГМ.ПолучитьКонстанту("ПарамПроверятьСрокГодностиКоличествоДней");
					Если ( СрокГодности1 - СрокГодностиКоличествоДней)<= ТекущаяДата() Тогда
						Сообщить("Выбранна просроченная партия По срокам годности (менее "+ СрокГодностиКоличествоДней +" дней до окончания срока) в строке "+док.НомерСтроки,"!");
						Результат = 0;
					КонецЕсли;		
				КонецЕсли;		
			КонецЕсли;
				
			Если док.Партия.ДатаИзготовления1=док.Партия.ДатаСрокГодности1 Тогда
				Сообщить("указаны неверные даты изготовления И срока годности в строке "+док.НомерСтроки,"!");
				Результат = 0;
			КонецЕсли;			

	        ДатаИзготовления1=ГМ.Преобразовать_СтрокаДата_в_Дата( док.Партия.ДатаИзготовления1 );
	        Если ДатаИзготовления1>ТекущаяДата() Тогда
	            Сообщить("указана БУДУЩАЯ дата изготовления в строке "+док.НомерСтроки,"!");
	            Результат = 0;
	        КонецЕсли;			
		КонецЕсли;	
		
	КонецЦикла;

	Если ПустоеЗначение(док.ТермическоеСостояние)=1 Тогда
		Предупреждение("В "+док+" не указано ТермическоеСостояние","!");
		Результат = 0;
	КонецЕсли;

	
	Возврат Результат;
КонецФункции


Функция Отправить_ВСД2_транзакция(док, тзУсловияПеревозки="", ПолучитьОтветВетис=1)  Экспорт
	//{Проверим на переопределение функции, Если переопределена вызовем из перекрывающего модуля
	ОбъектВыполнения = ПроверитьПереопределениеФункции("Отправить_ВСД2_транзакция");
	Если ПустоеЗначение(ОбъектВыполнения)=0 Тогда
		Возврат ОбъектВыполнения.Отправить_ВСД2_транзакция(док, тзУсловияПеревозки);
	КонецЕсли;
	//}

	t1 = ГМ.Старт();
	
	//стандартное поведение функции
	Если Проверка_ВСД2_транзакция(док)=0 Тогда
		//ДокОбъект = СоздатьОбъект("Документ");
		//ДокОбъект.НайтиДокумент(док);
		//ДокОбъект.applicationID = "";
		//ДокОбъект.Статус="";
		//ДокОбъект.Записать();
		Возврат 0;
	КонецЕсли;

	//ЖД - решение проблемы загрузки партий из ответа в правильную площадку при наличии неск. площадок у ХС
	ГМ.СписокКонстант.Установить("Отправитель_Площадка",док.Отправитель_Площадка);

	Попытка
		Если ПустоеЗначение(тзУсловияПеревозки) = 1 Тогда  // не переданы в параметре, посмотрим в документе
			ИмяФайла = СокрЛП(док.ИмяФайлаРегионализация);
			Если ФС.СуществуетФайл(ИмяФайла) = 1 Тогда
				ТзУсловияПеревозки = ЗначениеИзФайла(ИмяФайла);
			КонецЕсли;
		КонецЕсли;
	Исключение	КонецПопытки;

	ЗапросXML = ВСД2_транзакция_ЗапросXML(док,тзУсловияПеревозки);
	
	appID = ОтправитьЗапрос2(док, ЗапросXML, ПолучитьОтветВетис);

	ГМ.Финиш(t1, Сам(), "Отправить_ВСД2_транзакция", док);
	
	Возврат appID;

КонецФункции


Функция ОтправитьВсе_ВСД2_Транзакция(СписокВСД="",НачДата, КонДата) Экспорт
	//{Проверим на переопределение функции, Если переопределена вызовем из перекрывающего модуля
	ОбъектВыполнения = ПроверитьПереопределениеФункции("ОтправитьВсе_ВСД2_Транзакция");
	Если ПустоеЗначение(ОбъектВыполнения)=0 Тогда
		Возврат ОбъектВыполнения.ОтправитьВсе_ВСД2_Транзакция(СписокВСД,НачДата, КонДата);
	КонецЕсли;
	//}

	//стандартное поведение функции
	Состояние("Меркурий: отправка ВСД2 исходящие");
	ГМ.СообщитьИнфо("Начало отправки документов","", "i");

	t1 = ГМ.Старт();
	
	Если ПустоеЗначение(СписокВСД)=1 Тогда
		ГМ.СообщитьИнфо("Не выбраны ВСД для отправки");
		Возврат 0;
	КонецЕсли;

	КолвоДок=0;
	Для Д=1 По СписокВСД.РазмерСписка() Цикл
		ВСД = СписокВСД.ПолучитьЗначение(Д);

		Если ГМ.СтатусЗакрыт( ВСД )=1 Тогда
			продолжить;
		КонецЕсли;
		Если ВСД.ПометкаУдаления()=1 Тогда
			Продолжить;
		КонецЕсли;

		ГМ.СообщитьИнфо("Отправка "+ВСД+" "+ВСД.Статус, ВСД);

		Отправить_ВСД2_транзакция( ВСД );
		КолвоДок=КолвоДок+1;

	КонецЦикла;
	
	ГМ.Финиш(t1, Сам(), "ОтправитьВсе_ВСД2_Транзакция", , , " Количество="+КолвоДок );

	ГМ.СообщитьИнфо("Отправка документов завершена","");
КонецФункции

Функция ОтправитьВсе_ВСД2_Транзакция_Параллельно(СписокВСД="",НачДата, КонДата) Экспорт

	//стандартное поведение функции
	Состояние("Меркурий: отправка ВСД2 исходящие");
	ГМ.СообщитьИнфо("Начало отправки документов","");

	t1 = ГМ.Старт();

	Если ПустоеЗначение(СписокВСД)=1 Тогда
		ГМ.СообщитьИнфо("Не выбраны ВСД для отправки");
		Возврат 0;
	КонецЕсли;

	// проверка списка, удаляем лишние
	А = СписокВСД.РазмерСписка();
	Пока А>0 Цикл
		ВСД = СписокВСД.ПолучитьЗначение( А );
		Если ( ГМ.СтатусЗакрыт( ВСД )=1 ) ИЛИ 
			 (ВСД.ПометкаУдаления()=1) Тогда  
			СписокВСД.УдалитьЗначение( А );
		КонецЕсли;
		А=А-1;
	КонецЦикла;
	
	// запрос условий перевозки
	ОсновнойРегион = ГМ.ПолучитьКонстанту("Регион");
	Для Д=1 По СписокВСД.РазмерСписка() Цикл
		ВСД = СписокВСД.ПолучитьЗначение(Д);
		
		Если (ПустоеЗначение(ВСД.Получатель_Площадка.Регион)=0 ) И
			 (ВСД.Получатель_Площадка.Регион <> ОсновнойРегион) И
			 ( ПустоеЗначение( ВСД.ИмяФайлаРегионализация) = 1 ) Тогда
			 	
			УсловияПеревозки_Получить( ВСД, 0, 1 );
			
		КонецЕсли;		

	КонецЦикла;

	ГМ.Пауза( ); 
	
	// ответ условий перевозки
	ОсновнойРегион = ГМ.ПолучитьКонстанту("Регион");
	Для Д=1 По СписокВСД.РазмерСписка() Цикл
		ВСД = СписокВСД.ПолучитьЗначение(Д);

		Если ( ПустоеЗначение(ВСД.Получатель_Площадка.Регион)=0 ) И		
			 ( ВСД.Получатель_Площадка.Регион <> ОсновнойРегион ) И
			 ( ПустоеЗначение( ВСД.ИмяФайлаРегионализация) = 1 ) Тогда
			Если ПустоеЗначение( ВСД.applicationID ) = 0 Тогда				
				
				тзУсловияПеревозки = УсловияПеревозки_Ответ( ВСД.applicationID, ВСД, 1, 0);				
				
			КонецЕсли;
		КонецЕсли;		
	КонецЦикла;
	
	// отправка
	Для Д=1 По СписокВСД.РазмерСписка() Цикл
		ВСД = СписокВСД.ПолучитьЗначение(Д);
		
		Отправить_ВСД2_транзакция( ВСД, "", 0);

	КонецЦикла;

	ГМ.Пауза( ); 
	
	// получаем ответ
	Для Д=1 По СписокВСД.РазмерСписка() Цикл
		ВСД = СписокВСД.ПолучитьЗначение(Д);

		спПараметры = СоздатьОбъект("СписокЗначений");	
		спПараметры.Установить("applicationID", ВСД.applicationID);
		спПараметры.Установить("докСсылка", ВСД );		
		
		Результат = ПолучитьРезультат_ВСД_2( спПараметры, 0 );
		
		ГМ.СообщитьИнфо("Результат = "+Результат, ВСД);
		Для А=1 По 10 Цикл
			Если (Результат="IN_PROCESS") Тогда
				Результат = ПолучитьРезультат_ВСД_2( спПараметры, 1 );
			КонецЕсли;
		КонецЦикла;

	КонецЦикла;
		
	ГМ.Финиш(t1, Сам(), "ОтправитьВсе_ВСД2_Транзакция_Параллельно", , , " Количество="+СписокВСД.РазмерСписка() );
	
	ГМ.СообщитьИнфо("Отправка документов завершена","");
	Возврат 1;
	
КонецФункции


Функция ОформитьВСД2(РеализацияТоваров) Экспорт
	КонтФормы = "";
	ОткрытьФорму("Документ.ВСД2_Транзакция",КонтФормы, РеализацияТоваров);
КонецФункции


Функция ВСД2_транзакция_ПолучитьОтветВЕТИС( док ) Экспорт

	Если (Док.Вид() = "Реализация") ИЛИ 
		 (Док.Вид() = "ПеремещениеТМЦ") ИЛИ 
		 (Док.Вид() = ГМ.СписокКонстант.Получить("НазваниеВидаДокументаРеализация")) Тогда 
		 	
		Док_ВСД_транзакция = ГМ.Выбрать_ВСД_транзакция(Док);
		тзВСД = ГМ.Выбрать_ВСД(Док_ВСД_транзакция);

	Иначе
		Док_ВСД_транзакция = док;
	КонецЕсли;

	спПараметры = СоздатьОбъект("СписокЗначений");	
	спПараметры.Установить("applicationID", Док_ВСД_транзакция.applicationID );
	
	ПолучитьРезультат_ВСД_2( спПараметры );

КонецФункции
//	 }

// { ========================== ВСД2_производство ==========================
Функция ВСД2_производство_ЗапросXML(док)
	//док = СоздатьОбъект("Документ.ВСД_производство");
	//{Проверим на переопределение функции, Если переопределена вызовем из перекрывающего модуля
	ОбъектВыполнения = ПроверитьПереопределениеФункции("ВСД2_производство_ЗапросXML");
	Если ПустоеЗначение(ОбъектВыполнения)=0 Тогда
		Возврат ОбъектВыполнения.ВСД2_производство_ЗапросXML(док);
	КонецЕсли;
	//}

	Запрос ="<SOAP-ENV:Envelope
		|xmlns:dt='http://api.vetrf.ru/schema/cdm/dictionary/v2'
		|xmlns:bs='http://api.vetrf.ru/schema/cdm/base'
		|xmlns:merc='http://api.vetrf.ru/schema/cdm/mercury/g2b/applications/v2'
		|xmlns:apldef='http://api.vetrf.ru/schema/cdm/application/ws-definitions'
		|xmlns:apl='http://api.vetrf.ru/schema/cdm/application'
		|xmlns:vd='http://api.vetrf.ru/schema/cdm/mercury/vet-document/v2'
		|xmlns:SOAP-ENV='http://schemas.xmlsoap.org/soap/envelope/'>
		|<SOAP-ENV:Header/>
		|<SOAP-ENV:Body>
		|<apldef:submitApplicationRequest>
		|<apldef:apiKey>"+ СокрЛП( ГМ.СписокКонстант.Получить("param_api_key") ) +"</apldef:apiKey>
		|<apl:application>
		|<apl:serviceId>mercury-g2b.service:2.0</apl:serviceId>
		|<apl:issuerId>"+ СокрЛП( ГМ.СписокКонстант.Получить("param_issuer_id") ) +"</apl:issuerId>
		|<apl:issueDate>" + ГМ.Преобразовать_Дата_в_Строка(док.ДатаДок, "T00:00:00") + "</apl:issueDate>
		|<apl:data>
		|<merc:registerProductionOperationRequest>
		|<merc:localTransactionId>" + СокрЛП(док.НомерДок) + "</merc:localTransactionId>
		|<merc:initiator>
		|<vd:login>"+ СокрЛП( ГМ.СписокКонстант.Получить("param_intiator_login") ) +"</vd:login>
		|</merc:initiator>
		|<merc:enterprise>
		|<bs:guid>"+ СокрЛП(док.Производитель_Площадка.GUID) +"</bs:guid>
		|</merc:enterprise>
		|<merc:productionOperation> ";

// НЗП
		Если (ПустоеЗначение(Док.operationId) = 0) Тогда
			Запрос=Запрос+"
			|<vd:operationId>"+ СокрЛП(Док.operationId) +"</vd:operationId>
			|";
		КонецЕсли;

		Док.ВыбратьСтроки();
		Пока Док.ПолучитьСтроку() = 1 Цикл
			//порядок добавления имеет значение -
			// 1. rawBatch = списание
			// Уровни упаковки можно использовать, пока без упаковки
			Если док.ВидДвижения=2 Тогда
				Запрос=Запрос+"
				| <vd:rawBatch>
				|<vd:sourceStockEntry>
				| <bs:guid>" + СокрЛП(док.Партия.guid) + "</bs:guid>
				|</vd:sourceStockEntry>
				|<vd:volume>"+ док.Количество +"</vd:volume>
				|<vd:unit>
				| <bs:guid>"+ СокрЛП(док.ЕдиницаИзмерения.Guid) +"</bs:guid>
				|</vd:unit>
				| </vd:rawBatch>
				|";
			КонецЕсли;
		КонецЦикла;



	Док.ВыбратьСтроки();
	Пока Док.ПолучитьСтроку() = 1 Цикл
		Если док.ВидДвижения=1 Тогда // Выпуск
	
			Запрос=Запрос+"
				|<vd:productiveBatch>
				|<vd:productType>"+ док.Продукция.Тип +"</vd:productType>
				|<vd:product>
				|<bs:guid>"+ СокрЛП(док.Продукция.GUID) +"</bs:guid>
				|</vd:product>
				|<vd:subProduct>
				|<bs:guid>"+ СокрЛП(док.ВидПродукции.GUID) +"</bs:guid>
				|</vd:subProduct>
				|<vd:productItem>
				// Обязательно должно быть заполнено хотя бы одно из полей: либо UUID, либо GUID. При указании обоих полей приоритет у UUID.
				|<bs:guid>"+ СокрЛП(док.Продукция_Элемент.GUID) +"</bs:guid>
				|</vd:productItem>
				|<vd:volume>"+ Док.Количество +"</vd:volume>
				|<vd:unit>
				|<bs:guid>"+ СокрЛП(док.ЕдиницаИзмерения.GUID) +"</bs:guid>
				|</vd:unit>";
		//		|<vd:dateOfProduction> ";
				//Может быть указана датой ИЛИ диапазоном дат. В случае точной даты выработки указывается только первая компонента - firstDate.
				//Для скоропортящейся продукции обязательны к указанию год/месяц/день И могут быть указаны час/минута.
				//Для не скоропортящейся продукции обязательны к указанию год/месяц И может быть указан день.
				//Для не скоропортящейся продукции дата выработки И дата окончания срока годности должны быть заданы в одном формате
				//	(например, Если дата выработки указывается как год/месяц, то указать дату окончания срока годности как год/месяц/день нельзя).
	
			Запрос = Запрос + ГМ.Изготовление_СрокГодности_в_XML(док, "vd" );
	
			//Уникальный идентификатор производственной партии продукции.
			Запрос=Запрос+"
			|<vd:batchID>"+ СокрЛП( док.НомерПартии ) +"</vd:batchID>
			|<vd:perishable>"+ ГМ.булево(док.Скоропортящийся)+"</vd:perishable>
			|<vd:lowGradeCargo>"+ ГМ.булево(док.Некачественный)+"</vd:lowGradeCargo>
			|";
		
				
			Запрос = Запрос + МаркировкаДокументаВЗапрос( Док );

			Запрос=Запрос+"
			|</vd:productiveBatch>";
		КонецЕсли; // производство


	КонецЦикла;
    // завершение операции
	Запрос=Запрос+"
	|<vd:finalizeOperation>"+ГМ.булево(док.ЗавершитьОперацию)+"</vd:finalizeOperation>";


	Запрос=Запрос+"
	|</merc:productionOperation>
	|<merc:vetDocument>
	|<vd:authentication>
	|<vd:cargoInspected>"+ ГМ.булево(док.cargoInspected) +"</vd:cargoInspected>";
	//				cargoExpertized = Проводилась ли ветсанэкспертиза.
	//				Возможные значения: VSEFULL, VSERAW, UNFULFILLED
	Запрос=Запрос+"
	|<vd:cargoExpertized>"+док.РезультатыИсследований.Идентификатор()+"</vd:cargoExpertized>";
	Запрос = Запрос+ ?(Пустоезначение(док.особыеотметки) = 0, "<vd:specialMarks>"+ СокрЛП(док.особыеотметки) +"</vd:specialMarks>","");
	Запрос=Запрос+"
	|</vd:authentication>
	|</merc:vetDocument>
	|</merc:registerProductionOperationRequest>
	|</apl:data>
	|</apl:application>
	|</apldef:submitApplicationRequest>
	|</SOAP-ENV:Body>
	|</SOAP-ENV:Envelope>";

	Возврат Запрос;
КонецФункции

Функция Проверка_ВСД_производство(док) Экспорт
	//{Проверим на переопределение функции, Если переопределена вызовем из перекрывающего модуля
	ОбъектВыполнения = ПроверитьПереопределениеФункции("Проверка_ВСД_производство");
	Если ПустоеЗначение(ОбъектВыполнения)=0 Тогда
		Возврат ОбъектВыполнения.Проверка_ВСД_производство(док);
	КонецЕсли;
	//}

	//стандартное поведение функции
	Результат = 1; //все в порядке

	Если ( ПустоеЗначение( док.applicationID ) = 0 ) И ( СокрЛП(док.Статус) <> "REJECTED") Тогда
	    Сообщить("Запрос отправлен, повторная отправка запроса запрещена","!!");
		Возврат 0;
	КонецЕсли;	
	
	Если ПустоеЗначение(док.Производитель_ХозСубъект.GUID)=1 Тогда
		Предупреждение("Не указан Отправитель_ХозСубъект.GUID Выполните синхронизацию справочников","!");
		Результат = 0;
	КонецЕсли;

	Если ПустоеЗначение(док.Производитель_площадка.GUID)=1 Тогда
		Предупреждение("Не указан Отправитель_Площадка.GUID Выполните синхронизацию справочников","!");
		Результат = 0;
	КонецЕсли;

	Если ПустоеЗначение(док.РезультатыИсследований)=1 Тогда
		Предупреждение("Не указаны Результаты Исследований","!");
		Результат = 0;
	КонецЕсли;

	Док.ВыбратьСтроки();
	Пока Док.ПолучитьСтроку() = 1 Цикл

		//Если ПустоеЗначение(док.Продукция_Элемент.GUID)=1 Тогда
		//	Предупреждение("Не указан Продукция_Элемент.GUID Выполните синхронизацию справочников","!");
		//	Результат = 0;
		//КонецЕсли;
		Если ПустоеЗначение(док.ЕдиницаИзмерения)=1 Тогда
			Сообщить("Не указана ЕдиницаИзмерения в строке "+Док.НомерСтроки,"!");
			Результат = 0;
		КонецЕсли;
		Если док.Количество=0 Тогда
			Предупреждение("не указано Количество в строке "+док.НомерСтроки,"!");
			Результат = 0;
		КонецЕсли;

		Если ( док.ВидДвижения=1) Тогда 

			Если док.Продукция_Элемент.Производитель_Площадка <> док.Производитель_площадка Тогда
				Сообщить("в строке "+док.НомерСтроки+" Продукция производится на другой площадке!","!");
				Результат = 0;
			КонецЕсли;
			Если ( док.Продукция_Элемент.Производитель_ХозСубъект <> док.Производитель_ХозСубъект ) Тогда
				Сообщить("в строке "+док.НомерСтроки+" Ошибка MERC56534 = Хозяйствующий субъект должен являться фирмой производителем в записи продукции","!");
				Результат = 0;
			КонецЕсли;
			
	        ДатаИзготовления1=ГМ.Преобразовать_СтрокаДата_в_Дата( док.ДатаИзготовления1 );
	        Если ДатаИзготовления1>ТекущаяДата() Тогда
	            Сообщить("в строке "+док.НомерСтроки+" указана БУДУЩАЯ дата изготовления","!");
	            Результат = 0;
			КонецЕсли;			
			
		КонецЕсли;

	КонецЦикла;

	Возврат Результат;
КонецФункции


Функция Отправить_ВСД2_Производство(док) Экспорт
	//{Проверим на переопределение функции, Если переопределена вызовем из перекрывающего модуля
	ОбъектВыполнения = ПроверитьПереопределениеФункции("Отправить_ВСД2_производство");
	Если ПустоеЗначение(ОбъектВыполнения)=0 Тогда
		Возврат ОбъектВыполнения.Отправить_ВСД2_производство(док);
	КонецЕсли;
	//}

	t1 = ГМ.Старт();
	
	//стандартное поведение функции
	Если Проверка_ВСД_производство(док)=0 Тогда
		Возврат 0;
	КонецЕсли;

	//ЖД - решение проблемы загрузки партий из ответа в правильную площадку при наличии неск. площадок у ХС
	ГМ.СписокКонстант.Установить("Отправитель_Площадка",док.Производитель_площадка);


	ЗапросXML = ВСД2_производство_ЗапросXML(док);
	
	appID = ОтправитьЗапрос2(док, ЗапросXML);

	ГМ.Финиш(t1, Сам(), "Отправить_ВСД2_Производство", док);	
	
	Возврат appID;

КонецФункции

Функция ОтправитьВсе_ВСД2_Производство(СписокВСД="",НачДата, КонДата) Экспорт
	//{Проверим на переопределение функции, Если переопределена вызовем из перекрывающего модуля
	ОбъектВыполнения = ПроверитьПереопределениеФункции("ОтправитьВсе_ВСД2_Производство");
	Если ПустоеЗначение(ОбъектВыполнения)=0 Тогда
		Возврат ОбъектВыполнения.ОтправитьВсе_ВСД2_Производство(СписокВСД,НачДата, КонДата);
	КонецЕсли;
	//}

	//стандартное поведение функции
	Состояние("Меркурий: отправка ВСД Производство");
	Сообщить("Начало отправки документов","i");

	Если ПустоеЗначение(СписокВСД)=1 Тогда
		Сообщить("отправляются ВСД производство за период "+ПериодСтр(НачДата, КонДата));

		СписокВСД = СоздатьОбъект("СписокЗначений");
		Док = СоздатьОбъект("Документ.ВСД2_Производство");
		Док.ВыбратьДокументы(НачДата, КонДата);
		Пока Док.ПолучитьДокумент() = 1 Цикл

			СписокВСД.ДобавитьЗначение(Док.ТекущийДокумент());

		КонецЦикла;
	Иначе
		Сообщить("отправляются ВСД производство "+СписокВСД.РазмерСписка()+" документов");
	КонецЕсли;

	Для Д=1 По СписокВСД.РазмерСписка() Цикл
		ВСД = СписокВСД.ПолучитьЗначение(Д);

		//Если СокрЛП(ВСД.Статус)="REJECTED" Тогда
		//	Продолжить;
		//КонецЕсли;
		Если ГМ.СтатусЗакрыт( ВСД )=1 Тогда			
			продолжить;
		КонецЕсли;
		Если ВСД.ПометкаУдаления()=1 Тогда
			Продолжить;
		КонецЕсли;

		//tt1=_GetPerformanceCounter();
		Сообщить(""+ВСД+" "+ВСД.Статус);

		Отправить_ВСД2_производство( ВСД );

	КонецЦикла;

	Сообщить("Отправка документов завершена","i");
КонецФункции

//}

//{ =====================   Продукция 2.0

Функция ПолучитьРезультат_Список_Продукции(ФайлОтвета,продукцияЭлемент="") Экспорт

    objDom=СоздатьОбъект("MSXML2.DOMDocument.6.0");
    objDom.load(ФайлОтвета);

	Если ГМ.НайтиОшибки()>0 Тогда
		Возврат "";
	КонецЕсли;

    objDom.setProperty("SelectionNamespaces", "
	|xmlns:bs='http://api.vetrf.ru/schema/cdm/base'
	|xmlns:dt='http://api.vetrf.ru/schema/cdm/dictionary/v2'
	|xmlns:v2='http://api.vetrf.ru/schema/cdm/registry/ws-definitions/v2'
	|" );

	//applicationId = objDom.selectSingleNode("//*[local-name() = 'applicationId']").text ;

	productItemList = objDom.selectNodes("//*[local-name() = 'productItem']") ;

    Для i1 = 0 По productItemList.length - 1 Цикл
        productItem = productItemList.item(i1);

		active 	 = НРЕГ(productItem.selectSingleNode("bs:active").text);
		last 	 = НРЕГ(productItem.selectSingleNode("bs:last").text);

		//st  	 = productItem.selectSingleNode("bs:status").text;
		//prodType = productItem.selectSingleNode("dt:productType").text;

		СпрОбъект = СоздатьОбъект("Справочник.ВСД_Продукция_Элемент");
		Если ПустоеЗначение(продукцияЭлемент)=0 Тогда
			СпрОбъект.НайтиЭлемент(продукцияЭлемент);
		Иначе

			Если СпрОбъект.НайтиПоРеквизиту("GUID", productItem.selectSingleNode("bs:guid").text,1)=0 Тогда
				Если СпрОбъект.НайтиПоНаименованию( productItem.selectSingleNode("dt:name").text)=0 Тогда
					СпрОбъект.Новый();
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;

		//Если active="false" Тогда
		//	Продолжить;
		//КонецЕсли;

		СпрОбъект.Наименование = productItem.selectSingleNode("dt:name").text;
		СпрОбъект.Продукция = ГМ.НайтиПродукцию(productItem.selectSingleNode("dt:product").selectSingleNode("bs:guid").text);
		СпрОбъект.ВидПродукции = ГМ.НайтиВидПродукции(productItem.selectSingleNode("dt:subProduct").selectSingleNode("bs:guid").text);
		СпрОбъект.UUID = productItem.selectSingleNode("bs:uuid").text;
		СпрОбъект.GUID = productItem.selectSingleNode("bs:guid").text;
		Попытка СпрОбъект.СоответствуетГОСТу = ?(productItem.selectSingleNode("dt:correspondsToGost").text = "true",1,0); Исключение	КонецПопытки;
		Попытка СпрОбъект.Гост = productItem.selectSingleNode("dt:gost").text; Исключение	КонецПопытки;
		Попытка СпрОбъект.GTIN = productItem.selectSingleNode("dt:globalID").text; Исключение КонецПопытки;
		Попытка СпрОбъект.Артикул	 = productItem.selectSingleNode("dt:code").text; Исключение КонецПопытки;
		СпрОбъект.ТермическоеСостояние = ?( ПустоеЗначение(СпрОбъект.ТермическоеСостояние) = 1, ГМ.СписокКонстант.Получить("ТермУсловияПеревозки"), СпрОбъект.ТермическоеСостояние );
		// Фасовка
		Попытка packaging = productItem.selectSingleNode("*[local-name()='packaging']"); Исключение packaging=""	КонецПопытки;
		Попытка ФасовкаНаименование = packaging.selectSingleNode("dt:packagingType").selectSingleNode("dt:name").text; 	Исключение ФасовкаНаименование=""	КонецПопытки;
		Попытка СпрОбъект.ТипФасовки = ГМ.НайтиФормуУпаковки( packaging.selectSingleNode("dt:packagingType").selectSingleNode("bs:guid").text,ФасовкаНаименование );	Исключение 	КонецПопытки;
		Попытка	СпрОбъект.КолВоЕдиницФасовки = Число(packaging.selectSingleNode("dt:quantity").text);	Исключение	КонецПопытки;
		Попытка	СпрОбъект.ОбъемЕдиницыФасовки = Число(packaging.selectSingleNode("dt:volume").text); Исключение	КонецПопытки;
		Попытка	СпрОбъект.ЕдИзмОбъемаФасовки = ГМ.НайтиЕдИзмерения(packaging.selectSingleNode("dt:unit").selectSingleNode("bs:guid").text);	Исключение 	КонецПопытки;

		//Площадка(И) производитель(И) - Пока в 1 экземпляре
		Попытка СпрОбъект.Площадка = ГМ.НайтиПлощадку(productItem.selectSingleNode("dt:producing").selectSingleNode("dt:location").selectSingleNode("bs:guid").text);  Исключение	КонецПопытки;
			//ГМ.ЗагрузитьПлощадкуПоGUID(productItem.selectSingleNode("dt:producing").selectSingleNode("dt:location").selectSingleNode("bs:guid").text);  Исключение	КонецПопытки;
			
		//Попытка СпрОбъект.Площадка = ГМ.НайтиПлощадку(productItem.selectSingleNode("dt:producing").selectSingleNode("dt:location").selectSingleNode("bs:guid").text);  Исключение	КонецПопытки;
		Попытка СпрОбъект.Производитель_ХозСубъект = ГМ.НайтиХСпоGUID(productItem.selectSingleNode("dt:producer").selectSingleNode("bs:guid").text);  Исключение	КонецПопытки;
		Попытка СпрОбъект.Собственник_ХозСубъект = ГМ.НайтиХСпоGUID(productItem.selectSingleNode("dt:tmOwner").selectSingleNode("bs:guid").text);  Исключение	КонецПопытки;
		Попытка	СпрОбъект.Производитель_Площадка = СпрОбъект.Площадка; Исключение КонецПопытки;
			
		Попытка СпрОбъект.active = ?( active="true",1,0) Исключение	КонецПопытки;
		Попытка СпрОбъект.last = ?( last="true",1,0) Исключение	КонецПопытки;
			
		СпрОбъект.Записать();

		ГМ.СообщитьИнфо("Записан ["+СпрОбъект.ТекущийЭлемент()+"] uuid =["+СпрОбъект.uuid+"]", СпрОбъект.ТекущийЭлемент());
		
		//Если ((last="true") И (active="false")) Тогда
		Если (active="false") Тогда
			//СпрОбъект.GUID = "";
			//СпрОбъект.Записать();
			СпрОбъект.Удалить(0);
			ГМ.СообщитьИнфо("Удален ["+СпрОбъект.ТекущийЭлемент()+"] uuid =["+СпрОбъект.uuid+"]", СпрОбъект.ТекущийЭлемент());
		Иначе
			СпрОбъект.СнятьПометкуУдаления();
		КонецЕсли;		
	КонецЦикла;

	ГМ.УдалитьXML( ГМ.Компонента.LogFilename );

	Возврат "";
КонецФункции


Функция Продукция_Инфо_ЗапросXML( продукция )
	Запрос = "
	|<soapenv:Envelope
	|xmlns:soapenv='http://schemas.xmlsoap.org/soap/envelope/'
	|xmlns:ws='http://api.vetrf.ru/schema/cdm/registry/ws-definitions/v2'
	|xmlns:bs='http://api.vetrf.ru/schema/cdm/base'>
	| <soapenv:Header/>
	| <soapenv:Body>
	|<ws:getProductItemByGuidRequest>
	| <bs:guid>"+ СокрЛП(продукция.GUID) +"</bs:guid>
	|</ws:getProductItemByGuidRequest>
	| </soapenv:Body>
	|</soapenv:Envelope>
	|";

	Возврат запрос;
КонецФункции

Процедура Получить_Инфо_Продукции( Продукция ) Экспорт
	Если ПустоеЗначение(Продукция) = 1 Тогда
	    Сообщить("Не указана Продукция для получения инфо");
		Возврат;
	КонецЕсли;
	Если ПустоеЗначение(Продукция.GUID) = 1 Тогда
	    Сообщить("У Продукции "+Продукция+" отсутствует GUID, запрос невозможен");
		Возврат;
	КонецЕсли;

	ЗапросXML = Продукция_Инфо_ЗапросXML( Продукция );

	метод = ГМ.Компонента.GetMethod();
    метод.Service = "platform/services/2.0/ProductService";
    метод.Action = "GetProductItemByGuid";

	appID = ГМ.ОтправитьЗапросSoap(ЗапросXML, метод);

	Результат = ПолучитьРезультат_Список_Продукции(ГМ.Компонента.LogFilename,Продукция);

	ГМ.УдалитьXML( ГМ.Компонента.LogFilename);

КонецПроцедуры

Функция  ПолучитьРезультат_Изменить_Продукцию(продукцияЭлемент, appID) Экспорт

	Если ПустоеЗначение(appID)=1 Тогда
		Сообщить("Пустая ссылка applicationID");
		Возврат "Пустая ссылка applicationID";
	КонецЕсли;

	ГМ.Пауза( );

	Сообщить("Отправляем запрос ["+продукцияЭлемент+"]" );
	//Результат = ГМ.Компонента.SendRequestResult( appID );
	Результат = ГМ.ПолучитьРезультатСервер( appID );

	Если ГМ.НайтиОшибки()>0 Тогда
		Возврат Результат;
	КонецЕсли;

	ПолучитьРезультат_Список_Продукции(ГМ.Компонента.LogFilename,продукцияЭлемент);
	ГМ.УдалитьXML( ГМ.Компонента.LogFilename );

	Возврат Результат;
КонецФункции

// документация - http://help.vetrf.ru/wiki/ModifyProducerStockListOperation_v2.0
Функция Изменить_Продукцию_ЗапросXML(продукцияЭлемент,RegisterModificationType)

	//{Проверим на переопределение функции, Если переопределена вызовем из перекрывающего модуля
	ОбъектВыполнения = ПроверитьПереопределениеФункции("Изменить_Продукцию_ЗапросXML");
	Если ПустоеЗначение(ОбъектВыполнения)=0 Тогда
		Возврат ОбъектВыполнения.Изменить_Продукцию_ЗапросXML(продукцияЭлемент,RegisterModificationType);
	КонецЕсли;
	//}

	Запрос ="<SOAP-ENV:Envelope
		|xmlns:dt='http://api.vetrf.ru/schema/cdm/dictionary/v2'
		|xmlns:bs='http://api.vetrf.ru/schema/cdm/base'
		|xmlns:merc='http://api.vetrf.ru/schema/cdm/mercury/g2b/applications/v2'
		|xmlns:apldef='http://api.vetrf.ru/schema/cdm/application/ws-definitions'
		|xmlns:apl='http://api.vetrf.ru/schema/cdm/application'
		|xmlns:vd='http://api.vetrf.ru/schema/cdm/mercury/vet-document/v2'
		|xmlns:SOAP-ENV='http://schemas.xmlsoap.org/soap/envelope/'>
		|<SOAP-ENV:Header/>
		|<SOAP-ENV:Body>
		|<apldef:submitApplicationRequest>
		|<apldef:apiKey>"+ СокрЛП( ГМ.СписокКонстант.Получить("param_api_key") ) +"</apldef:apiKey>
		|<apl:application>
		|<apl:serviceId>mercury-g2b.service:2.0</apl:serviceId>
		|<apl:issuerId>"+ СокрЛП( ГМ.СписокКонстант.Получить("param_issuer_id") ) +"</apl:issuerId>
		|<apl:issueDate>" + ГМ.Преобразовать_Дата_в_Строка(ТекущаяДата(), "T00:00:00") + "</apl:issueDate>
		|<apl:data>
		|<merc:modifyProducerStockListRequest>
		|<merc:localTransactionId>" + "[GUID]" + "</merc:localTransactionId>
		|<merc:initiator>
		|<vd:login>"+ СокрЛП( ГМ.СписокКонстант.Получить("param_intiator_login") ) +"</vd:login>
		|</merc:initiator>
		|<merc:modificationOperation>
	    |<vd:type>"+RegisterModificationType+"</vd:type>
		|";

		Если RegisterModificationType = "DELETE" Тогда
			Запрос = Запрос + "
		    |<vd:affectedList>
		    |<dt:productItem>";
		    Если ПустоеЗначение(продукцияЭлемент.uuid) = 0 Тогда
				Запрос = Запрос + "
	    		|<bs:uuid>"+СокрЛП(продукцияЭлемент.uuid)+"</bs:uuid>";
			КонецЕсли;

			Запрос = Запрос + "
			|</dt:productItem>
		    |</vd:affectedList>";

		Иначе
			Запрос = Запрос + "
		    |<vd:resultingList>
		    |<dt:productItem>";

			//При Модификации
	//	    Если ПустоеЗначение(продукцияЭлемент.guid) = 0 Тогда
	//			Запрос = Запрос + "
	//    		|<dt:guid>"+СокрЛП(продукцияЭлемент.guid)+"</dt:guid>";
	//		КонецЕсли;

		    Если ПустоеЗначение(продукцияЭлемент.uuid) = 0 Тогда
				Запрос = Запрос + "
	    		|<bs:uuid>"+СокрЛП(продукцияЭлемент.uuid)+"</bs:uuid>";
			КонецЕсли;


			Попытка //Нет пока такого реквизита в справочнике
			    Если ПустоеЗначение(продукцияЭлемент.GTIN) = 0 Тогда
					Запрос = Запрос + "
	    			|<dt:globalID>"+ продукцияЭлемент.GTIN +"</dt:globalID>";
				КонецЕсли;
			Исключение КонецПопытки;

			Название = ЗаменитьСпецСимволы( продукцияЭлемент.Наименование );
			Запрос = Запрос + "
		    |<dt:name>"+СокрЛП( Название )+"</dt:name>";
		    Если ПустоеЗначение(продукцияЭлемент.Артикул) = 0 Тогда
				Запрос = Запрос + "
	    		|<dt:code>"+СокрЛП(продукцияЭлемент.Артикул)+"</dt:code>";
			КонецЕсли;
			Запрос = Запрос + "
		    |<dt:productType>"+продукцияЭлемент.Продукция.Тип+"</dt:productType>
	    	|<dt:product>
		    |<bs:guid>"+Сокрлп(продукцияЭлемент.Продукция.GUID)+"</bs:guid>
	    	|</dt:product>
		    |<dt:subProduct>
	    	|<bs:guid>"+Сокрлп(продукцияЭлемент.ВидПродукции.GUID)+"</bs:guid>
		    |</dt:subProduct>
			|<dt:correspondsToGost>"+ГМ.Булево(продукцияЭлемент.СоответствуетГОСТу)+"</dt:correspondsToGost>";
		    Если продукцияЭлемент.СоответствуетГОСТу = 1 Тогда
				Запрос = Запрос + "
		    	|<dt:gost>"+СокрЛП(продукцияЭлемент.Гост)+"</dt:gost>";
			КонецЕсли;
			// ХС Производитель И владелец ТМ
			Запрос = Запрос + "
		    |<dt:producer>
	    	|<bs:guid>"+Сокрлп(ГМ.СписокКонстант.Получить("Отправитель_ХозСубъект").GUID)+"</bs:guid>
		    |</dt:producer>
		    |<dt:tmOwner>
	    	|<bs:guid>"+Сокрлп(ГМ.СписокКонстант.Получить("Отправитель_ХозСубъект").GUID)+"</bs:guid>
		    |</dt:tmOwner>";


	//45 	·········<dt:producing> 	dt:ProductItemProducing 	[0..*] 	Список площадок, на которых выпускается данная продукция.
	//46 	············<dt:location> 	dt:Enterprise 	[1..1] 	Площадка, осуществляющая выпуск продукции.
	//47 	···············<bs:uuid> 	bs:UUID 	[1..1] 	Идентификатор версии предприятия. 	<bs:uuid>9c706614-b995-4bdd-bda3-8fa9de70803a</bs:uuid>
	//48 	···············<bs:guid> 	bs:UUID 	[1..1] 	Глобальный идентификатор предприятия. 	<bs:guid>12f04ea3-e236-4b71-b427-e04d41b26de4</bs:guid>
	//49 	············</dt:location>
	//50 	·········</dt:producing>
			Запрос = Запрос + "
		    |<dt:producing>
			|<dt:location>
	    	| <bs:guid>"+Сокрлп(продукцияЭлемент.Площадка.GUID)+"</bs:guid>
		    |</dt:location>
		    |</dt:producing>";

	// ЖД ФАСОВКА
			Если ПустоеЗначение(продукцияЭлемент.ТипФасовки)= 0 Тогда
			Запрос = Запрос + "
			|<dt:packaging>
		    |<dt:packagingType>
	    	|<bs:guid>"+Сокрлп(продукцияЭлемент.ТипФасовки.GUID)+"</bs:guid>
		    |</dt:packagingType>
	    	|<dt:quantity>"+продукцияЭлемент.КолвоЕдиницФасовки+"</dt:quantity>
		    |<dt:volume>"+продукцияЭлемент.ОбъемЕдиницыФасовки+"</dt:volume>
	    	|<dt:unit>
		    |<bs:guid>"+ СокрЛП(продукцияЭлемент.ЕдИзмОбъемаФасовки.Guid) +"</bs:guid>
	    	|</dt:unit>
		    |</dt:packaging>";

			КонецЕсли;
			Запрос = Запрос + "
			|</dt:productItem>
		    |</vd:resultingList>";

		КонецЕсли;

		Запрос = Запрос + "
	    |</merc:modificationOperation>
	    |</merc:modifyProducerStockListRequest>
	    |</apl:data>
	    |</apl:application>
	    |</apldef:submitApplicationRequest>
		|</SOAP-ENV:Body>
		|</SOAP-ENV:Envelope>";


	Возврат Запрос;
КонецФункции

Функция Проверить_ВСД2_Продукция_Элемент( ПродукцияЭлемент )

	Если ПустоеЗначение(продукцияЭлемент.Площадка.GUID)=1 Тогда
		Сообщить("В продукцияЭлемент.Площадка пустой GUID");
		Возврат 0;
	КонецЕсли;
	
	Если ПустоеЗначение(ПродукцияЭлемент.ЕдиницаИзмерения) = 1 Тогда
	    Сообщить("Не указана Единица Измерения","!!");
		Возврат 0;
	КонецЕсли;	

//	Если ПустоеЗначение(ПродукцияЭлемент.ЕдИзмОбъемаФасовки) = 1 Тогда
//	    Сообщить("Не указана ЕдИзмОбъемаФасовки","!!");
//		Возврат 0;
//	КонецЕсли;	
//
//	Если (ПродукцияЭлемент.ОбъемЕдиницыФасовки) = 0 Тогда
//	    Сообщить("Не указана ОбъемЕдиницыФасовки","!!");
//		Возврат 0;
//	КонецЕсли;	
	
	Возврат 1;
КонецФункции

Функция  Изменить_Продукцию( ПродукцияЭлемент , RegisterModificationType) Экспорт

	Если Проверить_ВСД2_Продукция_Элемент(продукцияЭлемент)=0 Тогда
		Сообщить("["+ПродукцияЭлемент+"] не заполнен ","!");
		Возврат "REJECTED";
	КонецЕсли;
	
	ЗапросXML = Изменить_Продукцию_ЗапросXML(продукцияЭлемент, RegisterModificationType);

	метод = ГМ.Компонента.GetMethod();
	метод.Service = "platform/services/2.0/ApplicationManagementService";
    метод.Action = "submitApplicationRequest";

	appID = ГМ.ОтправитьЗапросSoap(ЗапросXML, метод);

	ГМ.УдалитьXML( ГМ.Компонента.LogFilename);
	Результат = "";
	Если ПустоеЗначение(appID) = 0 Тогда
		Результат = ПолучитьРезультат_Изменить_Продукцию(продукцияЭлемент, appID);
		Сообщить("Результат = "+Результат);
		Для А=1 По 10 Цикл
			Если (Результат="IN_PROCESS") Тогда
				Результат = ПолучитьРезультат_Изменить_Продукцию(продукцияЭлемент, appID);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	ГМ.УдалитьXML( ГМ.Компонента.LogFilename);

	Возврат Результат;

КонецФункции

Функция Получить_Список_Продукции_ЗапросXML(Площадка)

	Запрос = "
	|<soapenv:Envelope xmlns:soapenv='http://schemas.xmlsoap.org/soap/envelope/'
	|xmlns:ws='http://api.vetrf.ru/schema/cdm/registry/ws-definitions/v2'
	|xmlns:bs='http://api.vetrf.ru/schema/cdm/base'
	|xmlns:dt='http://api.vetrf.ru/schema/cdm/dictionary/v2'>
    |<soapenv:Header/>
    |<soapenv:Body>
    | <ws:getProductItemListRequest>
    |<bs:listOptions>
    | <bs:count>1000</bs:count>
    | <bs:offset>0</bs:offset>
    |</bs:listOptions>
    |<dt:enterprise>
    | <bs:guid>"+ СокрЛП(Площадка.GUID) +"</bs:guid>
    |</dt:enterprise>
    | </ws:getProductItemListRequest>
    |</soapenv:Body>
	|</soapenv:Envelope>
	|";
	Возврат Запрос;
КонецФункции

Функция Получить_Список_Продукции( Площадка ) Экспорт

	ЗапросXML = Получить_Список_Продукции_ЗапросXML( Площадка );

	метод = ГМ.Компонента.GetMethod();

	метод.Service = "platform/services/2.0/ProductService";
    метод.Action = "GetProductItemList";

	appID = ГМ.ОтправитьЗапросSoap(ЗапросXML, метод);

	Если ГМ.НайтиОшибки()>0 Тогда
		Возврат "";
	КонецЕсли;
	ПолучитьРезультат_Список_Продукции(ГМ.Компонента.LogFilename);
	ГМ.УдалитьXML( ГМ.Компонента.LogFilename);
	Возврат appID;

КонецФункции

//} =======================  Продукция окончание 

//{ ====================== Аннулирование ВСД Транспортного v2 ===========================

Функция ВСД2_Аннулирование_ЗапросXML(док)
	//{Проверим на переопределение функции, Если переопределена вызовем из перекрывающего модуля
	ОбъектВыполнения = ПроверитьПереопределениеФункции("ВСД2_Аннулирование_ЗапросXML");
	Если ПустоеЗначение(ОбъектВыполнения)=0 Тогда
		Возврат ОбъектВыполнения.ВСД2_Аннулирование_ЗапросXML(док);
	КонецЕсли;
	//}

	Запрос = "
	|<SOAP-ENV:Envelope
	|xmlns:merc='http://api.vetrf.ru/schema/cdm/mercury/g2b/applications/v2'
	|xmlns:apldef='http://api.vetrf.ru/schema/cdm/application/ws-definitions'
	|xmlns:apl='http://api.vetrf.ru/schema/cdm/application'
	|xmlns:vd='http://api.vetrf.ru/schema/cdm/mercury/vet-document/v2'
	|xmlns:SOAP-ENV='http://schemas.xmlsoap.org/soap/envelope/'>
	|<SOAP-ENV:Header/>
	|<SOAP-ENV:Body>
	|<apldef:submitApplicationRequest>
	|<apldef:apiKey>"+ ГМ.СписокКонстант.Получить("param_api_key") + "</apldef:apiKey>
	|<apl:application>
	|<apl:serviceId>mercury-g2b.service:2.0</apl:serviceId>
	|<apl:issuerId>"+ГМ.СписокКонстант.Получить("param_issuer_id")+"</apl:issuerId>
	|<apl:issueDate>"+ ГМ.Преобразовать_Дата_в_Строка(ТекущаяДата(), "T00:00:00") +"</apl:issueDate>
	|<apl:data>
	|<merc:withdrawVetDocumentRequest>
	|<merc:localTransactionId>[GUID]</merc:localTransactionId>
	|<merc:initiator>
	|<vd:login>"+ГМ.СписокКонстант.Получить("param_intiator_login")+"</vd:login>
	|</merc:initiator>
	|<merc:vetDocumentId>"+ СокрЛП(док.UUID) +"</merc:vetDocumentId>
	|<merc:withdrawReason>"+ГМ.СписокКонстант.Получить("ПарамПричинаАннулирования")+"</merc:withdrawReason>
	|<merc:withdrawDate>"+ ГМ.Преобразовать_Дата_в_Строка(док.ДатаДок, "T23:59:59") +"</merc:withdrawDate>
	|<merc:specifiedPerson>
	|<vd:login>"+ ГМ.СписокКонстант.Получить("param_vetdoctor_login")+"</vd:login>
	|</merc:specifiedPerson>
	|</merc:withdrawVetDocumentRequest>
	|</apl:data>
	|</apl:application>
	|</apldef:submitApplicationRequest>
	|</SOAP-ENV:Body>
	|</SOAP-ENV:Envelope>";

	Возврат Запрос;
КонецФункции


Функция Отправить_ВСД2_Аннулирование( док ) Экспорт
	
	t1 = ГМ.Старт();

	Если ПустоеЗначение(док)=1 Тогда
		Возврат 0;
	КонецЕсли;

	Если СокрЛП(док.ТипВСД) = "PRODUCTIVE" Тогда
		Сообщить("Производственный ВСД не аннулируем " + Док);
		Возврат "";
	КонецЕсли;

	Если ПустоеЗначение(док.uuid) = 1 Тогда
		Сообщить("В документе "+док+" не указан uuid, аннулирование невозможно");
		Возврат "";
	КонецЕсли;

	//ЖД - решение проблемы загрузки партий из ответа в правильную площадку при наличии неск. площадок у ХС
	ГМ.СписокКонстант.Установить("Отправитель_Площадка",док.Отправитель_Площадка);

	ЗапросXML = ВСД2_Аннулирование_ЗапросXML(док);

	метод = ГМ.Компонента.GetMethod();
	метод.Service = "platform/services/2.0/ApplicationManagementService";
    метод.Action = "submitApplicationRequest";

	appID = ГМ.ОтправитьЗапросSoap(ЗапросXML, метод);

	ГМ.СохранитьЗапросВДокументе( док, appID );
	
	Если ГМ.НайтиОшибки( док )>0 Тогда
		Возврат appID;
	КонецЕсли;	
	
	ГМ.УдалитьXML( ГМ.Компонента.LogFilename);

	Если ПустоеЗначение(appID)=0 Тогда

		спПараметры = СоздатьОбъект("СписокЗначений");	
		спПараметры.Установить("applicationID", appID );
		спПараметры.Установить("докСсылка", док );
		
		Результат = ПолучитьРезультат_ВСД_2( спПараметры );			
		
		ГМ.СообщитьИнфо("Результат = "+Результат, док);
		Для А=1 По 10 Цикл
			Если (Результат="IN_PROCESS") Тогда
				Результат = ПолучитьРезультат_ВСД_2( спПараметры );					
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;

	ГМ.Финиш(t1, Сам(), "Отправить_ВСД2_Аннулирование", док);
	
	Возврат Результат;
КонецФункции


Процедура Аннулировать_ВСД2_транзакция( док ) Экспорт
	
	Если ПустоеЗначение(док) = 1 Тогда
		Возврат;
	КонецЕсли;

	тзВСД = ГМ.Выбрать_ВСД(док);

	тзВСД.ВыбратьСтроки();
	Пока тзВСД.ПолучитьСтроку()=1 Цикл
		ДокОснование = тзВСД.Док.ДокОснование;
		Если (тзВСД.Док.СтатусВСД <> Перечисление.ВСД_СтатусВСД.CONFIRMED) Тогда 
			ГМ.СообщитьИнфо("Аннулировать "+ тзВСД.Док+" невозможно, статус ВСД = "+тзВСД.Док.СтатусВСД, тзВСД.Док);
		    Продолжить;  
		КонецЕсли;
		Результат = Отправить_ВСД2_Аннулирование(тзВСД.Док);		
		Если НЕ ( Результат = "COMPLETED" ) Тогда  
			ГМ.СообщитьИнфо("Не удалось Аннулировать "+тзВСД.Док+" для "+док, тзВСД.Док);
		КонецЕсли;		
	КонецЦикла;

	Если ( ПустоеЗначение(ДокОснование)=0 ) Тогда 
		// проверим если все(!) ВСД_Док аннулированы - изменяем статус ДокОснование 
		тзВСД = ГМ.Выбрать_ВСД( ДокОснование );
		
		ДокЗапросАннулирован = 1;
		
		тзВСД.ВыбратьСтроки();
		Пока тзВСД.ПолучитьСтроку() = 1 Цикл
			Если тзВСД.Док.СтатусВСД <> Перечисление.ВСД_СтатусВСД.WITHDRAWN Тогда 
				ДокЗапросАннулирован =0;
			КонецЕсли;
		КонецЦикла;
					
			Если ДокЗапросАннулирован =1 Тогда 
				Попытка
					докОбъект = СоздатьОбъект("Документ");
					докОбъект.НайтиДокумент( ДокОснование );
					докОбъект.Статус = Результат; 
					докОбъект.СтатусВСД = Перечисление.ВСД_СтатусВСД.WITHDRAWN;		
					докОбъект.Записать();
					докОбъект.Удалить(0);
				Исключение
					ГМ.СообщитьИнфо("Не удалось записать статус документа "+ДокОснование+" "+ОписаниеОшибки(), ДокОснование);
				КонецПопытки;
			КонецЕсли;
	КонецЕсли;

КонецПроцедуры

//} **************** Аннулирование ВСД Транспортного окончание v2

//{ ====================== Инвентаризация v2

// http://help.vetrf.ru/wiki/ResolveDiscrepancyOperation_v2.0
Функция ВСД2_инвентаризация_ЗапросXML(док)
	//{Проверим на переопределение функции, Если переопределена вызовем из перекрывающего модуля
	ОбъектВыполнения = ПроверитьПереопределениеФункции("ВСД2_инвентаризация_ЗапросXML");
	Если ПустоеЗначение(ОбъектВыполнения)=0 Тогда
		Возврат ОбъектВыполнения.ВСД2_инвентаризация_ЗапросXML(док);
	КонецЕсли;
	//}

	//стандартное поведение функции
	//док = СоздатьОбъект("Документ.ВСД_инвентаризация");

	Запрос = "
	|<SOAP-ENV:Envelope xmlns:dt='http://api.vetrf.ru/schema/cdm/dictionary/v2'
	|xmlns:bs='http://api.vetrf.ru/schema/cdm/base'
	|xmlns:merc='http://api.vetrf.ru/schema/cdm/mercury/g2b/applications/v2'
	|xmlns:apldef='http://api.vetrf.ru/schema/cdm/application/ws-definitions'
	|xmlns:apl='http://api.vetrf.ru/schema/cdm/application'
	|xmlns:vd='http://api.vetrf.ru/schema/cdm/mercury/vet-document/v2'
	|xmlns:SOAP-ENV='http://schemas.xmlsoap.org/soap/envelope/'>
  	|<SOAP-ENV:Header/>
  	|<SOAP-ENV:Body>
	|<apldef:submitApplicationRequest>
	|<apldef:apiKey>"+ ГМ.СписокКонстант.Получить("param_api_key") + "</apldef:apiKey>
	|<apl:application>
	|<apl:serviceId>mercury-g2b.service:2.0</apl:serviceId>
	|<apl:issuerId>"+ГМ.СписокКонстант.Получить("param_issuer_id")+"</apl:issuerId>
	|<apl:issueDate>"+ ГМ.Преобразовать_Дата_в_Строка(ТекущаяДата(), "T00:00:00") +"</apl:issueDate>
	|<apl:data>
	|<merc:resolveDiscrepancyRequest>
	|<merc:localTransactionId>" + СокрЛП(док.НомерДок) + "</merc:localTransactionId>
	|<merc:initiator>
	|<vd:login>"+ СокрЛП( ГМ.СписокКонстант.Получить("param_intiator_login") ) +"</vd:login>
	|</merc:initiator>
	|<merc:enterprise>
	|<bs:guid>"+ СокрЛП(док.Владелец_Площадка.GUID) +"</bs:guid>
	|</merc:enterprise>
	|<merc:inventoryDate>" + ГМ.Преобразовать_Дата_в_Строка(док.ДатаДок, "T00:00:00") + "</merc:inventoryDate>
	|<merc:responsible>
	|<vd:login>"+ СокрЛП( ГМ.СписокКонстант.Получить("param_intiator_login") ) +"</vd:login>
	|</merc:responsible>
	|";
	Док.ВыбратьСтроки();
	Пока Док.ПолучитьСтроку() = 1 Цикл

		Запрос = Запрос + "
		|<merc:stockDiscrepancy id='_"+ док.НомерСтроки +"'>
		|<vd:resultingList>
		|<vd:stockEntry>";
	    Если ПустоеЗначение(Док.Партия)=0 Тогда // изменяем конкретную партию
			Запрос = Запрос + "
			|<bs:uuid>" + СокрЛП(док.Партия.uuid) + "</bs:uuid>";
			//На случай, когда ПродукцияЭлемент в Партии устаревший, возьмем данные из Партии
		    Если док.Партия.Продукция_Элемент.ВидПродукции <> док.Партия.ВидПродукции Тогда
				Запрос = Запрос + "
				| <vd:batch>
				|<vd:productType>"+ Док.Партия.Продукция.Тип +"</vd:productType>
				|<vd:product>
				|<bs:guid>"+ СокрЛП(Док.Партия.Продукция.GUID) +"</bs:guid>
				|</vd:product>
				|<vd:subProduct>
				|<bs:guid>"+ СокрЛП(Док.Партия.ВидПродукции.GUID) +"</bs:guid>
				|</vd:subProduct>
				|<vd:productItem>";
			Иначе
				Запрос = Запрос + "
				| <vd:batch>
				|<vd:productType>"+ док.Продукция.Тип +"</vd:productType>
				|<vd:product>
				|<bs:guid>"+ СокрЛП(док.Продукция.GUID) +"</bs:guid>
				|</vd:product>
				|<vd:subProduct>
				|<bs:guid>"+ СокрЛП(док.ВидПродукции.GUID) +"</bs:guid>
				|</vd:subProduct>
				|<vd:productItem>";
				Если ( Док.Продукция_Элемент.ПометкаУдаления() = 1 ) Тогда
					//удаленные ProductItem отправляем без GUID
					ГМ.СообщитьИнфо("в строке №"+ Док.НомерСтроки +" ВСД_Продукция_Элемент ["+ СокрЛП(Док.Продукция_Элемент.Наименование) +"] удален => отправляем с пустым GUID",док, "!");
				ИначеЕсли ПустоеЗначение(док.Продукция_Элемент.GUID) = 0 Тогда
					Запрос = Запрос+ "
					|<bs:guid>"+ СокрЛП(док.Продукция_Элемент.GUID) +"</bs:guid>";
				КонецЕсли;
				
				НазваниеПродукции = ЗаменитьСпецСимволы( док.НаименованиеПродукции );
				Если ПустоеЗначение( НазваниеПродукции )= 0 Тогда 
   			    	Запрос = Запрос + " <dt:name>"+ НазваниеПродукции +"</dt:name>";
   				КонецЕсли;
		    КонецЕсли;
		Иначе
			Запрос = Запрос + "
			| <vd:batch>
			|<vd:productType>"+ док.Продукция.Тип +"</vd:productType>
			|<vd:product>
			|<bs:guid>"+ СокрЛП(док.Продукция.GUID) +"</bs:guid>
			|</vd:product>
			|<vd:subProduct>
			|<bs:guid>"+ СокрЛП(док.ВидПродукции.GUID) +"</bs:guid>
			|</vd:subProduct>
			|<vd:productItem>";
			Если ПустоеЗначение(док.Продукция_Элемент.GUID) = 0 Тогда
				Запрос = Запрос+ "
				|<bs:guid>"+ СокрЛП(док.Продукция_Элемент.GUID) +"</bs:guid>";
			КонецЕсли;
			
			НазваниеПродукции = ЗаменитьСпецСимволы( док.НаименованиеПродукции );
			Если ПустоеЗначение( НазваниеПродукции )= 0 Тогда 
			   	Запрос = Запрос + " <dt:name>"+ НазваниеПродукции +"</dt:name>";
   			КонецЕсли;
   		КонецЕсли;

   		Если ПустоеЗначение(док.ТипФасовки)=0 Тогда
			Запрос = Запрос+ "
   	    	|<dt:packaging>
        	|<dt:packagingType>
	        |<bs:guid>"+ СокрЛП(док.ТипФасовки.GUID) +"</bs:guid>
   		    |</dt:packagingType>
       		|<dt:quantity>"+ док.КолВоЕдиницФасовки +"</dt:quantity>
	        |<dt:volume>"+ док.ОбъемЕдиницыФасовки +"</dt:volume>
   		    |<dt:unit>
       		|<bs:guid>"+ СокрЛП(док.ЕдИзмОбъемаФасовки.GUID) +"</bs:guid>
	        |</dt:unit>
   		    |</dt:packaging>";
		КонецЕсли;

        Запрос = Запрос +"
		| </vd:productItem>
		|<vd:volume>"+ Док.Количество +"</vd:volume>
		|<vd:unit>
		|<bs:uuid>"+ СокрЛП(док.ЕдиницаИзмерения.UUID) +"</bs:uuid>
		|</vd:unit>";


		Запрос = Запрос + ГМ.Изготовление_СрокГодности_в_XML(док, "vd");

		Запрос=Запрос+"
		|<vd:perishable>"+ ГМ.булево(док.Скоропортящийся)+"</vd:perishable>
		|<vd:origin>";
		Если (ПустоеЗначение(док.Производитель_Страна) = 0)Тогда
			Запрос=Запрос+"
			|<vd:country>
			|<bs:uuid>"+СокрЛП(док.Производитель_Страна.uuid)+"</bs:uuid>
			|</vd:country>";
		КонецЕсли;
		Если (ПустоеЗначение(док.Производитель_Площадка.GUID) = 0)Тогда
			Запрос=Запрос+"
			| <vd:producer>
			|	<dt:enterprise>
			|	  <bs:guid>"+ СокрЛП(док.Производитель_Площадка.GUID) +"</bs:guid>
			|	</dt:enterprise>
			| <dt:role>PRODUCER</dt:role>
			|</vd:producer>";
		КонецЕсли;
		Запрос=Запрос+"
		|</vd:origin>
		|<vd:lowGradeCargo>"+ ГМ.булево(док.Некачественный)+"</vd:lowGradeCargo>";

		Запрос = Запрос + МаркировкаДокументаВЗапрос( Док );		

		Запрос=Запрос+"
		|<vd:owner>
		|<bs:guid>"+ СокрЛП(док.Владелец_Площадка.GUID) +"</bs:guid>
		|</vd:owner>
		|</vd:batch>
		|</vd:stockEntry>
		|</vd:resultingList>
		|</merc:stockDiscrepancy>
		|";
	КонецЦикла;
	Док.ВыбратьСтроки();
	Пока Док.ПолучитьСтроку() = 1 Цикл
		Запрос = Запрос + "

		|<merc:discrepancyReport for='_"+ док.НомерСтроки +"'>
		|<vd:issueNumber>"+ Док.НомерДок +"</vd:issueNumber>
		|<vd:issueDate>" + ГМ.Преобразовать_Дата_в_Строка(док.ДатаДок) + "</vd:issueDate>
		|<vd:reason>
		|<vd:name>"+СокрЛП(док.ПричинаРасхождения)+"</vd:name>
		|</vd:reason>
		|<vd:description>"+СокрЛП(док.ОписаниеНесоответствия)+"</vd:description>
		|</merc:discrepancyReport>
		|";
	КонецЦикла;
	Запрос = Запрос + "
	|</merc:resolveDiscrepancyRequest>
	|</apl:data>
	|</apl:application>
	|</apldef:submitApplicationRequest>
	|</SOAP-ENV:Body>
	|</SOAP-ENV:Envelope>";

	Возврат Запрос;
КонецФункции

Функция Проверка_ВСД2_Инвентаризация(док)
	// дата изготовления И срок годности обязательны
	// при добавлении партии обязательны Страна И Производитель_Площадка
	Рез = 1;
	
	Если ( ПустоеЗначение( док.applicationID ) = 0 ) И ( СокрЛП(док.Статус) <> "REJECTED") Тогда
	    Сообщить("Запрос отправлен, повторная отправка запроса запрещена","!!");
		Возврат 0;
	КонецЕсли;	
	
	Если ПустоеЗначение(док.Владелец_ХозСубъект) = 1 Тогда
	    Сообщить("Не указан владелец ХС");
		Возврат 0;
	КонецЕсли;
	Если ПустоеЗначение(док.Владелец_Площадка) = 1 Тогда
	    Сообщить("Не указана Площадка владельца");
		Возврат 0;
	КонецЕсли;

	док.ВыбратьСтроки();
	Пока док.ПолучитьСтроку()=1 Цикл
		Если док.количество>0 Тогда
	        Если (ПустоеЗначение(док.Продукция)=1) ИЛИ (ПустоеЗначение(док.ВидПродукции)=1) ИЛИ (ПустоеЗначение(док.Продукция_Элемент)=1) Тогда
	        	Сообщить("Не указаны Продукция, Вид Продукции ИЛИ Продукция_Элемент"+" в строке "+док.НомерСтроки,"!");
				Возврат 0;
	        КонецЕсли;
	
			Если (ПустоеЗначение(док.ЕдиницаИзмерения)=1) Тогда
	        	Сообщить("Не указана Единица измерения"+" в строке "+док.НомерСтроки,"!");
				Возврат 0;
	        КонецЕсли;
	
	        Если (ПустоеЗначение(док.ДатаИзготовления1)=1) И (ПустоеЗначение(док.ДатаИзготовления2)=1) Тогда
	        	Сообщить("Не указана Дата изготовления"+" в строке "+док.НомерСтроки,"!");
				Возврат 0;
	        КонецЕсли;
	        Если (ПустоеЗначение(док.ДатаСрокГодности1)=1) И (ПустоеЗначение(док.ДатаСрокГодности2)=1) Тогда
	        	Сообщить("Не указан срок годности"+" в строке "+док.НомерСтроки,"!");
				Возврат 0;
	        КонецЕсли;
	        Если (док.ДатаИзготовления1 = док.ДатаИзготовления2 ) Тогда
	        	Сообщить("Дата Изготовления совпадают "+" в строке "+док.НомерСтроки,"!");
				Возврат 0;
	        КонецЕсли;
	        Если (док.ДатаСрокГодности1 = док.ДатаСрокГодности2 ) Тогда
	        	Сообщить("Дата Срок годности совпадают "+" в строке "+док.НомерСтроки,"!");
				Возврат 0;
	        КонецЕсли;
		    Если ПустоеЗначение(док.Партия) = 1 Тогда
		        Если (ПустоеЗначение(док.Производитель_Страна)=1) ИЛИ (ПустоеЗначение(док.Производитель_Площадка)=1) Тогда
	    	    	Сообщить("Не указана Страна производителя ИЛИ его Площадка"+" в строке "+док.НомерСтроки,"!");
					Возврат 0;
		        КонецЕсли;
			КонецЕсли;
			Если (ПустоеЗначение(док.ТипФасовки)=0) И (ПустоеЗначение(док.ЕдИзмОбъемаФасовки)=1) Тогда
	        	Сообщить("Не указана Единица измерения Фасовки"+" в строке "+док.НомерСтроки,"!");
				Возврат 0;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
    Возврат Рез;
КонецФункции

Функция Отправить_ВСД2_Инвентаризация(док) Экспорт
	//{Проверим на переопределение функции, Если переопределена вызовем из перекрывающего модуля
	ОбъектВыполнения = ПроверитьПереопределениеФункции("Отправить_ВСД2_Инвентаризация");
	Если ПустоеЗначение(ОбъектВыполнения)=0 Тогда
		Возврат ОбъектВыполнения.Отправить_ВСД2_Инвентаризация(док);
	КонецЕсли;
	//}

	t1 = ГМ.Старт();
	
	//стандартное поведение функции

	Если Проверка_ВСД2_инвентаризация(док)=0 Тогда
		Возврат 0;
	КонецЕсли;

	//ГМ.ИнициализацияКомпоненты(док.Фирма);

	ГМ.СписокКонстант.Установить("Отправитель_Площадка",док.Владелец_Площадка);

	ЗапросXML = ВСД2_инвентаризация_ЗапросXML(док);
	
	appID = ОтправитьЗапрос2(док, ЗапросXML);
	
//    //Текст = СоздатьОбъект("Текст");
//	//Текст.ДобавитьСтроку(ЗапросXML);
//	//Текст.Показать();
//	метод = ГМ.Компонента.GetMethod();
//	метод.Service = "platform/services/2.0/ApplicationManagementService";
//    метод.Action = "submitApplicationRequest";
//
//	appID = ГМ.ОтправитьЗапросSoap(ЗапросXML, метод);
//
//	ГМ.СохранитьЗапросВДокументе(док);
//
//	Если ГМ.НайтиОшибки( док )>0 Тогда
//		Возврат appID;
//	КонецЕсли;	
//	
//	ГМ.УдалитьXML( ГМ.Компонента.LogFilename);	
//
//	Если ПустоеЗначение(appID)=0 Тогда
//		спПараметры = СоздатьОбъект("СписокЗначений");	
//		спПараметры.Установить("applicationID", appID );
//		спПараметры.Установить("докСсылка", док );
//		
//		Результат = ПолучитьРезультат_ВСД_2( спПараметры );	
//		//Результат = ПолучитьРезультат_ВСД_2(appID, док); //док
//		Сообщить("Результат = "+Результат);
//		Для А=1 По 10 Цикл
//			Если (Результат="IN_PROCESS") Тогда		
//				Результат = ПолучитьРезультат_ВСД_2( спПараметры );					
//				//Результат = ПолучитьРезультат_ВСД_2( appID, док);
//			КонецЕсли;
//		КонецЦикла;
//	КонецЕсли;

	//докОбъект = СоздатьОбъект("Документ");
	//докОбъект.НайтиДокумент( док );
	//Если СокрЛП(докОбъект.Статус) = "COMPLETED" Тогда
	//	докОбъект.Записать();
	//	докОбъект.Провести();
	//КонецЕсли;

	ГМ.Финиш(t1, Сам(), "Отправить_ВСД2_Инвентаризация", док);

	Возврат appID;

КонецФункции

Функция ВСД2_Инвентаризация_ЗаполнитьТЧ(Док)  Экспорт
	//{Проверим на переопределение функции, Если переопределена вызовем из перекрывающего модуля
	ОбъектВыполнения = ПроверитьПереопределениеФункции("ВСД2_Инвентаризация_ЗаполнитьТЧ");
	Если ПустоеЗначение(ОбъектВыполнения)=0 Тогда
		Возврат ОбъектВыполнения.ВСД2_Инвентаризация_ЗаполнитьТЧ(Док);
	КонецЕсли;
	//}

	ПараПараметров = СоздатьОбъект("СписокЗначений");
   	ПараПараметров.Установить("ВладелецХС", Док.Владелец_ХозСубъект);
   	ПараПараметров.Установить("ВладелецПлощадка", Док.Владелец_площадка);

   	ОткрытьФормуМодально("Отчет", ПараПараметров,    ГМ.ПолучитьКонстанту("КаталогМодуля")+"ВСД_ПараметрыИнвентаризации.ert");
   	
   	Если ПараПараметров.Получить("СтатусВозврата") <> 1 Тогда
   		Возврат 0;
   	КонецЕсли;
   	
	КолвоМакс = ПараПараметров.Получить("МаксКоличество");
	ДатаМакс = ПараПараметров.Получить("МинСрок");
	ДатаИзг = ПараПараметров.Получить("МинДата");
	Производитель_площадка = ПараПараметров.Получить("Производитель_площадка");
	ВыбНоменклатура = ПараПараметров.Получить("Номенклатура");

	Док.УдалитьСтроки();
	
	СпрПартии = СоздатьОбъект("Справочник.ВСД_Партия");
	СпрПартии.ВыбратьЭлементы();
	колво=0;
	Пока СпрПартии.ПолучитьЭлемент()=1 Цикл
		Если (СпрПартии.ПометкаУдаления()=1) Тогда
			Продолжить;
		КонецЕсли;
		Если (СпрПартии.Получатель_Площадка <>  Док.Владелец_площадка) Тогда
			Продолжить;
		КонецЕсли;
		Если (СпрПартии.Получатель_ХозСубъект <>  Док.Владелец_ХозСубъект) Тогда
			Продолжить;
		КонецЕсли;

		Если ПустоеЗначение(ДатаМакс)=0 Тогда
			Если ПустоеЗначение(СпрПартии.ДатаСрокГодности1)=0 Тогда
				Если ГМ.Преобразовать_СтрокаДата_в_Дата(СпрПартии.ДатаСрокГодности1) > ДатаМакс Тогда
					Продолжить;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;

		Если ПустоеЗначение(ДатаИзг)=0 Тогда
			Если ПустоеЗначение(СпрПартии.ДатаИзготовления1)=0 Тогда
				Если ГМ.Преобразовать_СтрокаДата_в_Дата(СпрПартии.ДатаИзготовления1) > ДатаИзг Тогда
					Продолжить;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
		Если ПустоеЗначение(Производитель_площадка) = 0 Тогда
			Если СпрПартии.Производитель_Площадка <> Производитель_площадка Тогда
				Продолжить;
			КонецЕсли;
		КонецЕсли;
		Если ПустоеЗначение(ВыбНоменклатура) = 0 Тогда
			ТЗНоменклатуры = ГМ.ПолучитьСписок_Номенклатура_По_ВСД_Продукция_Элемент(СпрПартии.Продукция_Элемент);
			ТЗНоменклатуры.ВыбратьСтроки();
			Нашли = 0;
			Пока ТЗНоменклатуры.ПолучитьСтроку() = 1 Цикл
				Если ТЗНоменклатуры.Номенклатура.ПринадлежитГруппе(ВыбНоменклатура) = 1 Тогда
					Нашли = 1; Прервать;
				КонецЕсли;
			КонецЦикла;
			Если Нашли = 0 Тогда
				Продолжить;
			КонецЕсли;
		КонецЕсли;

		колво =колво+1;
		Док.НоваяСтрока();
		Док.Партия = СпрПартии.ТекущийЭлемент();
		
		ГМ.ЗаполнитьСтрокуДокумента( Док, Док, Док, 1);

		Если колво >= КолвоМакс Тогда
			Прервать;
		КонецЕсли;
	КонецЦикла;
КонецФункции

Функция ВСД2_Инвентаризация_ЗаполнитьТчПоТЗ( Док, тзВСД )  Экспорт
	//{Проверим на переопределение функции, Если переопределена вызовем из перекрывающего модуля
	ОбъектВыполнения = ПроверитьПереопределениеФункции("ВСД2_Инвентаризация_ЗаполнитьТчПоТЗ");
	Если ПустоеЗначение(ОбъектВыполнения)=0 Тогда
		Возврат ОбъектВыполнения.ВСД2_Инвентаризация_ЗаполнитьТчПоТЗ( Док, тзВСД );
	КонецЕсли;
	//}

//	ПараПараметров = СоздатьОбъект("СписокЗначений");
//   	ПараПараметров.Установить("ВладелецХС", Док.Владелец_ХозСубъект);
//   	ПараПараметров.Установить("ВладелецПлощадка", Док.Владелец_площадка);
//
//   	ОткрытьФормуМодально("Отчет", ПараПараметров,    ГМ.ПолучитьКонстанту("КаталогМодуля")+"ВСД_ПараметрыИнвентаризации.ert");
//   	
//   	Если ПараПараметров.Получить("СтатусВозврата") <> 1 Тогда
//   		Возврат 0;
//   	КонецЕсли;
   	//
   	КолвоМакс = 500;
	//КолвоМакс = ПараПараметров.Получить("МаксКоличество");
	//ДатаМакс = ПараПараметров.Получить("МинСрок");
	//ДатаИзг = ПараПараметров.Получить("МинДата");
	//Производитель_площадка = ПараПараметров.Получить("Производитель_площадка");
	//ВыбНоменклатура = ПараПараметров.Получить("Номенклатура");

	Док.УдалитьСтроки();
	
	тзВСД.ВыбратьСтроки();
	колво=0;
	Пока тзВСД.ПолучитьСтроку()=1 Цикл
		Если ПустоеЗначение(тзВСД.Пометка)=1 Тогда
			Продолжить;
		КонецЕсли;
		//Если ПустоеЗначение(тзВСД.Партия)=1 Тогда
		//	//Сообщить("");
		//	Продолжить;
		//КонецЕсли;
		//Если (СпрПартии.Получатель_Площадка <>  Док.Владелец_площадка) Тогда
		//	Продолжить;
		//КонецЕсли;
		//Если (СпрПартии.Получатель_ХозСубъект <>  Док.Владелец_ХозСубъект) Тогда
		//	Продолжить;
		//КонецЕсли;

		колво =колво+1;
		Док.НоваяСтрока();
		Док.Партия = тзВСД.Партия;
		
		ГМ.ЗаполнитьСтрокуДокумента( Док, Док, Док, 1);

		Если колво >= КолвоМакс Тогда
			Прервать;
		КонецЕсли;
	КонецЦикла;
КонецФункции


//} =========================== Инвентаризация

Функция GetRussianEnterpriseList(Регион, Название, Смещение = 0)
	Запрос = "
	|<soapenv:Envelope xmlns:soapenv='http://schemas.xmlsoap.org/soap/envelope/'
	|xmlns:ws='http://api.vetrf.ru/schema/cdm/registry/ws-definitions/v2'
	|xmlns:bs='http://api.vetrf.ru/schema/cdm/base'
	|xmlns:dt='http://api.vetrf.ru/schema/cdm/dictionary/v2'>
	| <soapenv:Header/>
	| <soapenv:Body>
	|<ws:getRussianEnterpriseListRequest>
	| 	<bs:listOptions>
	|<bs:count>"+Смещение+"</bs:count>
	|<bs:offset>0</bs:offset>
	|</bs:listOptions>
	| 	<dt:enterprise>";
//	|<bs:guid>"+СокрЛП(ХС.GUID)+"</bs:guid>";
	Если ПустоеЗначение(Название) = 0 Тогда
	    Запрос = Запрос + "
		|<dt:name>"+СокрЛП(Название)+"</dt:name>";
	КонецЕсли;
	Запрос = Запрос + "
	|<dt:address>
	| <dt:region>
	|<bs:guid>"+СокрЛП(Регион.GUID)+"</bs:guid>
	| </dt:region>
	|</dt:address>
	|</dt:enterprise>
	|</ws:getRussianEnterpriseListRequest>
	| </soapenv:Body>
	|</soapenv:Envelope>";
	Возврат Запрос;
КонецФункции



//======================================================================
Функция ПоискПлощадок(НаименованиеПлощадки, Регион, ХС = "") Экспорт
	Сообщить(" Запрос GetRussianEnterpriseListByName [ "+НаименованиеПлощадки+" ]","i");

	тзПлощадки = СоздатьОбъект("ТаблицаЗначений");
	тзПлощадки.НоваяКолонка("Наименование");
	тзПлощадки.НоваяКолонка("Адрес");
	тзПлощадки.НоваяКолонка("Активна");
	тзПлощадки.НоваяКолонка("GUID");
	тзПлощадки.НоваяКолонка("UUID");
	тзПлощадки.НоваяКолонка("ХозСубъект");
	тзПлощадки.НоваяКолонка("ДатаСоздания");
	тзПлощадки.НоваяКолонка("ДатаИзменения");

	ПолученоЗаписей = 1000; Смещение = 0;

	Пока ПолученоЗаписей >= 1000 Цикл
    	ПолученоЗаписей = 0;
		ЗапросXML = GetRussianEnterpriseList( Регион, НаименованиеПлощадки, Смещение);

		метод = ГМ.Компонента.GetMethod();
		метод.Service = "platform/services/2.0/EnterpriseService";
	    метод.Action = "GetRussianEnterpriseList";

		appID = ГМ.ОтправитьЗапросSoap(ЗапросXML, метод);

		Сообщить("Загрузка XML-файла: "+ГМ.Компонента.LogFilename);


	    objDom=СоздатьОбъект("MSXML2.DOMDocument.6.0");
		objDom.load(ГМ.Компонента.LogFilename);

		Если ГМ.НайтиОшибки()>0 Тогда
			Возврат appID;
		КонецЕсли;

		objDom.setProperty("SelectionNamespaces", "
		|xmlns:bs='http://api.vetrf.ru/schema/cdm/base'
		|xmlns:dt='http://api.vetrf.ru/schema/cdm/dictionary/v2'
		|xmlns:v2='http://api.vetrf.ru/schema/cdm/registry/ws-definitions/v2'
		|xmlns:default='http://www.w3.org/2001/XMLSchema'
		|" );

        Попытка
			enterpriseList =  objDom.selectNodes("//*[local-name() = 'enterprise']");

			ПолученоЗаписей = enterpriseList.length;

			Для i1 = 0 По enterpriseList.length - 1 Цикл
				enterprise=enterpriseList.item(i1);

				guid = enterprise.selectSingleNode("bs:guid").text;
				uuid = enterprise.selectSingleNode("bs:uuid").text;
				name = enterprise.selectSingleNode("dt:name").text;
				active = enterprise.selectSingleNode("bs:active").text;

				address = enterprise.selectSingleNode("dt:address").selectSingleNode("dt:addressView").text;

				тзПлощадки.НоваяСтрока();
				тзПлощадки.Наименование = name;
				тзПлощадки.адрес = address;
				тзПлощадки.guid = guid;
				тзПлощадки.uuid = uuid;
				тзПлощадки.активна = active;
				тзПлощадки.ХозСубъект = ХС;
			КонецЦикла;
		Исключение
			Сообщить("Ошибка при получении площадок, привязанных к ХозСубъекту");
	    КонецПопытки;
        Смещение = Смещение + 1000;
		ГМ.УдалитьXML( ГМ.Компонента.LogFilename );
	КонецЦикла;

	Сообщить("Загружено "+тзПлощадки.КоличествоСтрок()+" элементов","i");

	ГМ.УдалитьXML( ГМ.Компонента.LogFilename );
	
	Возврат тзПлощадки;
КонецФункции // ПоискПлощадки

Функция НайтиПлощадкиПоНазванию(ХС, Регион, УточнитьНазвание) Экспорт

	//{Проверим на переопределение функции, Если переопределена вызовем из перекрывающего модуля
	ОбъектВыполнения = ПроверитьПереопределениеФункции("НайтиПлощадкиПоНазванию");
	Если ПустоеЗначение(ОбъектВыполнения)=0 Тогда
		Возврат ОбъектВыполнения.НайтиПлощадкиПоНазванию2_0( Регион, УточнитьНазвание);
	КонецЕсли;
	//}

	//стандартное поведение функции
	Если ПустоеЗначение(ХС.GUID)=1 Тогда
		ГМ.ХС_ПолучитьGuid(ХС);
	КонецЕсли;

	Стр = ГМ.ПолучитьНаименованиеКлиента(ХС.Контрагент);

	ИНН = ГМ.ПолучитьИНН(ХС.Контрагент);
	Если СтрДлина( СокрЛП(ИНН) )=12 Тогда
		стр = СтрЗаменить(стр, " ИП","");
		стр = СтрЗаменить(стр, "ИП ","");
	КонецЕсли;

	Если УточнитьНазвание=1 Тогда
		Если ВвестиЗначение(стр,"Название контрагента","Строка")=0 Тогда
			Возврат 0;
		КонецЕсли;
	КонецЕсли;

	тзПлощадки = ПоискПлощадок(стр, Регион, ХС);

	СписокПараметров = СоздатьОбъект("СписокЗначений");
	СписокПараметров.ДобавитьЗначение("ЗагрузитьПлощадки", "Команда");
	СписокПараметров.ДобавитьЗначение(тзПлощадки, "тзПлощадки");

	КаталогОбработки = ГМ.ПолучитьКонстанту("КаталогМодуля");
	ОткрытьФорму("Отчет",СписокПараметров,КаталогОбработки+"Меркурий_ЗагрузкаПлощадок.ert");

	Возврат 1;
КонецФункции


//{ ===================== Мультимодальные перевозки
// **************** UpdateTransportMovementDetailsOperation

Функция ПолучитьИмяФайла_МаршрутДоставки(Док) Экспорт
	//{Проверим на переопределение функции, Если переопределена вызовем из перекрывающего модуля
	ОбъектВыполнения = ПроверитьПереопределениеФункции("ПолучитьИмяФайла_МаршрутДоставки");
	Если ПустоеЗначение(ОбъектВыполнения)=0 Тогда
		Возврат ОбъектВыполнения.ПолучитьИмяФайла_МаршрутДоставки(Док);
	КонецЕсли;
	//}

	КаталогПеревозки = ГМ.ПолучитьКонстанту("КаталогМодуля")+"MultiModal\";
	Если ФС.СуществуетФайл(КаталогПеревозки) = 0 Тогда
		Попытка
		    ФС.СоздатьКаталог(КаталогПеревозки);
		Исключение
			Предупреждение("Не создан каталог перевозок!"); Возврат "";
		КонецПопытки;
	КонецЕсли;
	Если ПустоеЗначение(Док.UUID)=0 Тогда 
		Код = СокрЛП(Док.UUID);
	Иначе
		Код = СокрЛП(Док.НомерДок);
	КонецЕсли;
	Рез = КаталогПеревозки+док.Вид()+"_"+Код+".mrk";

	Возврат Рез;
КонецФункции

Функция ПолучитьОтвет_ОбновитьТранспорт( applicationID, докСсылка )
	//стандартное поведение функции
	Если ПустоеЗначение(applicationID)=1 Тогда
		Сообщить("Пустая ссылка applicationID","!");
		Возврат "Пустая ссылка applicationID";
	КонецЕсли;

	ГМ.Пауза( );

	Сообщить("Отправляем запрос ["+докСсылка+"]" );
	//Результат = ГМ.Компонента.SendRequestResult( applicationID );
	Результат = ГМ.ПолучитьРезультатСервер( applicationID );
    Файл_Ответа_XML = ГМ.Компонента.LogFilename;
	Если ПустоеЗначение(докСсылка)=0 Тогда
		ГМ.СохранитьСтатусЗапроса(докСсылка);
		//ЗагрузитьXML_ВСД2( Файл_Ответа_XML, докСсылка);
	КонецЕсли;

	Если ГМ.НайтиОшибки( докСсылка )>0 Тогда
		Возврат Результат;
	КонецЕсли;

	ЗагрузитьXML_ВСД2( Файл_Ответа_XML,,1);

	ГМ.СохранитьОтветВДокументе( докСсылка, , applicationID);

	ГМ.УдалитьXML( Файл_Ответа_XML );

	Если (ПустоеЗначение(докСсылка)=0) И
	 	((СокрЛП(докСсылка.Статус)="COMPLETED") ИЛИ (СокрЛП(докСсылка.Статус)="CONFIRMED") ) Тогда
		Попытка
		// проведем только успешно выполненный запрос
			докОснованиеОбъект = СоздатьОбъект("Документ");
			докОснованиеОбъект.НайтиДокумент(докСсылка);
			докОснованиеОбъект.Провести();
		Исключение
		КонецПопытки;

	КонецЕсли;

	Возврат Результат;

КонецФункции

Функция ОбновитьТранспорт_ЗапросXML( док )

	//Если док.ДокОснование.Вид() = "ВСД2" Тогда
	//	докОснование = док;
	//Иначе
	//	тз_ВСД = ГМ.Выбрать_ВСД( док.ДокОснование );
	//КонецЕсли;

	Запрос = "
	|<SOAP-ENV:Envelope
	|xmlns:dt='http://api.vetrf.ru/schema/cdm/dictionary/v2'
	|xmlns:bs='http://api.vetrf.ru/schema/cdm/base'
	|xmlns:merc='http://api.vetrf.ru/schema/cdm/mercury/g2b/applications/v2'
	|xmlns:apldef='http://api.vetrf.ru/schema/cdm/application/ws-definitions'
	|xmlns:apl='http://api.vetrf.ru/schema/cdm/application'
	|xmlns:vd='http://api.vetrf.ru/schema/cdm/mercury/vet-document/v2'
	|xmlns:SOAP-ENV='http://schemas.xmlsoap.org/soap/envelope/'>
	|<SOAP-ENV:Header/>
	|<SOAP-ENV:Body>
	|<apldef:submitApplicationRequest>
	|<apldef:apiKey>"+ ГМ.СписокКонстант.Получить("param_api_key") + "</apldef:apiKey>
	|<apl:application>
	|<apl:serviceId>mercury-g2b.service:2.0</apl:serviceId>
	|<apl:issuerId>"+ГМ.СписокКонстант.Получить("param_issuer_id")+"</apl:issuerId>
	|<apl:issueDate>"+ ГМ.Преобразовать_Дата_в_Строка(ТекущаяДата(), "T00:00:00") +"</apl:issueDate>
	|<apl:data>
	|<merc:updateTransportMovementDetailsRequest>
	|<merc:localTransactionId>"+ СокрЛП(док.НомерДок) +"</merc:localTransactionId>
	|<merc:initiator>
	|<vd:login>"+ СокрЛП( ГМ.СписокКонстант.Получить("param_intiator_login") ) +"</vd:login>
	|</merc:initiator>
	|
	|<vd:deliveryParticipant>
	|<dt:businessEntity>
	| <bs:guid>"+СокрЛП(док.Отправитель_ХозСубъект.GUID)+"</bs:guid>
	|</dt:businessEntity>
	|<dt:enterprise>
	|<bs:guid>"+ СокрЛП(док.Отправитель_Площадка.GUID) +"</bs:guid>
	|</dt:enterprise>
	|</vd:deliveryParticipant>
	|";

	//Запрос=Запрос+"
	//	|<vd:vetDocumentUuid>"+ док.ДокОснование.UUID +"</vd:vetDocumentUuid>
	//	|";

	док.ВыбратьСтроки();
	Пока док.ПолучитьСтроку() = 1 Цикл
	Запрос=Запрос+"
		|<vd:vetDocumentUuid>"+ док.ВСД_Док.UUID +"</vd:vetDocumentUuid>
		|";
	КонецЦикла;

	Запрос=Запрос+"
	|<vd:shipmentRoute>
	|";
	док.ВыбратьСтроки();
	Пока док.ПолучитьСтроку() = 1 Цикл
		Запрос=Запрос+"
		|<vd:routePoint>
		|<bs:uuid>"+ СокрЛП( док.ТочкаМаршрутаUUID ) +"</bs:uuid>
		|<vd:sqnId>"+ СокрЛП(док.SequenceNumber) +"</vd:sqnId>
		|<vd:nextTransport>
		|<vd:transportType>"+ док.transportType +"</vd:transportType>
		|<vd:transportNumber>";
		//Попытка
			Если док.transportType = 1 Тогда
				 // Контейнер при наличии всегда первый
				Если ПустоеЗначение(док.containerNumber) = 0 Тогда
					Запрос=Запрос+"
       				|<vd:containerNumber>"+СокрЛП(док.containerNumber)+"</vd:containerNumber>";
				КонецЕсли;
				Запрос=Запрос+"
				|<vd:vehicleNumber>"+ СокрЛП(док.vehicleNumber) +"</vd:vehicleNumber>";

				Если ПустоеЗначение(док.trailerNumber) = 0 Тогда
					Запрос=Запрос+"
       				|<vd:trailerNumber>"+СокрЛП(док.trailerNumber)+"</vd:trailerNumber>";
				КонецЕсли;
			ИначеЕсли док.transportType = 2 Тогда
				Запрос=Запрос+"
				|<vd:wagonNumber>"+ СокрЛП(док.wagonNumber) +"</vd:wagonNumber>";
			ИначеЕсли док.transportType = 3 Тогда
				Запрос=Запрос+"
				|<vd:flightNumber>"+ СокрЛП(док.flightNumber) +"</vd:flightNumber>";
			ИначеЕсли (док.transportType = 4) ИЛИ (док.transportType = 5) ИЛИ (док.transportType = 6) Тогда
				Запрос=Запрос+"
				|<vd:shipName>"+ СокрЛП(док.shipName) +"</vd:shipName>";
			КонецЕсли;
		//Исключение
		//	Сообщить("Обновите конфигурацию - отсутствуют ревизиты тч в документе ВСД2_ОбновитьТранспорт");
		//	Сообщить(ОписаниеОшибки());
		//КонецПопытки;

		Запрос=Запрос+"
		|</vd:transportNumber>
		|</vd:nextTransport>
		|</vd:routePoint>
		|";
	КонецЦикла;
	Запрос=Запрос+"
	|</vd:shipmentRoute>
	|</merc:updateTransportMovementDetailsRequest>
	|</apl:data>
	|</apl:application>
	|</apldef:submitApplicationRequest>
	|</SOAP-ENV:Body>
	|</SOAP-ENV:Envelope>
	|";

	Возврат Запрос;
КонецФункции

// док = ВСД2_ОбновитьТранспорт
// http://help.vetrf.ru/wiki/UpdateTransportMovementDetailsOperation_v2.0
Функция Отправить_ОбновитьТранспортВМаршуртеДоставки( док ) Экспорт
	//{Проверим на переопределение функции, Если переопределена вызовем из перекрывающего модуля
	ОбъектВыполнения = ПроверитьПереопределениеФункции("Отправить_ОбновитьТранспортВМаршуртеДоставки");
	Если ПустоеЗначение(ОбъектВыполнения)=0 Тогда
		Возврат ОбъектВыполнения.Отправить_ОбновитьТранспортВМаршуртеДоставки( док );
	КонецЕсли;
	//}

	t1 = ГМ.Старт();

	ЗапросXML = ОбновитьТранспорт_ЗапросXML( док );
	
	appID = ОтправитьЗапрос2( док, ЗапросXML );

//	метод = ГМ.Компонента.GetMethod();
//	метод.Service = "platform/services/2.0/ApplicationManagementService";
//    метод.Action = "submitApplicationRequest";
//
//	appID = ГМ.ОтправитьЗапросSoap(ЗапросXML, метод);
//
//	ГМ.СохранитьЗапросВДокументе( док );
//
//	ГМ.УдалитьXML( ГМ.Компонента.LogFilename);
//
//	Если ПустоеЗначение(appID)=0 Тогда
//
//		Результат = ПолучитьОтвет_ОбновитьТранспорт( appID, док );
//		Сообщить("Результат = "+Результат);
//		Для А=1 По 10 Цикл
//			Если (Результат="IN_PROCESS") Тогда
//				Результат = ПолучитьОтвет_ОбновитьТранспорт( appID, док );
//			КонецЕсли;
//		КонецЦикла;
//	КонецЕсли;

	ГМ.Финиш(t1, Сам(), "Отправить_ОбновитьТранспортВМаршуртеДоставки", док);	
	
	Возврат appID;

КонецФункции
//}

//{ ===================== Объединение Партий
Функция ПроверитьВСД2_ОбъединениеПартий(ВСДСсылка)
	
	Если ПустоеЗначение(ВСДСсылка.applicationID) = 0 Тогда
	    Сообщить("Запрос отправлен, повторная отправка запроса запрещена","!!");
		Возврат 0;
	КонецЕсли;	
	
	Если ПустоеЗначение(ВСДСсылка) = 1 Тогда
		Возврат 0;
	КонецЕсли;
	Попытка
		//Фирма есть не везде		
		Если ПустоеЗначение(ВСДСсылка.Фирма) = 1 Тогда
			Сообщить("Не указана Организация в документе "+ВСДСсылка);
			Возврат 0;
		КонецЕсли;
	Исключение 
	КонецПопытки;
	Если (СокрЛП(ВСДСсылка.Статус)="COMPLETED") Тогда
		Сообщить(""+ВСДСсылка+" статус "+ВСДСсылка.Статус+" НЕ отправляем");
		Возврат 0;
	КонецЕсли;
	//Проверить Отправителя ХС
//	Если НЕ(ВСДСсылка.Владелец_ХозСубъект = Отправитель_Хозсубъект) Тогда
//		Сообщить("Другой ХС Производитель - Выберите в Параметрах организацию для  "+ВСДСсылка.Владелец_ХозСубъект);
//		Возврат 0;
//	КонецЕсли;
	Если ВСДСсылка.ОперацияПрисоединения = 0 Тогда
		Если ПустоеЗначение(ВСДСсылка.ВСД_ПродукцияЭлемент) = 1 Тогда
			Сообщить("При объединении Партий значение Продукция_Элемент Обязательно для Заполнения !!!");
			Возврат 0;
		КонецЕсли;
	КонецЕсли;
	ЕстьОшибки = 0;
	ВСДСсылка.ВыбратьСтроки();
	Пока ВСДСсылка.ПолучитьСтроку() = 1 Цикл
		Если ПустоеЗначение(ВСДСсылка.Партия) = 1 Тогда
			Сообщить("Не указана Партия в документе "+ВСДСсылка+" не отправлен!");
			ЕстьОшибки = 1;
			Прервать;
		КонецЕсли;
		Если (ПустоеЗначение(ВСДСсылка.ДатаИзготовления1) = 1) ИЛИ (ПустоеЗначение(ВСДСсылка.ДатаСрокГодности1) = 1) Тогда
			ЕстьОшибки = 1;
			Сообщить("Не указаны Дата Изготовления И/ИЛИ Срок годности  "+ВСДСсылка.Партия+" в документе "+ВСДСсылка+" не отправлен!");
			Прервать;
		КонецЕсли;

	КонецЦикла;
	Если ЕстьОшибки = 1 Тогда
		Возврат 0;
	Иначе
		Возврат 1;
	КонецЕсли;
КонецФункции

Функция ВСД2_ОбъединениеПартий_ЗапросXML(док)
	//{Проверим на переопределение функции, Если переопределена вызовем из перекрывающего модуля
	ОбъектВыполнения = ПроверитьПереопределениеФункции("ВСД2_ОбъединениеПартий_ЗапросXML");
	Если ПустоеЗначение(ОбъектВыполнения)=0 Тогда
		Возврат ОбъектВыполнения.ВСД2_ОбъединениеПартий_ЗапросXML( док );
	КонецЕсли;
	//}

	Запрос ="<SOAP-ENV:Envelope
		|xmlns:dt='http://api.vetrf.ru/schema/cdm/dictionary/v2'
		|xmlns:bs='http://api.vetrf.ru/schema/cdm/base'
		|xmlns:merc='http://api.vetrf.ru/schema/cdm/mercury/g2b/applications/v2'
		|xmlns:apldef='http://api.vetrf.ru/schema/cdm/application/ws-definitions'
		|xmlns:apl='http://api.vetrf.ru/schema/cdm/application'
		|xmlns:vd='http://api.vetrf.ru/schema/cdm/mercury/vet-document/v2'
		|xmlns:SOAP-ENV='http://schemas.xmlsoap.org/soap/envelope/'>
		|<SOAP-ENV:Header/>
		|<SOAP-ENV:Body>
		|<apldef:submitApplicationRequest>
		|<apldef:apiKey>"+ СокрЛП( ГМ.СписокКонстант.Получить("param_api_key") ) +"</apldef:apiKey>
		|<apl:application>
		|<apl:serviceId>mercury-g2b.service:2.0</apl:serviceId>
		|<apl:issuerId>"+ СокрЛП( ГМ.СписокКонстант.Получить("param_issuer_id") ) +"</apl:issuerId>
		|<apl:issueDate>" + ГМ.Преобразовать_Дата_в_Строка(док.Датадок, "T00:00:00") + "</apl:issueDate>
		|<apl:data>
		|<merc:mergeStockEntriesRequest>
		|<merc:localTransactionId>" + СокрЛП(док.НомерДок) + "</merc:localTransactionId>
		|<merc:initiator>
		|<vd:login>"+ СокрЛП( ГМ.СписокКонстант.Получить("param_intiator_login") ) +"</vd:login>
		|</merc:initiator>
		|<merc:enterprise>
		|<bs:guid>"+ СокрЛП(док.Владелец_Площадка.GUID) +"</bs:guid>
		|</merc:enterprise>
		|<merc:mergeOperation>
		|<vd:type>"+?(Док.ОперацияПрисоединения = 1,"ATTACH","MERGE")+"</vd:type>";
		Док.ВыбратьСтроки();
		Пока Док.ПолучитьСтроку() = 1 Цикл
			Запрос=Запрос+"
			|<vd:sourceStockEntry>
			| <bs:uuid>" + СокрЛП(Док.Партия.uuid) + "</bs:uuid>
			|</vd:sourceStockEntry>";
		КонецЦикла;
		Если Док.ОперацияПрисоединения=0 Тогда
			Запрос=Запрос+"
			|<vd:resultStockEntry>
        	|<vd:batch>
        	|<vd:subProduct>
//        	|<bs:uuid>a3cecd19-98ca-46f9-ecd5-90c41fd95e13</bs:uuid>
        	|<bs:guid>"+ СокрЛП(Док.ВСД_ПродукцияЭлемент.ВидПродукции.GUID) +"</bs:guid>
        	|</vd:subProduct>";
			Запрос = Запрос + "
        	|<vd:productItem>";
			Если ПустоеЗначение(Док.ВСД_ПродукцияЭлемент.guid) = 0 Тогда
				Запрос = Запрос + "
        		|<bs:guid>" + СокрЛП(Док.ВСД_ПродукцияЭлемент.guid) + "</bs:guid>";
			КонецЕсли;
			Если ПустоеЗначение(Док.ВСД_ПродукцияЭлемент.GTIN) = 0 Тогда
				Запрос = Запрос + "
        		|<dt:globalID>" + СокрЛП(Док.ВСД_ПродукцияЭлемент.GTIN) + "</dt:globalID>";
			КонецЕсли;
			Запрос = Запрос + "
        	|<dt:name>" + ЗаменитьСпецСимволы(Док.ВСД_ПродукцияЭлемент.Наименование) + "</dt:name>";
			Если ПустоеЗначение(Док.ВСД_ПродукцияЭлемент.Артикул) = 0 Тогда
				Запрос = Запрос + "
        		|<dt:code>" + СокрЛП(Док.ВСД_ПродукцияЭлемент.Артикул) + "</dt:code>";
			КонецЕсли;
        	Запрос = Запрос + "
        	|</vd:productItem>";
			Запрос=Запрос+"
        	|</vd:batch>
        	|</vd:resultStockEntry>";
		КонецЕсли;
		Запрос=Запрос+"
		|</merc:mergeOperation>
		|</merc:mergeStockEntriesRequest>
		|</apl:data>
		|</apl:application>
		|</apldef:submitApplicationRequest>
		|</SOAP-ENV:Body>
		|</SOAP-ENV:Envelope>";

	Возврат Запрос;
КонецФункции

Функция Отправить_ВСД2_ОбъединениеПартий(док) Экспорт

	t1 = ГМ.Старт();
	
	Если ПроверитьВСД2_ОбъединениеПартий(док) = 0 Тогда
		Возврат 0;
	КонецЕсли;

//	Организация = док.Организация;//док.ДокументОснование.Организация;
//	Инициализация();

	ГМ.СписокКонстант.Установить("Отправитель_Площадка",док.Владелец_Площадка);

	ЗапросXML = ВСД2_ОбъединениеПартий_ЗапросXML(док);

	appID = ОтправитьЗапрос2( док, ЗапросXML );
	
//	метод = ГМ.Компонента.GetMethod();
//	метод.Service = "platform/services/2.0/ApplicationManagementService";
//    //метод.Service = "platform/services/ApplicationManagementService";
//    метод.Action = "submitApplicationRequest";
//
//	appID = ГМ.ОтправитьЗапросSoap(ЗапросXML, метод);
//	ГМ.СохранитьЗапросВДокументе(док);
//
//	Если ГМ.НайтиОшибки( док )>0 Тогда
//		Возврат appID;
//	КонецЕсли;	
//
//	ГМ.УдалитьXML( ГМ.Компонента.LogFilename);
//
//	Если ПустоеЗначение(appID) = 0 Тогда
//		спПараметры = СоздатьОбъект("СписокЗначений");	
//		спПараметры.Установить("applicationID", appID );
//		спПараметры.Установить("докСсылка", док );		
//
//		Результат = ПолучитьРезультат_ВСД_2( спПараметры );			
//		//Результат = ПолучитьРезультат_ВСД_2(appID, док );
//
//		Сообщить("Результат = "+Результат);
//		Для А=1 По 10 Цикл
//			Если (Результат="IN_PROCESS") Тогда
//				Результат = ПолучитьРезультат_ВСД_2( спПараметры );	
//				//Результат = ПолучитьРезультат_ВСД_2(appID, док );
//			КонецЕсли;
//		КонецЦикла;
//	КонецЕсли;
//	докОбъект = СоздатьОбъект("Документ");
//	докОбъект.НайтиДокумент( док );
//	Если СокрЛП(докОбъект.Статус) = "COMPLETED" Тогда
//		докОбъект.Записать();
//		докОбъект.Провести();
//	КонецЕсли;
	
	ГМ.Финиш(t1, Сам(), "Отправить_ВСД2_ОбъединениеПартий", док);
	
	Возврат appID;
КонецФункции

//}*************** Объединение Партий v2 Окончание

//*************** ХС **************************

Функция ЗагрузитьХС(GUID) Экспорт

	//{Проверим на переопределение функции, Если переопределена вызовем из перекрывающего модуля
	ОбъектВыполнения = ПроверитьПереопределениеФункции("ЗагрузитьХС");
	Если ПустоеЗначение(ОбъектВыполнения)=0 Тогда
		Возврат ОбъектВыполнения.ЗагрузитьХС(guid);
	КонецЕсли;
	//}

	Если ПустоеЗначение(GUID)=1 Тогда
		Сообщить("Пустой GUID для поиска ХС");
		Возврат "REJECTED";
	КонецЕсли;
	ЗапросXML = "
	|<soapenv:Envelope xmlns:bs='http://api.vetrf.ru/schema/cdm/base'
	|		  xmlns:ws='http://api.vetrf.ru/schema/cdm/registry/ws-definitions/v2'
	|		  xmlns:soapenv='http://schemas.xmlsoap.org/soap/envelope/'>
	|<soapenv:Header/>
	|<soapenv:Body>
	| <ws:getBusinessEntityByGuidRequest>
	|<bs:guid>"+GUID+"</bs:guid>
	| </ws:getBusinessEntityByGuidRequest>
	|</soapenv:Body>
	|</soapenv:Envelope>";


	метод = ГМ.Компонента.GetMethod();
	метод.Service = "platform/services/2.1/EnterpriseService";
	метод.Action = "GetBusinessEntityByGUID";

	Сообщить(" Запрос getBusinessEntityByGuid [ "+СокрЛП(GUID)+" ]");

	appID = ГМ.ОтправитьЗапросSoap(ЗапросXML, метод);

	Возврат appID;
КонецФункции

//*************** Заполнение документов **********************

Функция ЗаполнитьВСД2_Транзакция_Корректировочный( КонтДокумента, ДокОснование ) Экспорт
	
	КонтДокумента.Отправитель_ХозСубъект = ДокОснование.Отправитель_ХозСубъект;
	КонтДокумента.Отправитель_Площадка = ДокОснование.Отправитель_Площадка ;	
	КонтДокумента.Получатель_ХозСубъект = ДокОснование.Получатель_ХозСубъект;
	КонтДокумента.Получатель_Площадка = ДокОснование.Получатель_Площадка;
	КонтДокумента.cargoInspected = ДокОснование.cargoInspected;
	КонтДокумента.РезультатыИсследований = ДокОснование.РезультатыИсследований;
	КонтДокумента.Местность = ДокОснование.Местность;
	
	Попытка КонтДокумента.Автор = ГМ.ПолучитьАвтора(); Исключение КонецПопытки;
	
	ВыбФирма = ГМ.ПолучитьФирмуИзКонтекста(ДокОснование);
	ФирмаИмяРеквизита = "";
	ГМ.ПолучитьИмяРеквизитаФирма(КонтДокумента.Вид(), ФирмаИмяРеквизита);
	Если ФирмаИмяРеквизита <> "" Тогда
		КонтДокумента.УстановитьАтрибут(ФирмаИмяРеквизита, ВыбФирма);
	КонецЕсли;	
	
	
	тзВСД = СоздатьОбъект("ТаблицаЗначений");
	тзВСД = ГМ.Выбрать_ВСД( ДокОснование );
	
	тзВСД.ВыбратьСтроки();
	Пока тзВСД.ПолучитьСтроку() = 1 Цикл
		Если тзВСД.Док.СтатусВСД = Перечисление.ВСД_СтатусВСД.WITHDRAWN Тогда 
			КонтДокумента.НоваяСтрока();
			//КонтДокумента.Партия = тзВСД.Док.Партия;
			КонтДокумента.Количество = тзВСД.Док.Количество; 
			КонтДокумента.СвязанныйДокументВСД = тзВСД.Док;
			
			ГМ.ЗаполнитьСтрокуДокумента( КонтДокумента, тзВСД.Док, тзВСД.Док );		
		КонецЕсли;
	КонецЦикла;
	
	КонтДокумента.ТермическоеСостояние = ГМ.УстановитьТермическоеСостояние( КонтДокумента );
	
	КонтДокумента.номерАвто = ДокОснование.номерАвто;
	Попытка		
		КонтДокумента.ТтнСерия = ДокОснование.ТтнСерия;
		КонтДокумента.ТтнНомер = ДокОснование.ТтнНомер;
		КонтДокумента.ТтнДата = ДокОснование.ТтнДата;
	Исключение 	КонецПопытки;
	
	Попытка
		КонтДокумента.НомерЗаказаПокупателя = ДокОснование.НомерЗаказаПокупателя;
		КонтДокумента.ДатаЗаказаПокупателя = ДокОснование.ДатаЗаказаПокупателя;
	Исключение 	КонецПопытки;
	
	Попытка
		КонтДокумента.ТОРГ12Номер = ДокОснование.ТОРГ12Номер;
		КонтДокумента.ТОРГ12Дата = ДокОснование.ТОРГ12Дата;
	Исключение 	КонецПопытки;
	
КонецФункции

Функция ЗаполнитьВСД2_Транзакция( КонтДокумента, ДокОснование, тзТабличнаяЧасть="", ЗаполнитьПартии=0) Экспорт
	
	// заполнение кастомного ВСД  - ЖД Доработать в этом случае отменить Ввод на основании И открыть Созданный док
	Если ФС.СуществуетФайл(ГМ.ПолучитьКонстанту("КаталогМодуля")+"ПодключаемыеМодули\Меркурий_Подключаемый_Создание_ВСД2_транзакция.ert")=1 Тогда
		//{ переопределение функции создания ВСД 
		// интеграция переопределяется в Меркурий_Подключаемый_Создание_ВСД_Производство.ert
		Сообщить("ПодключаемыеМодули\Меркурий_Подключаемый_Создание_ВСД2_транзакция.ert отключено.
		|Используйте модуль Меркурий_Интеграция.ert для переопределения функций","!");

		//}
	КонецЕсли;
	
	КонтДокумента.ДокОснование = ДокОснование;
	
	// стандартное создание ВСД
	Если ПустоеЗначение(ДокОснование)=0 Тогда	
		Если ДокОснование.Вид() = "ВСД2_Транзакция" Тогда
			// корректировочный ВСД2_Транзакцичя на основании аннулированного ВСД2_Транзакция
			
			ЗаполнитьВСД2_Транзакция_Корректировочный(КонтДокумента, ДокОснование);
			Возврат "";
			
		Иначе
				
			Если ГМ._ВводНаОсновании( КонтДокумента, ДокОснование) = 0 Тогда
			    //Возврат "";
			КонецЕсли;
			
			ВыбФирма = ГМ.ПолучитьФирмуИзКонтекста(ДокОснование);
		КонецЕсли;
	Иначе 
		ВыбФирма = ГМ.ПолучитьФирмуИзКонтекста(КонтДокумента);
	КонецЕсли;

	Если ПустоеЗначение(КонтДокумента.Отправитель_Площадка)=1 Тогда
		Сообщить("Не указан Отправитель_Площадка!","!!");
	КонецЕсли;
	Если ПустоеЗначение(КонтДокумента.Отправитель_ХозСубъект)=1 Тогда
		Сообщить("Не указан Отправитель_ХозСубъект!","!!");
	КонецЕсли;
	

	
	Попытка КонтДокумента.Автор = ГМ.ПолучитьАвтора(); Исключение КонецПопытки;
	
	ФирмаИмяРеквизита = "";
	ГМ.ПолучитьИмяРеквизитаФирма(КонтДокумента.Вид(), ФирмаИмяРеквизита);
	Если ФирмаИмяРеквизита <> "" Тогда
		КонтДокумента.УстановитьАтрибут(ФирмаИмяРеквизита, ВыбФирма);
	КонецЕсли;	
	
	Если ПустоеЗначение(ДокОснование)=0 Тогда 
		КонтДокумента.ДатаДок = ДокОснование.ДатаДок;			
		КонтДокумента.ТтнСерия = "";
		КонтДокумента.ТтнНомер = ДокОснование.НомерДок; 
		КонтДокумента.ТтнДата = ДокОснование.ДатаДок;
		КонтДокумента.номерАвто = ГМ.ПолучитьНомерАвто( ДокОснование );
	Иначе
		КонтДокумента.ДатаДок = ТекущаяДата();			
		КонтДокумента.ТтнСерия = "";
		КонтДокумента.ТтнНомер = КонтДокумента.НомерДок; 
		КонтДокумента.ТтнДата = КонтДокумента.ДатаДок;
		КонтДокумента.номерАвто = "Не используется";
	КонецЕсли;
	
	КонтДокумента.cargoInspected  = 1;		
	КонтДокумента.Местность 		= ГМ.ПолучитьКонстанту("ВСД_Местность");
	Если ГМ.ПолучитьКонстанту("ОсобыеОтметкиЗагружатьИзПараметров")<>1 Тогда 
		КонтДокумента.ОсобыеОтметки 	= ГМ.ПолучитьКонстанту("ВСД_ОсобыеОтметки");
	КонецЕсли;
	
	Если ПустоеЗначение(тзТабличнаяЧасть)=1 Тогда
		тчДокумента = ГМ.ЗаполнитьТчДокументаПоАктуальнымПартиям( ДокОснование, КонтДокумента.Отправитель_Площадка, КонтДокумента.Отправитель_ХозСубъект);
		КонтДокумента.ЗагрузитьТабличнуюЧасть(тчДокумента);
	Иначе
		тзПартий = тзТабличнаяЧасть;
		
		тзПартий.ВыбратьСтроки();
		Пока тзПартий.ПолучитьСтроку() = 1 Цикл
			КонтДокумента.НоваяСтрока();
			КонтДокумента.Партия = тзПартий.Партия;
			КонтДокумента.Количество = тзПартий.Количество; 
			
			Попытка КонтДокумента.ПолеСортировки = тзПартий.ДатаИзготовления;  Исключение КонецПопытки;   //!! для выбора партии По Дате Изготовления
			
			ГМ.ЗаполнитьСтрокуДокумента( КонтДокумента, тзПартий, ДокОснование );		
		КонецЦикла;		
		
		//Для внутренних перемещений - перезаполняем партии По другому ХС/Площадке
		Если ЗаполнитьПартии=1 Тогда 
			тчДокумента = ГМ.ЗаполнитьТчДокументаПоАктуальнымПартиям( КонтДокумента, КонтДокумента.Отправитель_Площадка, КонтДокумента.Отправитель_ХозСубъект);
			КонтДокумента.ЗагрузитьТабличнуюЧасть(тчДокумента);
		КонецЕсли;
	КонецЕсли;

	КонтДокумента.ТермическоеСостояние = ГМ.УстановитьТермическоеСостояние( КонтДокумента );
	
	Попытка
		СзРеквДокумента = ГМ.ПолучитьСвязанныйДокумент(ДокОснование,"ТТН");
		КонтДокумента.ТтнСерия = СзРеквДокумента.Получить("ДокСерия");
		КонтДокумента.ТтнНомер = СзРеквДокумента.Получить("ДокНомер");
		КонтДокумента.ТтнДата = СзРеквДокумента.Получить("ДокДата");
	Исключение 	КонецПопытки;
	
	Попытка
		СзРеквДокумента = ГМ.ПолучитьСвязанныйДокумент(ДокОснование,"Заказ");  
		КонтДокумента.НомерЗаказаПокупателя = СзРеквДокумента.Получить("ДокНомер");
		КонтДокумента.ДатаЗаказаПокупателя = СзРеквДокумента.Получить("ДокДата");
	Исключение 	КонецПопытки;
	
	Попытка
		СзРеквДокумента = ГМ.ПолучитьСвязанныйДокумент(ДокОснование,"ТОРГ12");  
		КонтДокумента.ТОРГ12Номер = СзРеквДокумента.Получить("ДокНомер");
		КонтДокумента.ТОРГ12Дата = СзРеквДокумента.Получить("ДокДата");
	Исключение 	КонецПопытки;

    ОбъектВыполнения = ПроверитьПереопределениеФункции("ЗаполнитьВСД2_Транзакция_После");
    Если ПустоеЗначение(ОбъектВыполнения)=0 Тогда
           Возврат ОбъектВыполнения.ЗаполнитьВСД2_Транзакция_После(КонтДокумента, ДокОснование, тзТабличнаяЧасть, ЗаполнитьПартии);
    КонецЕсли;	
КонецФункции

// { Площадки v2.1

Функция Площадка_ЗагрузитьПоGUID( ВыбПлощадка, GUID, ХС="") Экспорт

	Если ПустоеЗначение(GUID)=1 Тогда
		Сообщить("Пустой GUID для поиска Площадки");
		Возврат "REJECTED";
	КонецЕсли;
	
	ЗапросXML = "
	|<soapenv:Envelope xmlns:bs='http://api.vetrf.ru/schema/cdm/base'
	|		  xmlns:ws='http://api.vetrf.ru/schema/cdm/registry/ws-definitions/v2'
	|		  xmlns:soapenv='http://schemas.xmlsoap.org/soap/envelope/' >
	|<soapenv:Header/>
	|<soapenv:Body>
	| <ws:getEnterpriseByGuidRequest>
	|<bs:guid>"+GUID+"</bs:guid>
	| </ws:getEnterpriseByGuidRequest>
	|</soapenv:Body>
	|</soapenv:Envelope>";


	метод = ГМ.Компонента.GetMethod();
	метод.Service = "platform/services/2.1/EnterpriseService";
	метод.Action = "GetEnterpriseByGUID";

	Сообщить(" Запрос GetEnterpriseByGUID [ "+СокрЛП(GUID)+" ]");

	Результат = ГМ.ОтправитьЗапросSoap(ЗапросXML, метод);

	//Возврат appID;
	
    objDom=СоздатьОбъект("MSXML2.DOMDocument");
    objDom.load( ГМ.Компонента.LogFilename );

	Если ГМ.НайтиОшибки()>0 Тогда
		Возврат Результат;
	КонецЕсли;

	СпрОбъект = СоздатьОбъект("Справочник.ВСД_Площадка");
	СпрОбъект.НайтиЭлемент( ВыбПлощадка );

	НачатьТранзакцию();
	Попытка
		
		enterprise = objDom.selectSingleNode("//dt:enterprise") ;
		
		active = enterprise.selectSingleNode("bs:active").text;
		Если Врег(active)="TRUE" Тогда  
			СпрОбъект.active =1;
			СпрОбъект.СнятьПометкуУдаления();
		Иначе 
			СпрОбъект.active =0;
			СпрОбъект.Удалить(0);
		КонецЕсли;
	    СпрОбъект.Адрес = enterprise.selectSingleNode("dt:address").selectSingleNode("dt:addressView").text;
	    Попытка 
			СтранаGUID = enterprise.selectSingleNode("dt:address").selectSingleNode("dt:country").selectSingleNode("bs:guid").text;
			СтранаName = enterprise.selectSingleNode("dt:address").selectSingleNode("dt:country").selectSingleNode("dt:name").text;
			СпрОбъект.Страна = Сам().НайтиСтрануПоGUID( СтранаGUID, СтранаName );
		Исключение
		КонецПопытки;
	    Попытка 
			regionGUID = enterprise.selectSingleNode("dt:address").selectSingleNode("dt:region").selectSingleNode("bs:guid").text;
			regionName = enterprise.selectSingleNode("dt:address").selectSingleNode("dt:region").selectSingleNode("dt:name").text;
			СпрОбъект.Регион = Сам().Справочник_НайтиПоGUID( "ВСД_Регион", regionGUID, regionName, СпрОбъект.Страна );
		Исключение
		КонецПопытки;
	    Попытка 
			districtGUID = enterprise.selectSingleNode("dt:address").selectSingleNode("dt:district").selectSingleNode("bs:guid").text;
			districtName = enterprise.selectSingleNode("dt:address").selectSingleNode("dt:district").selectSingleNode("dt:name").text;
			СпрОбъект.Район = Сам().Справочник_НайтиПоGUID( "ВСД_Район", districtGUID, districtName, СпрОбъект.Регион );
		Исключение
		КонецПопытки;
	    Попытка 
			localityGUID = enterprise.selectSingleNode("dt:address").selectSingleNode("dt:locality").selectSingleNode("bs:guid").text;
			localityName = "";
			СпрОбъект.Город = Сам().Справочник_НайтиПоGUID( "ВСД_Город", localityGUID, localityName, СпрОбъект.Регион );
		Исключение
		КонецПопытки;
	    Попытка 
			streetGUID = enterprise.selectSingleNode("dt:address").selectSingleNode("dt:street").selectSingleNode("bs:guid").text;
			streetName = enterprise.selectSingleNode("dt:address").selectSingleNode("dt:street").selectSingleNode("dt:name").text;
			СпрОбъект.Улица = Сам().Справочник_НайтиПоGUID( "ВСД_Улица", streetGUID, streetName, СпрОбъект.Город );
		Исключение
		КонецПопытки;
	    Попытка 
			СпрОбъект.Дом = enterprise.selectSingleNode("dt:address").selectSingleNode("dt:house").text;
		Исключение
		КонецПопытки;
	    Попытка 
			СпрОбъект.Строение = enterprise.selectSingleNode("dt:address").selectSingleNode("dt:building").text;
		Исключение
		КонецПопытки;
	    Попытка 
			СпрОбъект.Помещение = enterprise.selectSingleNode("dt:address").selectSingleNode("dt:room").text;
		Исключение
		КонецПопытки;

	    Попытка 
			registryStatus = enterprise.selectSingleNode("dt:registryStatus").text;
			СпрОбъект.Статус = Перечисление.ВСД_СтатусПлощадки.ЗначениеПоИдентификатору( registryStatus );			
		Исключение
			ГМ.СообщитьИнфо(ОписаниеОшибки());
		КонецПопытки;
		
		СпрОбъект.Наименование = enterprise.selectSingleNode("dt:name").text;
		СпрОбъект.GUID = enterprise.selectSingleNode("bs:guid").text;
		СпрОбъект.UUID = enterprise.selectSingleNode("bs:uuid").text;
	
		ГМ.УдалитьXML( ГМ.Компонента.LogFilename);
		
		Если ПустоеЗначение(ХС)=0 Тогда			
			СпрОбъект.GUIDХозСубъекта = ХС.GUID;			
		Иначе						
			Если ПустоеЗначение(ХС)=0 Тогда			
				Сообщить("Не выбран ХозСубъект "+ СпрОбъект+" "+ СпрОбъект.Наименование +" "+СпрОбъект.Адрес );
				//Сообщить("ХозСубъект не записан","!");			
			КонецЕсли;
		КонецЕсли;
		Сообщить("Записан "+СпрОбъект.Вид()+" ["+СпрОбъект+"] GUID = "+ СпрОбъект.Guid+" Адрес: "+СпрОбъект.Адрес);					
		СпрОбъект.Записать();
		Результат = СпрОбъект.ТекущийЭлемент();
		ЗафиксироватьТранзакцию();		
		
	Исключение
		Сообщить("Площадка не найдена "+GUID,"!");
		Сообщить(ОписаниеОшибки());
	КонецПопытки;
	
	Возврат Результат;
	
КонецФункции


// }