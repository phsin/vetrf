Перем Сервис;

//******************************************************************************
// глЕстьРеквизитШапки(ИмяРеквизита, ВидДокумента)
//
// Параметры:
//  ИмяРеквизита - имя реквизита (строка)
//  ВидДокумента - вид документа (строка)
//
// Возвращаемое значение:
//  0 - нет реквизита,
//  1 - есть реквизит
//
// Описание:
//  Проверяет, есть ли в структуре метаданных документа указанного вида
// 	реквизит шапки с указанным названием
//
Функция глЕстьРеквизитШапки(ИмяРеквизита, ВидДокумента) Экспорт


	Если ПустоеЗначение(ИмяРеквизита) = 1 Тогда
		Возврат 0;

	ИначеЕсли Метаданные.ОбщийРеквизитДокумента(ИмяРеквизита).Выбран() = 1 Тогда
		Возврат 1;

	ИначеЕсли ПустоеЗначение(ВидДокумента) = 1 Тогда
		Возврат 0;

	ИначеЕсли Метаданные.Документ(ВидДокумента).РеквизитШапки(ИмяРеквизита).Выбран() = 1 Тогда
		Возврат 1;

	Иначе
		Возврат 0;
	КонецЕсли;

КонецФункции //глЕстьРеквизитШапки()
//******************************************************************************
// глНомерНаПечать(Документ, НеПечататьВходящийНомер = 0) Экспорт
//
// Параметры:
//  Документ                - документ, чей номер надо получить для печати.
//  НеПечататьВходящийНомер - Число, 0 - печатем входящий номер, если он есть,
//                                   1 - независимо ни от чего будем печатать номер документа
//
// Возвращаемое значение.
//  Строка - номер документа для печати
//
// Описание:
//  Процедура предназначена для преобразования номера документа.
//  Используется, например, при выводе номера документа на печать.
//
Функция глНомерНаПечать(Документ, НеПечататьВходящийНомер = 0) Экспорт

	Номер = "";

	Если ПустоеЗначение(Документ)= 0 Тогда
		// Если документ имеет входящий номер, то печатать будем его.
		Если (глЕстьРеквизитШапки("НомерДокВходящий", Документ.Вид()) = 1)
		   и (НеПечататьВходящийНомер = 0) Тогда
			Номер=СокрЛП(Документ.НомерДокВходящий);
		КонецЕсли;

		ВидДокумента = Документ.Вид();
		// Если входящий номер пуст или его вообще нет, то печатаем номер документа
		Если ВидДокумента = "СчетФактураВыданный" Тогда
			Номер=СокрЛП(Документ.НомерДок);
			Номер=Сред(Номер, 2);
			// в документах, перенессеных из старых редакций, может остаться "минус" впереди
			Если Лев(Номер, 1) = "-" Тогда
				Номер	= Сред(Номер, 2);
			КонецЕсли;

			// удаление ведущих нулей
			Пока Лев(Номер, 1)="0" Цикл
				Номер=Сред(Номер, 2);
			КонецЦикла;
		ИначеЕсли ПустоеЗначение(Номер) = 1 Тогда
			Номер=СокрЛП(Документ.НомерДок);

			// назначенный документу префикс
			Префикс=СокрЛП(Константа.ПрефиксИБ);//+ СокрЛП(Документ.Фирма.ЮрЛицо.ПрефиксНомеровДокументов);

			// удаление префикса из номера документа
			Если Найти(Номер, Префикс)=1 Тогда
				Номер=Сред(Номер, СтрДлина(Префикс)+1);
			КонецЕсли;

			// в документах, перенессеных из старых редакций, может остаться "минус" впереди
			Если Лев(Номер, 1) = "-" Тогда
				Номер	= Сред(Номер, 2);
			КонецЕсли;

			// удаление ведущих нулей
			Пока Лев(Номер, 1)="0" Цикл
				Номер=Сред(Номер, 2);
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;

	Возврат Номер;
КонецФункции // глНомерНаПечать()

Процедура ВывестиСекциюВСД(Таб, ВСД)

	Если ПустоеЗначение(ВСД.UUID)=1 Тогда
		Сообщить("Пропущен "+ВСД+" - "+ВСД.Статус+" - UUID ["+ВСД.UUID+"]","!");
		Возврат;
	КонецЕсли;
	Если НЕ(СокрЛП(ВСД.Статус) = "CONFIRMED") Тогда
	    Возврат;
	КонецЕсли;

	тВСД = "Ветеринарное свидетельство (оформлен) Код: "+ВСД.UUID;
	тОтправитель = "Фирма-отправитель: "+ ГМ.ПолучитьНаименованиеКлиента(ВСД.Отправитель_ХозСубъект.Контрагент)+", ИНН:"+
		ГМ.ПолучитьИНН(ВСД.Отправитель_ХозСубъект.Контрагент)+", ТТН: № "+СокрЛП(ВСД.ТТНномер)+" от "+ВСД.ТТНДата;
	Попытка
		тПолучатель = "Фирма-получатель: "+ГМ.ПолучитьНаименованиеКлиента(ВСД.Получатель_ХозСубъект.Контрагент)+", ИНН: "+
			ГМ.ПолучитьИНН(ВСД.Получатель_ХозСубъект.Контрагент);
	Исключение
		тПолучатель ="";
	КонецПопытки;

	Попытка
		тПлощадка = "Предприятие-получатель: "+ГМ.ПолучитьНаименованиеКлиента(ВСД.Получатель_Площадка.Контрагент)+" - "+СокрЛП(ВСД.Получатель_Площадка.Адрес);//ГМ.ПолучитьФактАдрес(ВСД.Получатель_Площадка.Контрагент);
	Исключение
		//тПлощадка = "Предприятие-получатель: "+ВСД.Получатель_Площадка+" - "+ВСД.Получатель_ХозСубъект.Контрагент.ЮрФизЛицо.ФактАдрес;
		тПлощадка = "Предприятие-получатель: "+СокрЛП(ВСД.Получатель_Площадка)+" - "+СокрЛП(ВСД.Получатель_Площадка.Адрес);//ГМ.ПолучитьФактАдрес(ВСД.Получатель_Площадка.Контрагент);
	КонецПопытки;
	тПродукция = "Продукция: "+ СокрЛП(ВСД.НаименованиеПродукции)+"; "+ВСД.Количество+" "+ВСД.ЕдиницаИзмерения;
	тВыработано = "Выработана: ";
	Если ПустоеЗначение(ВСД.ДатаИзготовления1)=0 Тогда
		тВыработано = тВыработано + СокрЛП(ВСД.ДатаИзготовления1);
		Если ПустоеЗначение(ВСД.ДатаИзготовления2)=0 Тогда
			тВыработано=тВыработано+ "-" + СокрЛП(ВСД.ДатаИзготовления2);
		КонецЕсли;
	Иначе
		тВыработано = тВыработано + СокрЛП(ВСД.ДатаИзготовления);
	КонецЕсли;
	тКод = "Код: "+ВСД.UUID;

	Автор = ГМ.ПолучитьАвтора();
	тСгенерировано = "Сгенерировано системой '1С' 7.7 "+ТекущаяДата()+" "+ТекущееВремя()+" vetis.kb99.pro
	|"+ГМ.СписокКонстант.Получить("param_vetdoctor_post")+" "+ГМ.СписокКонстант.Получить("param_vetdoctor_fio");

	Сервис = СоздатьОбъект("Сервис");
	шк = "http://mercury.vetrf.ru/pub/operatorui?_language=ru&_action=showVetDocumentFormByUuid&uuid="+ СокрЛП(ВСД.UUID);

	Таб.ВывестиСекцию("Штрихкод");

КонецПроцедуры

Процедура ПечатьСертификат(Докум, Устройство=0, КолвоКопий=0, СразуНаПринтер=0)
	//******************************************************************************

	КолвоСтрокШапка = 9;// в стандартных строках = 11,25
	КолвоСтрокПодвал = 7;// в стандартных строках = 11,25
	КолвоСтрокНаСтранице =65; // в стандартных строках = 11,25
	КолвоСтрокВСД = 9;
	КолвоСтрок = 0;// КолвоСтрокШапка + КолвоСтрокПодвал;

	ТЗ = СоздатьОбъект("ТаблицаЗначений");
	ТЗ.НоваяКолонка("Сертификат");

	Если Докум.Вид() = "ВСД2_транзакция" Тогда
		НомерДок =  глНомерНаПечать(Докум.ДокОснование);
	ИначеЕсли Докум.Вид() = "ВСД2" Тогда
		НомерДок = глНомерНаПечать(Докум.ДокОснование.ДокОснование);//.НомерДок;
	Иначе
		НомерДок = глНомерНаПечать(Докум);//.НомерДок;
	КонецЕсли;

	Таб = СоздатьОбъект("Таблица");
	Таб.ИсходнаяТаблица("Таблица");
	Таб.ВывестиСекцию("Сек_1");


   	Если Докум.Вид() = "ВСД2" Тогда
		ВывестиСекциюВСД(Таб, Докум );
   	Иначе
   		ВСД="";
		Доки =  СоздатьОбъект("Документ");
		Доки.ВыбратьПодчиненныеДокументы( НачГода(Докум.ДатаДок), КонГода(ТекущаяДата()), Докум);
		Пока Доки.ПолучитьДокумент() = 1 Цикл
			Если (Доки.Вид() = "ВСД") или (Доки.Вид() = "ВСД2") Тогда

				Если (КолвоСтрок + КолвоСтрокВСД) > КолвоСтрокНаСтранице Тогда
					КолвоСтрок = 0;
					Таб.НоваяСтраница();
				КонецЕсли;

				Сообщить("Найден "+Доки.ТекущийДокумент()+" "+Доки.Статус);
				ВывестиСекциюВСД(Таб, Доки.ТекущийДокумент());

				КолвоСтрок = КолвоСтрок + КолвоСтрокВСД;
			Иначе

				Док2 = СоздатьОбъект("Документ");
				Док2.ВыбратьПодчиненныеДокументы( НачГода(Доки.ДатаДок), КонГода(Доки.ДатаДок), Доки.ТекущийДокумент());
				Пока Док2.ПолучитьДокумент() = 1 Цикл
					Если (Док2.Вид() = "ВСД") или (Док2.Вид() = "ВСД2") Тогда
						Если (КолвоСтрок + КолвоСтрокВСД) > КолвоСтрокНаСтранице Тогда
							КолвоСтрок = 0;
							Таб.НоваяСтраница();
						КонецЕсли;
                                          
						Если ГМ.ПолучитьКонстанту("ВыводитьПодробнуюИнформацию")=1 Тогда 
							Сообщить("Найден "+Док2.ТекущийДокумент()+" "+Док2.Статус);
						КонецЕсли;
						
						ВывестиСекциюВСД(Таб, Док2.ТекущийДокумент());

						КолвоСтрок = КолвоСтрок + КолвоСтрокВСД;
					КонецЕсли;
				КонецЦикла;
	   		КонецЕсли;
	   	КонецЦикла;
	КонецЕсли;

   	Таб.ПараметрыСтраницы(2,100,1,0,0,0,0,0,0,0,0,Устройство);
   	Таб.ТолькоПросмотр(1);
    таб.Повторятьприпечатистроки(1,3);

	Если СразуНаПринтер = 0 Тогда
		Таб.Опции(0,0,0,0,"ВСДСокращенный");//"ОпцииПечатиРеализация"
		Таб.Показать("ВСД_"+Докум.НомерДок,"");

	Иначе
		//Таб.КоличествоЭкземпляров(КолвоКопий);
		Таб.Напечатать(0);
	КонецЕсли;

КонецПроцедуры

//******************************************************************************
// ПоКнопкеПечать()
//
// Параметры:
//  Нет
//
// Возвращаемое значение:
//  Нет
//
// Описание:
//
Процедура ПоКнопкеПечать()

	Если Док.Выбран() = 0 Тогда
	    Предупреждение("Не выбран документ!", 60);
		Возврат;
	КонецЕсли;

	ПечатьСертификат(Док);

КонецПроцедуры // ПоКнопкеПечать()

//******************************************************************************
// Предопределенная процедура
//
Процедура ПриОткрытии()
	Устройство = 0;
	КолвоКопий = 0;
	СразуНаПринтер = 0;
	Если ПустоеЗначение(Форма.Параметр) = 0 Тогда
		Если ТипЗначенияСтр(Форма.Параметр) = "СписокЗначений" Тогда
			Докум      = Форма.Параметр.Получить("Контекст");
			Устройство = Форма.Параметр.Получить("Устройство");
			КолвоКопий = Форма.Параметр.Получить("КоличествоКопий");
			СразуНаПринтер = Число(Форма.Параметр.Получить("СразуПечать"));
		ИначеЕсли ТипЗначенияСтр(Форма.Параметр) = "Документ" Тогда
			Докум = Форма.Параметр;
		КонецЕсли;
		ПечатьСертификат(Докум, Устройство, КолвоКопий, СразуНаПринтер);
		Статусвозврата(0);
		Возврат;
	КонецЕсли;


КонецПроцедуры // ПриОткрытии()

Функция ШК(Объект, код)
	Сервис.НарисоватьШтрихкод(Объект, 58, код);
КонецФункции // ТестВыводаКартинки
//*******************************************
