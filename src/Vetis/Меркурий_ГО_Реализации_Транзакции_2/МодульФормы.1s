// Меркурий_ГО_Реализации_Транзакции_2.ert = Групповая обработка для оформления ВСД2_Транзакции на основании реализаций 
// для Ветис 2.*
//
// ===============================================================
//Авторы:
// Синявский Филипп phsin@kb99.pro
// Жуков Дмитрий zhukov@kb99.pro

Перем ЗаполненПоТЗ; // для заполнения по Маршрутному листу и корректному отображению
Перем ВыбСтрока;
Перем ВыбФирма Экспорт;
Перем оПривязки; //:Меркурий.Привязки
Перем тпДокументов, СписокДокументов;
Перем тпНеобходимыеПартии, тзНеобходимыеПартии;
Перем тпПартииПроизводство, тзПартииПроизводство;
Перем ПалитраЦветов,ОсновнойРегион; //[+]serpent, 16.05.2019

Процедура ПриВыбореЗакладки(НомерЗакладки, ЗначениеЗакладки) Далее

//Служебные функции
Функция Этот(Конт) 	Возврат Конт; КонецФункции
Функция Сам() 	Возврат Этот(Контекст); КонецФункции

//======================================================================
Процедура ПриНачалеВыбораЗначения(ЭлементДиалога,ФлагСтандОбр)
	Если ЭлементДиалога = "Отправитель_Площадка" Тогда
		ВремЭлем = Отправитель_ХозСубъект;
		ОткрытьФорму("Справочник.ВСД_Площадка",ВремЭлем);
		ФлагСтандОбр = 0;
	КонецЕсли;
КонецПроцедуры // ПриНачалеВыбораЗначения()

//=========================== СЛУЖЕБНЫЕ ФУНКЦИИ =============================

Процедура ОбновитьИнф()

	Форма.Инфо.Заголовок("Документов: "+СписокДокументов.КоличествоСтрок()+", количество: "+Окр(СписокДокументов.Итог("Колво"))+" ");

	ВыбКолво =0;
	ВыбДок = 0;
	СписокДокументов.ВыбратьСтроки();
	Пока СписокДокументов.ПолучитьСтроку() = 1 Цикл
		Если СписокДокументов.Пометка = 1 Тогда
			ВыбКолво = ВыбКолво + (СписокДокументов.Колво);
			ВыбДок=ВыбДок+1;
		КонецЕсли;
	КонецЦикла;
	ВыбКолво = Окр(ВыбКолво);

	Форма.ИнфоВыбор.Заголовок("Выбрано "+ВыбДок+" документов, количество = "+ВыбКолво+" ");

КонецПроцедуры


Процедура ЗаголовокНадпись()

	форма.НачалоПериодаТекст.Заголовок(СокрЛП(НачДата));
	форма.КонецПериодаТекст.Заголовок(сокрлп(КонДата));

КонецПроцедуры // ЗаголовокНадпись

Процедура Клик_партии()
	
	текСтр = тзНеобходимыеПартии.ТекущаяСтрока();
	текКол = тзНеобходимыеПартии.ТекущаяКолонка();

	Если текКол="Пометка" Тогда
		Пометка = тзНеобходимыеПартии.ПолучитьЗначение(ТекСтр,"Пометка");
		Если Пометка = 1 Тогда
			тзНеобходимыеПартии.Пометка=0;
		Иначе
			тзНеобходимыеПартии.Пометка=1;
		КонецЕсли;
	ИначеЕсли (текКол="Продукция_Элемент") и (тзНеобходимыеПартии.ВидимостьКолонки("ДокРеализации") > 0) Тогда
		Если (ПустоеЗначение(тзНеобходимыеПартии.Продукция_Элемент) = 1) Тогда
			
			//Выбрать и привязать
			ВыбСтрока = текСтр;
			ТекНоменклатура = тзНеобходимыеПартии.ПолучитьЗначение(ТекСтр,"Номенклатура");
			Продукция_элемент = СоздатьОбъект("Справочник.ВСД_Продукция_Элемент");
			Если Продукция_элемент.Выбрать("Укажите элемент для установки соответствия","ФормаСписка") = 0 Тогда
				Возврат;
			КонецЕсли;
			ТВопроса = "Установить соответствие "+ТекНоменклатура+" -> "+ Продукция_элемент.ТекущийЭлемент();
			Если Вопрос(ТВопроса,"Да+Нет") = "Да" Тогда

				ГМ.УстановитьСоответсвие_Номенклатура_Продукция_Элемент( ТекНоменклатура.ТекущийЭлемент(), Продукция_элемент.ТекущийЭлемент() );
				
				тзНеобходимыеПартии.УстановитьЗначение(ВыбСтрока,"Продукция_Элемент", Продукция_элемент.ТекущийЭлемент());
				тпНеобходимыеПартии.ОбновитьСтроки();
				Предупреждение("Необходимо перезаполнить партии !!!");
			КонецЕсли;
			
		Иначе
			Эл = тзНеобходимыеПартии.ПолучитьЗначение(текСтр, текКол);
			Если ТипЗначенияСтр(Эл) = "СписокЗначений" Тогда 
				Зн="";
				Эл.ВыбратьЗначение(Зн,);
			Иначе
				ОткрытьФорму(Эл);
			КонецЕсли;
		КонецЕсли;
	Иначе
		Эл = тзНеобходимыеПартии.ПолучитьЗначение(текСтр, текКол);
		ОткрытьФорму(Эл);
	КонецЕсли;

	ОбновитьИнф();
КонецПроцедуры


//{=========================== ПЕЧАТНЫЕ ФОРМЫ =============================

Процедура Реестр()
	ДобавлятьУпаковки = ГМ.ПолучитьКонстанту("ДобавлятьУпаковки");

	Таб = СоздатьОбъект("Таблица");
	Таб.ВывестиСекцию("Шапка|Осн");
	Если ДобавлятьУпаковки = 1 Тогда
		Таб.ПрисоединитьСекцию("Шапка|ВесМест");
	КонецЕсли;
	Таб.ПрисоединитьСекцию("Шапка|Хвост");

	СписокДокументов.ВыбратьСтроки();
	Пока СписокДокументов.ПолучитьСтроку() = 1 Цикл

		Описание = "";
		Если ПустоеЗначение(СписокДокументов.ХозСубъект.GUID) = 1 Тогда
			Описание = "В ХозСубъекте не заполнен GUID";
		КонецЕсли;
		Если  (ПустоеЗначение(СписокДокументов.Площадка) = 1) Тогда
			Описание = "Не выбрана площадка";
		КонецЕсли;
		
		Таб.ВывестиСекцию("Строка|Осн");
		Если ДобавлятьУпаковки = 1 Тогда
			Таб.ПрисоединитьСекцию("Строка|ВесМест");
		КонецЕсли;
		Таб.ПрисоединитьСекцию("Строка|Хвост");
	КонецЦикла;

	Таб.Опции(0,0,0,0);
	Таб.ТолькоПросмотр();
	Таб.Показать("Реестр");
КонецПроцедуры

Процедура ПечатьСокрВСД(ВыбПринтер="")

	тз = СоздатьОбъект("ТаблицаЗначений");
	СписокДокументов.Выгрузить(тз);
	тз.ВыбратьСтроки();
	Пока тз.ПолучитьСтроку() = 1 Цикл
		Если (тз.Пометка<>1) или (Пустоезначение(тз.ВСД)=1) Тогда
			Продолжить;
		КонецЕсли;
		ГМ.ПечатьСокрВСД(тз.ВСД.ТекущийДокумент(), ВыбПринтер );
	КонецЦикла;

КонецПроцедуры


Процедура МенюПечать()
	спМеню = СоздатьОбъект("СписокЗначений");
	спМеню.ДобавитьЗначение("Реестр", "Реестр");
	спМеню.ДобавитьЗначение("СокрВСД", "Сокращенный ВСД");
	спМеню.ДобавитьЗначение("СокрВСД_печать", "Сокращенный ВСД (сразу печать)");
	зн = "";
	спМеню.ВыбратьЗначение(зн,,,,1);
	Если зн = "Реестр" Тогда
		Реестр();
	ИначеЕсли зн = "СокрВСД" Тогда
		ПечатьСокрВСД();
	ИначеЕсли зн = "СокрВСД_печать" Тогда
		ТекущийПринтер="";
		Принтер.ПолучитьЗначение(Принтер.ТекущаяСтрока(),ТекущийПринтер);
		ПечатьСокрВСД( ТекущийПринтер );
	КонецЕсли;

КонецПроцедуры
//}

//=========================== ФУНКЦИИ КНОПОК =============================

Процедура ЗаполнитьСписокДокументов_по_ТЗ( тз )
	
	Если ТипЗначенияСтр(тз) = "ТаблицаЗначений" Тогда
		
		тз.ВыбратьСтроки();
		НетВМерк = 0; //[+], 24.06.2019
		
		Пока тз.ПолучитьСтроку() = 1 Цикл
			СписокДокументов.НоваяСтрока();
			СписокДокументов.Пометка = 0;
			СписокДокументов.Грузополучатель = тз.Грузополучатель;
			СписокДокументов.ВСД = тз.ДокВСД;
			СписокДокументов.Колво = тз.Количество;
			СписокДокументов.Док = тз.Док;
			Попытка СписокДокументов.Контрагент = тз.Контрагент; Исключение КонецПопытки;
	
			Если ПустоеЗначение(тз.ХС_ГрузоПолучатель)=1 Тогда
				СписокДокументов.ХозСубъект = ГМ.НайтиХозСубъект(тз.Контрагент);
			Иначе
				СписокДокументов.ХозСубъект = тз.ХС_ГрузоПолучатель; //[+]serpent, 10.04.2019
			КонецЕсли;

			Если ПустоеЗначение(СписокДокументов.ХозСубъект)=0 Тогда
				
				Если ПустоеЗначение(тз.ПЛ_ГрузоПолучатель)=1 Тогда
					
					СписокДокументов.Площадка = ГМ.НайтиПлощадкуПоКонтрагенту( СписокДокументов.Грузополучатель );
					Если ПустоеЗначение( СписокДокументов.Площадка )=1 Тогда
						Состояние("НЕТ Площадки по: "+СокрЛП(СписокДокументов.Грузополучатель));
						НетВМерк = НетВМерк+1;
					Иначе
						СписокДокументов.Регион_ПЛ = СписокДокументов.Площадка.Регион; 
					КонецЕсли;
					
				Иначе
					СписокДокументов.Площадка = тз.ПЛ_ГрузоПолучатель; //[+]serpent, 10.04.2019	
					СписокДокументов.Регион_ПЛ = тз.ПЛ_ГрузоПолучатель.Регион; //[+]serpent, 04.07.2019
				КонецЕсли;
				
				
			КонецЕсли;
			Попытка СписокДокументов.Склад = тз.Склад; Исключение КонецПопытки;
			Попытка СписокДокументов.Статус = тз.Статус; Исключение КонецПопытки;
			Попытка СписокДокументов.Отправлен = тз.Отправлен; Исключение КонецПопытки;		
			
			Попытка	
				СписокДокументов.НомерАвто = тз.НомерАвто; 
			Исключение 
				СписокДокументов.НомерАвто = ГМ.ПолучитьНомерАвто( тз.док );
			КонецПопытки;
		КонецЦикла;
		
		Если НетВМерк>0 Тогда //[+]serpent, 10.06.2019
			Сообщить("не найдены "+Строка(НетВМерк)+" Торговых точек / Площадок !","!!!");
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры 

Процедура ЗаполнитьСписокДокументов_Реализации()

	Если ЗаполненПоТЗ=1 Тогда 
		// при заполнении по из тз Маршрутного листа 
		Возврат;
	КонецЕсли;
	
	t1 = ГМ.Старт();
		
	СписокПараметров = СоздатьОбъект("СписокЗначений");
	СписокПараметров.ДобавитьЗначение(НачДата, "НачДата");
	СписокПараметров.ДобавитьЗначение(КонДата, "КонДата");
	СписокПараметров.ДобавитьЗначение(ВыбКонтрагент, "ВыбКонтрагент");
	СписокПараметров.ДобавитьЗначение(ВыбФирма, "ВыбФирма");
	СписокПараметров.ДобавитьЗначение(Отправитель_Площадка,"Площадка"); //ЖД для фильтра по складу
	СписокПараметров.ДобавитьЗначение(1,"Версия2");
	Если флФильтрПоСкладу = 1 Тогда
		СписокПараметров.ДобавитьЗначение(флФильтрПоСкладу,"флФильтрПоСкладу"); //ТАС для фильтра по складу
	КонецЕсли;

	тз = ГМ.ПолучитьТзРеализаций(СписокПараметров);

	СписокДокументов.УдалитьСтроки();
	
	ТекСтрока = СписокДокументов.ТекущаяСтрока();

	ЗаполнитьСписокДокументов_по_ТЗ( тз );
	
	Если ПустоеЗначение(тпДокументов) = 0 Тогда
		тпДокументов.ОбновитьСтроки(); 
	КонецЕсли;
	
	ОбновитьИнф();
               
	ГМ.Финиш(t1, Сам(), "ЗаполнитьСписокДокументов_Реализации", );
	
КонецПроцедуры

Процедура ЗаполнитьСписокДокументов_Перемещения()

	t1 = ГМ.Старт();
		
	СписокПараметров = СоздатьОбъект("СписокЗначений");
	СписокПараметров.ДобавитьЗначение(НачДата, "НачДата");
	СписокПараметров.ДобавитьЗначение(КонДата, "КонДата");
	СписокПараметров.ДобавитьЗначение(ВыбКонтрагент, "ВыбКонтрагент");
	СписокПараметров.ДобавитьЗначение(ВыбФирма, "ВыбФирма");
	СписокПараметров.ДобавитьЗначение(Отправитель_Площадка,"Площадка"); 
	СписокПараметров.ДобавитьЗначение(1,"Версия2");
	Если флФильтрПоСкладу = 1 Тогда
		СписокПараметров.ДобавитьЗначение(флФильтрПоСкладу,"флФильтрПоСкладу"); 
	КонецЕсли;

	тз = ГМ.ПолучитьТзПеремещений(СписокПараметров);

	СписокДокументов.УдалитьСтроки();
	
	ТекСтрока = СписокДокументов.ТекущаяСтрока();

	ЗаполнитьСписокДокументов_по_ТЗ( тз );
	
	Если ПустоеЗначение(тпДокументов) = 0 Тогда
		тпДокументов.ОбновитьСтроки(); 
	КонецЕсли;
	
	ОбновитьИнф();

	ГМ.Финиш(t1, Сам(), "ЗаполнитьСписокДокументов_Перемещения", );
	
КонецПроцедуры


процедура ВыбратьПериод()
	если ВвестиПериод(НачДата,КонДата)=1 тогда
		ЗаголовокНадпись();
		//ПриВыбореЗакладки(Форма.Закладки.ТекущаяСтрока(),Форма.Закладки.ПолучитьЗначение(Форма.Закладки.ТекущаяСтрока()));
		СписокДокументов.УдалитьСтроки();
		тзНеобходимыеПартии.УдалитьСтроки();
		//тзПартии.УдалитьСтроки();
		ЗаполнитьСписокДокументов_Реализации();
	конецесли;
конецпроцедуры

//{ =========================== Создание документов =============================

Процедура СоздатьВСД_ПоТзРаспределения()
	Если тзНеобходимыеПартии.КоличествоСтрок()=0 Тогда
		Сообщить("Не заполнена таблица с распределенными партиями.","!");
		Возврат;
	КонецЕсли;

	КаталогОбработки = ГМ.ПолучитьКонстанту("КаталогМодуля");
	Если ФС.СуществуетФайл(КаталогОбработки+"ПодключаемыеМодули\Меркурий_Подключаемый_Создание_ВСД2_транзакция.ert")=1 Тогда
		Сообщить("ПодключаемыеМодули\Меркурий_Подключаемый_Создание_ВСД2_транзакция.ert отключено.
		|Используйте модуль Меркурий_Интеграция.ert для переопределения функций","!");
		Возврат;
	КонецЕсли;

	// отфильтруем партии в тзНеобходимыеПартии по документу СписокДокументов.Док 	
	тзИндТаблица = СоздатьОбъект("ИндексированнаяТаблица");
	тзИндТаблица.Загрузить(тзНеобходимыеПартии);
	тзИндТаблица.ДобавитьИндекс("ДокРеализации","ДокРеализации");
		
	
	СписокДокументов.ВыбратьСтроки();
	Пока СписокДокументов.ПолучитьСтроку() = 1 Цикл
		Если СписокДокументов.Пометка<>1 Тогда
			Продолжить;
		КонецЕсли;

		Если (ПустоеЗначение(СписокДокументов.ВСД)=0) Тогда
			Если (ПустоеЗначение(СписокДокументов.ВСД.Статус) = 1) Тогда
				Сообщить("Для "+СписокДокументов.Док+" ВСД уже создан, но не отправлен","i");
				Продолжить;
			КонецЕсли;
		КонецЕсли;

		стр = 0;
		Если тзНеобходимыеПартии.НайтиЗначение(СписокДокументов.Док,стр,"ДокРеализации") = 0 тогда
			Сообщить("Для "+СписокДокументов.Док+" нет данных в тз Распределенных партий","!"); //[+]serpent, 28.06.2019
			Продолжить;
		КонецЕсли;

		тзНоваяТЧ = СоздатьОбъект("ТаблицаЗначений");
		тзИндТаблица.УстановитьФильтр( СписокДокументов.Док, СписокДокументов.Док, "ДокРеализации");
		тзИндТаблица.Выгрузить( тзНоваяТЧ, "ДокРеализации");			
		
		тзНеобходимыеПартии.ПолучитьСтрокуПоНомеру(стр);

		Состояние("Создание ВСД ");
		ДокВСД = СоздатьОбъект("Документ.ВСД2_транзакция");
		ДокВСД.Новый();
		Попытка ДокВСД.Фирма = ВыбФирма; Исключение КонецПопытки;
		ДокВСД.Отправитель_ХозСубъект = Отправитель_ХозСубъект;
		ДокВСД.Отправитель_Площадка = Отправитель_Площадка;
		ДокВСД.Получатель_ХозСубъект = СписокДокументов.ХозСубъект;;
		ДокВСД.Получатель_Площадка = СписокДокументов.Площадка;
		ДокВСД.Перевозчик_ХозСубъект = Перевозчик_ХозСубъект;
			
		// остальные реквизиты заполним стандартно 
		ГМ2.ЗаполнитьВСД2_Транзакция( ДокВСД, СписокДокументов.Док, тзНоваяТЧ );
		
		Если ДокВСД.КоличествоСтрок() = 0 Тогда
		    Продолжить;
		КонецЕсли;

		ГМ.ИзменитьВремяДокумента(ДокВСД); //[+]serpent, 20.05.2019
		ДокВСД.Записать();
		ДокВСД.Провести();
		ДокументСсылка = ДокВСД.ТекущийДокумент();

		СписокДокументов.ВСД = ДокументСсылка;

		Сообщить("["+СписокДокументов.Грузополучатель+"] создан документ "+СписокДокументов.ВСД,"i");
	КонецЦикла;
		
	ЗаполнитьСписокДокументов_Реализации();
	
	ПриВыбореЗакладки(1,"Реализации");
	
	//[+]serpent, 28.01.2019
	тзНеобходимыеПартии.УдалитьСтроки();
	тпНеобходимыеПартии.ОбновитьСтроки();
	//[+]_
	
КонецПроцедуры

Процедура Создать_ВСД_Производство1()
	ВремПартии = СоздатьОбъект("ТаблицаЗначений");
	тзПартииПроизводство.Выгрузить(ВремПартии);
	ВремПартии.ВыбратьСтроки();
	Пока ВремПартии.ПолучитьСтроку() = 1 Цикл
		Если ПустоеЗначение(ВремПартии.ВСД_Производство)=0 Тогда
			Сообщить("В строке партий № "+ВремПартии.НомерСтроки+" указан документ. Пропускаем...");
			Продолжить;
		КонецЕсли;
		Если ВремПартии.КолвоНаСкладе >= ВремПартии.Количество Тогда
		    Продолжить;
		КонецЕсли;
		Если ПустоеЗначение(ВремПартии.Продукция_Элемент) = 1 Тогда
		    Продолжить;
		КонецЕсли;

		Состояние("Создание ВСД_Производство ");

		КаталогОбработки = ГМ.ПолучитьКонстанту("КаталогМодуля");
		Если ФС.СуществуетФайл(КаталогОбработки+"ПодключаемыеМодули\Меркурий_Подключаемый_Создание_ВСД2_Производство.ert")=1 Тогда

			//{ переопределение функции создания ВСД
			// интеграция переопределяется в Меркурий_Подключаемый_Создание_ВСД_Производство.ert
			//

			СписокПараметров = СоздатьОбъект("СписокЗначений");
			СписокПараметров.ДобавитьЗначение(ВыбФирма,				"Фирма");

			СписокПараметров.ДобавитьЗначение(Отправитель_ХозСубъект,				"Отправитель_ХозСубъект");
			СписокПараметров.ДобавитьЗначение(Отправитель_Площадка,					"Отправитель_Площадка");
			СписокПараметров.ДобавитьЗначение( ВремПартии.Количество,			"Колво");
			СписокПараметров.ДобавитьЗначение( ВремПартии.КолвоНаСкладе,			"КолвоВНаличии");
			СписокПараметров.ДобавитьЗначение( ВремПартии.Продукция_Элемент,	"Продукция_Элемент");
			//
			//СписокПараметров.ДобавитьЗначение( ВСД_Экспертиза,		"ВСД_Экспертиза");
			СписокПараметров.ДобавитьЗначение( ВСД_Местность,		"ВСД_Местность");
			СписокПараметров.ДобавитьЗначение( ВСД_ОсобыеОтметки,	"ВСД_ОсобыеОтметки");

			СписокПараметров.ДобавитьЗначение( НачДата,				"НачДата");
			СписокПараметров.ДобавитьЗначение( КонДата,				"КонДата");

			ОткрытьФормуМодально("Отчет",СписокПараметров,КаталогОбработки+"ПодключаемыеМодули\Меркурий_Подключаемый_Создание_ВСД2_Производство.ert");

			ДокументСсылка = "";

			Если ТипЗначенияСтр(СписокПараметров)  = "СписокЗначений" Тогда

				ДокументСсылка = СписокПараметров.Получить("ДокументСсылка");

			КонецЕсли;

			Если ПустоеЗначение(ДокументСсылка)=1 Тогда
				Предупреждение("В подключаемом модуле не удалось создать ВСД","!");
				//Возврат "";
			КонецЕсли;
			//}

		Иначе

			// ТЕСТ
			Если ВремПартии.КолвоНаСкладе  >= ВремПартии.Количество Тогда
			    Продолжить; //
			КонецЕсли;

			ДокВСД = СоздатьОбъект("Документ.ВСД2_Производство");
			ДокВСД.Новый();
			//ДокВСД.ДатаДок = ДокВСД.ДокОснование.ДатаДок;
			ДокВСД.ДатаДок = НачДата;
			ДокВСД.Производитель_ХозСубъект = Отправитель_ХозСубъект;
			ДокВСД.Производитель_площадка = Отправитель_Площадка;

			Автор = ГМ.ПолучитьАвтора();
			Попытка	ДокВСД.Автор 	= Автор;	Исключение 	КонецПопытки;
			Попытка	ДокВСД.Филиал 	= Автор.Филиал;	Исключение	КонецПопытки;
			Попытка ДокВСД.Фирма 	= ВыбФирма; Исключение КонецПопытки;
			ДокВСД.Местность 		= ВСД_Местность;
			ДокВСД.ОсобыеОтметки 	= ВСД_ОсобыеОтметки;
			ДокВСД.cargoExpertized  = 1;
			ДокВСД.cargoInspected  	= 1;
			ДокВСД.РезультатыИсследований = ГМ.СписокКонстант.Получить("ВСД_РезультатыИсследований");
		
			ДокВСД.НоваяСтрока();

			ДокВСД.Номенклатура 		= ВремПартии.Номенклатура;
			ДокВСД.Продукция_Элемент 	= ВремПартии.Продукция_Элемент;
			ДокВСД.Количество 			= ВремПартии.Количество - ВремПартии.КолвоНаСкладе; 
			ДокВСД.ЕдиницаИзмерения 	= ДокВСД.Продукция_Элемент.ЕдиницаИзмерения;
			ДокВСД.Продукция 			= ДокВСД.Продукция_Элемент.Продукция;
			ДокВСД.ВидПродукции 		= ДокВСД.Продукция_Элемент.ВидПродукции;
			ДокВСД.ВидДвижения 			= 1;
			ДокВСД.НаименованиеПродукции = ДокВСД.Продукция_Элемент.Наименование;


			ДокВСД.ЗавершитьОперацию = 1;
			ДокВСД.ДатаИзготовления1 = ГМ.Преобразовать_Дата_в_Строка(НачДата);
			Если КонДата>НачДата Тогда
				ДокВСД.ДатаИзготовления2 = ГМ.Преобразовать_Дата_в_Строка(КонДата);
			КонецЕсли;
			ДокВСД.ДатаСрокГодности1 = ГМ.Преобразовать_Дата_в_Строка(НачДата+ДокВСД.Продукция_Элемент.СрокГодности, ДокВСД.Продукция_Элемент.СрокГодностиЧасов);


			ДокВСД.Записать();
			ДокументСсылка = ДокВСД.ТекущийДокумент();
			
			тз = ГМ.Производство_ВыбратьПартииСписания( ДокументСсылка );
			тз.ВыбратьСтроки();
			Пока тз.ПолучитьСтроку() = 1 Цикл
				
				ДокВСД.НоваяСтрока();
				ДокВСД.ВидДвижения = 2; // расход				
				ДокВСД.Продукция_Элемент = тз.Продукция_Элемент;
				ДокВСД.Партия = тз.Партия;
				ДокВСД.Количество = тз.Количество;
				ДокВСД.Продукция = тз.Партия.Продукция;
				ДокВСД.ВидПродукции = тз.Партия.ВидПродукции;
				ДокВСД.НаименованиеПродукции = тз.Партия.НаименованиеПродукции;
				ДокВСД.ЕдиницаИзмерения = тз.Партия.ЕдиницаИзмерения;
				ДокВСД.ДатаИзготовления1 = тз.Партия.ДатаИзготовления1;
				ДокВСД.ДатаИзготовления2 = тз.Партия.ДатаИзготовления2;
				ДокВСД.ДатаСрокГодности1 = тз.Партия.ДатаСрокГодности1;
				ДокВСД.ДатаСрокГодности2 = тз.Партия.ДатаСрокГодности2;

			КонецЦикла;
		КонецЕсли;

		ВремПартии.ВСД_Производство = ДокументСсылка;

		Сообщить(" создан документ "+ВремПартии.ВСД_Производство,"i");
	КонецЦикла;
	ВремПартии.Выгрузить( тзПартииПроизводство );
	тпПартииПроизводство.ОбновитьСтроки();
КонецПроцедуры

Процедура Создать_ВСД_Производство2()

	ДокВСД = СоздатьОбъект("Документ.ВСД2_Производство");
	ДокВСД.Новый();
	//ДокВСД.ДатаДок = ДокВСД.ДокОснование.ДатаДок;
	ДокВСД.ДатаДок = НачДата;
	ДокВСД.Производитель_ХозСубъект = Отправитель_ХозСубъект;
	ДокВСД.Производитель_площадка = Отправитель_Площадка;

	Автор = ГМ.ПолучитьАвтора();
	Попытка	ДокВСД.Автор 	= Автор;	Исключение 	КонецПопытки;
	Попытка	ДокВСД.Филиал 	= Автор.Филиал;	Исключение	КонецПопытки;
	Попытка ДокВСД.Фирма 	= ВыбФирма; Исключение КонецПопытки;
	ДокВСД.Местность 		= ВСД_Местность;
	ДокВСД.ОсобыеОтметки 	= ВСД_ОсобыеОтметки;
	//ДокВСД.cargoExpertized  = 1;
	ДокВСД.cargoInspected  	= 1;
	ДокВСД.РезультатыИсследований = ГМ.СписокКонстант.Получить("ВСД_РезультатыИсследований");
	ДокВСД.ЗавершитьОперацию = 1;

	
	ВремПартии = СоздатьОбъект("ТаблицаЗначений");	
	тзПартииПроизводство.Выгрузить(ВремПартии);	
	
	ВремПартии.ВыбратьСтроки();
	Пока ВремПартии.ПолучитьСтроку() = 1 Цикл
		
		Если ПустоеЗначение(ВремПартии.ВСД_Производство)=0 Тогда
			Сообщить("В строке партий № "+ВремПартии.НомерСтроки+" указан документ. Пропускаем...");
			Продолжить;
		КонецЕсли;
		Если ВремПартии.КолвоНаСкладе >= ВремПартии.Количество Тогда
		    Продолжить;
		КонецЕсли;
		Если ПустоеЗначение(ВремПартии.Продукция_Элемент) = 1 Тогда
		    Продолжить;
		КонецЕсли;

		Если ВремПартии.КолвоНаСкладе  >= ВремПартии.Количество Тогда
		    Продолжить; //
		КонецЕсли;
		
		ДокВСД.НоваяСтрока();

		ДокВСД.Номенклатура 		= ВремПартии.Номенклатура;
		Если ТипЗначенияСтр(ВремПартии.Продукция_Элемент) = "СписокЗначений" Тогда 
			ДокВСД.Продукция_Элемент 	= ВремПартии.Продукция_Элемент.ПолучитьЗначение(1);
		Иначе
			ДокВСД.Продукция_Элемент 	= ВремПартии.Продукция_Элемент;
		КонецЕсли;
		ДокВСД.Количество 			= ВремПартии.Количество - ВремПартии.КолвоНаСкладе; 
		ДокВСД.ЕдиницаИзмерения 	= ДокВСД.Продукция_Элемент.ЕдиницаИзмерения;
		ДокВСД.Продукция 			= ДокВСД.Продукция_Элемент.Продукция;
		ДокВСД.ВидПродукции 		= ДокВСД.Продукция_Элемент.ВидПродукции;
		ДокВСД.ВидДвижения 			= 1;
		ДокВСД.НаименованиеПродукции = ДокВСД.Продукция_Элемент.Наименование;

		ДокВСД.ДатаИзготовления1 = ГМ.Преобразовать_Дата_в_Строка(НачДата);
		Если КонДата>НачДата Тогда
			ДокВСД.ДатаИзготовления2 = ГМ.Преобразовать_Дата_в_Строка(КонДата);
		КонецЕсли;
		ДокВСД.ДатаСрокГодности1 = ГМ.Преобразовать_Дата_в_Строка(НачДата+ДокВСД.Продукция_Элемент.СрокГодности, ДокВСД.Продукция_Элемент.СрокГодностиЧасов);

	КонецЦикла;
		
	Если ДокВСД.КоличествоСтрок()>0 Тогда 
		ДокВСД.Записать();
		Сообщить(" создан документ "+ДокВСД,"i");
	Иначе
		Возврат;
	КонецЕсли;	
	
		ДокументСсылка = ДокВСД.ТекущийДокумент();
		
		тз = ГМ.Производство_ВыбратьПартииСписания( ДокументСсылка );
		тз.ВыбратьСтроки();
		Пока тз.ПолучитьСтроку() = 1 Цикл
			
			ДокВСД.НоваяСтрока();
			ДокВСД.ВидДвижения = 2; // расход				
			ДокВСД.Продукция_Элемент = тз.Продукция_Элемент;
			ДокВСД.Партия = тз.Партия;
			ДокВСД.Количество = тз.Количество;
			ДокВСД.Продукция = тз.Партия.Продукция;
			ДокВСД.ВидПродукции = тз.Партия.ВидПродукции;
			ДокВСД.НаименованиеПродукции = тз.Партия.НаименованиеПродукции;
			ДокВСД.ЕдиницаИзмерения = тз.Партия.ЕдиницаИзмерения;
			ДокВСД.ДатаИзготовления1 = тз.Партия.ДатаИзготовления1;
			ДокВСД.ДатаИзготовления2 = тз.Партия.ДатаИзготовления2;
			ДокВСД.ДатаСрокГодности1 = тз.Партия.ДатаСрокГодности1;
			ДокВСД.ДатаСрокГодности2 = тз.Партия.ДатаСрокГодности2;

		КонецЦикла;

		ВремПартии.ВСД_Производство = ДокументСсылка;
	
	ДокВСД.Записать();
	
	ВремПартии.Выгрузить( тзПартииПроизводство );
	тпПартииПроизводство.ОбновитьСтроки();
	
	ОткрытьФорму(ДокументСсылка);
КонецПроцедуры


Функция СоздатьВСД_Перемещение()

	ХС = ГМ.СписокКонстант.Получить("Поставщик_ХозСубъект");
	ВыбПлощадка  = ГМ.СписокКонстант.Получить("Поставщик_Площадка");

	Если ПустоеЗначение(ХС) = 1 Тогда
		Сообщить("Не заполнен параметр Поставщик_ХозСубъект","!");
		Возврат "";
		//СпрВрем = СоздатьОбъект("Справочник.ВСД_ХозСубъект");
		////	 Выберем руками
		//Если СпрВрем.Выбрать("Выберите ХС отправителя",) = 0 Тогда
	  	//  Сообщить("Невозможно создать документ без ХС");
		//	Возврат "";
		//КонецЕсли;
		//ХС = СпрВрем.ТекущийЭлемент();
	КонецЕсли;

	Если ПустоеЗначение(ВыбПлощадка) = 1 Тогда
		Сообщить("Не заполнен параметр Поставщик_Площадка","!");
		Возврат "";
		//СпрВрем = СоздатьОбъект("Справочник.ВСД_Площадка");
		////	 Выберем руками
		//Если СпрВрем.Выбрать("Выберите Площадку Отправителя",) = 0 Тогда
	  	//  Сообщить("Невозможно создать документ без Площадки");
		//	Возврат "";
		//КонецЕсли;
		//ВыбПлощадка = СпрВрем.ТекущийЭлемент();
	КонецЕсли;

	Если ПустоеЗначение( ГМ.СписокКонстант.Получить("Поставщик_Фирма") ) = 1 Тогда
		Сообщить("Не заполнен параметр Поставщик_Фирма","!");
		Возврат "";
		//СпрВрем = СоздатьОбъект("Справочник.ВСД_Площадка");
		////	 Выберем руками
		//Если СпрВрем.Выбрать("Выберите Площадку Отправителя",) = 0 Тогда
	  	//  Сообщить("Невозможно создать документ без Площадки");
		//	Возврат "";
		//КонецЕсли;
		//ВыбПлощадка = СпрВрем.ТекущийЭлемент();
	КонецЕсли;
	
		КаталогОбработки = ГМ.ПолучитьКонстанту("КаталогМодуля");
//		// создание кастомного ВСД
//		Если ФС.СуществуетФайл(КаталогОбработки+"ПодключаемыеМодули\Меркурий_Подключаемый_Создание_ВСД2_транзакцияПоТзПартий.ert")=1 Тогда
//
//			//{ переопределение функции создания ВСД
//			// интеграция переопределяется в Меркурий_Подключаемый_Создание_ВСД_транзакция.ert
//			//
//			СписокПараметров = СоздатьОбъект("СписокЗначений");
//			СписокПараметров.ДобавитьЗначение(ВыбФирма,						"Фирма");
//			СписокПараметров.ДобавитьЗначение(ХС,							"Отправитель_ХозСубъект");
//			СписокПараметров.ДобавитьЗначение(ВыбПлощадка,					"Отправитель_Площадка");
//			СписокПараметров.ДобавитьЗначение( Отправитель_ХозСубъект,		"Получатель_ХозСубъект");
//			СписокПараметров.ДобавитьЗначение( Отправитель_Площадка,		"Получатель_Площадка");
//			СписокПараметров.ДобавитьЗначение( СписокДокументов.Колво,		"Колво");
//			СписокПараметров.ДобавитьЗначение( ВСД_Местность,				"ВСД_Местность");
//			СписокПараметров.ДобавитьЗначение( ВСД_ОсобыеОтметки,			"ВСД_ОсобыеОтметки");
//			//СписокПараметров.ДобавитьЗначение( тзПартии,				"тзПартии");
//
//			ОткрытьФормуМодально("Отчет",СписокПараметров,КаталогОбработки+"ПодключаемыеМодули\Меркурий_Подключаемый_Создание_ВСД2_транзакцияПоТзПартий.ert");
//			ДокументСсылка = "";
//
//			Если ТипЗначенияСтр(СписокПараметров)  = "СписокЗначений" Тогда
//				ДокументСсылка = СписокПараметров.Получить("ДокументСсылка");
//			КонецЕсли;
//
//			Если ПустоеЗначение(ДокументСсылка)=1 Тогда
//				Предупреждение("В подключаемом модуле не удалось создать ВСД","!");
//				Возврат "";
//			КонецЕсли;
//			// }
//		Иначе
			// стандартное создание ВСД
			Состояние("Заполнение ВСД2_Транзакция ");

			ДокВСД = СоздатьОбъект("Документ.ВСД2_транзакция");
			ДокВСД.Новый();
			
			//1. заполняем реквизиты 
			ДокВСД.Фирма = ГМ.СписокКонстант.Получить("Поставщик_Фирма");
			ДокВСД.Отправитель_ХозСубъект =  ХС;
			ДокВСД.Отправитель_Площадка = ВыбПлощадка;
			ДокВСД.Получатель_ХозСубъект = Отправитель_ХозСубъект;
			ДокВСД.Получатель_Площадка = Отправитель_Площадка;
			ДокВСД.Перевозчик_ХозСубъект = ХС;
			ДокВСД.ВсдНомер = ДокВСД.НомерДок;
			ДокВСД.ВсдДата = ДокВСД.ДатаДок;
			ДокВСД.НомерАвто = "Не используется";

			////Оставим только пустые партии = нехватающее кол-во
			//А = тзНеобходимыеПартии.КоличествоСтрок();
			//
			//Пока (А>0) Цикл
			//	ВыбПартия = тзНеобходимыеПартии.ПолучитьЗначение(А, "Партия");
			//	Если ПустоеЗначение( ВыбПартия )=0 Тогда 
			//		тзНеобходимыеПартии.УдалитьСтроку(А);
			//	КонецЕсли;
			//	А=А-1;
			//КонецЦикла;
			Попытка	тзПартииПроизводство.НоваяКолонка("Партия"); Исключение КонецПопытки;
			
			//остальные реквизиты заполним по умолчанию
			ГМ2.ЗаполнитьВСД2_Транзакция( ДокВСД, СписокДокументов.Док, тзПартииПроизводство, 1);
			
			ГМ.ИзменитьВремяДокумента(ДокВСД); //[+]serpent, 20.05.2019
			ДокВСД.Записать();
			ДокВСД.Провести();
			ДокументСсылка = ДокВСД.ТекущийДокумент();

		//КонецЕсли;

		Сообщить("Создан документ " + ДокументСсылка,"i");

		Возврат ДокументСсылка;
КонецФункции

Процедура Создать_Инвентаризацию()
	Состояние("Создание ВСД_Инвентаризация ");
	ДокВСД = СоздатьОбъект("Документ.ВСД2_Инвентаризация");
	ДокВСД.Новый();
	ДокВСД.ДатаДок = НачДата;
	Попытка ДокВСД.Фирма=ВыбФирма; Исключение КонецПопытки;
	ДокВСД.Владелец_ХозСубъект = Отправитель_ХозСубъект;
	ДокВСД.Владелец_площадка = Отправитель_Площадка;
	ДокВСД.ПричинаРасхождения = ГМ.ПолучитьКонстанту("ПарамПричинаРасхожденияВИнвентаризации"); // "Отсутсвие партий";
	ДокВСД.ОписаниеНесоответствия = ГМ.ПолучитьКонстанту("ПарамОписаниеНесоответствияВИнвентаризации"); // "Отсутсвие партий";
	Попытка	ДокВСД.Автор = ГМ.ПолучитьАвтора();;	Исключение 	КонецПопытки;
	Попытка	ДокВСД.Филиал = ДокВСД.Автор .Филиал;	Исключение	КонецПопытки;
	Попытка ДокВСД.Фирма = ВыбФирма; Исключение КонецПопытки;

	ВремтзПартии = СоздатьОбъект("ТаблицаЗначений");
	тзПартииПроизводство.Выгрузить(ВремтзПартии);
	ВремтзПартии.ВыбратьСтроки();
	Пока ВремтзПартии.ПолучитьСтроку() = 1 Цикл
		Если ПустоеЗначение(ВремтзПартии.ВСД_Производство)=0 Тогда
			Сообщить("В строке партий № "+ВремтзПартии.НомерСтроки+" указан документ. Пропускаем...");
			Продолжить;
		КонецЕсли;
		Если ВремтзПартии.КолвоНаСкладе >= ВремтзПартии.Количество Тогда
		    Продолжить;
		КонецЕсли;
		Если ПустоеЗначение(ВремтзПартии.Продукция_Элемент) = 1 Тогда
		    Продолжить;
		КонецЕсли;


		разница= ВремтзПартии.Количество - ВремтзПартии.КолвоНаСкладе;
		ДокВСД.НоваяСтрока();

		ДокВСД.Продукция_Элемент 	= ВремтзПартии.Продукция_Элемент;
		если ТипЗначенияСтр(ВремтзПартии.Продукция_Элемент)  = "СписокЗначений"  Тогда
			ДокВСД.Продукция_Элемент 	= ВремтзПартии.Продукция_Элемент.ПолучитьЗначение(1);
		КонецЕсли;
		ДокВСД.Количество 			= разница;//тзПартии.Количество;
		//Попытка ДокВСД.КоличествоМест 		= тзПартии.КолвоМестСписания;Исключение КонецПопытки;

		ДокВСД.Продукция 			= ДокВСД.Продукция_Элемент.Продукция;
		ДокВСД.ВидПродукции 		= ДокВСД.Продукция_Элемент.ВидПродукции;
	    ДокВСД.ЕдиницаИзмерения 	= ДокВСД.Продукция_Элемент.ЕдиницаИзмерения;
		ДокВСД.НаименованиеПродукции = ДокВСД.Продукция_Элемент.Наименование;
		ДокВСД.ДатаИзготовления1 = ГМ.Преобразовать_Дата_в_Строка(НачДата);
		Если КонДата>НачДата Тогда
			ДокВСД.ДатаИзготовления2 = ГМ.Преобразовать_Дата_в_Строка(КонДата);
		КонецЕсли;
		
		СрокГодности=ДокВСД.Продукция_Элемент.СрокГодности;
		Если СрокГодности=0 Тогда
			СрокГодности=360;
		КонецЕсли;
		ДокВСД.ДатаСрокГодности1 = ГМ.Преобразовать_Дата_в_Строка(НачДата+СрокГодности); 
		
		ДокВСД.Производитель_Страна	=гм.СписокКонстант.Получить("Страна");
		ДокВСД.Производитель_площадка=Отправитель_Площадка;
		
		Попытка
			Если ПустоеЗначение( ДокВСД.Продукция_Элемент.Производитель_площадка ) =0 Тогда 
				ДокВСД.Производитель_площадка = ДокВСД.Продукция_Элемент.Производитель_площадка;
			КонецЕсли;
		Исключение
		КонецПопытки;		
		Попытка
			Если ПустоеЗначение( ДокВСД.Продукция_Элемент.Производитель_Страна ) =0 Тогда 
				ДокВСД.Производитель_Страна	=ДокВСД.Продукция_Элемент.Производитель_Страна;
			КонецЕсли;
		Исключение
		КонецПопытки;	
		
		ГМ.ИзменитьВремяДокумента(ДокВСД); //[+]serpent, 20.05.2019
		ДокВСД.Записать();
		//ДокВСД.Провести();
		ДокументСсылка = ДокВСД.ТекущийДокумент();

	КонецЦикла;
	Сообщить(" создан документ "+ДокументСсылка,"i");
	ОткрытьФорму( ДокументСсылка);
КонецПроцедуры
	

Процедура СоздатьПродукцию()
	//+
	//Если Метаданные.Справочник("Номенклатура").Реквизит("ПодконтроленМеркурию").Выбран() = 0 Тогда
	//	Сообщить("Отсутствует реквизит ПодконтроленМеркурию в справочнике Номенклатура. Выполнение невозможно");
	//	Возврат;
	//КонецЕсли;
	Если ПустоеЗначение(Отправитель_ХозСубъект)=1 Тогда
		Сообщить("Не выбран параметр Отправитель_ХозСубъект ","!");
		Возврат;
	КонецЕсли;
	Если ПустоеЗначение(Отправитель_Площадка)=1 Тогда
		Сообщить("Не выбран параметр Отправитель_Площадка ","!");
		Возврат;
	КонецЕсли;
	//Если ПустоеЗначение(Перевозчик_ХозСубъект)=1 Тогда
	//	Сообщить("Не выбран параметр Перевозчик_ХозСубъект ","!");
	//	Возврат;
	//КонецЕсли;
	//+
	ТЗ = СоздатьОбъект("ТаблицаЗначений");
	ТЗ.НоваяКолонка("Номенклатура");

	СписокДокументов.ВыбратьСтроки();
	Пока СписокДокументов.ПолучитьСтроку() = 1 Цикл
		Если СписокДокументов.Пометка<>1 Тогда
			Продолжить;
		КонецЕсли;

		//	тз = ГМ.СвернутьТч();
		до=СписокДокументов.Док;
		до.ВыбратьСтроки();
		Пока до.ПолучитьСтроку() = 1 Цикл

			//Если (до.Номенклатура.ПодконтроленМеркурию.Получить(ТекущаяДата()) = 1) И (ПустоеЗначение(до.Номенклатура.ВСД_Продукция_Элемент) = 1) Тогда
			//Если (ПустоеЗначение(до.Номенклатура.ВСД_Продукция_Элемент) = 1) Тогда
				ТЗ.НоваяСтрока();
				ТЗ.Номенклатура = до.Номенклатура;
			//КонецЕсли;
		КонецЦикла;

	КонецЦикла;

	КаталогОбработки = ГМ.ПолучитьКонстанту("КаталогМодуля");
	Если ТЗ.КоличествоСтрок() > 0 Тогда
		ОбработкаСоздатьПродукция_Элемент = КаталогОбработки+"ВСД_СоздатьПродукция_Элемент.ert";
		Если ФС.СуществуетФайл(ОбработкаСоздатьПродукция_Элемент) = 0 Тогда
			Сигнал();
			Предупреждение("Не найдена обработка " + ОбработкаСоздатьПродукция_Элемент, 6 );
		Иначе
			Конт="";
			ОткрытьФормуМодально("Отчет",ТЗ,ОбработкаСоздатьПродукция_Элемент);
		КонецЕсли;
	КонецЕсли;	
КонецПроцедуры

//}

Процедура ОтменитьВсеДокументы()
	СписокДокументов.ВыбратьСтроки();
	Пока СписокДокументов.ПолучитьСтроку() = 1 Цикл
		СписокДокументов.Пометка=0;
	КонецЦикла;

	ТПДокументов.ОбновитьСтроки();
	тзНеобходимыеПартии.УдалитьСтроки();

	ОбновитьИнф();
КонецПроцедуры

Процедура ВыделитьВсеДокументы()
	спМеню = СоздатьОбъект("СписокЗначений");
	спМеню.ДобавитьЗначение("ВыделитьВсе", "Выделить все документы");
	спМеню.ДобавитьЗначение("ВыделитьСоздать", "Выделить пустые ВСД");
	спМеню.ДобавитьЗначение("ВыделитьНеОтправленные", "Выделить НЕ ОТПРАВЛЕННЫЕ");

	зн = "";
	спМеню.ВыбратьЗначение(зн,,,,1);
	Если зн = "ВыделитьВсе" Тогда
		СписокДокументов.ВыбратьСтроки();
		Пока СписокДокументов.ПолучитьСтроку() = 1 Цикл
			СписокДокументов.Пометка=1;
		КонецЦикла;
	ИначеЕсли зн = "ВыделитьСоздать" Тогда
		СписокДокументов.ВыбратьСтроки();
		Пока СписокДокументов.ПолучитьСтроку() = 1 Цикл
			Если ПустоеЗначение(СписокДокументов.ВСД) = 1 Тогда
				СписокДокументов.Пометка=1;
			КонецЕсли;
		КонецЦикла;
	ИначеЕсли зн = "ВыделитьНеОтправленные" Тогда
		СписокДокументов.ВыбратьСтроки();
		Пока СписокДокументов.ПолучитьСтроку() = 1 Цикл
			Если ( ПустоеЗначение(СписокДокументов.ВСД) = 0 ) Тогда
				Если ( СокрЛП(СписокДокументов.ВСД.Статус) <> "COMPLETED" ) Тогда
					СписокДокументов.Пометка=1;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;

	ТПДокументов.ОбновитьСтроки();
	тзНеобходимыеПартии.УдалитьСтроки();

	ОбновитьИнф();
КонецПроцедуры


Процедура ЗаполнитьПартии()
	
	тзНеобходимыеПартии.УдалитьСтроки();
	тпНеобходимыеПартии.ОбновитьСтроки();
	тзПартииПроизводство.УдалитьСтроки();
	тпПартииПроизводство.ОбновитьСтроки();
	
//	Форма.Закладки.ТекущаяСтрока(2);
//	Форма.ИспользоватьСлой("Основной, Партии");

	//Пропустим заполненные ВСД
	КолвоДок=0;
	СписокДокументов.ВыбратьСтроки();
	Пока СписокДокументов.ПолучитьСтроку() = 1 Цикл
		Если ПустоеЗначение(СписокДокументов.ВСД)=0 Тогда 
			СписокДокументов.Пометка=0;
			Сообщить("В строке "+СписокДокументов.НомерСтроки+" выбран "+СписокДокументов.ВСД+", пропускаем...");
		КонецЕсли;
		Если СписокДокументов.Пометка=1 Тогда
			КолвоДок=КолвоДок+1;
		КонецЕсли;
	КонецЦикла;
	
	тпДокументов.ОбновитьСтроки(); 
	
	Если КолвоДок=0 Тогда
		Предупреждение("Не выбраны документы");
		Возврат;
	КонецЕсли;

	тз = ГМ.РаспределитьДокументыПоПартиямИДатеИзготовления( СписокДокументов, Отправитель_Площадка, Отправитель_ХозСубъект );
	тзНеобходимыеПартии.Загрузить(тз);
	тпНеобходимыеПартии.ОбновитьСтроки();

	тз = ГМ.ЗаполнитьПартииПроизводстваПоТзПартий( тзНеобходимыеПартии );
	тзПартииПроизводство.Загрузить( тз );
	тпПартииПроизводство.ОбновитьСтроки();
	ТребуетсяСоздавать=0;
	тзПартииПроизводство.ВыбратьСтроки();
	Пока тзПартииПроизводство.ПолучитьСтроку() = 1 Цикл
		если тзПартииПроизводство.Количество-тзПартииПроизводство.КолвоНаСкладе>0 Тогда
			ТребуетсяСоздавать=1;
		КонецЕсли;
		
	КонецЦикла;
	
	если ТребуетсяСоздавать=1 Тогда
		Форма.Закладки.ТекущаяСтрока(3);
		Форма.ИспользоватьСлой("Основной, Производство");
	иначе	
		Форма.Закладки.ТекущаяСтрока(2);
		Форма.ИспользоватьСлой("Основной, Партии");
	КонецЕсли;

КонецПроцедуры


Процедура Клик_ПартииПроизводства()
	текСтр = тзПартииПроизводство.ТекущаяСтрока();
	текКол = тзПартииПроизводство.ТекущаяКолонка();

	Если текКол="Пометка" Тогда
		Пометка = тзПартииПроизводство.ПолучитьЗначение(ТекСтр,"Пометка");
		Если Пометка = 1 Тогда
			тзПартииПроизводство.Пометка=0;
		Иначе
			тзПартииПроизводство.Пометка=1;
		КонецЕсли;
	ИначеЕсли (текКол="Продукция_Элемент") Тогда
		Если (ПустоеЗначение( тзПартииПроизводство.Продукция_Элемент) = 1) Тогда
			
			//Выбрать и привязать
			ВыбСтрока = текСтр;
			ТекНоменклатура = тзПартииПроизводство.ПолучитьЗначение(ТекСтр,"Номенклатура");
			Продукция_элемент = СоздатьОбъект("Справочник.ВСД_Продукция_Элемент");
			Если Продукция_элемент.Выбрать("Укажите элемент для установки соответствия","ФормаСписка") = 0 Тогда
				Возврат;
			КонецЕсли;
			ТВопроса = "Установить соответствие "+ТекНоменклатура+" -> "+ Продукция_элемент.ТекущийЭлемент();
			Если Вопрос(ТВопроса,"Да+Нет") = "Да" Тогда

				ГМ.УстановитьСоответсвие_Номенклатура_Продукция_Элемент( ТекНоменклатура.ТекущийЭлемент(), Продукция_элемент.ТекущийЭлемент() );
				
				тзПартииПроизводство.УстановитьЗначение(ВыбСтрока,"Продукция_Элемент", Продукция_элемент.ТекущийЭлемент());
				тпПартииПроизводство.ОбновитьСтроки();
				Предупреждение("Необходимо перезаполнить партии !!!");
			КонецЕсли;
			
		Иначе
			Эл = тзПартииПроизводство.ПолучитьЗначение(текСтр, текКол);
			Если ТипЗначенияСтр(Эл) = "СписокЗначений" Тогда 
				Зн="";
				//выберем значения из списка
				Если Эл.ВыбратьЗначение(Зн,)=1 Тогда 
					//установим в списке без сохранения
					тзПартииПроизводство.УстановитьЗначение(текСтр,"Продукция_Элемент", Зн);
					тпПартииПроизводство.ОбновитьСтроки();					
				КонецЕсли;
			Иначе
				ОткрытьФорму(Эл);
			КонецЕсли;
		КонецЕсли;
	Иначе
		Эл = тзПартииПроизводство.ПолучитьЗначение(текСтр, текКол);
		ОткрытьФорму(Эл);
	КонецЕсли;

	ОбновитьИнф();
КонецПроцедуры

Функция ПроверитьВСД(ВСД)
	Рез =1;
	Если ПустоеЗначение(ВСД)=1 Тогда
		Возврат 0;
	КонецЕсли;
	Если ( ГМ.СтатусЗакрыт( ВСД )= 1) Тогда
		Возврат 0;
	КонецЕсли;
	Если ПустоеЗначение(ВСД.ТермическоеСостояние)=1 Тогда
		Сообщить("ВСД "+ВСД+" не указано ТермическоеСостояние","!");
		Возврат 0;
	КонецЕсли;

	Возврат 1;
КонецФункции

Процедура ОтправитьВСД()

	СписокВСД = СоздатьОбъект("СписокЗначений");
	СписокДокументов.ВыбратьСтроки();
	Пока СписокДокументов.ПолучитьСтроку() = 1 Цикл
		Если СписокДокументов.Пометка = 1 Тогда
			Если ПроверитьВСД(СписокДокументов.ВСД)=1 Тогда
				СписокВСД.ДобавитьЗначение(СписокДокументов.ВСД);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;

	ЗаписьЖурналаРегистрации("Меркурий отправка исходящих ВСД ");

	ГМ2.ОтправитьВсе_ВСД2_Транзакция(СписокВСД, НачДата, КонДата);

	ЗаполнитьСписокДокументов_Реализации();	
	
	ПриВыбореЗакладки(1,"Реализации");
КонецПроцедуры

Процедура ОтправитьВСДбыстро()

	СписокВСД = СоздатьОбъект("СписокЗначений");
	СписокДокументов.ВыбратьСтроки();
	Пока СписокДокументов.ПолучитьСтроку() = 1 Цикл
		Если СписокДокументов.Пометка = 1 Тогда
			Если ПроверитьВСД(СписокДокументов.ВСД)=1 Тогда
				СписокВСД.ДобавитьЗначение(СписокДокументов.ВСД);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;

	ЗаписьЖурналаРегистрации("Меркурий отправка исходящих ВСД ");

	ГМ2.ОтправитьВсе_ВСД2_Транзакция_Параллельно(СписокВСД, НачДата, КонДата);

	ЗаполнитьСписокДокументов_Реализации();	
	
	ПриВыбореЗакладки(1,"Реализации");
КонецПроцедуры

Процедура ПолучитьПартии()
	ЗаписьЖурналаРегистрации("Ветис получение партий ");

	ПартииНачало = НачДата-30;
	ПартииОкончание = КонДата;
	если ВвестиДату(ПартииНачало,"Загрузить партии с :")=1 Тогда 
		Если ПустоеЗначение(ПартииНачало) = 1 Тогда
			//Все хотят
			ПартииНачало = "";
			ПартииОкончание = "";
			ГМ.СообщитьИнфо("Ветис получение всех партий по площадке "+Отправитель_Площадка);
		Иначе
			ГМ.СообщитьИнфо("Ветис получение ненулевых партий по площадке "+СокрЛП(Отправитель_Площадка.Наименование)+" за период "+ПериодСтр(ПартииНачало,ПартииОкончание));
		КонецЕсли;
		Состояние("Ветис получение партий 2.1");
		
		спПараметры = СоздатьОбъект("СписокЗначений");	
		спПараметры.Установить("Отправитель_Площадка", Отправитель_Площадка);
		спПараметры.Установить("Отправитель_ХозСубъект", Отправитель_ХозСубъект);
		спПараметры.Установить("Смещение", 0);
		спПараметры.Установить("УдалятьПартии", ГМ.ПолучитьКонстанту("ОчищатьСправочникВСД_Партии") );		
		спПараметры.Установить("ПартияНачПериода", ПартииНачало); 
		спПараметры.Установить("ПартияКонПериода", ПартииОкончание);
		
		ГМ2.ПолучитьПартии2( спПараметры );
	КонецЕсли;
	
КонецПроцедуры

Процедура МенюПлощадки()

	ГМ.ПриАктивизацииСтрокиТП(тпДокументов, СписокДокументов);
	
	//меню
	СписокДействийВСД = СоздатьОбъект("СписокЗначений");
	СписокДействийВСД.ДобавитьЗначение("ЗагрузитьПоХС","Загрузить площадки по ХС");
	СписокДействийВСД.ДобавитьЗначение("НайтиПоИмени","Найти площадки по названию");
	СписокДействийВСД.ДобавитьЗначение("СоздатьХС","Создать ХозСубъект");
	СписокДействийВСД.ДобавитьЗначение("СоздатьПлощадку","Создать Площадку");

	стр=0; Зн="";
	Если СписокДействийВСД.ВыбратьЗначение(Зн, "", стр, 60, 1) = 1 Тогда

		Если Зн ="ЗагрузитьПоХС"  Тогда
			ГМ2.ПолучитьПлощадкиПоХозсубъекту(СписокДокументов.ХозСубъект);
			//РаскраситьСписокДокументов();
		ИначеЕсли Зн = "НайтиПоИмени" Тогда
			//ГМ2.НайтиПлощадкиПоНазванию( СписокДокументов.ХозСубъект, Регион, 1);
			//РаскраситьСписокДокументов();
			
			Параметры = СоздатьОбъект("СписокЗначений");
			Параметры.ДобавитьЗначение(СписокДокументов.ХозСубъект, "ХозСубъект");
			
			КаталогОбработки = ГМ.ПолучитьКонстанту("КаталогМодуля");
			ОткрытьФорму("Отчет", Параметры, КаталогОбработки+"ВСД_ПоискПлощадок.ert");
			
		ИначеЕсли Зн = "СоздатьХС" Тогда
			ГМ.СоздатьХС( СписокДокументов );
			//РаскраситьСписокДокументов();
		ИначеЕсли Зн = "СоздатьПлощадку" Тогда
			ГМ.СоздатьПлощадку( СписокДокументов );
			//РаскраситьСписокДокументов();
		КонецЕсли;
		тпДокументов.ОбновитьСтроки();
	КонецЕсли;

	//ПриВыбореЗакладки(Форма.Закладки.ТекущаяСтрока(),"Реализации");
КонецПроцедуры


Функция ПроверитьВСД_Производство(ВСД)
	Рез =1;
	Если ПустоеЗначение(ВСД)=1 Тогда
		Возврат 0;
	КонецЕсли;
	Если ( ГМ.СтатусЗакрыт( ВСД )= 1) Тогда
		Возврат 0;
	КонецЕсли;
	//Если ПустоеЗначение(ВСД.ТермическоеСостояние)=1 Тогда
	//	Сообщить("ВСД "+ВСД+" не указано ТермическоеСостояние","!");
	//	Возврат 0;
	//КонецЕсли;

	Возврат 1;
КонецФункции


Процедура Отправить_ВСД_Производство()
	СписокВСД = СоздатьОбъект("СписокЗначений");
	тзПартииПроизводство.ВыбратьСтроки();
	Пока тзПартииПроизводство.ПолучитьСтроку() = 1 Цикл
		Если ПроверитьВСД_Производство( тзПартииПроизводство.ВСД_Производство)=1 Тогда
			СписокВСД.ДобавитьЗначение( тзПартииПроизводство.ВСД_Производство);
		КонецЕсли;
	КонецЦикла;

	ЗаписьЖурналаРегистрации("Меркурий отправка ВСД Производство");

	ГМ2.ОтправитьВсе_ВСД2_Производство(СписокВСД, НачДата, КонДата);
    //ВывестиТзПартийВариант2();

КонецПроцедуры

Процедура Очистить_Партию()
	тзПартииПроизводство.ВСД_Производство="";
КонецПроцедуры

Процедура ПриИзмененииПлощадки()
	ГМ.СписокКонстант.Установить("Отправитель_Площадка",Отправитель_Площадка);
	//Обновить список документов
	СписокДокументов.УдалитьСтроки();
	//тзПартии.УдалитьСтроки();
	ЗаполнитьСписокДокументов_Реализации();
КонецПроцедуры

Процедура АннулироватьВСД()
	Если Вопрос("Вы уверены, что хотите аннулировать выбранные ВСД?",4,30)=6 Тогда
		Сообщить("Выполняется аннулирование ВСД");

		СписокДокументов.ВыбратьСтроки();
		Пока СписокДокументов.ПолучитьСтроку() = 1 Цикл
			Если СписокДокументов.Пометка = 1 Тогда
				ГМ2.Аннулировать_ВСД2_транзакция(СписокДокументов.ВСД);
			КонецЕсли;
		КонецЦикла;
		
		ЗаполнитьСписокДокументов_Реализации();
		
		ПриВыбореЗакладки(1,"Реализации");	
	КонецЕсли;
КонецПроцедуры

Процедура ПолучитьУсловияПеревозки()
	Состояние("Получаем условия перевозки");

	ДокОб = создатьОбъект("Документ");

	СписокДокументов.ВыбратьСтроки();
	Пока СписокДокументов.ПолучитьСтроку() = 1 Цикл
		Если СписокДокументов.Пометка = 1 Тогда
			
			ГМ2.УсловияПеревозки_Получить( СписокДокументов.ВСД, 1, 1 );
			
		КонецЕсли;
	КонецЦикла;

	ПриВыбореЗакладки(1,"Реализации");
	Состояние("");
КонецПроцедуры

Процедура ПолучитьОтветВетис()
		Сообщить("Выполняется запрос ответов от ВЕТИС на отправленные документы без ответа");

		СписокДокументов.ВыбратьСтроки();
		Пока СписокДокументов.ПолучитьСтроку() = 1 Цикл
			Попытка
				Если СписокДокументов.Пометка = 1 Тогда 
				
					спПараметры = СоздатьОбъект("СписокЗначений");	
					спПараметры.Установить("applicationID", СписокДокументов.ВСД.applicationID);
					спПараметры.Установить("докСсылка", СписокДокументов.ВСД );
					ГМ2.ПолучитьРезультат_ВСД_2( спПараметры, 0 );								
					
				КонецЕсли;
			Исключение
			    Сообщить(ОписаниеОшибки());
			КонецПопытки;
		КонецЦикла;

		ЗаполнитьСписокДокументов_Реализации();		
		
		ПриВыбореЗакладки(1,"Реализации");
КонецПроцедуры

Процедура ДобавитьМаршрут()

	спПараметры = СоздатьОбъект("СписокЗначений");	
	
	СписокДокументов.ВыбратьСтроки();
	Пока СписокДокументов.ПолучитьСтроку() = 1 Цикл
		Если ( СписокДокументов.Пометка = 1 ) и ( ПустоеЗначение(СписокДокументов.ВСД)=0 ) Тогда 
			
			спПараметры.ДобавитьЗначение( СписокДокументов.ВСД );
			
		КонецЕсли;
	КонецЦикла;

	СписокПараметров = СоздатьОбъект("СписокЗначений");
	СписокПараметров.Установить("Документ", спПараметры);
	ОткрытьФорму("Отчет", СписокПараметров, ГМ.ПолучитьКонстанту("КаталогМодуля")+"Меркурий_МультимодальнаяПеревозка.ert");
		
КонецПроцедуры

//{ ==================== МЕНЮ кнопок ===========================

Процедура МенюВСД()
	спМеню = СоздатьОбъект("СписокЗначений");
	спМеню.ДобавитьЗначение("СоздатьВСД2", "Создать ВСД");
	спМеню.ДобавитьЗначение("ОтправитьВСДбыстро", "Отправить ВСД быстро");
	спМеню.ДобавитьЗначение("ОтправитьВСД", "Отправить ВСД");	
	спМеню.ДобавитьЗначение("АннулироватьВСД", "Аннулировать ВСД");
	спМеню.ДобавитьЗначение("ПолучитьУсловияПеревозки", "Получить условия перевозки");
	спМеню.ДобавитьЗначение("ПолучитьОтветВетис", "Получить ответ ВЕТИС");
	спМеню.ДобавитьЗначение("ДобавитьМаршрут", "Добавить маршрут доставки");

	зн = "";
	спМеню.ВыбратьЗначение(зн,,,,1);
	Если зн = "СоздатьВСД2" Тогда
		СоздатьВСД_ПоТзРаспределения();
	ИначеЕсли зн = "ОтправитьВСД" Тогда
		ОтправитьВСД();
	ИначеЕсли зн = "ОтправитьВСДбыстро" Тогда
		ОтправитьВСДбыстро();
	ИначеЕсли зн = "АннулироватьВСД" Тогда
		АннулироватьВСД();
	ИначеЕсли зн = "ПолучитьУсловияПеревозки" Тогда
		ПолучитьУсловияПеревозки();
	ИначеЕсли зн = "ПолучитьОтветВетис" Тогда
		ПолучитьОтветВетис();
	ИначеЕсли зн = "ДобавитьМаршрут" Тогда
		ДобавитьМаршрут();
	КонецЕсли;

КонецПроцедуры

Процедура МенюПроизводство()
	спМеню = СоздатьОбъект("СписокЗначений");
//	спМеню.ДобавитьЗначение("Очистить_Партию", "Очистить выбранную партию");
	спМеню.ДобавитьЗначение("Создать_ВСД_Производство1", "Создать ВСД Производство (построчно)");
	спМеню.ДобавитьЗначение("Создать_ВСД_Производство2", "Создать ВСД Производство (объединенно)");
	спМеню.ДобавитьЗначение("Отправить_ВСД_Производство", "Отправить ВСД Производство");
	спМеню.ДобавитьЗначение("Создать_Продукцию", "Создать Продукцию");
	зн = "";
	спМеню.ВыбратьЗначение(зн,,,,1);
	Если зн = "Очистить_Партию" Тогда
		Очистить_Партию();
	ИначеЕсли зн = "Создать_ВСД_Производство1" Тогда
		Создать_ВСД_Производство1();
	ИначеЕсли зн = "Создать_ВСД_Производство2" Тогда
		Создать_ВСД_Производство2();
	ИначеЕсли зн = "Отправить_ВСД_Производство" Тогда
		Отправить_ВСД_Производство();
	ИначеЕсли зн = "Создать_Продукцию" Тогда
		СоздатьПродукцию();
	КонецЕсли;

КонецПроцедуры

//}


Процедура ПереместитьВХС()
	// Переместить с 1 ХС на другой и Погасить
	ТВопроса = "Создать ВСД2_Транзакцию по списку партий
	|на перемещение ?";
	Если Вопрос(ТВопроса,"Да+Нет")="Нет" Тогда
	    Возврат;
	КонецЕсли;

	ДокВСД = СоздатьВСД_Перемещение();

	Если ПустоеЗначение(ДокВСД) = 1 Тогда
	    Сообщить("Документ ВСД2_Транзакция не удалось создать");
		Возврат;
	КонецЕсли;

	ОткрытьФорму(ДокВСД);
	Сообщить("После гашения нажмите Выбрать Партии");
	Возврат;

КонецПроцедуры

Процедура ПриИзмененииФирмы()

	ГМ.Инициализация(Контекст);
	ГМ.ЗагрузитьПараметрыВФорму(Контекст);

КонецПроцедуры

//======================================================================
Процедура ПриИзмененииСФ()
	Если СписокФирм.ТекущаяСтрока() <> 0 Тогда
		ВыбФирма = СписокФирм.ПолучитьЗначение(СписокФирм.ТекущаяСтрока());
		ПриИзмененииФирмы();
	КонецЕсли;
КонецПроцедуры // ПриИзмененииСФ


///******************************** ADirks 25.06.2018 ************
Процедура ПриИзмененииРазмераОкна(ТипСобытия, НовШирина, НовВысота) Экспорт
	оПривязки.ПриИзмененииРазмераОкна(ТипСобытия, НовШирина, НовВысота);
КонецПроцедуры
///******************************** ADirks 25.06.2018 ************

///******************************** ADirks 25.06.2018 ************
Процедура Привязки_Инит()
	//оПривязки = СоздатьОбъект("Меркурий.Привязки");
	//оПривязки.Инит(Контекст);

	//оПривязки.Добавить("кнОК, кнЗаписать, кнЗакрыть", "ВН", "Форма");
	//оПривязки.Добавить("Версия", "ВН", "Форма");

	оПривязки.УстановитьФорму(Форма);
	оПривязки.Привязка("Документы", "H", "Форма", "W", "Форма");
	оПривязки.Привязка("НеобходимыеПартии", "H", "Форма", "W", "Форма");
	оПривязки.Привязка("ПартииПроизводство", "H", "Форма", "W", "Форма");
	оПривязки.Привязка("Версия,Инфо,ИнфоВыбор, тРеализация,тКолвоСписания","T","Форма");

КонецПроцедуры
///******************************** ADirks 25.06.2018 ************

//_____________________________________________________________________________
Процедура ПослеОткрытия()
	оПривязки.ПослеОткрытия();
КонецПроцедуры

//=========================== ПРЕДОПРЕДЕЛЕННЫЕ ПРОЦЕДУРЫ =============================

Процедура ОбработкаПодбора( ВыбЭлемент, КонтФормы )
	
	Если ВыбЭлемент.Вид()="ВСД_Площадка" Тогда
		
		//НовыйГрузополучатель = ВыбЭлемент.Контрагент;
		СтарыйГрузополучатель = СписокДокументов.ПолучитьЗначение(ВыбСтрока,"Грузополучатель");
		СтараяПлощадка = СписокДокументов.ПолучитьЗначение(ВыбСтрока,"Площадка");
		Если ВыбЭлемент = СтараяПлощадка Тогда
			КонтФормы.Форма.Закрыть();
			Возврат;
		КонецЕсли;

		СпрПлощадка = СоздатьОбъект("Справочник.ВСД_Площадка");
		//очищаем старую привязку
		Если ПустоеЗначение(СтараяПлощадка)=0 Тогда 
			СпрПлощадка.НайтиЭлемент(СтараяПлощадка);
			СпрПлощадка.Контрагент = "";
			СпрПлощадка.Записать();
		КонецЕсли;
		
		Если (ПустоеЗначение( ВыбЭлемент.Контрагент )=0) И
			(ВыбЭлемент.Контрагент <> СтарыйГрузополучатель) Тогда
			Если Вопрос("Выбранная площадка закреплена за "+ВыбЭлемент.Контрагент+" Скопировать площадку?",4,30)=6 Тогда
				СпрПлощадка.Новый();
				СпрПлощадка.Наименование = ВыбЭлемент.Наименование;
				СпрПлощадка.Контрагент = СтарыйГрузополучатель;
				СпрПлощадка.GUID = ВыбЭлемент.GUID;
				СпрПлощадка.UUID = ВыбЭлемент.UUID;
				СпрПлощадка.Адрес = ВыбЭлемент.Адрес;
				СпрПлощадка.GuidХозСубъекта = ВыбЭлемент.GuidХозСубъекта;
				СпрПлощадка.Склад = ВыбЭлемент.Склад;
				СпрПлощадка.НомерПлощадки = ВыбЭлемент.НомерПлощадки;
				СпрПлощадка.Записать();

			Иначе //замена контрагента в площадке

				Если СпрПлощадка.ВыбратьЭлементыПоРеквизиту("Контрагент", СтарыйГрузополучатель, 0,0)=1 Тогда
					Пока СпрПлощадка.ПолучитьЭлемент()=1 Цикл
						//Сообщить("Очистили привязку площадки "+СпрПлощадка.Контрагент);
						СпрПлощадка.Контрагент = "";
						СпрПлощадка.Записать();
					КонецЦикла;
				КонецЕсли;
				СпрПлощадка.НайтиЭлемент(ВыбЭлемент);
				СпрПлощадка.Контрагент = СтарыйГрузополучатель;
				СпрПлощадка.Записать();

			КонецЕсли;
		Иначе
			СпрПлощадка.НайтиЭлемент(ВыбЭлемент);
			СпрПлощадка.Контрагент = СтарыйГрузополучатель;
			СпрПлощадка.Записать();
		КонецЕсли;
		КонтФормы.Форма.Закрыть();

		СписокДокументов.УстановитьЗначение(ВыбСтрока, "Площадка", СпрПлощадка.ТекущийЭлемент());

		ЗаполнитьСписокДокументов_Реализации();
		
	КонецЕсли;

КонецПроцедуры

Процедура ЗаполнитьПринтеры()
	Перем Стр;
	//очистим список
	Принтер.УдалитьВсе();

	тзПринтеры = СоздатьОбъект("ТаблицаЗначений");
	тзПринтеры.НоваяКолонка("Имя");
	тзПринтеры.НоваяКолонка("Порт");

	//заполним список доступных принтеров
	Состояние("Заполняем список принтеров...");
    wshNetwork    = createObject("WScript.Network");
	oPrinters        = wshNetwork.EnumPrinterConnections();
	i = 0;
	Пока i < oPrinters.Count() - 1 Цикл
		//Если Найти(oPrinters.Item(i+1),"\\tsclient") > 0 Тогда
		//	i = i + 2;
		//	Продолжить;
		//КонецЕсли;
		тзПринтеры.НоваяСтрока();
		тзПринтеры.Имя = oPrinters.Item(i+1);
		тзПринтеры.Порт = oPrinters.Item(i);

		Принтер.ДобавитьЗначение(oPrinters.Item(i+1),oPrinters.Item(i+1));
		i = i + 2;
	КонецЦикла;

	//Принтер.ДобавитьЗначение("\\TSCLIENT\E","\\TSCLIENT\E"); // печать на принтере терминала по умолчанию

    //    Принтер по умолчанию
	Попытка
	    wshPrint = CreateObject("WScript.Shell");
	    Prn = СокрЛП(wshPrint.RegRead("HKCU\Software\Microsoft\Windows NT\CurrentVersion\Windows\Device")); //принтер по умолчанию, но вместе с портом
		Сч =  1;
		Пока Сч <= Принтер.РазмерСписка() Цикл
			Принтер.ПолучитьЗначение(Сч,Стр);
			Если Найти(Prn,Стр) > 0 Тогда
				//Это и есть принтер по умолчанию
				Прервать;
			КонецЕсли;
			Сч = Сч + 1;
			Стр = "";
		КонецЦикла;
		Принтер.ТекущаяСтрока(Сч);
		Форма.Принтер.Доступность(1);
	Исключение
	КонецПопытки;

КонецПроцедуры // ЗаполнитьПринтеры()

Процедура ПриОткрытии()
	//Если Метаданные.Справочник("Номенклатура").Реквизит("ПодконтроленМеркурию").Выбран() = 0 Тогда
	//	Форма.кнСоздатьПродукцию.Доступность(0);
	//КонецЕсли;

	ВыбФирма = "";
	ГМ._ПриОткрытии(Контекст);

	тзПартииПроизводство = ГМ.СоздатьТзПартийПроизводство();	

	тзНеобходимыеПартии = СоздатьОбъект("ТаблицаЗначений");
	тзНеобходимыеПартии.НоваяКолонка("ДокРеализации",,,,,20,);
	тзНеобходимыеПартии.НоваяКолонка("Номенклатура",,,,,20,);
	тзНеобходимыеПартии.НоваяКолонка("Продукция_Элемент",,,,,20,);
	тзНеобходимыеПартии.НоваяКолонка("Партия",,,,,20,);
	тзНеобходимыеПартии.НоваяКолонка("КолвоНаСкладе","Число",,,,10,);
	тзНеобходимыеПартии.НоваяКолонка("Количество","Число",,,,10,);
	тзНеобходимыеПартии.НоваяКолонка("ПолеСортировки",,,, ГМ.ПолучитьКонстанту("ПарамКолонкаСортировкиПартииСписания"),15,);
	
	флФильтрПоСкладу = ВосстановитьЗначение("ФильтрПоСкладу");

	ЗаголовокНадпись();

	Форма.ИспользоватьЗакладки(1);
	Форма.Закладки.ДобавитьЗначение("Реализации");
	Форма.Закладки.ДобавитьЗначение("Партии");
	Форма.Закладки.ДобавитьЗначение("Производство");
	Форма.Закладки.ДобавитьЗначение("Параметры");

	Форма.ИспользоватьСлой("Основной, СписокДокументов, Реализации");

	Партия = "";

	// Обработку можно вызвать для заполнения списка реализаций на основании маршрута
	// для этого в Маршрут в печатные формы добавьте ПечФорма_ВСД_ГрупповаяОбработка.ert
	//
	Парам = Форма.Параметр;
	Если ТипЗначенияСтр(Парам)="ТаблицаЗначений" Тогда
		
		ЗаполнитьСписокДокументов_по_ТЗ( Парам );

		ЗаполненПоТЗ = 1;
		
	Иначе
		////стандартное заполнение документов
		ЗаполнитьСписокДокументов_Реализации();
		
		ЗаполненПоТЗ = 0;			
		
	КонецЕсли;
	//

	Если ПустоеЗначение( ГМ.ПолучитьКонстанту("ДобавлятьУпаковки") )=1 Тогда

		СписокДокументов.УдалитьКолонку("КолвоМест");

	КонецЕсли;

	ГМ.СписокФирм.Выгрузить(СписокФирм);
	Если СписокФирм.РазмерСписка() = 0 Тогда
		Предупреждение("Нет сохраненных настроек, обмен невозможен!");
		Возврат; СтатусВозврата(0);
	Иначе
		Поз = СписокФирм.НайтиЗначение(ВыбФирма);
		Если Поз <> 0 Тогда
			СписокФирм.ТекущаяСтрока(Поз);
		КонецЕсли;
		ПриИзмененииФирмы();
	КонецЕсли;

	ЗаполнитьПринтеры();

	Привязки_Инит(); //ADirks 25.06.2018
	
	ФормаРасш = СоздатьОбъект("РасширениеФормы");
	ФормаРасш.УстановитьФорму(Форма);	
КонецПроцедуры

 // предопределенная процедура
 Процедура ПриВыбореЗакладки(НомерЗакладки, ЗначениеЗакладки)
	
 	Если ЗначениеЗакладки="Реализации" Тогда
		Форма.ИспользоватьСлой("Основной, СписокДокументов, Реализации");
		//СписокДокументов.УдалитьСтроки();
		//Если СписокДокументов.КоличествоСтрок()=0 Тогда
		//	ЗаполнитьСписокДокументов_Реализации();
		//КонецЕсли;
	ИначеЕсли ЗначениеЗакладки="Партии" Тогда
		Форма.ИспользоватьСлой("Основной, Партии");
		//ЗаполнитьПартии();
	ИначеЕсли ЗначениеЗакладки="Производство" Тогда
		Форма.ИспользоватьСлой("Основной, Производство");
		//ЗаполнитьПартииПроизводства();
		//тз = ГМ.ЗаполнитьПартииПроизводства( СписокДокументов ); 
		тз = ГМ.ЗаполнитьПартииПроизводстваПоТзПартий( тзНеобходимыеПартии );
		тзПартииПроизводство.Загрузить( тз );
		тпПартииПроизводство.ОбновитьСтроки();
	Иначе
		Форма.ИспользоватьСлой("Параметры");
	КонецЕсли;
	
	Форма.Закладки.ТекущаяСтрока(НомерЗакладки);
	
КонецПроцедуры

//======================================================================
Процедура ПриЗакрытии()
	СохранитьЗначение("ФильтрПоСкладу", флФильтрПоСкладу);
	тпДокументов.СохранитьНастройки();
КонецПроцедуры // ПриЗакрытии

//======================================================================
Процедура ПослеСозданияФормы()

	//тпДокументов = ГМ.СоздатьТабличноеПоле( Сам(), "Документы", СписокДокументов,1,1);
	тпНеобходимыеПартии = ГМ.СоздатьТабличноеПоле( Сам(), "НеобходимыеПартии", тзНеобходимыеПартии,0,1);
	тпПартииПроизводство = ГМ.СоздатьТабличноеПоле( Сам(), "ПартииПроизводство", тзПартииПроизводство,0,1);
	
    тпДокументов = СоздатьОбъект("Меркурий.ТабличноеПоле.ТЗ");
    тпДокументов.Инит( Сам(), "Документы");
    тпДокументов.ИмяНастройки = "ВСД_Реализации";
    тпДокументов.УстановитьТЗ( СписокДокументов );
    тпДокументов.Показать();	
	
	тпДокументов.ОбновитьСтроки(); 
КонецПроцедуры

//========================= ТАБЛИЧНОЕ ПОЛЕ ДОКУМЕНТОВ =================
//{
Процедура ДокументыПриАктивизацииСтроки(ТабличноеПоле)
	
	ГМ.ПриАктивизацииСтрокиТП(ТабличноеПоле, СписокДокументов);
	
КонецПроцедуры

Процедура ДокументыВыбор()
	
	ГМ.ПриАктивизацииСтрокиТП(ТПДокументов, СписокДокументов);
	ГМ.ПриАктивизацииКолонкиТП(ТПДокументов, СписокДокументов);
	
	текСтр=СписокДокументов.ТекущаяСтрока();
	Если текСтр=0 Тогда Возврат; КонецЕсли;
	текКол = СписокДокументов.ТекущаяКолонка();
	
	Если текКол="Площадка" Тогда 
		ВыбСтрока = текСтр; //для ОбработкаПодбора 
		
		Площадка = СписокДокументов.ПолучитьЗначение(ТекСтр,текКол);
		ХозСубъект = СписокДокументов.ПолучитьЗначение(текСтр, "ХозСубъект");
				
		Если ПустоеЗначение(Площадка)=0 Тогда 
			
			меню = СоздатьОбъект("СписокЗначений");
			меню.ДобавитьЗначение("Открыть","Открыть");
			//меню.ДобавитьЗначение("Добавить","Добавить");
			меню.ДобавитьЗначение("Изменить","Изменить"); 
			меню.ДобавитьЗначение("Очистить","Очистить"); 
				
			стр=0; Зн="";
			Если меню.ВыбратьЗначение(Зн, "", стр, 60, 1) = 1 Тогда
								
				Если Зн ="Открыть"  Тогда 	
									
					ОткрытьФорму( Площадка );
					
				ИначеЕсли Зн ="Изменить"  Тогда
					
					тз = ГМ.ВыбратьВсеПлощадкиХС(ХозСубъект);
					
					СписокОтбора = СоздатьОбъект("СписокЗначений");
					
					Тз.ВыбратьСтроки();
					Пока ТЗ.ПолучитьСТроку()=1 Цикл
						СписокОтбора.ДобавитьЗначение(тз.id);
					КонецЦикла;
					//ЖД - нет по ХС площадок - открывались все
					Если СписокОтбора.размерСписка() = 0 Тогда
						СписокОтбора.ДобавитьЗначение("***");
						//или написать что нет площадок и выйти отсюда 
					КонецЕсли;
					ОткрытьПодбор("Справочник.ВСД_Площадка", "ФормаСписка", СписокОтбора,0, Площадка);
	
				ИначеЕсли Зн ="Очистить"  Тогда 	
	
					Если ( ПустоеЗначение( Площадка )=0 ) Тогда
						
						СпрПлощадка = СоздатьОбъект("Справочник.ВСД_Площадка");
						СпрПлощадка.НайтиЭлемент( Площадка );
						СпрПлощадка.Контрагент = "";
						СпрПлощадка.Записать();					
						СписокДокументов.УстановитьЗначение(ТекСтр,"Площадка","");
						
						ЗаполнитьСписокДокументов_Реализации();
					КонецЕсли;
					
				КонецЕсли;
			КонецЕсли;
		Иначе // открываем подбор

			тз = ГМ.ВыбратьВсеПлощадкиХС(ХозСубъект);			
			СписокОтбора = СоздатьОбъект("СписокЗначений");			
			Тз.ВыбратьСтроки();
			Пока ТЗ.ПолучитьСТроку()=1 Цикл
				СписокОтбора.ДобавитьЗначение(тз.id);
			КонецЦикла;
			//ЖД - нет по ХС площадок - открывались все
			НетПлощадок=0; //[+]serpent, 09.07.2019
			Если СписокОтбора.размерСписка() = 0 Тогда
				СписокОтбора.ДобавитьЗначение("***");
				//или написать что нет площадок и выйти отсюда 
				НетПлощадок = 1; //[+]serpent, 09.07.2019
			КонецЕсли;
			Если НетПлощадок = 0 Тогда
				ОткрытьПодбор("Справочник.ВСД_Площадка", "ФормаСписка", СписокОтбора,0, Площадка);
			Иначе
				Ответ = Вопрос("Площадок НЕТ... Загрузить?","Да+Нет+Отмена",10);
				Если Ответ="Да" Тогда
					ГМ.ЗагрузитьПлощадки(СписокДокументов.ХозСубъект); //[+]serpent, 09.07.2019
				ИначеЕсли Ответ="Отмена" Тогда
					тз = ГМ.ВыбратьВсеПлощадкиХС(ХозСубъект);			
					СписокОтбора = СоздатьОбъект("СписокЗначений");			
					Тз.ВыбратьСтроки();
					Пока ТЗ.ПолучитьСТроку()=1 Цикл
						СписокОтбора.ДобавитьЗначение(тз.id);
					КонецЦикла;
					//ЖД - нет по ХС площадок - открывались все
					Если СписокОтбора.размерСписка() = 0 Тогда
						СписокОтбора.ДобавитьЗначение("***");
						//или написать что нет площадок и выйти отсюда 
					КонецЕсли;
					ОткрытьПодбор("Справочник.ВСД_Площадка", "ФормаСписка", СписокОтбора,0, Площадка);
				КонецЕсли;
			КонецЕсли;
			
		КонецЕсли;

		тпДокументов.ОбновитьСтроки();
		
	ИначеЕсли текКол="ХозСубъект" Тогда 
		
		ХозСубъект = СписокДокументов.ПолучитьЗначение(текСтр, "ХозСубъект");	
		Если ПустоеЗначение(ХозСубъект)=1 Тогда 
			ОткрытьФорму("Справочник.ВСД_ХозСубъект");
		Иначе
			ОткрытьФорму(ХозСубъект);
		КонецЕсли;
		
	ИначеЕсли текКол="Регион_ПЛ" Тогда //[+]serpent, 04.07.2019
		
		ТекРегион   = СписокДокументов.ПолучитьЗначение(ТекСтр,текКол);
		ТекСтатус   = СокрЛП(СписокДокументов.ПолучитьЗначение(текСтр,"Статус"));
		ТекПлощадка = СписокДокументов.ПолучитьЗначение(текСтр, "Площадка");
		ТекВСД      = СписокДокументов.ПолучитьЗначение(текСтр, "ВСД");	
		Если ПустоеЗначение(ТекРегион)=1 Тогда
			Если ПустоеЗначение(ТекПлощадка.GUID)=1 Тогда
				Сообщить("У площадки нет ""GUID"". Загрузка не возможна.","!");
			Иначе
				Если ПустоеЗначение(ТекПлощадка.Регион)=1 Тогда
					Если Вопрос("Получить Регион из ГИС?","Да+Нет",10)="Да" Тогда
						Сообщить("Заполняем: "+СокрЛП(ТекПлощадка),"I");
						ГМ.Площадка_ЗагрузитьПоGUID( ТекПлощадка.ТекущийЭлемент(), ТекПлощадка.GUID );
						ВремСпр = СоздатьОбъект("Справочник.ВСД_Площадка");
						ВремСпр.НайтиЭлемент(ТекПлощадка);
						
						СписокДокументов.УстановитьЗначение(ТекСтр,текКол,ВремСпр.Регион.ТекущийЭлемент());
					
						тпДокументов.ОбновитьСтроки();
						Сообщить("2: "+СокрЛП(СписокДокументов.ПолучитьЗначение(ТекСтр,текКол)));
					КонецЕсли;
				Иначе
					Предупреждение("Ранее уже была получена",10);
					//СписокДокументов.УстановитьЗначение(ТекСтр,текКол,ТекПлощадка.Регион.ТекущийЭлемент());
					//тпДокументов.ОбновитьСтроки();
				КонецЕсли;
			КонецЕсли;
		ИначеЕсли (ПустоеЗначение(ТекРегион)=0) 
				и (ПустоеЗначение(ТекВСД)=0)  Тогда 
				//и ((ТекСтатус = "REJECTED") ИЛИ (ТекСтатус = "IN_PROCESS")) Тогда
					
			Если  ТекРегион = ОсновнойРегион Тогда
				Предупреждение("Это свой регион. Условия перевозки НЕ нужны!",10);
			Иначе
					
				//Запросим Регионализацию
				Если ПустоеЗначение( ТекВСД.ИмяФайлаРегионализация) = 1 Тогда
					Если Вопрос("Получить условия перевозки?","Да+Нет",10)="Да" Тогда						
						
						ГМ2.УсловияПеревозки_Получить( ТекВСД, 1, 1 );
						
					КонецЕсли;
				Иначе
					Предупреждение("Условия перевозки уже получены, ОТПРАВЛЯЙТЕ ВСД.",10);
				КонецЕсли;
			КонецЕсли;
		Иначе
			//просто открываем элемент
			Эл = СписокДокументов.ПолучитьЗначение(текСтр, текКол);	
			ОткрытьФорму(Эл);
		КонецЕсли;
		
	ИначеЕсли текКол="Статус" Тогда //[+]serpent, 25.07.2019
		ТекВСД      = СписокДокументов.ПолучитьЗначение(текСтр, "ВСД");	
		//ТекСтатус   = СокрЛП(СписокДокументов.ПолучитьЗначение(текСтр,"Статус"));
		//Если (ТекСтатус = "REJECTED") ИЛИ (ТекСтатус = "IN_PROCESS") Тогда
			Если ПустоеЗначение(СокрЛП(ТекВСД.Комментарий))=0 Тогда
				Предупреждение( СокрЛП(ТекВСД.Комментарий) ); //глЛегкоеСообщение
			КонецЕсли;
		//КонецЕсли;
	Иначе
		Эл = СписокДокументов.ПолучитьЗначение(текСтр, текКол);	
		ОткрытьФорму(Эл);
	КонецЕсли;
КонецПроцедуры

Процедура ДокументыПриВыводеСтроки(ТабличноеПоле,ОформлениеСтроки,ДанныеСтроки,ТипРегиона)

	Если ТипРегиона = 3 Тогда
		ГМ.ВывестиФлажок(ОформлениеСтроки, ДанныеСтроки, "Пометка");
		ТекВСД = ДанныеСтроки.ВСД;
		ОбъектЯчейкаРег=ОформлениеСтроки.Ячейки.Получить("Регион_ПЛ"); //[+]serpent, 04.07.2019
		Если (ПустоеЗначение(ТекВСД)=0) Тогда
			
			Если ( ГМ.СтатусЗакрыт( ТекВСД )=1 )  Тогда
				ОформлениеСтроки.ЦветФона = ПалитраЦветов.БледноЗеленый;
			Иначе
				Если ПустоеЗначение(ДанныеСтроки.Регион_ПЛ)=1 Тогда
					
				    ОбъектЯчейкаРег.Текст      = "НЕТ региона";
					ОбъектЯчейкаРег.ЦветТекста = ПалитраЦветов.Томатный;
					ОбъектЯчейкаРег.ОтображатьПодсказку=1; //[+]serpent, 25.07.2019
					ОбъектЯчейкаРег.Подсказка="Регион площадки(нажми и можно обновить.)"; //[+]serpent, 25.07.2019
					
				ИначеЕсли ПустоеЗначение(ТекВСД.ИмяФайлаРегионализация)=0 Тогда
					
					ОбъектЯчейкаРег.ЦветФона   = ПалитраЦветов.БледноЗеленый;
				    ОбъектЯчейкаРег.Текст      = "Условия перевозки получены";
					ОбъектЯчейкаРег.ЦветТекста = ПалитраЦветов.Черный;
					ОбъектЯчейкаРег.ОтображатьПодсказку=1; 
					ОбъектЯчейкаРег.Подсказка="можно отправлять ВСД"; 
					
				ИначеЕсли ДанныеСтроки.Регион_ПЛ <> ОсновнойРегион Тогда
					
					ОбъектЯчейкаРег.ЦветФона   = ПалитраЦветов.СветлоРозовый;
					ОбъектЯчейкаРег.ЦветТекста = ПалитраЦветов.Черный;
					ОбъектЯчейкаРег.Текст      = "ПОЛУЧИ условия перевозки";
					ОбъектЯчейкаРег.ОтображатьПодсказку=1; //[+]serpent, 25.07.2019
					ОбъектЯчейкаРег.Подсказка="Нажми два раза и в документ загрузятся условия для перевозки"; //[+]serpent, 25.07.2019
					
				ИначеЕсли ДанныеСтроки.Регион_ПЛ = ОсновнойРегион Тогда
					ОбъектЯчейкаРег.Текст      ="";// Свой Регион
				КонецЕсли;
			КонецЕсли;
			ОформлениеСтроки.Ячейки.ВСД.Текст = "№ "+СокрЛП(ТекВСД.НомерДок); //[+]serpent, 13.06.2019
		ИначеЕсли ПустоеЗначение(ДанныеСтроки.ХозСубъект.GUID)=1 Тогда 			

			ОформлениеСтроки.ЦветФона = ПалитраЦветов.Коралловый;	// Красный
			ОбъектЯчейка=ОформлениеСтроки.Ячейки.Получить("Площадка"); 
			ОбъектЯчейка.ОтображатьПодсказку=1; //[+]serpent, 25.07.2019
			ОбъектЯчейка.Подсказка="НЕТ GUID, загрузка площадок невозможна."; //[+]serpent, 25.07.2019

		ИначеЕсли (ПустоеЗначение(ДанныеСтроки.Площадка)=0) Тогда

			Если (ПустоеЗначение(ДанныеСтроки.Площадка.GUID)=0)  Тогда 
				
				Если ( ДанныеСтроки.Площадка.Статус = Перечисление.ВСД_СтатусПлощадки.CANCELED ) ИЛИ
					( ДанныеСтроки.Площадка.Статус = Перечисление.ВСД_СтатусПлощадки.DELETED ) Тогда

				    ОбъектЯчейкаРег.Текст      = "ЗАПРЕЩЕНО";
					ОбъектЯчейкаРег.ЦветТекста = ПалитраЦветов.Красный;
					ОбъектЯчейкаРег.ОтображатьПодсказку=1; 
					ОбъектЯчейкаРег.Подсказка="Площадка НЕ ИСПОЛЬЗУЕТСЯ";
					
					ОбъектЯчейка = ОформлениеСтроки.Ячейки.Получить("Площадка"); 
					ОбъектЯчейка.ЦветТекста = ПалитраЦветов.Красный;
					ОбъектЯчейка.ОтображатьПодсказку=1; 
					ОбъектЯчейка.Подсказка="Площадка НЕ ИСПОЛЬЗУЕТСЯ";
					
				ИначеЕсли ПустоеЗначение(ДанныеСтроки.Регион_ПЛ)=1 Тогда
					
				    ОбъектЯчейкаРег.Текст      = "НЕТ региона";
					ОбъектЯчейкаРег.ЦветТекста = ПалитраЦветов.Томатный;
					ОбъектЯчейкаРег.ОтображатьПодсказку=1; //[+]serpent, 25.07.2019
					ОбъектЯчейкаРег.Подсказка="Регион площадки(нажми и можно обновить.)"; //[+]serpent, 25.07.2019
										
				ИначеЕсли ДанныеСтроки.Регион_ПЛ = ОсновнойРегион Тогда
					
					ОбъектЯчейкаРег.Текст      =""; // Свой Регион

				Иначе

					ОбъектЯчейкаРег.Текст      = "СОЗДАЙТЕ ВСД";
					ОбъектЯчейкаРег.ЦветТекста = ПалитраЦветов.Черный;
					ОбъектЯчейкаРег.ОтображатьПодсказку=1; 
					ОбъектЯчейкаРег.Подсказка="Создайте ВСД и получите условия перевозки"; 
					
				КонецЕсли;
			КонецЕсли;

		Иначе			
			ОформлениеСтроки.ЦветФона = ПалитраЦветов.Желтый;	//Лимонный Желтый
			
			Если ПустоеЗначение(ДанныеСтроки.Площадка)=1 Тогда
				ОбъектЯчейка=ОформлениеСтроки.Ячейки.Получить("Площадка"); 
				ОбъектЯчейка.ОтображатьПодсказку=1; //[+]serpent, 25.07.2019
				ОбъектЯчейка.Подсказка="Нажми Два раза и можно загрузить площадки."; //[+]serpent, 25.07.2019
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

Процедура ДокументыПриВыбореФлажка(ТабличноеПоле,Стр, Колонка, ТипРегиона)
	
	ГМ.ПриАктивизацииСтрокиТП(ТабличноеПоле, СписокДокументов);
	СписокДокументов.Пометка = ?(СписокДокументов.Пометка = 1,0,1);
	ТабличноеПоле.ОбновитьСтроки();
	ОбновитьИнф();

	тзНеобходимыеПартии.УдалитьСтроки();
	//тзПартии.УдалитьСтроки();
	
КонецПроцедуры

//========================= ТАБЛИЧНОЕ ПОЛЕ ДОКУМЕНТОВ =================

Процедура НеобходимыеПартииПриВыводеСтроки(ТабличноеПоле,ОформлениеСтроки,ДанныеСтроки,ТипРегиона)

	Если ТипРегиона = 3 Тогда
		//ГМ.ВывестиФлажок(ОформлениеСтроки, ДанныеСтроки, "Пометка");
		
		Если (ДанныеСтроки.КолвоНаСкладе > 0 ) и ( ПустоеЗначение(ДанныеСтроки.Партия)=0 ) Тогда 
			ОформлениеСтроки.ЦветФона = ГМ.цвЗеленый;	// Зеленый
		Иначе
 				ОформлениеСтроки.ЦветФона = ГМ.цвКрасный;	// Красный
		КонецЕсли;
 			
		ЯчейкаТП=ОформлениеСтроки.Ячейки.Получить("Партия");
		ЯчейкаТП.Текст = "№"+ДанныеСтроки.Партия.Код+" "+СокрЛП(ДанныеСтроки.Партия.Наименование)+" "+ДанныеСтроки.Партия.Количество;
		
	КонецЕсли;
КонецПроцедуры

Процедура НеобходимыеПартииВыбор()
	
	ГМ.ПриАктивизацииСтрокиТП(тпНеобходимыеПартии, тзНеобходимыеПартии);
	ГМ.ПриАктивизацииКолонкиТП(тпНеобходимыеПартии, тзНеобходимыеПартии);
		
	Клик_партии();
		
КонецПроцедуры
//========================= ТАБЛИЧНОЕ ПОЛЕ ДОКУМЕНТОВ =================

Процедура ПартииПроизводствоПриВыводеСтроки(ТабличноеПоле,ОформлениеСтроки,ДанныеСтроки,ТипРегиона)

	Если ТипРегиона = 3 Тогда
		//ГМ.ВывестиФлажок(ОформлениеСтроки, ДанныеСтроки, "Пометка");
 			
 		Если (ДанныеСтроки.КолвоНаСкладе = 0 ) Тогда
			ОформлениеСтроки.ЦветФона = ГМ.цвКрасный;	// Красный
		ИначеЕсли (ДанныеСтроки.КолвоНаСкладе >= ДанныеСтроки.Количество) Тогда
			ОформлениеСтроки.ЦветФона = ГМ.цвЗеленый;	// Зеленый
		Иначе
			ОформлениеСтроки.ЦветФона = ГМ.цвЖелтый;	// Желтый
		КонецЕсли;
		
	КонецЕсли;
КонецПроцедуры

Процедура ПартииПроизводствоВыбор()
	
	ГМ.ПриАктивизацииСтрокиТП(тпПартииПроизводство, тзПартииПроизводство);
	ГМ.ПриАктивизацииКолонкиТП(тпПартииПроизводство, тзПартииПроизводство);	
	
	Клик_ПартииПроизводства();
	
КонецПроцедуры

//}

//{========================= Список изменений =================
Функция ПоказатьИзмененияВерсии()
	
	ЗапуститьПриложение( ГМ.ВерсияСсылка() );
	
КонецФункции


Процедура ПриНажатииЛевойКнопки(Сост, х, у)
	
	ФормаРасш = СоздатьОбъект("РасширениеФормы");
	Атр = ФормаРасш.ПолучитьАтрибутПоКоординатам(х,у);
	Если ТипЗначенияСтр(Атр) = "АтрибутФормы" Тогда
		Если Атр.Идентификатор = "Версия" Тогда
			ПоказатьИзмененияВерсии();
		КонецЕсли;  
	КонецЕсли;	
	
КонецПроцедуры

//}===========================================================

НачДата = ТекущаяДата();
КонДата = ТекущаяДата();

СписокДокументов = СоздатьОбъект("ТаблицаЗначений");
СписокДокументов.НоваяКолонка("Пометка",,,,,5,);
СписокДокументов.НоваяКолонка("Грузополучатель",,,,,20,);
СписокДокументов.НоваяКолонка("Площадка","Справочник.ВСД_Площадка",,,,20,);
СписокДокументов.НоваяКолонка("Регион_ПЛ","Справочник.ВСД_Регион",,,"Условия перевозки",10,); //[+]serpent, 04.07.2019
СписокДокументов.НоваяКолонка("ВСД",,,,,10,);
СписокДокументов.НоваяКолонка("Отправлен",,,,,7,);
СписокДокументов.НоваяКолонка("Статус",,,,,10,);
СписокДокументов.НоваяКолонка("Колво","Число",10,3,,10,);
СписокДокументов.НоваяКолонка("КолвоМест",,,,,10,);
СписокДокументов.НоваяКолонка("НомерАвто",,,,,10,);//+
СписокДокументов.НоваяКолонка("Док",,,,,30,);
СписокДокументов.НоваяКолонка("Склад",,,,,10,);
СписокДокументов.НоваяКолонка("Контрагент",,,,,10,);
СписокДокументов.НоваяКолонка("ХозСубъект",,,,,10,);

Сервис = СоздатьОбъект("Сервис");
Сервис.ВключитьРаскраскуТаблиц();
Сервис.ИспользоватьПланРаскраски(0);

ПалитраЦветов = СоздатьОбъект("ПоставщикДанных.ПалитраЦветов"); //[+]serpent, 16.05.2019
ОсновнойРегион = ГМ.ПолучитьКонстанту("Регион");
