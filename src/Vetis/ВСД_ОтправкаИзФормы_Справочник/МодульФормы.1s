Перем ТекЭл;
Перем Операция;

// вспомогательные переменные, можно хранить в списке значений

// здесь форма документа уже закрыта, блокировки нет
// делаем необходимые манипуляции,
// после которых открываем форму документа
Процедура ПриЗакрытии()
    Спр = СоздатьОбъект("Справочник."+ТекЭл.Вид());
    Если Спр.НайтиЭлемент(ТекЭл) = 1 Тогда

   		Форма._Индикатор.Заголовок("Отправка "+ТекЭл);
   		ЗаписьЖурналаРегистрации("Меркурий отправка в ГИС Меркурий "+ТекЭл);
   	  
   		ОткрыватьФорму =1; //открыть форму справочника
	   	Если Спр.Вид()="ВСД_Продукция_Элемент" Тогда
	   		
	   	  	Если ПустоеЗначение(Операция) = 0 Тогда
				ГМ2.Изменить_Продукцию(Спр.ТекущийЭлемент(), Операция);
			Иначе
	   	  		ГМ2.Получить_Инфо_Продукции( Спр.ТекущийЭлемент() );
	   	  	КонецЕсли;
	   	  	
	   	ИначеЕсли Спр.Вид()="ВСД_Партия" Тогда
	   	  	
   	  		ГМ2.Партии_ПолучитьПоGUID( Спр.ТекущийЭлемент() );
   	  		
	   	ИначеЕсли Спр.Вид()="ВСД_ХозСубъект" Тогда
   	  		
	   	  	Если Операция = "Загрузить" Тогда
	   	  		
				ГМ.ЗагрузитьХСПоGUID(Спр.GUID, Спр.ТекущийЭлемент());
				
			ИначеЕсли Операция = "НайтиGUIDпоИНН" Тогда
				
				ГМ.ХС_ПолучитьGuid( Спр.ТекущийЭлемент() );
				
			ИначеЕсли Операция = "ЗагрузитьПлощадкиПоХозСубъекту" Тогда
				
				ГМ.ЗагрузитьПлощадки( Спр.ТекущийЭлемент() );
				
			ИначеЕсли Операция = "CREATE" Тогда
				
				ГМ.ХозСубъект_Создать( Спр.ТекущийЭлемент() );
				
			ИначеЕсли Операция = "НайтиПлощадкиПоНазванию" Тогда
				
				ГМ2.НайтиПлощадкиПоНазванию( Спр.ТекущийЭлемент(), ГМ.ПолучитьКонстанту("Регион"), 1);
				ОткрыватьФорму = 0;
				
			Иначе
	   	  		//ГМ2.Получить_Инфо_Продукции( Спр.ТекущийЭлемент() );
	   	  	КонецЕсли;
	   	  	
	   	ИначеЕсли Спр.Вид()="ВСД_Площадка" Тогда
	   	  	Если Операция = "Загрузить" Тогда
	   	  		
				ГМ.Площадка_ЗагрузитьПоGUID( Спр.ТекущийЭлемент(), Спр.GUID );

			ИначеЕсли Операция = "СоздатьСвязь" Тогда
	   	  		
				ГМ.Площадка_СоздатьСвязь( Спр.ТекущийЭлемент() );
				
			ИначеЕсли Операция = "ЗагрузитьПлощадкиПоХозСубъекту" Тогда
				
				ХС = ГМ.НайтиХСпоGUID( Спр.GuidХозСубъекта );
				ГМ.ЗагрузитьПлощадки( ХС );
				
			ИначеЕсли Операция = "CREATE" Тогда
				
				ГМ.Площадка_Создать( Спр.ТекущийЭлемент() );
				
			ИначеЕсли Операция = "НайтиПлощадкиПоНазванию" Тогда
				
				ХС = ГМ.НайтиХСпоGUID( Спр.GuidХозСубъекта );
				ГМ2.НайтиПлощадкиПоНазванию( ХС, ГМ.ПолучитьКонстанту("Регион"), 1);
				ОткрыватьФорму = 0;
				
			Иначе
	   	  		//ГМ2.Получить_Инфо_Продукции( Спр.ТекущийЭлемент() );
	   	  	КонецЕсли;
	   	Иначе
	   	  	Сообщить("Отправка справочника "+Спр.Вид()+" из формы не предусмотрена");
	   	КонецЕсли;
	   	Если ОткрыватьФорму = 1 Тогда 
	    	ОткрытьФорму(ТекЭл);
		КонецЕсли;
    КонецЕсли;
КонецПроцедуры // ПриЗакрытии

// при открытии формы обработки запоминаем переданные параметры,
// после чего закрываем форму документа (ставится "в очередь")
Процедура ПриОткрытии()
   	Если ТипЗначенияСтр(Форма.Параметр) = "СписокЗначений" Тогда
      	КонтекстСпр = Форма.Параметр.Получить("Контекст");
	  	Операция = Форма.Параметр.Получить("Операция");
      	ТекЭл = КонтекстСпр.ТекущийЭлемент();
	  	
	  	Если ( ГМ.ТекущаяИнициализированнаяФирма = "нет" ) Тогда 
			ГМ.Инициализация( КонтекстСпр );
		КонецЕсли;
	  	
      	КонтекстСпр.Форма.Закрыть(0); 
		Форма.Закрыть(0); 
   Иначе
      СтатусВозврата(0);
   КонецЕсли;
КонецПроцедуры // ПриОткрытии 