Перем оПривязки; //:Меркурий.Привязки
Перем тзПлощадки, тпПлощадки;
Перем Парам;

//======================================================================
Процедура ВыделитьВсе()
	тзПлощадки.ВыбратьСтроки();
	Пока тзПлощадки.ПолучитьСтроку() = 1 Цикл
		тзПлощадки.Пометка = 1;
	КонецЦикла;
	тпПлощадки.ОбновитьСтроки();
КонецПроцедуры

//======================================================================
Процедура ОтменитьВсе()
	тзПлощадки.ВыбратьСтроки();
	Пока тзПлощадки.ПолучитьСтроку() = 1 Цикл
		тзПлощадки.Пометка = 0;
	КонецЦикла;
	тпПлощадки.ОбновитьСтроки();
КонецПроцедуры

//======================================================================
Процедура тпПлощадкиПриВыводеСтроки(ТабличноеПоле,ОформлениеСтроки,ДанныеСтроки,ТипРегиона)
	Если ТипРегиона = 3 Тогда
		ГМ.ВывестиФлажок(ОформлениеСтроки, ДанныеСтроки, "Пометка");
		
		Если СокрЛП(ДанныеСтроки.Активна)="false" Тогда
			ОформлениеСтроки.ЦветФона = ГМ.цвКрасный;	
		ИначеЕсли СокрЛП(ДанныеСтроки.Статус)="UNVERIFIED" Тогда
			ОформлениеСтроки.ЦветФона = ГМ.цвЖелтый;	
		ИначеЕсли СокрЛП(ДанныеСтроки.Статус)="VERIFIED" Тогда
			ОформлениеСтроки.ЦветФона = ГМ.цвЗеленый;	
		Иначе
			ОформлениеСтроки.ЦветФона = ГМ.цвКрасный;
		КонецЕсли;
		
	КонецЕсли;
КонецПроцедуры

//======================================================================
Процедура тпПлощадкиПриВыбореФлажка(ТабличноеПоле,Стр, Колонка, ТипРегиона)
	ГМ.ПриАктивизацииСтрокиТП(ТабличноеПоле, тзПлощадки);
	тзПлощадки.Пометка = ?(тзПлощадки.Пометка = 1,0,1);
	ТабличноеПоле.ОбновитьСтроки();
КонецПроцедуры

//======================================================================
Процедура ЗаписатьПлощадки()
	Спр = СоздатьОбъект("Справочник.ВСД_Площадка");
	СпрХС = СоздатьОбъект("Справочник.ВСД_ХозСубъект");

	тзПлощадки.ВыбратьСтроки();
	Пока тзПлощадки.ПолучитьСтроку()=1 Цикл
		Если тзПлощадки.Пометка = 1 Тогда

			Если Спр.НайтиПоРеквизиту("GUID",тзПлощадки.GUID,1)=0 Тогда
				Спр.Новый();
				Спр.УстановитьНовыйКод("00");
			КонецЕсли;
			Спр.Наименование = тзПлощадки.Наименование;
			Спр.Адрес = тзПлощадки.Адрес;
			Спр.GUID = тзПлощадки.guid;
			Спр.UUID = тзПлощадки.uuid;
			
			//Обновим ссылку = иногда сслыка ссылается на старый объект
			СпрХС.НайтиЭлемент(тзПлощадки.ХозСубъект);
			Спр.GuidХозСубъекта = СпрХС.Guid;

			Спр.Записать();
			Сообщить("Загружена площадка ["+Спр.Наименование+"] GUID = "+ Спр.guid,"i");
		КонецЕсли;
	КонецЦикла;

КонецПроцедуры

Функция НазваниеПлощадки( ВыбСтрока )
	Ответ = Сокрлп(ВыбСтрока);
	Ответ = СтрЗаменить(Ответ,"ОБЩЕСТВО С ОГРАНИЧЕННОЙ ОТВЕТСТВЕННОСТЬЮ", "");
	Ответ = СтрЗаменить(Ответ,"ООО", "");
	Ответ = СтрЗаменить(Ответ , "ИП","");
	
	Возврат Ответ;
КонецФункции

//======================================================================
Процедура ПриИзмененииХС()     
	
	Наименование = НазваниеПлощадки( ГМ.ПолучитьНаименованиеКлиента( ВыбХС.Контрагент ));
	
КонецПроцедуры
	
//======================================================================
Процедура ПоКнопкеПоиск()
	//Если ПустоеЗначение(Регион) = 1 Тогда
	//	Предупреждение("Не выбран регион!"); Возврат;
	//КонецЕсли;                                               
	
	Если ПустаяСтрока(Наименование) = 1 Тогда
		Предупреждение("Не заполнено наименование для поиска!"); Возврат;
	КонецЕсли;
	                                              
	ВремТЗ = ГМ2.ПоискПлощадок( СокрЛП(Наименование), Регион, ВыбХС);
	тзПлощадки.УдалитьСтроки();
	
	Для СЦ = 1 По ВремТЗ.КоличествоСтрок() Цикл
		тзПлощадки.НоваяСтрока();
		Для СК = 1 По тзПлощадки.КоличествоКолонок() Цикл   
			идКолонки = тзПлощадки.ПолучитьПараметрыКолонки(СК);
			Попытка
				ВремЗнач = ВремТЗ.ПолучитьЗначение(СЦ,идКолонки);
				тзПлощадки.УстановитьЗначение(СЦ, идКолонки, ВремЗнач);
			Исключение
			КонецПопытки;
		КонецЦикла;
		
		Если тзПлощадки.Активна = "true" Тогда 
			тзПлощадки.Пометка = 1;
		КонецЕсли;
	КонецЦикла;
	
	тпПлощадки.ОбновитьСтроки();
КонецПроцедуры // ПоКнопкеПоиск

//======================================================================
Процедура ПриОткрытии()
	Если ТипЗначенияСтр(Форма.Параметр) = "СписокЗначений" Тогда

		ВыбХС = Форма.Параметр.Получить("ХозСубъект"); 
		Наименование = НазваниеПлощадки( ВыбХС.Наименование );
		
	  	Команда = Форма.Параметр.Получить("Команда");
      	Регион = Форма.Параметр.Получить("Регион");
	  	
	  	Если Команда = "ЗагрузитьПлощадки" Тогда 
	  		тзПлощадки = ГМ2.ПоискПлощадок(Наименование, Регион, ВыбХС);
	  	КонецЕсли;

	КонецЕсли;

   //Интеграционный модуль для переопределения функций, плюс несколько базовых функций
	ГМ._ПриОткрытии(Контекст);
	ГМ2.Инициализация(ГМ);
	//ГМ.ЗагрузитьПараметрыВФорму(Контекст);

	Регион ="";
	
	Если Форма.МодальныйРежим() = 0 Тогда
		оПривязки.Привязка("тпПлощадки", "H", "Форма", "W", "Форма");
		оПривязки.Привязка("кнДобавить, кнНайти","T","Форма");		
		оПривязки.Привязка("УсловияПоиска,ВыбХС,Регион,Наименование", "", "Форма", "W", "Форма");
		оПривязки.Привязка("Версия","T","Форма","L","Форма");		
	КонецЕсли;

КонецПроцедуры

//======================================================================
Процедура ПослеСозданияФормы()

	тпПлощадки = ГМ.СоздатьТабличноеПоле(Контекст, "тпПлощадки", тзПлощадки,1,1);
	
КонецПроцедуры

//======================================================================
Процедура ПослеОткрытия()
	ГМ._ПослеОткрытия(Контекст);
КонецПроцедуры

//======================================================================
Процедура ПриИзмененииРазмераОкна(ТипСобытия, НовШирина, НовВысота) Экспорт
	ГМ._ПриИзмененииРазмераОкна(Контекст, ТипСобытия, НовШирина, НовВысота);
КонецПроцедуры

//========================= Список изменений ===========================
Процедура ПриНажатииЛевойКнопки(Сост, х, у)
	
	ФормаРасш = СоздатьОбъект("РасширениеФормы");
	Атр = ФормаРасш.ПолучитьАтрибутПоКоординатам(х,у);
	Если ТипЗначенияСтр(Атр) = "АтрибутФормы" Тогда
		Если Атр.Идентификатор = "Версия" Тогда
			ЗапуститьПриложение("https://redmine.kb99.pro/issues/1529#note-18");
		ИначеЕсли Атр.Идентификатор = "сайт" Тогда			
			ЗапуститьПриложение("http://info.xn----ctbjbnchgq5bbglv.xn--p1ai/");
		КонецЕсли;  
	КонецЕсли;	
	
КонецПроцедуры

//======================================================================


тзПлощадки = СоздатьОбъект("ТаблицаЗначений");
тзПлощадки.НоваяКолонка("Пометка","Число",,,"V",3);
тзПлощадки.НоваяКолонка("Активна",,,,"Активна",5);
тзПлощадки.НоваяКолонка("Статус",,,,"Статус",10);
тзПлощадки.НоваяКолонка("Наименование","Строка",,,,15);
тзПлощадки.НоваяКолонка("Адрес",,,,,15);
тзПлощадки.НоваяКолонка("GUID","Строка",36,,,10);
тзПлощадки.НоваяКолонка("UUID","Строка",36,,,10);
тзПлощадки.НоваяКолонка("ХозСубъект",,,,"Хоз. субъект",15);
тзПлощадки.НоваяКолонка("Площадка",,,,"Площадка",15);
тзПлощадки.НоваяКолонка("ДатаСоздания",,,,"Дата создания",15);
//тзПлощадки.НоваяКолонка("ДатаИзменения",,,,"Дата изменения",15);
