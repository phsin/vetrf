Перем Сервис;

//******************************************************************************
// глЕстьРеквизитШапки(ИмяРеквизита, ВидДокумента)
//
// Параметры:
//  ИмяРеквизита - имя реквизита (строка)
//  ВидДокумента - вид документа (строка)
//
// Возвращаемое значение:
//  0 - нет реквизита,
//  1 - есть реквизит
//
// Описание:
//  Проверяет, есть ли в структуре метаданных документа указанного вида
// 	реквизит шапки с указанным названием
//
Функция глЕстьРеквизитШапки(ИмяРеквизита, ВидДокумента) Экспорт


	Если ПустоеЗначение(ИмяРеквизита) = 1 Тогда
		Возврат 0;

	ИначеЕсли Метаданные.ОбщийРеквизитДокумента(ИмяРеквизита).Выбран() = 1 Тогда
		Возврат 1;

	ИначеЕсли ПустоеЗначение(ВидДокумента) = 1 Тогда
		Возврат 0;

	ИначеЕсли Метаданные.Документ(ВидДокумента).РеквизитШапки(ИмяРеквизита).Выбран() = 1 Тогда
		Возврат 1;

	Иначе
		Возврат 0;
	КонецЕсли;

КонецФункции //глЕстьРеквизитШапки()
//******************************************************************************
// глНомерНаПечать(Документ, НеПечататьВходящийНомер = 0) Экспорт
//
// Параметры:
//  Документ                - документ, чей номер надо получить для печати.
//  НеПечататьВходящийНомер - Число, 0 - печатем входящий номер, если он есть,
//                                   1 - независимо ни от чего будем печатать номер документа
//
// Возвращаемое значение.
//  Строка - номер документа для печати
//
// Описание:
//  Процедура предназначена для преобразования номера документа.
//  Используется, например, при выводе номера документа на печать.
//
Функция глНомерНаПечать(Документ, НеПечататьВходящийНомер = 0) Экспорт

	Номер = "";

	Если ПустоеЗначение(Документ)= 0 Тогда
		// Если документ имеет входящий номер, то печатать будем его.
		Если (глЕстьРеквизитШапки("НомерДокВходящий", Документ.Вид()) = 1)
		   и (НеПечататьВходящийНомер = 0) Тогда
			Номер=СокрЛП(Документ.НомерДокВходящий);
		КонецЕсли;

		ВидДокумента = Документ.Вид();
		// Если входящий номер пуст или его вообще нет, то печатаем номер документа
		Если ВидДокумента = "СчетФактураВыданный" Тогда
			Номер=СокрЛП(Документ.НомерДок);
			Номер=Сред(Номер, 2);
			// в документах, перенессеных из старых редакций, может остаться "минус" впереди
			Если Лев(Номер, 1) = "-" Тогда
				Номер	= Сред(Номер, 2);
			КонецЕсли;

			// удаление ведущих нулей
			Пока Лев(Номер, 1)="0" Цикл
				Номер=Сред(Номер, 2);
			КонецЦикла;
		ИначеЕсли ПустоеЗначение(Номер) = 1 Тогда
			Номер=СокрЛП(Документ.НомерДок);

			// назначенный документу префикс
			Попытка
				Префикс=СокрЛП(Константа.ПрефиксИБ);//+ СокрЛП(Документ.Фирма.ЮрЛицо.ПрефиксНомеровДокументов);
			Исключение
				Префикс="";
			КонецПопытки;

			// удаление префикса из номера документа
			Если Найти(Номер, Префикс)=1 Тогда
				Номер=Сред(Номер, СтрДлина(Префикс)+1);
			КонецЕсли;

			// в документах, перенессеных из старых редакций, может остаться "минус" впереди
			Если Лев(Номер, 1) = "-" Тогда
				Номер	= Сред(Номер, 2);
			КонецЕсли;

			// удаление ведущих нулей
			Пока Лев(Номер, 1)="0" Цикл
				Номер=Сред(Номер, 2);
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;

	Возврат Номер;
КонецФункции // глНомерНаПечать()

Процедура ВывестиВСД(Таб, ВСД, Столбик, Ряд)

	Если ПустоеЗначение(ВСД.UUID)=1 Тогда
		Сообщить("Пропущен "+ВСД+" - "+ВСД.СтатусВСД+" - UUID ["+ВСД.UUID+"]","!");
		Возврат;
	КонецЕсли;

	Если НЕ( ( ВСД.СтатусВСД = Перечисление.ВСД_СтатусВСД.CONFIRMED) или ( ВСД.СтатусВСД = Перечисление.ВСД_СтатусВСД.UTILIZED) )  Тогда
		Сообщить("Пропущен "+ВСД+" - "+ВСД.СтатусВСД+" - UUID ["+ВСД.UUID+"]","!");
	    Возврат;
	КонецЕсли;

	Столбик	= ?(Столбик = 1, 0      , Столбик + 1);
	Ряд		= ?(Столбик = 0, Ряд + 1, Ряд);

	Если Ряд > 6 Тогда
 		Таб.НоваяСтраница();
 		Ряд = 1;
 	КонецЕсли;
 	
	тКод = ВСД.UUID;
	шк = "http://mercury.vetrf.ru/pub/operatorui?_language=ru&_action=showVetDocumentFormByUuid&uuid="+ СокрЛП(ВСД.UUID);
	тСгенерировано = "Сгенерировано '1С 7.7 http://vetis.kb99.pro";
	тВремя = СокрЛП(ТекущееВремя());  
	тВремя = СокрЛП(ТекущаяДата())+" "+тВремя;  
	Автор = ГМ.ПолучитьАвтора();
	тПродукция = СокрЛП(ВСД.НаименованиеПродукции)+"; "+ВСД.Количество+" "+ВСД.ЕдиницаИзмерения;

	тВыработано = " Выработана: ";
	Если ПустоеЗначение(ВСД.ДатаИзготовления1)=0 Тогда
		тВыработано = тВыработано + СокрЛП(ВСД.ДатаИзготовления1);
		Если ПустоеЗначение(ВСД.ДатаИзготовления2)=0 Тогда
			тВыработано=тВыработано+ "-" + СокрЛП(ВСД.ДатаИзготовления2);
		КонецЕсли;
	Иначе
		тВыработано = тВыработано + СокрЛП(ВСД.ДатаИзготовления);
	КонецЕсли;

	Попытка
		тПолучатель = " Получатель: "+ГМ.ПолучитьНаименованиеКлиента(ВСД.Получатель_ХозСубъект.Контрагент)+", ИНН: "+
			ГМ.ПолучитьИНН(ВСД.Получатель_ХозСубъект.Контрагент);
	Исключение
		тПолучатель ="";
	КонецПопытки;
	Попытка
		тПлощадка = " ("+ГМ.ПолучитьНаименованиеКлиента(ВСД.Получатель_Площадка.Контрагент)+" - "+СокрЛП(ВСД.Получатель_Площадка.Адрес)+")";
	Исключение
		тПлощадка = " ("+СокрЛП(ВСД.Получатель_Площадка)+" - "+СокрЛП(ВСД.Получатель_Площадка.Адрес)+")";
	КонецПопытки;
	
	ТелоВСД =ГМ.ПолучитьНаименованиеКлиента(ВСД.Отправитель_ХозСубъект.Контрагент)+", ИНН:"+
		ГМ.ПолучитьИНН(ВСД.Отправитель_ХозСубъект.Контрагент)+//", ТТН: № "+СокрЛП(ВСД.ТТНномер)+" от "+ВСД.ТТНДата+РазделительСтрок+
		тПолучатель+тПлощадка;//+РазделительСтрок+тПродукция+тВыработано;

	Если Столбик = 0 Тогда
 		Таб.ВывестиСекцию("ВСДМини|Тело");
 	Иначе
 		Таб.ПрисоединитьСекцию("ВСДМини|Тело");
 	КонецЕсли;

КонецПроцедуры
   
Процедура ПечатьВСДМини(Докум, Устройство=0, КолвоКопий=0, СразуНаПринтер=0)

	Если Докум.Вид() = "ВСД2_транзакция" Тогда
		НомерДок =  глНомерНаПечать(Докум.ДокОснование);
	ИначеЕсли Докум.Вид() = "ВСД2" Тогда
		НомерДок = глНомерНаПечать(Докум.ДокОснование.ДокОснование);//.НомерДок;
	Иначе
		НомерДок = глНомерНаПечать(Докум);//.НомерДок;
	КонецЕсли;

	Таб = СоздатьОбъект("Таблица");
	Таб.ИсходнаяТаблица("Таблица");
	Таб.ВывестиСекцию("Сек_1");
	
	Столбик 		= 1;
 	Ряд     		= 0;

 	Если Докум.Вид() = "ВСД2" Тогда
		ВывестиВСД(Таб, Докум, Столбик, Ряд);
   	Иначе
		Доки =  СоздатьОбъект("Документ");
		Доки.ВыбратьПодчиненныеДокументы( НачГода(Докум.ДатаДок), КонГода(ТекущаяДата()), Докум);
		Пока Доки.ПолучитьДокумент() = 1 Цикл
			Если (Доки.Вид() = "ВСД") или (Доки.Вид() = "ВСД2") Тогда

				Если ГМ.ПолучитьКонстанту("ВыводитьПодробнуюИнформацию")=1 Тогда 
					Сообщить("Найден "+Доки.ТекущийДокумент()+" "+Доки.Статус);
				КонецЕсли;
				ВывестиВСД(Таб, Доки.ТекущийДокумент(), Столбик, Ряд);

			Иначе

				Док2 = СоздатьОбъект("Документ");
				Док2.ВыбратьПодчиненныеДокументы( НачГода(Доки.ДатаДок), КонГода(Доки.ДатаДок), Доки.ТекущийДокумент());
				Пока Док2.ПолучитьДокумент() = 1 Цикл
					Если (Док2.Вид() = "ВСД") или (Док2.Вид() = "ВСД2") Тогда

						Сообщить("Найден "+Док2.ТекущийДокумент()+" "+Док2.Статус);
						ВывестиВСД(Таб, Док2.ТекущийДокумент(), Столбик, Ряд);
						
					КонецЕсли;
				КонецЦикла;
	   		КонецЕсли;
	   	КонецЦикла;
	КонецЕсли;

   	Таб.ТолькоПросмотр(1);
    таб.Повторятьприпечатистроки(1,3);
    таб.ПараметрыСтраницы(1,100,,0,0,0,0,,,0,, Устройство);

	Если СразуНаПринтер = 0 Тогда
		Таб.Опции(0,0,0,0,"ВСДМини");//"ОпцииПечатиРеализация"
		Таб.Показать("ВСД_"+СокрЛП(Докум.НомерДок),"");
	Иначе
		Таб.Напечатать(0);
	КонецЕсли;

КонецПроцедуры         


//******************************************************************************
// ПоКнопкеПечать()
//
// Параметры:
//  Нет
//
// Возвращаемое значение:
//  Нет
//
// Описание:
//
Процедура ПоКнопкеПечать()

	Если Док.Выбран() = 0 Тогда
	    Предупреждение("Не выбран документ!", 60);
		Возврат;
	КонецЕсли;

	ПечатьВСДМини(Док);
   	
КонецПроцедуры // ПоКнопкеПечать()

//******************************************************************************
// Предопределенная процедура
//
Процедура ПриОткрытии()       
	Сервис = СоздатьОбъект("Сервис");
	
	Устройство = 0;
	КолвоКопий = 0;
	СразуНаПринтер = 0;
	Если ПустоеЗначение(Форма.Параметр) = 0 Тогда
		Если ТипЗначенияСтр(Форма.Параметр) = "СписокЗначений" Тогда
			Докум      = Форма.Параметр.Получить("Контекст");
			Устройство = Форма.Параметр.Получить("Устройство");
			КолвоКопий = Форма.Параметр.Получить("КоличествоКопий");
			СразуНаПринтер = Число(Форма.Параметр.Получить("СразуПечать"));
		ИначеЕсли ТипЗначенияСтр(Форма.Параметр) = "Документ" Тогда
			Докум = Форма.Параметр;
		КонецЕсли;
		ПечатьВСДМини(Докум, Устройство, КолвоКопий, СразуНаПринтер);
		Статусвозврата(0);
		Возврат;
	КонецЕсли;

КонецПроцедуры // ПриОткрытии()

Функция ШК(Объект, код)
	Сервис.НарисоватьШтрихкод(Объект, 58, код);
КонецФункции // ТестВыводаКартинки
//*******************************************
