Перем базаДанных;
Перем ЭтоSQL;

Перем цвКрасный, цвЖелтый, цвЗеленый, цвГолубой;

Перем КаталогОбработки, ИмяФайлаНастроек;

перем ПартияИнфо;
Перем ВыбСтрока;
Перем ВерсияСистемы;

Перем ОбработкаОбмена;

//*******************************************
Функция НайтиХозСубъект(ВыбКлиент)
	
	Попытка		
		Если ПустоеЗначение(ВыбКлиент.Плательщик) = 0 Тогда 
			ВыбКлиент = ВыбКлиент.Плательщик;
		КонецЕсли;
	Исключение
	КонецПопытки;
	
	
	Если ЭтоSQL=1 Тогда 	
		RS = СоздатьОбъект("ODBCRecordset");
		RS.УстБД1С();     

		ТекстЗапроса = "select 
		|	СпрХС.ID [id $Справочник.ВСД_ХозСубъект],
		|	СпрХС.descr,
		|	$СпрХС.GUID,
		|	$СпрХС.Контрагент
		|from $Справочник.ВСД_ХозСубъект as СпрХС 
		|where 
		|	$СпрХС.Контрагент=:ВыбКлиент
		|";
		RS.УстановитьТекстовыйПараметр("ВыбКлиент", ВыбКлиент);  
		//RS.Отладка(1);
		тз = RS.ВыполнитьИнструкцию(ТекстЗапроса);			
	  	
	Иначе	
	
		Запрос = базаДанных.НовыйЗапрос();
	
		ТекстЗапроса = "select 
		|	СпрХС.ID [id $Справочник.ВСД_ХозСубъект],
		|	СпрХС.descr,
		|	СпрХС.GUID,
		|	СпрХС.Контрагент
		|from [Справочник.ВСД_ХозСубъект] as СпрХС 
		|where 
		|	СпрХС.Контрагент=:ВыбКлиент
		|";

		Запрос.Подставлять("ВыбКлиент", ВыбКлиент);
	
		//тз.Отладка();
		ТЗ = Запрос.ВыполнитьЗапрос(ТекстЗапроса);
		//ТЗ.ВыбратьСтроку();
	КонецЕсли;
	
	Тз.ВыбратьСтроки();
	Если ТЗ.ПолучитьСТроку()=1 Тогда
		результат = тз.id;
	Иначе
		СпрХС = СоздатьОбъект("Справочник.ВСД_ХозСубъект");
		СпрХС.Новый();
		СпрХС.Наименование = ВыбКлиент.Наименование;
		СпрХС.Контрагент = ВыбКлиент;
		СпрХС.Записать();
		Сообщить("Создан новый элемент справочника ВСД_ХозСубъект: "+ВыбКлиент);
		
		результат = спрХС.ТекущийЭлемент();
	КонецЕсли;
	
	Возврат результат;
КонецФункции

//*******************************************
Функция НайтиПлощадку(ВыбКлиент)
	результат = "";
	
	Если ЭтоSQL=1 Тогда 	
		RS = СоздатьОбъект("ODBCRecordset");
		RS.УстБД1С();     

		ТекстЗапроса = "select 
		|	СпрПл.ID [id $Справочник.ВСД_Площадка],
		|	СпрПл.descr,
		|	$СпрПл.GUID,
		|	$СпрПл.Контрагент
		|from $Справочник.ВСД_Площадка as СпрПл 
		|where 
		|	$СпрПл.Контрагент=:ВыбКлиент
		|";
		RS.УстановитьТекстовыйПараметр("ВыбКлиент", ВыбКлиент);  
		//RS.Отладка(1);
		тз = RS.ВыполнитьИнструкцию(ТекстЗапроса);			
	  	
	Иначе	
		Запрос = базаДанных.НовыйЗапрос();
	
		ТекстЗапроса = "select 
		|	СпрПл.ID [id $Справочник.ВСД_Площадка],
		|	СпрПл.descr,
		|	СпрПл.GUID,
		|	СпрПл.Контрагент
		|from [Справочник.ВСД_Площадка] as СпрПл 
		|where 
		|	СпрПл.Контрагент=:ВыбКлиент
		|";
		
		Запрос.Подставлять("ВыбКлиент", ВыбКлиент);
	
		//тз.Отладка();
		ТЗ = Запрос.ВыполнитьЗапрос(ТекстЗапроса);
		//ТЗ.ВыбратьСтроку();
	КонецЕсли;
	
	Тз.ВыбратьСтроки();
	Если ТЗ.ПолучитьСТроку()=1 Тогда
		результат = тз.id;
	КонецЕсли;
	
	Возврат результат;
КонецФункции

Функция НайтиПлощадкуПоСкладу(ВыбСклад)
	Рез = "";
	если ПустоеЗначение(ВыбСклад)=0 Тогда			
		Спр = СоздатьОбъект("Справочник.ВСД_Площадка");
		Если Спр.НайтиПоРеквизиту("Склад",ВыбСклад,1)=1 Тогда 
			Рез = Спр.ТекущийЭлемент();			
		Иначе
			Сообщить("Площадка со складом ["+ВыбСклад+"] не найдена: ","!");
			//создать площадку

			//ЗагрузитьПлощадкуПоGUID(guid);
			
		КонецЕсли;
	Иначе
		Сообщить("не указан склад Площадки","!");
	КонецЕсли;
		
	Возврат рез;
КонецФункции

//*******************************************
Процедура ОбновитьИнф()
	
	Форма.Инфо.Заголовок("Колво документов = "+СписокДокументов.КоличествоСтрок()+" Общий вес = "+СписокДокументов.Итог("Вес"));
	
КонецПроцедуры

Процедура ЗаголовокНадпись()
	
	Если ВерсияСистемы=2 Тогда 
		стр = "Меркурий - РАБОЧАЯ ";
	Иначе
		стр = "Меркурий - ТЕСТ[!!!] "
	КонецЕсли;
	форма.Заголовок(стр+"     ("+НачДата+" - "+КонДата+")",1);
	форма.НачалоПериодаТекст.Заголовок(СокрЛП(НачДата));
	форма.КонецПериодаТекст.Заголовок(сокрлп(КонДата));

КонецПроцедуры // ЗаголовокНадпись


Процедура ЗаполнитьПараметрыСписок()
	Если фс.СуществуетФайл(ИмяФайлаНастроек)=0 Тогда
		Сообщить("Файл с константами ["+ИмяФайлаНастроек+"] не найден","!");
		Возврат;
	Иначе
		СписокКонстант=СоздатьОбъект("СписокЗначений");
		ЗначениеИзФайла(ИмяФайлаНастроек,СписокКонстант);
	КонецЕсли;

	Отправитель_ХозСубъект = СписокКонстант.Получить("Отправитель_ХозСубъект");	
	Отправитель_Площадка = СписокКонстант.Получить("Отправитель_Площадка");

	ВСД_Экспертиза 	= СписокКонстант.Получить("ВСД_Экспертиза");	
	Если ПустоеЗначение(ВСД_Экспертиза)=1 Тогда		
		ВСД_Экспертиза 	= "Произ.лабор ";
	КонецЕсли;
	
	ВСД_Местность 	= СписокКонстант.Получить("ВСД_Местность");
	Если ПустоеЗначение(ВСД_Местность)=1  Тогда 
		ВСД_Местность 	= "местность благополучная по остро инфекционным заболеваниям с\х животных и птиц";
	КонецЕсли;

	ВСД_ОсобыеОтметки = СписокКонстант.Получить("ВСД_ОсобыеОтметки");
	
	
	НазваниеРеквизитаГрузополучатель  = СписокКонстант.Получить("НазваниеРеквизитаГрузополучатель");
	Если ПустоеЗначение(НазваниеРеквизитаГрузополучатель)=1 Тогда
		НазваниеРеквизитаГрузополучатель="Контрагент";
	КонецЕсли;

	Перевозчик_ХозСубъект = Отправитель_ХозСубъект;
	
	ВыбФирма = глПользователь.ОсновнаяФирма;
КонецПроцедуры


Процедура РаскраситьСписокДокументов()
	СписокДокументов.ВыбратьСтроки();
	Пока СписокДокументов.ПолучитьСтроку() = 1 Цикл
		СписокДокументов.Площадка = НайтиПлощадку(СписокДокументов.Контрагент);

		СписокДокументов.инфо = 35;
		Если ПустоеЗначение(СписокДокументов.ХозСубъект.GUID)=1 Тогда 			
			СписокДокументов.сЦвет = цвКрасный;
		ИначеЕсли (ПустоеЗначение(СписокДокументов.ВСД)=0) Тогда
			Если СокрЛП(СписокДокументов.ВСД.Статус)="COMPLETED" Тогда
				СписокДокументов.инфо = 36;
				СписокДокументов.сЦвет = цвЗеленый;
			КонецЕсли;
		ИначеЕсли (ПустоеЗначение(СписокДокументов.Площадка)=0) Тогда
			Если (ПустоеЗначение(СписокДокументов.Площадка.GUID)=0)  Тогда 
				СписокДокументов.инфо = 36;
				СписокДокументов.сЦвет = "";							
			КонецЕсли;
		Иначе			
			СписокДокументов.сЦвет = цвЖелтый;							
		КонецЕсли;
		
	КонецЦикла;
КонецПроцедуры

Функция ВыбратьВсеПлощадкиХС(ХС)
	Запрос = СоздатьОбъект("Запрос");
	ТекстЗапроса = "
	|id = Справочник.ВСД_Площадка.ТекущийЭлемент;
	|GuidХозСубъекта = Справочник.ВСД_Площадка.GuidХозСубъекта;
	|Условие (GuidХозСубъекта = ВыбGuidХозСубъекта);
	|Группировка id;
	|Без Итогов;
	|";
	
	ВыбGuidХозСубъекта = (ХС.GUID);
	Запрос.Выполнить(ТекстЗапроса);
	тз =СоздатьОбъект("ТаблицаЗначений");
	Запрос.Выгрузить(тз);
	//ТЗ.ВыбратьСтроку();	
	
	Возврат тз;
	
КонецФункции

Процедура ОбработкаПодбора(Элемент, КонтФормы)
	Если Элемент.Вид()="ВСД_Площадка" Тогда
		СтараяПлощадка = СписокДокументов.ПолучитьЗначение(ВыбСтрока,"Площадка");
		
		СпрПлощадка = СоздатьОбъект("Справочник.ВСД_Площадка");
		Если ПустоеЗначение(СтараяПлощадка)=0 Тогда 
			Если СпрПлощадка.ВыбратьЭлементыПоРеквизиту("Контрагент",СтараяПлощадка.Контрагент,0,0)=1 Тогда
				Пока СпрПлощадка.ПолучитьЭлемент()=1 Цикл
					//Сообщить("Очистили привязку площадки "+СпрПлощадка.Контрагент);
					СпрПлощадка.Контрагент = "";
					СпрПлощадка.Записать();				
				КонецЦикла;
			КонецЕсли;
		КонецЕсли;
		
		СпрПлощадка.НайтиЭлемент(Элемент);
		СпрПлощадка.Контрагент = СписокДокументов.ПолучитьЗначение(ВыбСтрока,"Грузополучатель");
		СпрПлощадка.Записать();
		//Сообщить("Установили привязку площадки "+СпрПлощадка.Контрагент);
		КонтФормы.Форма.Закрыть();
		
		РаскраситьСписокДокументов();		
		
	КонецЕсли;
	
КонецПроцедуры

//*******************************************
Процедура Клик()
	текСтр = СписокДокументов.ТекущаяСтрока();
	текКол = СписокДокументов.ТекущаяКолонка();
	
	Если текКол="Пометка" Тогда		
		Пометка = СписокДокументов.ПолучитьЗначение(ТекСтр,"Пометка");
		Если Пометка = 2 Тогда
			СписокДокументов.Пометка=1;
		Иначе
			СписокДокументов.Пометка=2;
		КонецЕсли;
	ИначеЕсли текКол="Площадка" Тогда 
		ВыбСтрока = текСтр;
		Площадка = СписокДокументов.ПолучитьЗначение(ТекСтр,текКол);
		
		ХозСубъект = СписокДокументов.ПолучитьЗначение(текСтр, "ХозСубъект");
					
		тз = ВыбратьВсеПлощадкиХС(ХозСубъект);
		
		СписокОтбора = СоздатьОбъект("СписокЗначений");
		
		Тз.ВыбратьСтроки();
		Пока ТЗ.ПолучитьСТроку()=1 Цикл
			СписокОтбора.ДобавитьЗначение(тз.id);
		КонецЦикла;			
				
		ОткрытьПодбор("Справочник.ВСД_Площадка", "ФормаСписка", СписокОтбора,0, Площадка);
		
	ИначеЕсли текКол="ХозСубъект" Тогда 
		
		ХозСубъект = СписокДокументов.ПолучитьЗначение(текСтр, "ХозСубъект");	
		Если ПустоеЗначение(ХозСубъект)=1 Тогда 
			ОткрытьФорму("Справочник.ВСД_ХозСубъект");
		Иначе
			ОткрытьФорму(ХозСубъект);
		КонецЕсли;
		
	Иначе
		Эл = СписокДокументов.ПолучитьЗначение(текСтр, текКол);	
		ОткрытьФорму(Эл);
	КонецЕсли;
	
	//ОбновитьНадписи();	
КонецПроцедуры

Процедура ЗаполнитьСписокДокументов_ПеремещенийТМЦ()
	Если ЭтоSQL=1 Тогда 	
		RS = СоздатьОбъект("ODBCRecordset");
		RS.УстБД1С();     
		
		ТекстЗапроса = "
		|SELECT
		|    Жур.IDDoc as [Док $Документ],
		|    Жур.IDDocDef as Док_вид,
		|	ДокВСД.IDDoc as [ДокВСД $Документ.ВСД_исходящий],
		|	Жур.DocNo as [НомерДок],
		|	$ДокР.Склад as [Склад $Справочник.Склады],
		|	$ДокР.СкладПолучатель as [СкладПолучатель $Справочник.Склады],
		|	SUM($ДокСтроки.Количество) [Количество],
		//|	SUM($ДокСтроки.КоличествоМест) [КоличествоМест],
		|	СпрЗн.descr as Свво
		|FROM
		|    _1SJourn Жур
		|INNER JOIN 
		|	$ДокументСтроки.ПеремещениеТМЦ as ДокСтроки 
		|		ON Жур.IDDoc = ДокСтроки.IDDoc
		|FULL JOIN 
		|	$Справочник.СвойстваНоменклатуры as СпрСв 
		|		ON СпрСв.ParentExt = $ДокСтроки.Номенклатура
		|			 and $СпрСв.ВидСвойства = :ВыбВидСвойств 		
		|FULL JOIN 
		|	$Справочник.ЗначенияСвойств as СпрЗн 
		|		ON СпрЗн.ID = $СпрСв.ЗначениеСвойства 
		|			and  СпрЗн.ParentExt = :ВыбВидСвойств 
		|INNER JOIN 
		|	$Документ.ПеремещениеТМЦ as ДокР 
		|		ON Жур.IDDoc = ДокР.IDDoc
		|Left join 
		|	$Документ.ВСД_исходящий as ДокВСД
		|	ON SUBSTRING($ДокВСД.ДокОснование, 5, 8) = Жур.IDDoc
	//	|		AND $ДокВСД.Статус = 'COMPLETED'
		|WHERE
		|    Жур.Date_Time_IDDoc BETWEEN :НачДата AND :КонДата~ AND
		|    Жур.IDDocDef = $ВидДокумента.ПеремещениеТМЦ
		|	and Жур.Closed & 1 = 1
		//|	%УсловиеПоГрузополучателю%
		|	%УсловиеПоСвойствуТовара%
		|	%УсловиеПоФирме%
		|	%УсловиеПоФилиалу%
		|
		|GROUP BY
		|	Жур.IDDoc,
		|	Жур.IDDocDef,
		|	ДокВСД.IDDoc,
		|	Жур.DocNo,
		|	$ДокР.Склад,
		|	$ДокР.СкладПолучатель,
		|	СпрЗн.descr
		|order by Жур.DocNo
		|";
	//"+НазваниеРеквизитаГрузополучатель+",
		
		УсловиеПоСвойствуТовара="";
		Если ПустоеЗначение(ВыбТипПродукции) = 0 Тогда		
			УсловиеПоСвойствуТовара = "and СпрЗн.ID = :ВыбТипПродукции";
		КонецЕсли;
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "%УсловиеПоСвойствуТовара%", УсловиеПоСвойствуТовара);
		
		//УсловиеПоГрузополучателю = "";
		//Если ПустоеЗначение(ВыбКонтрагент) = 0 Тогда		
		//	УсловиеПоГрузополучателю = " and $ДокР."+НазваниеРеквизитаГрузополучатель+" IN (SELECT Val FROM #ГруппаК) ";
		//КонецЕсли;
		//ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "%УсловиеПоГрузополучателю%", УсловиеПоГрузополучателю);
		
		//условие по фирме
		УсловиеПоФирме="";
		Если ПустоеЗначение(ВыбФирма) = 0 Тогда		
			УсловиеПоФирме = " and Жур.$ОбщийРеквизит.Фирма = :ВыбФирма";
		КонецЕсли;
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "%УсловиеПоФирме%", УсловиеПоФирме);

		//условие по филиалу
		Если ПустоеЗначение(глПользователь.Филиал) = 0 Тогда		
			УсловиеПоФилиалу = " and Жур.$ОбщийРеквизит.Филиал = :ВыбФилиал ";
		КонецЕсли;
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "%УсловиеПоФилиалу%", УсловиеПоФилиалу);

		
		RS.УложитьСписокОбъектов(ВыбКонтрагент, "#ГруппаК","Контрагенты");
		RS.УстановитьТекстовыйПараметр("ВыбВидСвойств", ВыбВидСвойств);    
		RS.УстановитьТекстовыйПараметр("ВыбТипПродукции", ВыбТипПродукции);    
		RS.УстановитьТекстовыйПараметр("НачДата", НачДата);
		RS.УстановитьТекстовыйПараметр("КонДата", КонДата);
		RS.УстановитьТекстовыйПараметр("ВыбФилиал", глПользователь.Филиал);
		RS.УстановитьТекстовыйПараметр("ВыбФирма", ВыбФирма);
		        
		//RS.Отладка(1);
		ТЗ = RS.ВыполнитьИнструкцию(ТекстЗапроса);
		//ТЗ.ВыбратьСтроку();
	  	
	Иначе	
			
		Запрос = базаДанных.НовыйЗапрос();
	
		ТекстЗапроса = "
		|SELECT
		|    Жур.IDDoc as [Док :Документ],
		|    Жур.IDDocDef as Док_вид,
		|	Жур.DocNo as [НомерДок],
		|	ДокВСД.IDDoc as [ДокВСД $Документ.ВСД_исходящий],
		|	ДокР."+НазваниеРеквизитаГрузополучатель+" as [Клиент :Справочник.Контрагенты],
		|	SUM(ДокСтроки.Количество) [Количество],
		//|	SUM(ДокСтроки.КоличествоМест) [КоличествоМест],
		|	СпрЗн.descr as Свво
		|FROM
		|    Журнал Жур
		|INNER JOIN 
		|	[ДокументСтроки.Реализация] as ДокСтроки 
		|		ON Жур.IDDoc = ДокСтроки.IDDoc
		|LEFT JOIN 
		|	[Справочник.СвойстваНоменклатуры] as СпрСв 
		|		ON СпрСв.ParentExt = ДокСтроки.Номенклатура
		|			 and СпрСв.ВидСвойства = :ВыбВидСвойств 		
		|LEFT JOIN 
		|	[Справочник.ЗначенияСвойств] as СпрЗн 
		|		ON СпрЗн.ID = СпрСв.ЗначениеСвойства 
		|			and  СпрЗн.ParentExt = :ВыбВидСвойств 
		|INNER JOIN 
		|	[Документ.Реализация] as ДокР 
		|		ON Жур.IDDoc = ДокР.IDDoc
		|Left join 
		|	[Документ.ВСД_исходящий] as ДокВСД
		|	ON SUBSTR(ДокВСД.ДокОснование, 5, 8) = Жур.IDDoc
		//|		AND ДокВСД.Статус = 'COMPLETED'	
		|WHERE
		|    Жур.idx_date_time_iddoc BETWEEN :НачДата AND :КонДата~ AND
		|    Жур.IDDocDef = :ВидДокумента.Реализация
		|	and Жур.Closed & 1 = 1
		|	%УсловиеПоГрузополучателю%
		|	%УсловиеПоСвойствуТовара%
		|	%УсловиеПоФирме%
		|
		|GROUP BY
		|	Жур.IDDoc,
		|	Жур.IDDocDef,
		|	Жур.DocNo,
		|	ДокР."+НазваниеРеквизитаГрузополучатель+",
		|	СпрЗн.descr
		|order by Жур.DocNo
		|";
	
		УсловиеПоГрузополучателю = "";
		УсловиеПоСвойствуТовара="";
		Если ПустоеЗначение(ВыбТипПродукции) = 0 Тогда		
			УсловиеПоСвойствуТовара = "and СпрЗн.ID = :ВыбТипПродукции";
		КонецЕсли;
		Если ПустоеЗначение(ВыбКонтрагент) = 0 Тогда		
			УсловиеПоГрузополучателю = " and ДокР."+НазваниеРеквизитаГрузополучатель+" IN (SELECT Val FROM ГруппаК) ";
			базаДанных.УложитьОбъекты(ВыбКонтрагент, "ГруппаК",, "Контрагенты");
		КонецЕсли;
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "%УсловиеПоГрузополучателю%", УсловиеПоГрузополучателю);
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "%УсловиеПоСвойствуТовара%", УсловиеПоСвойствуТовара);

		//условие по фирме
		УсловиеПоФирме="";
		Если ПустоеЗначение(ВыбФирма) = 0 Тогда		
			УсловиеПоФирме = " and Жур.Фирма = :ВыбФирма";
		КонецЕсли;
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "%УсловиеПоФирме%", УсловиеПоФирме);
		        
		//Запрос.Подставлять("ВыбКлиент", ВыбКлиент);
		Запрос.Подставлять("ВыбВидСвойств", ВыбВидСвойств);    
		Запрос.Подставлять("ВыбТипПродукции", ВыбТипПродукции);    
		Запрос.Подставлять("НачДата", НачДата);
		Запрос.Подставлять("КонДата", КонДата);
		Запрос.Подставлять("ВыбФирма", ВыбФирма);
	
		//Запрос.Отладка();
		ТЗ = Запрос.ВыполнитьЗапрос(ТекстЗапроса);
		//ТЗ.ВыбратьСтроку();
	КонецЕсли;
		
	СписокДокументов.УдалитьСтроки();
	
	тз.ВыбратьСтроки();
	Ном=0;
	ПредДок="";
	Пока тз.ПолучитьСтроку()=1 Цикл 
		СписокДокументов.НоваяСтрока();
		СписокДокументов.Пометка=1;
		СписокДокументов.Контрагент = тз.Склад;
		СписокДокументов.Грузополучатель = тз.СкладПолучатель;
		СписокДокументов.Док = тз.Док;
		СписокДокументов.Колво = тз.Количество;
		СписокДокументов.Вес = тз.Количество;
		Попытка			
			СписокДокументов.КолвоМест = тз.КоличествоМест;
		Исключение			
		КонецПопытки;
				
		//СписокДокументов.ХозСубъект = НайтиХозСубъект(тз.Склад);
		СписокДокументов.ХозСубъект = Отправитель_ХозСубъект;
		СписокДокументов.Площадка = НайтиПлощадкуПоСкладу(СписокДокументов.Грузополучатель);

		Попытка
			Если тз.ДокВСД.ПометкаУдаления()=0 Тогда 
				СписокДокументов.ВСД = тз.ДокВСД;
			КонецЕсли;
		Исключение
		КонецПопытки;
		
		СписокДокументов.инфо = 35;

	КонецЦикла; 
	    
	РаскраситьСписокДокументов();
	ОбновитьИнф();

КонецПроцедуры

//*******************************************
Процедура ЗаполнитьСписокДокументов_Реализации()  

	Если ЭтоSQL=1 Тогда 	
		RS = СоздатьОбъект("ODBCRecordset");
		RS.УстБД1С();     
		
		ТекстЗапроса = "
		|SELECT
		|    Жур.IDDoc as [Док $Документ],
		|    Жур.IDDocDef as Док_вид,
		|	ДокВСД.IDDoc as [ДокВСД $Документ.ВСД_исходящий],
		|	Жур.DocNo as [НомерДок],
		|	$ДокР."+НазваниеРеквизитаГрузополучатель+" as [Клиент $Справочник.Контрагенты],
		|	SUM($ДокСтроки.Количество) [Количество],
		|	SUM($ДокСтроки.КоличествоМест) [КоличествоМест],
		|	СпрЗн.descr as Свво
		|FROM
		|    _1SJourn Жур
		|INNER JOIN 
		|	$ДокументСтроки.Реализация as ДокСтроки 
		|		ON Жур.IDDoc = ДокСтроки.IDDoc
		|FULL JOIN 
		|	$Справочник.СвойстваНоменклатуры as СпрСв 
		|		ON СпрСв.ParentExt = $ДокСтроки.Номенклатура
		|			 and $СпрСв.ВидСвойства = :ВыбВидСвойств 		
		|FULL JOIN 
		|	$Справочник.ЗначенияСвойств as СпрЗн 
		|		ON СпрЗн.ID = $СпрСв.ЗначениеСвойства 
		|			and  СпрЗн.ParentExt = :ВыбВидСвойств 
		|INNER JOIN 
		|	$Документ.Реализация as ДокР 
		|		ON Жур.IDDoc = ДокР.IDDoc
		|Left join 
		|	$Документ.ВСД_исходящий as ДокВСД
		|	ON SUBSTRING($ДокВСД.ДокОснование, 5, 8) = Жур.IDDoc
	//	|		AND $ДокВСД.Статус = 'COMPLETED'
		|WHERE
		|    Жур.Date_Time_IDDoc BETWEEN :НачДата AND :КонДата~ AND
		|    Жур.IDDocDef = $ВидДокумента.Реализация
		|	and Жур.Closed & 1 = 1
		|	%УсловиеПоГрузополучателю%
		|	%УсловиеПоСвойствуТовара%
		|	%УсловиеПоФирме%
		|	%УсловиеПоФилиалу%
		|
		|GROUP BY
		|	Жур.IDDoc,
		|	Жур.IDDocDef,
		|	ДокВСД.IDDoc,
		|	Жур.DocNo,
		|	$ДокР."+НазваниеРеквизитаГрузополучатель+",
		|	СпрЗн.descr
		|order by Жур.DocNo
		|";
	
		
		УсловиеПоСвойствуТовара="";
		Если ПустоеЗначение(ВыбТипПродукции) = 0 Тогда		
			УсловиеПоСвойствуТовара = "and СпрЗн.ID = :ВыбТипПродукции";
		КонецЕсли;
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "%УсловиеПоСвойствуТовара%", УсловиеПоСвойствуТовара);
		
		УсловиеПоГрузополучателю = "";
		Если ПустоеЗначение(ВыбКонтрагент) = 0 Тогда		
			УсловиеПоГрузополучателю = " and $ДокР."+НазваниеРеквизитаГрузополучатель+" IN (SELECT Val FROM #ГруппаК) ";
		КонецЕсли;
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "%УсловиеПоГрузополучателю%", УсловиеПоГрузополучателю);
		
		//условие по фирме
		УсловиеПоФирме="";
		Если ПустоеЗначение(ВыбФирма) = 0 Тогда		
			УсловиеПоФирме = " and Жур.$ОбщийРеквизит.Фирма = :ВыбФирма";
		КонецЕсли;
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "%УсловиеПоФирме%", УсловиеПоФирме);

		//условие по филиалу
		Если ПустоеЗначение(глПользователь.Филиал) = 0 Тогда		
			УсловиеПоФилиалу = " and Жур.$ОбщийРеквизит.Филиал = :ВыбФилиал ";
		КонецЕсли;
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "%УсловиеПоФилиалу%", УсловиеПоФилиалу);

		
		RS.УложитьСписокОбъектов(ВыбКонтрагент, "#ГруппаК","Контрагенты");
		RS.УстановитьТекстовыйПараметр("ВыбВидСвойств", ВыбВидСвойств);    
		RS.УстановитьТекстовыйПараметр("ВыбТипПродукции", ВыбТипПродукции);    
		RS.УстановитьТекстовыйПараметр("НачДата", НачДата);
		RS.УстановитьТекстовыйПараметр("КонДата", КонДата);
		RS.УстановитьТекстовыйПараметр("ВыбФилиал", глПользователь.Филиал);
		RS.УстановитьТекстовыйПараметр("ВыбФирма", ВыбФирма);
		        
		//RS.Отладка(1);
		ТЗ = RS.ВыполнитьИнструкцию(ТекстЗапроса);
		//ТЗ.ВыбратьСтроку();
	  	
	Иначе	
			
		Запрос = базаДанных.НовыйЗапрос();
	
		ТекстЗапроса = "
		|SELECT
		|    Жур.IDDoc as [Док :Документ],
		|    Жур.IDDocDef as Док_вид,
		|	Жур.DocNo as [НомерДок],
		|	ДокВСД.IDDoc as [ДокВСД $Документ.ВСД_исходящий],
		|	ДокР."+НазваниеРеквизитаГрузополучатель+" as [Клиент :Справочник.Контрагенты],
		|	SUM(ДокСтроки.Количество) [Количество],
		//|	SUM(ДокСтроки.КоличествоМест) [КоличествоМест],
		|	СпрЗн.descr as Свво
		|FROM
		|    Журнал Жур
		|INNER JOIN 
		|	[ДокументСтроки.Реализация] as ДокСтроки 
		|		ON Жур.IDDoc = ДокСтроки.IDDoc
		|LEFT JOIN 
		|	[Справочник.СвойстваНоменклатуры] as СпрСв 
		|		ON СпрСв.ParentExt = ДокСтроки.Номенклатура
		|			 and СпрСв.ВидСвойства = :ВыбВидСвойств 		
		|LEFT JOIN 
		|	[Справочник.ЗначенияСвойств] as СпрЗн 
		|		ON СпрЗн.ID = СпрСв.ЗначениеСвойства 
		|			and  СпрЗн.ParentExt = :ВыбВидСвойств 
		|INNER JOIN 
		|	[Документ.Реализация] as ДокР 
		|		ON Жур.IDDoc = ДокР.IDDoc
		|Left join 
		|	[Документ.ВСД_исходящий] as ДокВСД
		|	ON SUBSTR(ДокВСД.ДокОснование, 5, 8) = Жур.IDDoc
		//|		AND ДокВСД.Статус = 'COMPLETED'	
		|WHERE
		|    Жур.idx_date_time_iddoc BETWEEN :НачДата AND :КонДата~ AND
		|    Жур.IDDocDef = :ВидДокумента.Реализация
		|	and Жур.Closed & 1 = 1
		|	%УсловиеПоГрузополучателю%
		|	%УсловиеПоСвойствуТовара%
		|	%УсловиеПоФирме%
		|
		|GROUP BY
		|	Жур.IDDoc,
		|	Жур.IDDocDef,
		|	Жур.DocNo,
		|	ДокР."+НазваниеРеквизитаГрузополучатель+",
		|	СпрЗн.descr
		|order by Жур.DocNo
		|";
	
		УсловиеПоГрузополучателю = "";
		УсловиеПоСвойствуТовара="";
		Если ПустоеЗначение(ВыбТипПродукции) = 0 Тогда		
			УсловиеПоСвойствуТовара = "and СпрЗн.ID = :ВыбТипПродукции";
		КонецЕсли;
		Если ПустоеЗначение(ВыбКонтрагент) = 0 Тогда		
			УсловиеПоГрузополучателю = " and ДокР."+НазваниеРеквизитаГрузополучатель+" IN (SELECT Val FROM ГруппаК) ";
			базаДанных.УложитьОбъекты(ВыбКонтрагент, "ГруппаК",, "Контрагенты");
		КонецЕсли;
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "%УсловиеПоГрузополучателю%", УсловиеПоГрузополучателю);
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "%УсловиеПоСвойствуТовара%", УсловиеПоСвойствуТовара);

		//условие по фирме
		УсловиеПоФирме="";
		Если ПустоеЗначение(ВыбФирма) = 0 Тогда		
			УсловиеПоФирме = " and Жур.Фирма = :ВыбФирма";
		КонецЕсли;
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "%УсловиеПоФирме%", УсловиеПоФирме);
		        
		//Запрос.Подставлять("ВыбКлиент", ВыбКлиент);
		Запрос.Подставлять("ВыбВидСвойств", ВыбВидСвойств);    
		Запрос.Подставлять("ВыбТипПродукции", ВыбТипПродукции);    
		Запрос.Подставлять("НачДата", НачДата);
		Запрос.Подставлять("КонДата", КонДата);
		Запрос.Подставлять("ВыбФирма", ВыбФирма);
	
		//Запрос.Отладка();
		ТЗ = Запрос.ВыполнитьЗапрос(ТекстЗапроса);
		//ТЗ.ВыбратьСтроку();
	КонецЕсли;
		
	СписокДокументов.УдалитьСтроки();
	
	тз.ВыбратьСтроки();
	Ном=0;
	ПредДок="";
	Пока тз.ПолучитьСтроку()=1 Цикл 
		СписокДокументов.НоваяСтрока();
		СписокДокументов.Пометка=1;
		СписокДокументов.Контрагент = тз.Клиент;
		СписокДокументов.Грузополучатель = тз.Док.ПолучитьАтрибут(НазваниеРеквизитаГрузополучатель);
		СписокДокументов.Док = тз.Док;
		СписокДокументов.Колво = тз.Количество;
		СписокДокументов.Вес = тз.Количество;
		Попытка			
			СписокДокументов.КолвоМест = тз.КоличествоМест;
		Исключение			
		КонецПопытки;
				
		СписокДокументов.ХозСубъект = НайтиХозСубъект(тз.Клиент);
		Если ПустоеЗначение(СписокДокументов.ХозСубъект)=0 Тогда
			СписокДокументов.Площадка = НайтиПлощадку( СписокДокументов.Грузополучатель);
		КонецЕсли;

		Попытка
			Если тз.ДокВСД.ПометкаУдаления()=0 Тогда 
				СписокДокументов.ВСД = тз.ДокВСД;
			КонецЕсли;
		Исключение
		КонецПопытки;

	КонецЦикла; 
	      
	РаскраситьСписокДокументов();
	ОбновитьИнф();
КонецПроцедуры

Процедура СоздатьВСД()

	Если ПустоеЗначение(Отправитель_ХозСубъект)=1 Тогда
		Сообщить("Не выбран параметр Отправитель_ХозСубъект ","!");			
		Возврат;
	КонецЕсли;
	Если ПустоеЗначение(Отправитель_Площадка)=1 Тогда
		Сообщить("Не выбран параметр Отправитель_Площадка ","!");			
		Возврат;
	КонецЕсли;
	Если ПустоеЗначение(Перевозчик_ХозСубъект)=1 Тогда
		Сообщить("Не выбран параметр Перевозчик_ХозСубъект ","!");			
		Возврат;
	КонецЕсли;
	Если ПустоеЗначение(Партия)=1 Тогда
		Сообщить("Не выбран параметр Партия ","!");			
		Возврат;
	КонецЕсли;			
	Если ПустоеЗначение(ТермическиеУсловияПеревозки)=1 Тогда
		Сообщить("Не выбран ТермическиеУсловияПеревозки ","!");			
		Возврат;
	КонецЕсли;			
	
	СписокДокументов.ВыбратьСтроки();
	Пока СписокДокументов.ПолучитьСтроку() = 1 Цикл
		Если СписокДокументов.Пометка<>2 Тогда
			Продолжить;
		КонецЕсли;

		Если (ПустоеЗначение(СписокДокументов.ВСД)=0) Тогда  
			Если (СокрЛП(СписокДокументов.ВСД.Статус)= "COMPLETED") Тогда 
				Сообщить("Для "+СписокДокументов.Док+" ВСД уже отправлен","i");
				Продолжить;
			КонецЕсли;
			
			Если (ПустоеЗначение(СписокДокументов.ВСД.Статус) = 1) Тогда 
				Сообщить("Для "+СписокДокументов.Док+" ВСД уже создан, но не отправлен","i");
				Продолжить;
			КонецЕсли;		
		КонецЕсли;		

		Сообщить("создание ВСД для "+СписокДокументов.Док);
		
		Если СписокДокументов.Контрагент.Вид()="Склады" Тогда 
			ХС = Отправитель_ХозСубъект;
		Иначе
			ХС = НайтиХозСубъект( СписокДокументов.Контрагент );
		КонецЕсли;
		Если СписокДокументов.Грузополучатель.Вид()="Склады" Тогда
			Площадка = НайтиПлощадкуПоСкладу(СписокДокументов.Грузополучатель);
		Иначе
			Площадка = НайтиПлощадку( СписокДокументов.Грузополучатель );
		КонецЕсли;
		
		// создание кастомного ВСД
		Если ФС.СуществуетФайл(КаталогОбработки+"ПодключаемыеМодули\Меркурий_Подключаемый_Создание_ВСД_исходящий.ert")=1 Тогда
			
			СписокПараметров = СоздатьОбъект("СписокЗначений");
			СписокПараметров.ДобавитьЗначение(СписокДокументов.Док,				"ДокОснование");    
			СписокПараметров.ДобавитьЗначение(СписокДокументов.Контрагент,		"Контрагент");    
			СписокПараметров.ДобавитьЗначение(СписокДокументов.Грузополучатель,	"Грузополучатель");    
			СписокПараметров.ДобавитьЗначение(Отправитель_ХозСубъект,			"Отправитель_ХозСубъект");    
			СписокПараметров.ДобавитьЗначение(Отправитель_Площадка,				"Отправитель_Площадка");    
			СписокПараметров.ДобавитьЗначение(Перевозчик_ХозСубъект,			"Перевозчик_ХозСубъект");    
			СписокПараметров.ДобавитьЗначение(Партия,							"Партия");    
			СписокПараметров.ДобавитьЗначение(ТермическиеУсловияПеревозки,		"ТермическиеУсловияПеревозки");    
			СписокПараметров.ДобавитьЗначение(Отправитель_Площадка,				"Отправитель_Площадка");    
			СписокПараметров.ДобавитьЗначение( ХС,				"ХС");    
			СписокПараметров.ДобавитьЗначение( Площадка,		"Площадка");    
			СписокПараметров.ДобавитьЗначение( СписокДокументов.Колво,			"Колво");    
			СписокПараметров.ДобавитьЗначение( СписокДокументов.КолвоМест,		"КолвоМест");    
			СписокПараметров.ДобавитьЗначение( ВыбТипПродукции,					"ВыбТипПродукции");    
			СписокПараметров.ДобавитьЗначение( ВыбВидСвойств,					"ВыбВидСвойств");
			
			СписокПараметров.ДобавитьЗначение( ВСД_Экспертиза,					"ВСД_Экспертиза");    
			СписокПараметров.ДобавитьЗначение( ВСД_Местность,					"ВСД_Местность");
			СписокПараметров.ДобавитьЗначение( ВСД_ОсобыеОтметки,				"ВСД_ОсобыеОтметки");    
			
				
			ОткрытьФормуМодально("Отчет",СписокПараметров,КаталогОбработки+"ПодключаемыеМодули\Меркурий_Подключаемый_Создание_ВСД_исходящий.ert");
			
			ДокументСсылка = "";
			
			Если ТипЗначенияСтр(СписокПараметров)  = "СписокЗначений" Тогда
				
				ДокументСсылка = СписокПараметров.Получить("ДокументСсылка");
				
			КонецЕсли;  
			
			Если ПустоеЗначение(ДокументСсылка)=1 Тогда
				Предупреждение("В подключаемом модуле не удалось создать ВСД","!");
				//Возврат "";
			КонецЕсли;
		Иначе

			Если ПустоеЗначение(СписокДокументов.Док.Маршрут)=1 Тогда
				Сообщить("Не указан маршрут в документе "+СписокДокументов.Док);
				Продолжить;
			КонецЕсли;
	
			ДокВСД = СоздатьОбъект("Документ.ВСД_исходящий");
			ДокВСД.Новый();
			//ДокВСД.НомерДок = ;
			ДокВСД.ДокОснование = СписокДокументов.Док;
			
			//ДокВСД.ДатаДок = ДокВСД.ДокОснование.ДатаДок;
			ДокВСД.ДатаДок = НачДата;
			
			ДокВСД.Отправитель_ХозСубъект = Отправитель_ХозСубъект;
			ДокВСД.Отправитель_Площадка = Отправитель_Площадка;
			
			ДокВСД.Получатель_ХозСубъект = ХС;		
			ДокВСД.Получатель_Площадка = Площадка;					
			ДокВСД.Перевозчик_ХозСубъект = Перевозчик_ХозСубъект;
	
			ДокВСД.Производитель_Площадка = Партия.Производитель_Площадка;
			ДокВСД.Количество = СписокДокументов.Колво;
			ДокВСД.КоличествоКор = СписокДокументов.КолвоМест;
			
			ТТН = СписокДокументов.Док.Маршрут;		
			//ТТН = СписокДокументов.Док.НомерМаршрута;	
						
			ДокВСД.ТтнСерия = ""; //ТТН.Серия;
			ДокВСД.ТтнНомер = СписокДокументов.Док.НомерДок;
			ДокВСД.ТтнДата = СписокДокументов.Док.ДатаДок;
			ДокВСД.номерАвто = ТТН.Авто.НомернойЗнак;
			
			ДокВСД.Партия = Партия;
			ДокВСД.Автор = глПользователь;
			Попытка				
				ДокВСД.Филиал = глПользователь.Филиал;
			Исключение
			КонецПопытки;
			ДокВСД.ФормаВСД = 1;
			ДокВСД.ЕдиницаИзмерения = ДокВСД.Партия.ЕдиницаИзмерения;
			ДокВСД.ФормаУпаковки = ДокВСД.Партия.ФормаУпаковки;
			ДокВСД.ТермическоеСостояние = ТермическиеУсловияПеревозки;
			
			ДокВСД.Экспертиза 		= ВСД_Экспертиза;
			ДокВСД.Местность 		= ВСД_Местность;
			ДокВСД.ОсобыеОтметки 	= ВСД_ОсобыеОтметки;
			ДокВСД.cargoExpertized  = 1;
			
			Если ПустоеЗначение( СокрЛП(ДокВСД.номерАвто) )=1 Тогда
				Сообщить("В документе "+ДокВСД.ТтнНомер+"не указан № автомобиля 
				|ВСД не сформирован");
				Продолжить;
			КонецЕсли;
			
			ДокВСД.Записать();
			ДокументСсылка = ДокВСД.ТекущийДокумент();
		КонецЕсли;
		
		СписокДокументов.ВСД = ДокументСсылка;
	КонецЦикла;
	
	Партия = "";
	
КонецПроцедуры

Процедура ПроверитьПартию()
	//Если Партия.ТермическоеСостояние=0 Тогда 
	//	Предупреждение("Не указано термическое состояние партии");
	//	Партия = "";
	//	Возврат;
	//КонецЕсли;
	Если ПустоеЗначение(Партия)=1 Тогда 
		ПартияИнфо = "партия не выбрана";
	Иначе
		ПартияИнфо = СокрЛП(Партия.НаименованиеПродукции) +" = "+Партия.Количество+" кг";
	КонецЕсли;
КонецПроцедуры	

//============================================================
Процедура УбратьВсеДокументы()
	СписокДокументов.ВыбратьСтроки();
	Пока СписокДокументов.ПолучитьСтроку() = 1 Цикл
		СписокДокументов.Пометка=1;		
	КонецЦикла;	 
	
	ОбновитьИнф();
КонецПроцедуры
//============================================================
Процедура ПометитьВсеДокументы()
	СписокДокументов.ВыбратьСтроки();
	Пока СписокДокументов.ПолучитьСтроку() = 1 Цикл
		Если СписокДокументов.инфо = 36 Тогда 
			СписокДокументов.Пометка=2;		
		КонецЕсли;
	КонецЦикла;	      

	ОбновитьИнф();	
КонецПроцедуры




Процедура ПриСменеВерсииСистемы()
	
	РасположениеФайла(КаталогОбработки, ИмяФайлаНастроек);
	Если ВерсияСистемы=2 Тогда
		// РАБОЧАЯ
		//настройки модуля хранятся в файле				
		ИмяФайлаНастроек=КаталогОбработки+"Меркурий_Константы_"+СокрЛП(глПользователь.Код)+".tx8";
		//ПодключитьВнешнююКомпоненту("AddIn.SoapDLL_test");
		//Компонента = СоздатьОбъект("AddIn.SoapDLL_test"); 		
		ОбработкаОбмена = "Меркурий_ВетСправки.ert";
		
		//Форма.Заголовок("Меркурий - РАБОЧАЯ");
	Иначе
		
		ВерсияСистемы = 1;
		// ТЕСТ
		//настройки модуля хранятся в файле		
		ИмяФайлаНастроек=КаталогОбработки+"Меркурий_Константы_ТЕСТ_"+СокрЛП(глПользователь.Код)+".tx8";
		//ПодключитьВнешнююКомпоненту("AddIn.SoapDLL_test");
		//Компонента = СоздатьОбъект("AddIn.SoapDLL_test"); 		
		ОбработкаОбмена = "Меркурий_ВетСправки_тест.ert";
	
		Форма.Заголовок("Меркурий - ТЕСТ [!!!]");		
	КонецЕсли;

	Если фс.СуществуетФайл(ИмяФайлаНастроек)=0 Тогда
		Предупреждение("Файл с константами ["+ИмяФайлаНастроек+"] не найден.
		|Заполните параметры в обработке "+ОбработкаОбмена);			
	КонецЕсли;		
	
	ЗаполнитьПараметрыСписок();

		
КонецПроцедуры



//******************************************************************************
 // предопределенная процедура
 //
 Процедура ПриВыбореЗакладки(НомерЗакладки, ЗначениеЗакладки)
	СписокДокументов.УдалитьСтроки();
 	
 	Если НомерЗакладки=1 Тогда
		Форма.ИспользоватьСлой("Основной, Реализации");
		ЗаполнитьСписокДокументов_Реализации();
	ИначеЕсли НомерЗакладки=2 Тогда
		Форма.ИспользоватьСлой("Основной, Перемещения");
		ЗаполнитьСписокДокументов_ПеремещенийТМЦ();
	Иначе
		Форма.ИспользоватьСлой("Параметры");
	КонецЕсли;     
	
	
	
КонецПроцедуры 

//*******************************************
Процедура ПриОткрытии()
	ЗагрузитьВнешнююКомпоненту("1cpp.dll");
	ЗагрузитьВнешнююКомпоненту("formex.dll");
	
	Сервис = СоздатьОбъект("Сервис");
	Сервис.ВключитьРаскраскуТаблиц();
	
	ВерсияСистемы = ВосстановитьЗначение("ВСД_ВерсияСистемы");	
	ПриСменеВерсииСистемы();		
	
	СпрВидСв = СоздатьОбъект("Справочник.ВидыСвойств");	
	Если СпрВидСв.НайтиПоНаименованию("Тип продукции ВСД (Охл / Зам / Колбаса)",0,1)=0 Тогда
		Сообщить("Тип св-в [Тип продукции ВСД (Охл / Зам / Колбаса)] не найдено");	
		//Возврат;
	Иначе
		ВыбВидСвойств = СпрВидСв.ТекущийЭлемент();	
	КонецЕсли;
	
	ЗаголовокНадпись();
	
	Форма.ИспользоватьЗакладки(1);
	Форма.Закладки.ДобавитьЗначение("Реализации");
	Форма.Закладки.ДобавитьЗначение("Перемещения");
	Форма.Закладки.ДобавитьЗначение("Параметры");
	          
	Форма.ИспользоватьСлой("Основной, Реализации");
	
	Партия = "";

	ЭтоSQL = ФС.СуществуетФайл(КаталогИБ()+"1cv7.dba");
	
	Если ЭтоSQL=1 Тогда 
		Попытка
			RS = СоздатьОбъект("ODBCRecordset");
			RS.УстБД1С();     
		Исключение
			Сообщить(ОписаниеОшибки());
		КонецПопытки;	
	Иначе
		
		Попытка
			базаДанных = СоздатьОбъект("SQLiteBase");
		Исключение
			ЗагрузитьВнешнююКомпоненту("1sqlite.dll");
			базаДанных = СоздатьОбъект("SQLiteBase");
		КонецПопытки;	
		базаДанных.Открыть(":memory:");	
	КонецЕсли;
		
	ЗаполнитьСписокДокументов_Реализации();
КонецПроцедуры

Процедура ОтправитьВСД()
	СписокПараметров = СоздатьОбъект("СписокЗначений");
	СписокПараметров.ДобавитьЗначение("ОтправитьВсдИсходящие", "Команда");
	СписокПараметров.ДобавитьЗначение(НачДата, "НачДата");
	СписокПараметров.ДобавитьЗначение(КонДата, "КонДата");


	СписокВСД = СоздатьОбъект("СписокЗначений");
	СписокДокументов.ВыбратьСтроки();
	Пока СписокДокументов.ПолучитьСтроку() = 1 Цикл
		Если ПустоеЗначение(СписокДокументов.ВСД)=0 Тогда 
			СписокВСД.ДобавитьЗначение(СписокДокументов.ВСД);
		КонецЕсли;
	КонецЦикла;
	СписокПараметров.ДобавитьЗначение(СписокВСД, "СписокВСД");

	ОткрытьФормуМодально("Отчет",СписокПараметров,КаталогОбработки+ОбработкаОбмена);
	
	ЗаписьЖурналаРегистрации("Меркурий отправка исходящих ВСД ");

КонецПроцедуры

Процедура ПолучитьПартии()
	СписокПараметров = СоздатьОбъект("СписокЗначений");
	СписокПараметров.ДобавитьЗначение("ПолучитьПартии", "Команда");
	ОткрытьФормуМодально("Отчет",СписокПараметров,КаталогОбработки+ОбработкаОбмена);
	
	ЗаписьЖурналаРегистрации("Меркурий получение партий ");
	
КонецПроцедуры

Процедура Реестр()
	Таб = СоздатьОбъект("Таблица");
	Таб.ВывестиСекцию("Шапка");
	
	СписокДокументов.ВыбратьСтроки();
	Пока СписокДокументов.ПолучитьСтроку() = 1 Цикл
		
		Таб.ВывестиСекцию("Строка");		
	КонецЦикла;

	Таб.Опции(0,0,0,0);
	Таб.ТолькоПросмотр();
	Таб.Показать("Реестр");	
КонецПроцедуры

Процедура ЗагрузитьПлощадки()
	СписокПараметров = СоздатьОбъект("СписокЗначений");
	СписокПараметров.ДобавитьЗначение("ЗагрузитьПлощадки", "Команда");
	СписокПараметров.ДобавитьЗначение(СписокДокументов.ХозСубъект, "ХозСубъект");

	//ЗаписьЖурналаРегистрации("Меркурий ");	
	ОткрытьФормуМодально("Отчет",СписокПараметров,КаталогОбработки+ОбработкаОбмена);
	
	ПриВыбореЗакладки(Форма.Закладки.ТекущаяСтрока(),"");
КонецПроцедуры

процедура выбратьПериод()
	если ВвестиПериод(НачДата,КонДата)=1 тогда
		ЗаголовокНадпись();
		ПриВыбореЗакладки(Форма.Закладки.ТекущаяСтрока(),"");
	конецесли;
конецпроцедуры

НачДата = ТекущаяДата();
КонДата = ТекущаяДата();

СписокДокументов.НоваяКолонка("сЦвет",,,,,1,);
СписокДокументов.НоваяКолонка("Пометка",,,,,5,);
СписокДокументов.НоваяКолонка("инфо",,,,,3,);
СписокДокументов.НоваяКолонка("Грузополучатель",,,,,70,);
СписокДокументов.НоваяКолонка("Площадка",,,,,30,);
СписокДокументов.НоваяКолонка("ВСД",,,,,10,);
СписокДокументов.НоваяКолонка("Колво",,,,,10,);
СписокДокументов.НоваяКолонка("Вес",,,,,10,);
СписокДокументов.НоваяКолонка("КолвоМест",,,,,10,);

СписокДокументов.НоваяКолонка("Док",,,,,20,);
СписокДокументов.НоваяКолонка("Контрагент",,,,,30,);
СписокДокументов.НоваяКолонка("ХозСубъект",,,,,30,);

СписокДокументов.ВыводитьПиктограммы("Пометка");
СписокДокументов.ВыводитьПиктограммы("Инфо");

//                    TransportationStorageType.FROZEN == 0 
//                    TransportationStorageType.CHILLED == 1 
//                    TransportationStorageType.COOLED == 2
//                    TransportationStorageType.VENTILATED ==3

ЦвЖелтый 	= "FONT[0]BRUSH[65535]FONT_S[0]BRUSH_S[65535]                       ";
ЦвЗеленый 	= "FONT[0]BRUSH[65280]FONT_S[0]BRUSH_S[65280]                       ";
ЦвКрасный  	= "FONT[0]BRUSH[255]FONT_S[0]BRUSH_S[255]                           " ;
ЦвГолубой	= "FONT[0]BRUSH[13421619]FONT_S[0]BRUSH_S[13421619]                 ";

НазваниеРеквизитаГрузополучатель = "Контрагент";
ПартияИнфо = "выберите партию";